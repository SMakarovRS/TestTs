
#Область ОбработчикиСобытий

Функция ЛюбойGET(Запрос)
	                     
	ОтносительныйURL	= СокрЛП(НРег(Запрос.ОтносительныйURL));
	КлючВебХук			= Сред(ОтносительныйURL, СтрНайти(ОтносительныйURL, "/", НаправлениеПоиска.СКонца) + 1);
	
	НастройкиБота = ОпределитьБотаПоКлючуВебХук(ОтносительныйURL);
	
	Если НастройкиБота <> Неопределено Тогда
		Текст = СтрШаблон(НСтр("ru = 'Привет. HTTP-сервис Telegram <font color=""green""><b>доступен</b></font>. Ключ web hook равен ""%1""'"), 
			КлючВебХук);
	Иначе
		Текст = СтрШаблон(НСтр("ru = 'Привет. HTTP-сервис Telegram <font color=""red""><b>НЕ доступен</b></font> по этому адресу.
                      |<ul>
					  |<li>Информационная база должна быть опубликована в Apache или IIS.</li>
					  |<li>Адрес должен быть равен <i>""имясервера/имябазы1с/hs/telegram/Ключ""</i> где Ключ задается в настройках бота.<br>
					  |Ключ в строке браузера равен %1. Установите его в настройках бота или задайте произвольный в настройках бота.</li>
					  |</ul>'"), КлючВебХук);
	КонецЕсли;
	
	
	Ответ = Новый HTTPСервисОтвет(200);
	Ответ.УстановитьТелоИзСтроки("<html><head><title>Telegram-бот</title><meta charset=""UTF-8""></head><body>" 
		+ Текст + "</body></html>");
	Возврат Ответ;
	
КонецФункции

Функция ЛюбойPOST(Запрос)
	
	Возврат ОбработатьРезультат(Запрос);
	
КонецФункции

#КонецОбласти

#Область Служебные

Функция ОбработатьРезультат(Запрос)
	
	// Проверка к какому боту относится запрос, если бот не найден по секртеному ID, то игнорируем.
	НастройкиБота = ОпределитьБотаПоКлючуВебХук(Запрос.ОтносительныйURL);
	
	// Обработка входящих данных.
	Если НастройкиБота <> Неопределено Тогда
	
		МассивНакопленныхДанных = Новый Массив;
		
		// Обрабатываем входящий запрос.
		TelegramСервер.ОбработатьОтветСервера(Запрос, НастройкиБота, МассивНакопленныхДанных);
		
		// Отправляем все данные.
		TelegramСервер.ОтправитьНакопленныеДанные(МассивНакопленныхДанных);
		
	КонецЕсли;
	
	Ответ = Новый HTTPСервисОтвет(200);
	Возврат Ответ;
	
КонецФункции

Функция ОпределитьБотаПоКлючуВебХук(Знач ОтносительныйURL)
	
	УстановитьПривилегированныйРежим(Истина);
	
	ОтносительныйURL	= СокрЛП(НРег(ОтносительныйURL));
	КлючВебХук			= Сред(ОтносительныйURL, СтрНайти(ОтносительныйURL, "/", НаправлениеПоиска.СКонца) + 1);
	
	НастройкиБота = Неопределено;
	
	Если ПустаяСтрока(КлючВебХук) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Запрос = Новый Запрос();
	Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	TelegramБоты.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.TelegramБоты КАК TelegramБоты
		|ГДЕ
		|	TelegramБоты.КлючВебхук = &КлючВебхук";
	
	Запрос.УстановитьПараметр("КлючВебхук", КлючВебХук);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		НастройкиБота = TelegramСервер.НастройкиБота(Выборка.Ссылка);
	КонецЕсли;
	
	Возврат НастройкиБота;
	
КонецФункции

#КонецОбласти