
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
		// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;	

	Если УправлениеITОтделом8УФПовтИсп.Право("ПолныеПрава") Тогда 	
		Элементы.ФормаОчистить.Видимость 			  = Истина;
		Элементы.СрокХраненияЗаписейЖурнала.Видимость = Истина;
		СрокХраненияЗаписейЖурнала = Константы.СрокХраненияЗаписейЖурналаПравилВыполненияСобытий.Получить();
	Иначе
		Элементы.ФормаОчистить.Видимость 			  = Ложь;
		Элементы.СрокХраненияЗаписейЖурнала.Видимость = Ложь;
	КонецЕсли;
	
	ОбновитьКоличествоСобытий();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура СрокХраненияЗаписейЖурналаПриИзменении(Элемент)
	
	СрокХраненияЗаписейЖурналаПриИзмененииНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОчиститьЗаписи(Команда)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ОчиститьЗаписиПослеВопроса", ЭтотОбъект);
	ПоказатьВопрос(ОписаниеОповещения, 
		НСтр("ru = 'ВНИМАНИЕ! Все записи регистра будут очищены. Продолжить?'"), РежимДиалогаВопрос.ДаНет); 	
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОчиститьЗаписиПослеВопроса(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	ОчиститьЗаписиНаСервере();
	Элементы.Список.Обновить();
	ОбновитьКоличествоСобытий();
	
КонецПроцедуры	

&НаСервереБезКонтекста
Процедура ОчиститьЗаписиНаСервере()
	
	НаборЗаписей = РегистрыСведений.ВыполнениеПравилСобытий.СоздатьНаборЗаписей();
	НаборЗаписей.Записать(Истина);
	
КонецПроцедуры

&НаСервере
Процедура СрокХраненияЗаписейЖурналаПриИзмененииНаСервере()
	
	Константы.СрокХраненияЗаписейЖурналаПравилВыполненияСобытий.Установить(СрокХраненияЗаписейЖурнала);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьКоличествоСобытий()
	
	КоличествоСобытий = 0;
	
	Запрос = Новый Запрос();
	Запрос.Текст =
		"ВЫБРАТЬ
		|	Количество(*) КАК КоличествоСобытий
		|ИЗ
		|	РегистрСведений.ВыполнениеПравилСобытий КАК ВыполнениеПравилСобытий";
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		КоличествоСобытий = Выборка.КоличествоСобытий;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьКоличествоЗаписей(Команда)
	
	ОбновитьКоличествоСобытий();
	
КонецПроцедуры

#КонецОбласти

