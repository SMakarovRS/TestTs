#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция ПрочитатьДанныеИзХранилища(Знач Владелец) Экспорт
	
	Результат = Новый Структура("Период, Логин, Пароль", Дата(1, 1, 1), "", "");
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЛогиныИПаролиХранилищеСрезПоследних.Период КАК Период,
		|	ЛогиныИПаролиХранилищеСрезПоследних.Логин КАК Логин,
		|	ЛогиныИПаролиХранилищеСрезПоследних.Пароль КАК Пароль
		|ИЗ
		|	РегистрСведений.ЛогиныИПаролиХранилище.СрезПоследних КАК ЛогиныИПаролиХранилищеСрезПоследних
		|ГДЕ
		|	ЛогиныИПаролиХранилищеСрезПоследних.Владелец = &Владелец";
	
	Запрос.УстановитьПараметр("Владелец", Владелец);	
	РезультатЗапроса		= Запрос.Выполнить();	
	ВыборкаДетальныеЗаписи	= РезультатЗапроса.Выбрать();
	
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		Результат.Вставить("Период",ВыборкаДетальныеЗаписи.Период);
		Результат.Вставить("Логин",	ВыборкаДетальныеЗаписи.Логин);
		Результат.Вставить("Пароль",ВыборкаДетальныеЗаписи.Пароль.Получить());
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции	

Процедура ЗаписатьДанныеВХранилище(Знач Владелец, Знач СтруктураДляЗаписи) Экспорт
	
	ХранилищеДанных				= РегистрыСведений.ЛогиныИПаролиХранилище.СоздатьМенеджерЗаписи();
	ХранилищеДанных.Владелец	= Владелец;	
	
	Если СтруктураДляЗаписи.Период <> Дата(1,1,1) Тогда
		мПериод = СтруктураДляЗаписи.Период;
	Иначе
		мПериод = ТекущаяДатаСеанса();
	КонецЕсли;
	
	ХранилищеДанных.Период = мПериод;
	ХранилищеДанных.Прочитать();
	
	ХранилищеДанных.Логин	= СтруктураДляЗаписи.Логин;
	ДанныеДляЗаписи			= Новый ХранилищеЗначения(СтруктураДляЗаписи.Пароль, Новый СжатиеДанных(6));
	ХранилищеДанных.Пароль	= ДанныеДляЗаписи;	
	ХранилищеДанных.ПериодМП= ХранилищеДанных.Период;
	
	Если ХранилищеДанных.Выбран() Тогда
		ХранилищеДанных.Записать(Истина);
	Иначе
		ХранилищеДанных.Владелец	= Владелец;
		ХранилищеДанных.Период		= мПериод;
		ХранилищеДанных.ПериодМП	= мПериод;
		ХранилищеДанных.Записать(Истина);
	КонецЕсли;	

КонецПроцедуры

Функция ПрочитатьДанныеИзХранилищаНаПериод(Знач Владелец, Знач Период) Экспорт
	
	Результат = Новый Структура("Период, Логин, Пароль", Дата(1, 1, 1), "", "");
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЛогиныИПаролиХранилище.Период КАК Период,
		|	ЛогиныИПаролиХранилище.Логин КАК Логин,
		|	ЛогиныИПаролиХранилище.Пароль КАК Пароль
		|ИЗ
		|	РегистрСведений.ЛогиныИПаролиХранилище КАК ЛогиныИПаролиХранилище
		|ГДЕ
		|	ЛогиныИПаролиХранилище.Владелец = &Владелец
		|	И ЛогиныИПаролиХранилище.Период = &Период";
	
	Запрос.УстановитьПараметр("Период", Период);
	Запрос.УстановитьПараметр("Владелец", Владелец);	
	РезультатЗапроса		= Запрос.Выполнить();	
	ВыборкаДетальныеЗаписи	= РезультатЗапроса.Выбрать();
	
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		Результат.Вставить("Период",ВыборкаДетальныеЗаписи.Период);
		Результат.Вставить("Логин",	ВыборкаДетальныеЗаписи.Логин);
		Результат.Вставить("Пароль",ВыборкаДетальныеЗаписи.Пароль.Получить());
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ПарольУникальный(Знач Владелец, Знач Пароль) Экспорт
	
	Результат = Истина;
	Если ПустаяСтрока(Пароль) Тогда		
		Возврат Результат;
	КонецЕсли;
	
	// Для пароля созданного сегодня проверку не делаем.
	ТекущийПериод = НачалоДня(ТекущаяДатаСеанса());
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЛогиныИПаролиХранилище.Период КАК Период,
		|	ЛогиныИПаролиХранилище.Владелец КАК Владелец,
		|	ЛогиныИПаролиХранилище.Логин КАК Логин,
		|	ЛогиныИПаролиХранилище.Пароль КАК Пароль
		|ИЗ
		|	РегистрСведений.ЛогиныИПаролиХранилище КАК ЛогиныИПаролиХранилище
		|ГДЕ
		|	ЛогиныИПаролиХранилище.Период <> &Период
		|	И ЛогиныИПаролиХранилище.Владелец = &Владелец";
	
	Запрос.УстановитьПараметр("Владелец", Владелец);
	Запрос.УстановитьПараметр("Период", ТекущийПериод);	
	РезультатЗапроса		= Запрос.Выполнить();	
	ВыборкаДетальныеЗаписи	= РезультатЗапроса.Выбрать();
	
	Если ВыборкаДетальныеЗаписи.Количество() = 0 Тогда
		Возврат Результат;
	КонецЕсли;
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Если Пароль = ВыборкаДетальныеЗаписи.Пароль.Получить() Тогда
			Результат = Ложь;
			Возврат Результат;
		КонецЕсли;	
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#КонецЕсли

