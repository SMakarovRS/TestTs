
////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС

// Добавляет запись в журнал.
// Параметры:
//	МобильныйКлиент - ссылка на узел плана обмена МобильноеПриложение;
//	МоментВремени - момент наступлени события в миллисекундах;
//	Период - дата наступления события;
//	ИдентификаторОбъекта - уникальный идентификатор объекта, с которым связано произошедшее событие;
//	ОписаниеСобытия - текстовое описание события;
//	ТипОбъекта - имя типа объекта, с которым связано событие;
//	ТиСобытия - значение перечисления ТипыСобытийЖурналаРаботыСМобильнымиКлиентами.
Процедура ДобавитьЗаписьВЖурналСКлиента(Знач МобильныйКлиент, 
										Знач МоментВремени,
										Знач Период,
										Знач ИдентификаторОбъекта,
										Знач ОписаниеСобытия,
										Знач ТипОбъекта,
										Знач ТипСобытия) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	МенеджерЗаписиРегистра = РегистрыСведений.ЖурналРаботыСМобильнымиКлиентами.СоздатьМенеджерЗаписи();
	МенеджерЗаписиРегистра.МобильныйКлиент      = МобильныйКлиент;
	МенеджерЗаписиРегистра.МоментВремени        = МоментВремени;
	МенеджерЗаписиРегистра.Период               = Период;		
	МенеджерЗаписиРегистра.ИдентификаторОбъекта = ИдентификаторОбъекта;
	МенеджерЗаписиРегистра.ОписаниеСобытия      = ОписаниеСобытия;
	МенеджерЗаписиРегистра.СКлиента             = Истина;
	МенеджерЗаписиРегистра.ТипОбъекта           = ТипОбъекта;
	МенеджерЗаписиРегистра.ТипСобытия           = ТипСобытия;
	
	МенеджерЗаписиРегистра.Записать();
	
КонецПроцедуры

// Добавляет запись об ошибке, связанную с объектом данных.
// Параметры:
//	Сообщение - описание ошибки;
//	ТипОбъекта - имя типа объекта;
//	ИдентификаторОбъекта - уникальный идентификатор объекта;
//	СКлиента - флаг, показывающий, где произошло событие: на мобильном клиенте или в центральной ИБ;
//	МобильныйКлиент - ссылка на узел плана обмена МобильноеПриложение.
Процедура ДобавитьОшибкуПоОбъекту(Знач Сообщение,
								  Знач ТипОбъекта, 
								  Знач ИдентификаторОбъекта, 
								  Знач СКлиента, 
								  Знач МобильныйКлиент) Экспорт
	
	ДобавитьЗаписьВЖурнал(Перечисления.ТипыСобытийЖурналаРаботыСМобильнымиКлиентами.Ошибка,
							Сообщение,
							ТипОбъекта,
							ИдентификаторОбъекта,
							СКлиента,
							МобильныйКлиент);
	
КонецПроцедуры

// Добавляет информационную запись о событии, связанным с объектом данных.
// Параметры:
//	Сообщение - описание события;
//	ТипОбъекта - имя типа объекта;
//	ИдентификаторОбъекта - уникальный идентификатор объекта;
//	СКлиента - флаг, показывающий, где произошло событие: на мобильном клиенте или в центральной ИБ;
//	МобильныйКлиент - ссылка на узел плана обмена МобильноеПриложение. 
Процедура ДобавитьИнформациюПоОбъекту(Знач Сообщение, 
									  Знач ТипОбъекта,
									  Знач ИдентификаторОбъекта,
									  Знач СКлиента,
									  Знач МобильныйКлиент) Экспорт
	
	ДобавитьЗаписьВЖурнал(Перечисления.ТипыСобытийЖурналаРаботыСМобильнымиКлиентами.Информация,
							Сообщение,
							ТипОбъекта,
							ИдентификаторОбъекта,
							СКлиента,
							МобильныйКлиент);
	
КонецПроцедуры

// Добавляет информационную запись о событии.
// Параметры:
//	Сообщение - описание события;
//	СКлиента - флаг, показывающий, где произошло событие: на мобильном клиенте или в центральной ИБ;
//	МобильныйКлиент - ссылка на узел плана обмена МобильноеПриложение. 
Процедура ДобавитьИнформацию(Знач Сообщение, 
							 Знач СКлиента,
							 Знач МобильныйКлиент) Экспорт
	
	ДобавитьЗаписьВЖурнал(Перечисления.ТипыСобытийЖурналаРаботыСМобильнымиКлиентами.Информация,
							Сообщение,
							,
							,
							СКлиента,
							МобильныйКлиент);
	
КонецПроцедуры

// Добавляет запись об ошибке.
// Параметры:
//	Сообщение - описание ошибки;
//	СКлиента - флаг, показывающий, где произошло событие: на мобильном клиенте или в центральной ИБ;
//	МобильныйКлиент - ссылка на узел плана обмена МобильноеПриложение. 
Процедура ДобавитьОшибку(Знач Сообщение, 
						 Знач СКлиента,
						 Знач МобильныйКлиент) Экспорт
	
	ДобавитьЗаписьВЖурнал(Перечисления.ТипыСобытийЖурналаРаботыСМобильнымиКлиентами.Ошибка,
							Сообщение,
							,
							,
							СКлиента,
							МобильныйКлиент);
	
КонецПроцедуры

// Добавляет предупреждение.
// Параметры:
//	Сообщение - описание предупреждения;
//	СКлиента - флаг, показывающий, где произошло событие: на мобильном клиенте или в центральной ИБ;
//	МобильныйКлиент - ссылка на узел плана обмена МобильноеПриложение. 
Процедура ДобавитьПредупреждение(Знач Сообщение, 
								 Знач СКлиента,
								 Знач МобильныйКлиент) Экспорт
	
	 ДобавитьЗаписьВЖурнал(Перечисления.ТипыСобытийЖурналаРаботыСМобильнымиКлиентами.Предупреждение,
							Сообщение,
							,
							,
							СКлиента,
							МобильныйКлиент);
	
КонецПроцедуры

// Добавляет запись с сообщением о проблеме, поступившим от пользователя.
// Параметры:
//	Сообщение - описание предупреждения;
//	СКлиента - флаг, показывающий, где произошло событие: на мобильном клиенте или в центральной ИБ;
//	МобильныйКлиент - ссылка на узел плана обмена МобильноеПриложение. 
Процедура ДобавитьСообщениеОПроблемеОтПользователя(Знач Сообщение, 
												   Знач СКлиента,
												   Знач МобильныйКлиент) Экспорт
	
	 ДобавитьЗаписьВЖурнал(Перечисления.ТипыСобытийЖурналаРаботыСМобильнымиКлиентами.СообщениеОПроблемеОтПользователя,
							Сообщение,
							,
							,
							СКлиента,
							МобильныйКлиент);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ
Процедура ДобавитьЗаписьВЖурнал(ТипСобытия = Неопределено, 
								Знач Описание,
								ТипОбъекта = "",
								ИдентификаторОбъекта = Неопределено,
								Знач СКлиента, 
								Знач МобильныйКлиент)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если ТипСобытия = Неопределено Тогда
		ТипСобытия = Перечисления.ТипыСобытийЖурналаРаботыСМобильнымиКлиентами.Информация;
	КонецЕсли;
	
	Если ИдентификаторОбъекта = Неопределено Тогда
		ИдентификаторОбъекта = Новый УникальныйИдентификатор("00000000-0000-0000-0000-000000000000");
	КонецЕсли;
	
	МенеджерЗаписиРегистра = РегистрыСведений.ЖурналРаботыСМобильнымиКлиентами.СоздатьМенеджерЗаписи();
	МенеджерЗаписиРегистра.МобильныйКлиент      = МобильныйКлиент;
	МенеджерЗаписиРегистра.МоментВремени        = ТекущаяУниверсальнаяДатаВМиллисекундах();
	МенеджерЗаписиРегистра.Период               = ТекущаяДатаСеанса();		
	МенеджерЗаписиРегистра.ИдентификаторОбъекта = ИдентификаторОбъекта;
	МенеджерЗаписиРегистра.ОписаниеСобытия      = Описание;
	МенеджерЗаписиРегистра.СКлиента             = СКлиента;
	МенеджерЗаписиРегистра.ТипОбъекта           = ТипОбъекта;
	МенеджерЗаписиРегистра.ТипСобытия           = ТипСобытия;
	
	МенеджерЗаписиРегистра.Записать();
	
КонецПроцедуры
