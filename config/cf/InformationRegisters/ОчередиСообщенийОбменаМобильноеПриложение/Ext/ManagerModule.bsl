
#Область ПрограммныйИнтерфейс

Процедура ПоместитьСообщениеВОчередь(Знач МобильныйКлиент, Сообщение) Экспорт
	
	МенеджерЗаписиОчередей = РегистрыСведений.ОчередиСообщенийОбменаМобильноеПриложение.СоздатьМенеджерЗаписи();
	МенеджерЗаписиОчередей.МобильныйКлиент = МобильныйКлиент;
	МенеджерЗаписиОчередей.Прочитать();
	
	Если МенеджерЗаписиОчередей.Выбран() Тогда
		Сообщение.Очередь = МенеджерЗаписиОчередей.Очередь;	
	Иначе		
		МенеджерЗаписиОчередей.МобильныйКлиент = МобильныйКлиент;		
		НоваяОчередь                   = Справочники.ОчередьСообщенийОбменаМобильноеПриложение.СоздатьЭлемент();
		НоваяОчередь.Наименование      = Строка(МенеджерЗаписиОчередей.МобильныйКлиент.Пользователь);
		НоваяОчередь.Записать();		
		МенеджерЗаписиОчередей.Очередь = НоваяОчередь.Ссылка;
		МенеджерЗаписиОчередей.Записать();		
		Сообщение.Очередь              = НоваяОчередь.Ссылка;	
	КонецЕсли;
	
	Сообщение.Записать();
	
КонецПроцедуры

Функция ПолучитьКоличествоСообщенийВОчереди(Знач МобильныйКлиент) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	КОЛИЧЕСТВО(СообщенияОбменаМобильноеПриложение.Ссылка) КАК Колво
		|ИЗ
		|	Справочник.СообщенияОбменаМобильноеПриложение КАК СообщенияОбменаМобильноеПриложение
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ОчередиСообщенийОбменаМобильноеПриложение КАК ОчередиСообщенийОбменаМобильноеПриложение
		|		ПО СообщенияОбменаМобильноеПриложение.Очередь = ОчередиСообщенийОбменаМобильноеПриложение.Очередь
		|ГДЕ
		|	ОчередиСообщенийОбменаМобильноеПриложение.МобильныйКлиент = &МобильныйКлиент
		|	И СообщенияОбменаМобильноеПриложение.ПометкаУдаления = ЛОЖЬ";
	Запрос.УстановитьПараметр("МобильныйКлиент", МобильныйКлиент);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Колво;
	Иначе
		Возврат 0;
	КонецЕсли;
			
КонецФункции

#КонецОбласти