////////////////////////////////////////////////////////////////////////////////
// База знаний клиент/сервер.
//  
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

Функция ЭтоСтрокаКартинки(Знач СтрокаПроверки) Экспорт
	
	СтрокаПроверки = СокрЛП(СтрокаПроверки);
	Возврат НРег(Лев(СтрокаПроверки, 10)) = НРег("[picture='");
	
КонецФункции

Функция ПолучитьИмяКартинки(Знач СтрокаКартинки) Экспорт
	
	ИмяКартинки = "";
	
	Если ЭтоСтрокаКартинки(СтрокаКартинки) Тогда
		
		СтрокаКартинки	= СокрЛП(СтрокаКартинки);
		ЧастьИмени		= Сред(СтрокаКартинки, 11);
		Окончание		= СтрНайти(ЧастьИмени, "'");
		
		Если Окончание > 0 Тогда
			ИмяКартинки = Сред(ЧастьИмени, 1, Окончание - 1);
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ИмяКартинки;
	
КонецФункции

Функция ЭтоСтрокаТаблицы(Знач СтрокаПроверки) Экспорт 
	
	СтрокаПроверки = СокрЛП(СтрокаПроверки);
	Возврат НРег(Лев(СтрокаПроверки, 8)) = НРег("[table='");
	
КонецФункции

Функция ПолучитьИмяТаблицы(Знач СтрокаТаблицы) Экспорт
	
	ИмяТаблицы = "";
	
	Если ЭтоСтрокаТаблицы(СтрокаТаблицы) Тогда
		
		СтрокаТаблицы	= СокрЛП(СтрокаТаблицы);
		ЧастьИмени		= Сред(СтрокаТаблицы, 9);
		Окончание		= СтрНайти(ЧастьИмени, "'");
		
		Если Окончание > 0 Тогда
			ИмяТаблицы = Сред(ЧастьИмени, 1, Окончание - 1);
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ИмяТаблицы;
	
КонецФункции

Функция ЭтоСтрокаПодсветкиКода(Знач СтрокаПроверки) Экспорт 
	
	СтрокаПроверки = СокрЛП(СтрокаПроверки);
	Возврат НРег(Лев(СтрокаПроверки, 7)) = НРег("[code='");
	
КонецФункции

Функция ПолучитьИмяПодсветкиКода(Знач СтрокаПроверки) Экспорт
	
	ИмяТаблицы = "";
	
	Если ЭтоСтрокаПодсветкиКода(СтрокаПроверки) Тогда
		
		СтрокаПроверки	= СокрЛП(СтрокаПроверки);
		ЧастьИмени		= Сред(СтрокаПроверки, 8);
		Окончание		= СтрНайти(ЧастьИмени, "'");
		
		Если Окончание > 0 Тогда
			ИмяТаблицы = Сред(ЧастьИмени, 1, Окончание - 1);
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ИмяТаблицы;
	
КонецФункции

Функция ПроверитьПравильностьЗаполненияИмени(Знач Имя) Экспорт
	
	ПерваяЦифра = СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(Лев(Имя, 1), Истина, Ложь);
	
	ПроверкаПройдена = Истина;
	Если НЕ ПерваяЦифра Тогда
		ДопустимыеСимволы	= БазаЗнанийКлиентСерверПовтИсп.ПолучитьДопустимыеСимволыИмени();
		ДлинаСтроки			= СтрДлина(Имя);
		Для Индекс = 1 По ДлинаСтроки Цикл
			СимволПроверки = Сред(Имя, Индекс, 1);
			Если СтрНайти(ДопустимыеСимволы, НРег(СимволПроверки)) = 0 Тогда
				ПроверкаПройдена = Ложь;
				Прервать;
			КонецЕсли;
		КонецЦикла;
	Иначе 
		ПроверкаПройдена = Ложь;
	КонецЕсли;
	
	Возврат ПроверкаПройдена;			
	
КонецФункции

Функция ОбработатьТекстСтатьи(Знач ТекстСтатьи) Экспорт
	
	// Необходимо удалить все [picture='...'].
	ПозицияСимвола = СтрНайти(ТекстСтатьи, "[picture='");
	Пока ПозицияСимвола > 0 Цикл
		НачСтроки = Лев(ТекстСтатьи, ПозицияСимвола - 1);
		КонСтроки = Сред(ТекстСтатьи, ПозицияСимвола + 7);
		
		КонПозиция	= СтрНайти(КонСтроки, "']");
		ТекстСтатьи = НачСтроки + Сред(КонСтроки, КонПозиция + 2);
		
		ПозицияСимвола = СтрНайти(ТекстСтатьи, "[picture='");
	КонецЦикла;
	
	// Необходимо удалить все [table='...'].
	ПозицияСимвола = СтрНайти(ТекстСтатьи, "[table='");
	Пока ПозицияСимвола > 0 Цикл
		НачСтроки = Лев(ТекстСтатьи, ПозицияСимвола - 1);
		КонСтроки = Сред(ТекстСтатьи, ПозицияСимвола + 7);
		
		КонПозиция	= СтрНайти(КонСтроки, "']");
		ТекстСтатьи = НачСтроки + Сред(КонСтроки, КонПозиция + 2);
		
		ПозицияСимвола = СтрНайти(ТекстСтатьи, "[table='");
	КонецЦикла;
	               
	// Необходимо удалить все [fileid='...'].
	ПозицияСимвола = СтрНайти(ТекстСтатьи, "[fileid='");
	Пока ПозицияСимвола > 0 Цикл
		НачСтроки = Лев(ТекстСтатьи, ПозицияСимвола - 1);
		КонСтроки = Сред(ТекстСтатьи, ПозицияСимвола + 7);
		
		КонПозиция	= СтрНайти(КонСтроки, "']");
		ТекстСтатьи = НачСтроки + Сред(КонСтроки, КонПозиция + 2);
		
		ПозицияСимвола = СтрНайти(ТекстСтатьи, "[fileid='");
	КонецЦикла;
	
	// Необходимо удалить все [pict_...].
	ПозицияСимвола = СтрНайти(ТекстСтатьи, "[pict_");
	Пока ПозицияСимвола > 0 Цикл
		НачСтроки = Лев(ТекстСтатьи, ПозицияСимвола - 1);
		КонСтроки = Сред(ТекстСтатьи, ПозицияСимвола + 7);
		
		КонПозиция	= СтрНайти(КонСтроки, "]");
		ТекстСтатьи = НачСтроки + Сред(КонСтроки, КонПозиция + 2);
		
		ПозицияСимвола = СтрНайти(ТекстСтатьи, "[pict_");
	КонецЦикла;
	
	// Необходимо удалить все [code='...'].
	ПозицияСимвола = СтрНайти(ТекстСтатьи, "[code='");
	Пока ПозицияСимвола > 0 Цикл
		НачСтроки = Лев(ТекстСтатьи, ПозицияСимвола - 1);
		КонСтроки = Сред(ТекстСтатьи, ПозицияСимвола + 7);
		
		КонПозиция	= СтрНайти(КонСтроки, "']");
		ТекстСтатьи = НачСтроки + Сред(КонСтроки, КонПозиция + 2);
		
		ПозицияСимвола = СтрНайти(ТекстСтатьи, "[code='");
	КонецЦикла;	
	
	Возврат ТекстСтатьи;
	
КонецФункции

// Типы ссылок см. в форме "Справочник.СтатьиБазыЗнаний.Формы.РедактированиеГиперссылки"
//
Функция ПолучитьСтруктуруСсылкиПоАдресу(Знач АдресСсылки) Экспорт
	
	// kb://api/ - это внутренние ссылки
	// mailto: - это отправка письма на эл. почту
	// http://www.google.ru/search? - это поиск в гугл
	// все остальное обычные гиперссылки
	
	СтруктураСсылки = Новый Структура("Тип, Адрес", 0, "");
	
	Если Лев(АдресСсылки, 7) = "mailto:" Тогда
		СтруктураСсылки.Тип		= 2;
		СтруктураСсылки.Адрес	= Сред(АдресСсылки, 8);
	ИначеЕсли Лев(АдресСсылки, 4) = "tel:" Тогда
		СтруктураСсылки.Тип		= 6;
		СтруктураСсылки.Адрес	= Сред(АдресСсылки, 5);
	ИначеЕсли Лев(АдресСсылки, 9) = "kb://api/" Тогда
		ДанныеСсылки = БазаЗнанийAPIКлиентСервер.РазобратьАдресСсылки(АдресСсылки);
		
		ИмяКоманды			= ?(ДанныеСсылки.Свойство("Команда"), ДанныеСсылки.Команда, "");
		ПараметрыКоманды	= ?(ДанныеСсылки.Свойство("Параметры"), ДанныеСсылки.Параметры, Новый Структура);
		Если ИмяКоманды = "page" Тогда
			ИмяСтраницы = ?(ПараметрыКоманды.Свойство("name"), ПараметрыКоманды.name, "");
			Если ИмяСтраницы = "article" Тогда
				ИДСтатьи	= ?(ПараметрыКоманды.Свойство("id"), ПараметрыКоманды.id, "");
				ИДРаздела	= ?(ПараметрыКоманды.Свойство("section"), ПараметрыКоманды.section, "");
				
				СтруктураСсылки.Тип		= 0;
				СтруктураСсылки.Адрес	= ИДСтатьи + ?(ПустаяСтрока(ИДРаздела), "", "#") + ИДРаздела;
			ИначеЕсли ИмяСтраницы = "category" Тогда
				ИДКатегории	= ?(ПараметрыКоманды.Свойство("id"), ПараметрыКоманды.id, "");
				
				СтруктураСсылки.Тип		= 3;
				СтруктураСсылки.Адрес	= ИДКатегории;
			КонецЕсли;
		ИначеЕсли ИмяКоманды = "search" Тогда
//			ТипПоиска	= ?(ПараметрыКоманды.Свойство("type"), ПараметрыКоманды.type, "");
			ЗначПоиска	= ?(ПараметрыКоманды.Свойство("text"), ПараметрыКоманды.text, "");
			
			СтруктураСсылки.Тип		= 4;
			СтруктураСсылки.Адрес	= ЗначПоиска;
		КонецЕсли;
	ИначеЕсли Лев(АдресСсылки, 28) = "http://www.google.ru/search?" Тогда
		ДанныеСсылки = БазаЗнанийAPIКлиентСервер.РазобратьАдресСсылки(АдресСсылки);
		
		ИмяКоманды			= ?(ДанныеСсылки.Свойство("Команда"), ДанныеСсылки.Команда, "");
		ПараметрыКоманды	= ?(ДанныеСсылки.Свойство("Параметры"), ДанныеСсылки.Параметры, Новый Структура);
		ЗначениеПоиска		= ?(ПараметрыКоманды.Свойство("q"), ПараметрыКоманды.q, "");
		
		СтруктураСсылки.Тип		= 5;
		СтруктураСсылки.Адрес	= ЗначениеПоиска;
	Иначе
		СтруктураСсылки.Тип		= 1;
		СтруктураСсылки.Адрес	= АдресСсылки;
	КонецЕсли;
		
	Возврат СтруктураСсылки;
	
КонецФункции

Функция ПолучитьАдресСсылкиПоСтруктуре(Знач СтруктураСсылки) Экспорт
	
	Если СтруктураСсылки.Тип = 0 Тогда
		Разделитель = СтрНайти(СтруктураСсылки.Адрес, "#");
		Если Разделитель > 0 Тогда
			ИДСтатья = Лев(СтруктураСсылки.Адрес, Разделитель - 1);
			ИДРаздел = Сред(СтруктураСсылки.Адрес, Разделитель + 1);
		Иначе 
			ИДСтатья = СтруктураСсылки.Адрес;
			ИДРаздел = "";
		КонецЕсли;
		
		Параметры	= Новый Структура;
		Параметры.Вставить("id", ИДСтатья);
		Если НЕ ПустаяСтрока(ИДРаздел) Тогда
			Параметры.Вставить("section", ИДРаздел);
		КонецЕсли;
		
		АдресСсылки = БазаЗнанийAPIКлиентСервер.КонструкторСсылки_page("article", Параметры);
	ИначеЕсли СтруктураСсылки.Тип = 1 Тогда
		АдресСсылки = СтруктураСсылки.Адрес;
	ИначеЕсли СтруктураСсылки.Тип = 2 Тогда
		АдресСсылки = СтруктураСсылки.Адрес;
	ИначеЕсли СтруктураСсылки.Тип = 3 Тогда
		ИДСтатья = СтруктураСсылки.Адрес;
		
		Параметры	= Новый Структура;
		Параметры.Вставить("id", ИДСтатья);
		
		АдресСсылки = БазаЗнанийAPIКлиентСервер.КонструкторСсылки_page("category", Параметры);
	ИначеЕсли СтруктураСсылки.Тип = 4 Тогда
		Параметры	= Новый Структура;
		Параметры.Вставить("text", СтруктураСсылки.Адрес);
		
		АдресСсылки = БазаЗнанийAPIКлиентСервер.КонструкторСсылки_search("text", Параметры);
	ИначеЕсли СтруктураСсылки.Тип = 5 Тогда
		СтрокаПоиска	= БазаЗнанийВызовСервера.ЗакодироватьСтроку(СтруктураСсылки.Адрес);
		АдресСсылки		= "http://www.google.ru/search?sourceid=chrome&ie=UTF-8&q=" + СтрокаПоиска;
	ИначеЕсли СтруктураСсылки.Тип = 6 Тогда
		АдресСсылки		= СтруктураСсылки.Адрес;
	Иначе 
		АдресСсылки = "";
	КонецЕсли;
	
	Возврат АдресСсылки;
	
КонецФункции

Функция РазобратьСтрокуСортировки(Знач СтрокаСортировки) Экспорт
	
	МассивСортировки = Новый Массив;
	
	МассивПолей = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(СтрокаСортировки, ",");
	Для Каждого СтрокаПоля Из МассивПолей Цикл
		СтрокаПоля	= СокрЛП(СтрокаПоля);
		Разделитель	= СтрНайти(СтрокаПоля, " ");
		
		Если Разделитель = 0 Тогда
			Поле		= СтрокаПоля;
			Направление	= "ВОЗР";
		Иначе 
			Поле		= СокрЛП(Лев(СтрокаПоля, Разделитель - 1));
			Направление	= ВРег(СокрЛП(Сред(СтрокаПоля, Разделитель + 1)));
		КонецЕсли;
		
		НаправлениеСорт	= ?(Направление = "УБЫВ", НаправлениеСортировки.Убыв, НаправлениеСортировки.Возр);
		СтруктураПоля	= Новый Структура("Поле, Направление", Поле, НаправлениеСорт);
		МассивСортировки.Добавить(СтруктураПоля);
	КонецЦикла;
	
	Возврат МассивСортировки;
	
КонецФункции

Функция ДобавитьГруппуОтбора(Коллекция, Знач ТипГруппы = Неопределено, Знач Представление = "") Экспорт
	
	Если НЕ ЗначениеЗаполнено(ТипГруппы) Тогда
		ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ;
	КонецЕсли;
	
	СтруктураГруппа = Новый Структура("ЭтоГруппа, ТипГруппы, Представление, Использование, Элементы",
		Истина,
		ТипГруппы,
		Представление,
		Истина,
		Новый Массив);
	
	Коллекция.Добавить(СтруктураГруппа);
	
	Возврат СтруктураГруппа;
	
КонецФункции

Функция ДобавитьЭлементОтбора(Коллекция, Знач Поле, Знач Значение, Знач ПарамВидСравнения = Неопределено) Экспорт
	
	Если НЕ ЗначениеЗаполнено(ПарамВидСравнения) Тогда
		ПарамВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	КонецЕсли;
	
	ЭлементОтбора = Новый Структура;
	ЭлементОтбора.Вставить("Поле"			, Поле);
	ЭлементОтбора.Вставить("Значение"		, Значение);
	ЭлементОтбора.Вставить("ВидСравнения"	, ПарамВидСравнения);
	
	Коллекция.Добавить(ЭлементОтбора);
	
	Возврат ЭлементОтбора;
	
КонецФункции

Функция ПолучитьИДФайла(Знач СтрокаФайла) Экспорт
	
	ИДФайла = "";
	
	Если ЭтоСтрокаФайла(СтрокаФайла) Тогда		
		СтрокаФайла = СокрЛП(СтрокаФайла);		
		ИДФайла	    = Сред(СтрокаФайла, СтрНайти(СтрокаФайла, НРег("[fileid='")) + 10, 32);		
	КонецЕсли;
	
	Возврат ИДФайла;
	
КонецФункции

Функция ЭтоСтрокаФайла(Знач СтрокаПроверки) Экспорт
	
	СтрокаПроверки = СокрЛП(СтрокаПроверки);
	Возврат СтрНайти(СтрокаПроверки, НРег("[fileid='")) > 0;
	
КонецФункции
	
#КонецОбласти