
#Область ПрограммныйИнтерфейс

Функция МассивПрограммУдаленногоУправленияОбъекта(Знач ОбъектУправления) Экспорт
		
	УстановитьПривилегированныйРежим(Истина);
	
	Результат = Новый Массив;
	Если ТипЗнч(ОбъектУправления) = Тип("СтрокаГруппировкиДинамическогоСписка") Тогда
		Возврат Результат;
	КонецЕсли;
	
	Запрос = Новый Запрос();
	Запрос.Текст =
		"ВЫБРАТЬ
		|	УдаленноеУправление.Наименование КАК Наименование,
		|	УдаленноеУправление.Код КАК Код,
		|	УдаленноеУправление.Ссылка КАК Ссылка,
		|	УдаленноеУправление.ТипПодключения КАК ТипПодключения
		|ИЗ
		|	Справочник.УдаленноеУправление КАК УдаленноеУправление
		|ГДЕ
		|	УдаленноеУправление.Владелец = &ОбъектУправления
		|	И УдаленноеУправление.ЭтоГруппа = ЛОЖЬ
		|
		|УПОРЯДОЧИТЬ ПО
		|	Наименование";
	
	Запрос.УстановитьПараметр("ОбъектУправления", ОбъектУправления);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Структура = Новый Структура("Наименование,Код,Ссылка,ТипПодключения");
		ЗаполнитьЗначенияСвойств(Структура, Выборка);
		Структура.Вставить("ИмяКартинки", ПредставлениеПеречисления(Выборка.ТипПодключения));
		Результат.Добавить(Структура);
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Функция ИспользуетсяУдаленноеУправление() Экспорт
	
	Если Не ПравоДоступа("Чтение", Метаданные.Справочники.УдаленноеУправление) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если УправлениеITОтделом8УФПовтИсп.ПолучитьКонстанту("ИспользоватьУдаленноеУправление") = Ложь Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

Функция ПредставлениеПеречисления(Знач ТипПеречисления) Экспорт
	
	Возврат Метаданные.Перечисления.ТипыПодключений.ЗначенияПеречисления.Получить(Перечисления.ТипыПодключений.Индекс(ТипПеречисления)).Имя;
	
КонецФункции

Функция НастройкаУдаленногоУправления(Знач Объект) Экспорт
	
	Настройка = Новый Структура();	
	Для Каждого Реквизит Из Метаданные.Справочники.УдаленноеУправление.Реквизиты Цикл
		Настройка.Вставить(Реквизит.Имя,	Объект[Реквизит.Имя]);
	КонецЦикла;
	Настройка.Вставить("Объект", 			Объект);
	
	Возврат Настройка;
	
КонецФункции

#КонецОбласти