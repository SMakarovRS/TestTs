///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2019, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Вызывается для получения настроек подсистемы.
//
// Параметры:
//  Настройки - Структура - со свойствами:
//   * Реквизиты - Соответствие - для переопределения имен реквизитов объекта, в которых содержится информация 
//                                о сумме и валюте, выводимых в списке связанных документов. 
//                                В ключе указывается полное имя объекта метаданных, в значении - соответствие 
//                                реквизитов Валюта и СуммаДокумента с реальными реквизитами объекта. 
//                                Если не задано, то значения зачитываются из реквизитов Валюта и СуммаДокумента.
//   * РеквизитыДляПредставления - Соответствие - для переопределения представления объектов, выводимых в списке 
//                                связанных документов. В ключе указывается полное имя объекта метаданных, в значении -
//                                массив имен реквизитов, значения которых участвуют в формировании представления.
//                                Для формирования представления перечисленных здесь объектов будет вызываться 
//                                процедура СтруктураПодчиненностиПереопределяемый.ПриПолученииПредставления.
//
// Пример:
//	Реквизиты = Новый Соответствие;
//	Реквизиты.Вставить("СуммаДокумента", Метаданные.Документы.СчетНаОплатуПокупателю.Реквизиты.СуммаОплаты.Имя);
//	Реквизиты.Вставить("Валюта", Метаданные.Документы.СчетНаОплатуПокупателю.Реквизиты.ВалютаДокумента.Имя);
//	Настройки.Реквизиты.Вставить(Метаданные.Документы.СчетНаОплатуПокупателю.ПолноеИмя(), Реквизиты);
//		
//	РеквизитыДляПредставления = Новый Массив;
//	РеквизитыДляПредставления.Добавить(Метаданные.Документы.ЭлектронноеПисьмоИсходящее.Реквизиты.ДатаОтправления.Имя);
//	РеквизитыДляПредставления.Добавить(Метаданные.Документы.ЭлектронноеПисьмоИсходящее.Реквизиты.Тема.Имя);
//	РеквизитыДляПредставления.Добавить(Метаданные.Документы.ЭлектронноеПисьмоИсходящее.Реквизиты.СписокПолучателейПисьма.Имя);
//	Настройки.РеквизитыДляПредставления.Вставить(Метаданные.Документы.ЭлектронноеПисьмоИсходящее.ПолноеИмя(), 
//		РеквизитыДляПредставления);
//
Процедура ПриОпределенииНастроек(Настройки) Экспорт
	
	//<< УИТ	
	Реквизиты = Новый Соответствие;
	Реквизиты.Вставить("СуммаДокумента", Метаданные.Документы.ЗаказПоставщику.Реквизиты.СуммаДокумента.Имя);
	Реквизиты.Вставить("Валюта", Метаданные.Документы.ЗаказПоставщику.Реквизиты.ВалютаДокумента.Имя);
	Настройки.Реквизиты.Вставить(Метаданные.Документы.ЗаказПоставщику.ПолноеИмя(), Реквизиты);
	
	РеквизитыДляПредставления = Новый Массив;
	РеквизитыДляПредставления.Добавить(Метаданные.Документы.ЗаказПоставщику.Реквизиты.СостояниеЗаказа.Имя);
	РеквизитыДляПредставления.Добавить(Метаданные.Документы.ЗаказПоставщику.Реквизиты.Закрыт.Имя);
	Настройки.РеквизитыДляПредставления.Вставить(Метаданные.Документы.ЗаказПоставщику.ПолноеИмя(), РеквизитыДляПредставления);
	
	РеквизитыДляПредставления = Новый Массив;
	РеквизитыДляПредставления.Добавить(Метаданные.Документы.Задание.Реквизиты.ТекущийЭтап.Имя);
	РеквизитыДляПредставления.Добавить(Метаданные.Документы.Задание.Реквизиты.ТекущийИсполнитель.Имя);
	Настройки.РеквизитыДляПредставления.Вставить(Метаданные.Документы.Задание.ПолноеИмя(), РеквизитыДляПредставления);
	
	РеквизитыДляПредставления = Новый Массив;
	РеквизитыДляПредставления.Добавить(Метаданные.Документы.Проблема.Реквизиты.Статус.Имя);
	Настройки.РеквизитыДляПредставления.Вставить(Метаданные.Документы.Проблема.ПолноеИмя(), РеквизитыДляПредставления);
	//>>	
	
КонецПроцедуры

// Вызывается для получения представления объектов, выводимых в списке связанных документов.
// Только для тех объектов, которые перечислены в свойстве РеквизитыДляПредставления параметра Настройки
// процедуры СтруктураПодчиненностиПереопределяемый.ПриОпределенииНастроек.
//
// Параметры:
//  ТипДанных - ЛюбаяСсылка - тип ссылки выводимого объекта, см. свойство Тип критерия отбора СвязанныеДокументы.
//  Данные    - ВыборкаИзРезультатаЗапроса, Структура - содержит значения полей, из которых формируется представление:
//               * Ссылка - ЛюбаяСсылка - ссылка объекта, выводимого в списке связанных документов.
//               * ДополнительныйРеквизит1 - Произвольный - значение первого реквизита, указанного в массиве 
//                 РеквизитыДляПредставления параметра Настройки процедуры ПриОпределенииНастроек.
//               * ДополнительныйРеквизит2 - Произвольный - значение второго реквизита...
//               ...
//  Представление - Строка - поместить в этот параметр рассчитанное представление объекта. 
//  СтандартнаяОбработка - Булево - поместить в этот параметр Ложь, если установлено значение параметра Представление.
//
Процедура ПриПолученииПредставления(ТипДанных, Данные, Представление, СтандартнаяОбработка) Экспорт
	
	//<< УИТ
	Если ТипДанных = Тип("ДокументСсылка.ЗаказПоставщику") Тогда
		
		СтандартнаяОбработка = Ложь;
		Представление = СтрШаблон(НСтр("ru = '%1 (%2%3) на сумму %4 %5'"), Данные.Представление,
		                                Данные.ДополнительныйРеквизит1,
		                                ?(Данные.ДополнительныйРеквизит2 = Истина, " Закрыт", ""),
										Данные.СуммаДокумента,
										Данные.Валюта);
		
	ИначеЕсли ТипДанных = Тип("ДокументСсылка.Задание") Тогда
		
		СтандартнаяОбработка = Ложь;
		Представление = СтрШаблон(НСтр("ru = '%1 (Этап: %2; Исполнитель: %3)'"), Данные.Представление,
		                                ?(ЗначениеЗаполнено(Данные.ДополнительныйРеквизит1), Строка(Данные.ДополнительныйРеквизит1), "<нет>"),
		                                ?(ЗначениеЗаполнено(Данные.ДополнительныйРеквизит2), Строка(Данные.ДополнительныйРеквизит2), "<нет>"));
		
	ИначеЕсли ТипДанных = Тип("ДокументСсылка.Проблема") Тогда
		
		СтандартнаяОбработка = Ложь;
		Представление = СтрШаблон(НСтр("ru = '%1 (Статус: %2)'"), Данные.Представление,
		                                ?(ЗначениеЗаполнено(Данные.ДополнительныйРеквизит1), Строка(Данные.ДополнительныйРеквизит1), "<нет>"));
		
	КонецЕсли;	
	//>>	
	
КонецПроцедуры	
	
#Область УстаревшиеПроцедурыИФункции

// Устарела. Следует использовать СтруктураПодчиненностиПереопределяемый.ПриОпределенииНастроек.
// См. свойство РеквизитыДляПредставления параметра Настройки.
// Формирует массив реквизитов документа. 
// 
// Параметры: 
//  ИмяДокумента - Строка - имя документа.
//
// Возвращаемое значение:
//   Массив - массив наименований реквизитов документа. 
//
Функция МассивРеквизитовОбъектаДляФормированияПредставления(ИмяДокумента) Экспорт
	
	Возврат Новый Массив;
	
КонецФункции

// Устарела. Следует использовать СтруктураПодчиненностиПереопределяемый.ПриПолученииПредставления.
// Получает представление документа для печати.
//
// Параметры:
//  Выборка  - КоллекцияДанных - структура или выборка из результатов запроса
//                 в которой содержатся дополнительные реквизиты, на основании
//                 которых можно сформировать переопределенное представление 
//                 документа для вывода в отчет "Структура подчиненности".
//
// Возвращаемое значение:
//   Строка,Неопределено   - переопределенное представление документа, или Неопределено,
//                           если для данного типа документов такое не задано.
//
Функция ПредставлениеОбъектаДляВыводаВОтчет(Выборка) Экспорт
	
	Возврат Неопределено;
	
КонецФункции

// Устарела. Следует использовать СтруктураПодчиненностиПереопределяемый.ПриОпределенииНастроек.
// См. свойство Реквизиты параметра Настройки.
// Возвращает имя реквизита документа, в котором содержится информация о Сумме и Валюте документа для вывода в
// структуру подчиненности.
// По умолчанию используются реквизиты Валюта и СуммаДокумента. Если для конкретного документа или конфигурации в целом
// используются другие
// реквизиты, то переопределить значения по умолчанию можно в данной функции.
//
// Параметры:
//  ИмяДокумента  - Строка - имя документа, для которого надо получить имя реквизита.
//  Реквизит      - Строка - строка, может принимать значения "Валюта" и "СуммаДокумента".
//
// Возвращаемое значение:
//   Строка   - имя реквизита документа, в котором содержится информация о Валюте или Сумме.
//
Функция ИмяРеквизитаДокумента(ИмяДокумента, Реквизит) Экспорт
	
	Возврат Неопределено;
	
КонецФункции

#КонецОбласти

#КонецОбласти
