
#Область ПрограммныйИнтерфейс

// Возвращает константу настроек сервисов.
//
// Параметры:
//	ИмяКонстанты - Строка - имя константы в конфигураторе.
//
// Возвращаемое значение:
//	Произольное значение - любое значение.
//
Функция ПолучитьКонстанту(Знач ИмяКонстанты) Экспорт
	
	Возврат Константы[ИмяКонстанты].Получить();
	
КонецФункции

// Находит в справочнике "ЛичныйКабинет" элемент в нужной иерархии папок 
// 	и для данного элемента.
//
// Параметры:
//	Путь - Строка - исходный путь.
//	ИмяФайла - Строка - имя файла.
//
// Возвращаемое значение:
//	СправочникСсылка.ЛичныйКабинет - найденная страница, либо Неопределено.
//
Функция ПолучитьСтраницуОтносительногоURL(Знач Путь, Знач ИмяФайла) Экспорт
	
	Если ПустаяСтрока(ИмяФайла) Тогда
		ИмяФайла = "index.html";
	КонецЕсли;
	
	Массив 				= СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(Путь, "/", Истина, Истина);	
	МассивРодителей 	= Новый Массив();
	ТекстРодители		= "";
	Запрос 				= Новый Запрос();
	
	ТекстЗапроса = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	Страницы.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.ЛичныйКабинет КАК Страницы
		|ГДЕ
		|	Страницы.Наименование = &ИмяФайла
		|	И НЕ Страницы.ПометкаУдаления";
	
	Запрос.УстановитьПараметр("ИмяФайла", ИмяФайла);
	
	Индекс = Массив.Количество() - 1;
	Пока Индекс >= 0 Цикл
		
		Файл = Массив[Индекс];
		
		Если Файл = "." Тогда
			Индекс = Индекс - 1;
			Продолжить;
		ИначеЕсли Файл = ".." Тогда
			Если МассивРодителей.Количество() > 0 Тогда
				МассивРодителей.Удалить(МассивРодителей.Количество()  -1);
			КонецЕсли;
			ТекстРодители = ТекстРодители + ".Родитель";
		КонецЕсли;
		
		ТекстРодители = ТекстРодители + ".Родитель";
		ТекстЗапроса = ТекстЗапроса + Символы.ПС + Символы.Таб 
			+ "И Страницы" + ТекстРодители + ".Наименование=&Файл" + Формат(Индекс, "ЧРД=; ЧРГ=; ЧН=0; ЧГ=");
		Запрос.УстановитьПараметр("Файл" + Формат(Индекс, "ЧРД=; ЧРГ=; ЧН=0; ЧГ="), Файл);
		
		МассивРодителей.Добавить(Файл);
		Индекс = Индекс - 1;
	КонецЦикла;
	
	Если МассивРодителей.Количество() = 0 Тогда
		ТекстЗапроса = ТекстЗапроса + Символы.ПС + Символы.Таб 
			+ "И Страницы.Родитель=&ПустойРодитель";
		Запрос.УстановитьПараметр("ПустойРодитель", Справочники.ЛичныйКабинет.ПустаяСсылка());
	КонецЕсли;
		
	Запрос.Текст = ТекстЗапроса;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Ссылка;
	КонецЕсли;
	
	Возврат Неопределено;

КонецФункции

// Получает данные страницы личного кабинета.
//
// Параметры:
// 	Страница - СправочникСсылка.ЛичныйКабинет - страница личного кабинета.
//
// Возвращаемое значение:
// 	Структура - результат настроек страницы
// 		* ТипФайла - Строка - тип с файлом страницы.
// 		* Алгоритм - Строка - алгоритм для страницы.
// 		* ХранилищеФайла - ХранилищеЗначения - в чем хранится файл.
// 		* Кэширование - Булево - страница кэшируется или нет.
// 		* ВремяКэширования - Число - срок кэширования в секундах.
// 		* НеПроверятьАвторизацию - Булево - проверять авторизацию у страницы или нет.   
//
Функция СтруктураСтраницы(Знач Страница) Экспорт
	
	Возврат HTTPСервисы.СтруктураСтраницы(Страница);
    
КонецФункции

// Возвращает относительный URL страницы личного кабинета.
//
// Параметры:
//	Страница - СправочникСсылка.СтраницыЛичногоКабинета - страница личного кабинета.
//
// Возвращаемое значение:
//	Строка - URL страницы. Пример folder/script.js (страница script.js находится в папке folder).
//
Функция ОтносительныйURLСтраницы(Знач Страница) Экспорт
	
	Результат = "";
	Запрос = Новый Запрос();
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ЛичныйКабинет.Наименование КАК Наименование,
		|	ЕСТЬNULL(ЛичныйКабинет.Родитель.Наименование, """") КАК Родитель,
		|	ЕСТЬNULL(ЛичныйКабинет.Родитель.Родитель.Наименование, """") КАК РодительРодитель,
		|	ЕСТЬNULL(ЛичныйКабинет.Родитель.Родитель.Родитель.Наименование, """") КАК РодительРодительРодитель,
		|	ЕСТЬNULL(ЛичныйКабинет.Родитель.Родитель.Родитель.Родитель.Наименование, """") КАК РодительРодительРодительРодитель,
		|	ЕСТЬNULL(ЛичныйКабинет.Родитель.Родитель.Родитель.Родитель.Родитель.Наименование, """") КАК РодительРодительРодительРодительРодитель
		|ИЗ
		|	Справочник.ЛичныйКабинет КАК ЛичныйКабинет
		|ГДЕ
		|	ЛичныйКабинет.Ссылка = &Страница"; 

	ТекСтраница = Страница;
	Пока Истина Цикл 
	    Запрос.УстановитьПараметр("Страница", ТекСтраница); 
	    РезультатЗапроса = Запрос.Выполнить(); 
	    Если РезультатЗапроса.Пустой() Тогда 
	        Прервать; 
	    КонецЕсли; 
	    Выборка = РезультатЗапроса.Выбрать(); 
	    Выборка.Следующий(); 
		
		Если ПустаяСтрока(Результат) Тогда
			Результат = Выборка.Наименование; 
		КонецЕсли;
		
	    Для НомерКолонки = 1 По РезультатЗапроса.Колонки.Количество() - 1 Цикл 
	        ТекСтраница = Выборка[НомерКолонки]; 
	        Если ТекСтраница = "" Тогда 
	            Прервать; 
	        Иначе 
				Результат = ТекСтраница + "/" + Результат;
	        КонецЕсли; 
	    КонецЦикла; 

	    Если НЕ ЗначениеЗаполнено(ТекСтраница) Тогда 
	        Прервать; 
	    КонецЕсли; 
	КонецЦикла;	
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти