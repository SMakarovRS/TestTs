////////////////////////////////////////////////////////////////////////////////
// Модуль удаленного управления и подключения к клиентским машинам.
//  
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Осуществляет запуск удаленного сеанса.
//
// Параметры:
//	
//
// Возвращаемое значение:
//	
//
Функция ЗапуститьУдаленноеУправление(ОбработчикРезультата, Знач НастройкаПодключения) Экспорт
	
	КомманднаяСтрока = ИтоговаяСтрокаЗапуска(НастройкаПодключения);
	
	Структура = Новый Структура;
	Структура.Вставить("ОбработчикРезультата", ОбработчикРезультата);
	Структура.Вставить("НастройкаПодключения", НастройкаПодключения);	
	Структура.Вставить("КомманднаяСтрока", КомманднаяСтрока);
	НачатьЗапускПриложения(
		Новый ОписаниеОповещения("ВыполнитьЗапускПриложенияЗавершение", УдаленноеУправлениеКлиент, Структура), 
		КомманднаяСтрока,,Ложь);
	
КонецФункции

// Возвращает для настроек подключения строку записка.
//
// Параметры:
//	НастройкаПодключения - Структура - настройки подключения.
//
// Возвращаемое значение:
//	Строка - итоговая строка запуска.
//
Функция ИтоговаяСтрокаЗапуска(Знач НастройкаПодключения) Экспорт
	
	ИсполнимыйФайл 		= "";
	ПараметрыЗапуска 	= "";
	Объект 				= НастройкаПодключения.Объект;	
	СкрыватьПароль 		= НастройкаПодключения.Свойство("СкрыватьПароль");
	Префикс				= "";
	ТекущийПользователь = ПользователиКлиент.ТекущийПользователь();
	
	Если Объект.ТипПодключения = ПредопределенноеЗначение("Перечисление.ТипыПодключений.AnyDesk") Тогда
		
		// https://support.anydesk.com/Command_Line_Interface
		// Получаем локальный путь.
		ИсполнимыйФайл = УправлениеITОтделом8УФПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(ТекущийПользователь,
			"ЛокальныйПутьКAnyDesk");
		Если ПустаяСтрока(ИсполнимыйФайл) Тогда
			ИсполнимыйФайл = "C:\Program Files (x86)\AnyDesk\AnyDesk.exe";
		КонецЕсли;
				
		// Добавляем параметры.		
		ПараметрыЗапуска =
			Объект.АдресПодключения
			+ " " + Объект.ПараметрыЗапуска;		
	
	ИначеЕсли Объект.ТипПодключения = ПредопределенноеЗначение("Перечисление.ТипыПодключений.LiteManager") Тогда
		
		// http://www.litemanager.ru/support/help_ru/command_key/
		// Получаем локальный путь.
		ИсполнимыйФайл = УправлениеITОтделом8УФПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(ТекущийПользователь,
			"ЛокальныйПутьКLiteManager");
		
		Если ПустаяСтрока(ИсполнимыйФайл) Тогда
			ИсполнимыйФайл = "C:\Program Files (x86)\LiteManager Pro - Viewer\ROMViewer.exe";
		КонецЕсли;
		
		// Добавляем параметры.		
		ПараметрыЗапуска =
			?(ПустаяСтрока(Объект.АдресПодключения), "", " /name:" + Объект.АдресПодключения)
			+ ?(ПустаяСтрока(Объект.Логин), "", " /USERNAME:" + Объект.Логин)
			+ ?(Объект.Порт = 0, "", " /port:" + Формат(Объект.Порт, "ЧРД=; ЧРГ=; ЧН=0; ЧГ="))
			+ ?(ПустаяСтрока(Объект.Пароль), "", " /password:" + ?(СкрыватьПароль, "****************", Объект.Пароль))
			+ " " + Объект.ПараметрыЗапуска;
			
	ИначеЕсли Объект.ТипПодключения = ПредопределенноеЗначение("Перечисление.ТипыПодключений.TeamViewer") Тогда
		
		// https://community.teamviewer.com/t5/Knowledge-Base/Are-there-parameters-to-start-TeamViewer/ta-p/4135
		// Получаем локальный путь.
		ИсполнимыйФайл = УправлениеITОтделом8УФПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(ТекущийПользователь,
			"ЛокальныйПутьКTeamViewer");
		Если ПустаяСтрока(ИсполнимыйФайл) Тогда
			ИсполнимыйФайл = "C:\Program Files (x86)\TeamViewer\TeamViewer.exe";
		КонецЕсли;
		
		// Добавляем параметры.		
		ПараметрыЗапуска =
			?(ПустаяСтрока(Объект.АдресПодключения), "", " -i " + Объект.АдресПодключения)
			+ ?(ПустаяСтрока(Объект.Пароль), "", " --Password " + ?(СкрыватьПароль, "****************", Объект.Пароль))
			+ " " + Объект.ПараметрыЗапуска;
			
	ИначеЕсли Объект.ТипПодключения = ПредопределенноеЗначение("Перечисление.ТипыПодключений.RAdmin") Тогда
		
		// Получаем локальный путь.
		ИсполнимыйФайл = УправлениеITОтделом8УФПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(ТекущийПользователь,
			"ЛокальныйПутьКRAdmin");
		Если ПустаяСтрока(ИсполнимыйФайл) Тогда
			ИсполнимыйФайл = "C:\Program Files (x86)\Radmin Viewer 3\Radmin.exe";
		КонецЕсли;		
				
		// Добавляем параметры.		
		ПараметрыЗапуска = 
			?(ПустаяСтрока(Объект.АдресПодключения), "", " /connect:" + Объект.АдресПодключения 
			+ ?(Объект.Порт = 0, "", ":" + Формат(Объект.Порт, "ЧРД=; ЧРГ=; ЧН=0; ЧГ=")))
			+ " " + Объект.ПараметрыЗапуска;
			
	ИначеЕсли Объект.ТипПодключения = ПредопределенноеЗначение("Перечисление.ТипыПодключений.DameWareMiniRemoteControl") Тогда
		
		// Получаем локальный путь.
		ИсполнимыйФайл = УправлениеITОтделом8УФПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(ТекущийПользователь, 
			"ЛокальныйПутьКDameWareMiniRemoteControl");
		Если ПустаяСтрока(ИсполнимыйФайл) Тогда
			ИсполнимыйФайл = "C:\Program Files\SolarWinds\DameWare Mini Remote Control 11.0 x64\DWRCC.exe";
		КонецЕсли;		
				
		// Добавляем параметры.		
		ПараметрыЗапуска = 
			"-c: -h: -a:2"
			+ ?(ПустаяСтрока(Объект.АдресПодключения), "", " -m:" + Объект.АдресПодключения)
			+ ?(Объект.Порт = 0, "", " -o:" + Формат(Объект.Порт, "ЧРД=; ЧРГ=; ЧН=0; ЧГ="))
			+ ?(ПустаяСтрока(Объект.Логин), "", " -u:" + Объект.Логин)
			+ ?(ПустаяСтрока(Объект.Пароль), "", " -p:" + ?(СкрыватьПароль, "****************", Объект.Пароль))
			+ " " + Объект.ПараметрыЗапуска;		
			
	ИначеЕсли Объект.ТипПодключения = ПредопределенноеЗначение("Перечисление.ТипыПодключений.RemoteManipulatorSystem") Тогда
		
		// https://rmansys.ru/forum/6/15/
		// Получаем локальный путь.
		ИсполнимыйФайл = УправлениеITОтделом8УФПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(ТекущийПользователь, 
			"ЛокальныйПутьКRemoteManupulatorSystem");
		Если ПустаяСтрока(ИсполнимыйФайл) Тогда
			ИсполнимыйФайл = "C:\Program Files (x86)\Remote Manipulator System - Viewer\rutview.exe";
		КонецЕсли;		

		ПараметрыЗапуска 	= ""
			+ " /create"
			+ " /name:" + Объект.Логин
			+ " /host:" + Объект.АдресПодключения
			+ ?(ПустаяСтрока(Объект.Пароль), "", " /password:" +  ?(СкрыватьПароль, "****************", Объект.Пароль))
			+ ?(Объект.ПолеБулево1, " /minimize", "")
			+ " " + Объект.ПараметрыЗапуска;
		
	ИначеЕсли Объект.ТипПодключения = ПредопределенноеЗначение("Перечисление.ТипыПодключений.UltraVNC") Тогда
		
		ИсполнимыйФайл = УправлениеITОтделом8УФПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(ТекущийПользователь, 
			"ЛокальныйПутьКUltraVNC");
		Если ПустаяСтрока(ИсполнимыйФайл) Тогда
			ИсполнимыйФайл = "C:\Program Files\uvnc bvba\UltraVNC\vncviewer.exe";
		КонецЕсли;		
		
		ПараметрыЗапуска = ""
			+ ?(ПустаяСтрока(Объект.Логин), "", " -user " + Объект.Логин)
			+ ?(ПустаяСтрока(Объект.Пароль), "", " -password " +  ?(СкрыватьПароль, "****************", Объект.Пароль))
			+ ?(ПустаяСтрока(Объект.АдресПодключения), "", " -server " + Объект.АдресПодключения)
			+ " " + Объект.ПараметрыЗапуска;
			
	ИначеЕсли Объект.ТипПодключения = ПредопределенноеЗначение("Перечисление.ТипыПодключений.AeroAdmin") Тогда
		
		// https://support.aeroadmin.com/kb/faq.php?id=2
		ИсполнимыйФайл = УправлениеITОтделом8УФПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(ТекущийПользователь,
			"ЛокальныйПутьКAeroAdmin");
		Если ПустаяСтрока(ИсполнимыйФайл) Тогда
			ИсполнимыйФайл = "Установите настройку ЛокальныйПутьКAeroAdmin в настройках пользователя";
		КонецЕсли;
		
		ПараметрыЗапуска = "-connect "
			+ СокрЛП(СтрЗаменить(Объект.АдресПодключения, " ", ""))
			+ ?(ПустаяСтрока(Объект.Пароль), "", " -pw " +  ?(СкрыватьПароль, "****************", Объект.Пароль))
			+ " " + Объект.ПараметрыЗапуска;
			
	ИначеЕсли Объект.ТипПодключения = ПредопределенноеЗначение("Перечисление.ТипыПодключений.RDP") Тогда
		
		// https://ab57.ru/cmdlist/mstsc.html
		ИсполнимыйФайл = "mstsc";
		
		ПараметрыЗапуска = ""	
			+ ?(ПустаяСтрока(Объект.АдресПодключения), "", "/v:" + Объект.АдресПодключения)
			+ ?(Объект.Порт = 0, "", ":" + Формат(Объект.Порт, "ЧРД=; ЧРГ=; ЧН=0; ЧГ="))
			+ ?(Объект.ПолеБулево1, " /admin ", "")
			+ ?(Объект.ПолеБулево2, " /f ", "")
			+ ?(Объект.ПолеБулево3, " /public ", "")
			+ " " + Объект.ПараметрыЗапуска;
			
	ИначеЕсли Объект.ТипПодключения = ПредопределенноеЗначение("Перечисление.ТипыПодключений.HTTP") Тогда
		
		ИсполнимыйФайл 		= Объект.АдресПодключения;
		ПараметрыЗапуска 	= Объект.ПараметрыЗапуска;
		
	ИначеЕсли Объект.ТипПодключения = ПредопределенноеЗначение("Перечисление.ТипыПодключений.AmmyAdmin") Тогда
		
		// http://oivt.ru/blog/kopaem-ammyy-admin
		ИсполнимыйФайл = УправлениеITОтделом8УФПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(ТекущийПользователь,
			"ЛокальныйПутьКAmmyAdmin");
		Если ПустаяСтрока(ИсполнимыйФайл) Тогда
			ИсполнимыйФайл = "Установите настройку ЛокальныйПутьКAmmyAdmin в настройках пользователя";
		КонецЕсли;
		
		ПараметрыЗапуска = "-connect "
			+ СокрЛП(СтрЗаменить(Объект.АдресПодключения, " ", ""))
			+ ?(ПустаяСтрока(Объект.Пароль), "", " -password " +  ?(СкрыватьПароль, "****************", Объект.Пароль))
			+ " " + Объект.ПараметрыЗапуска;
		
	ИначеЕсли Объект.ТипПодключения = ПредопределенноеЗначение("Перечисление.ТипыПодключений.ПроизвольнаяКоманда") Тогда
		
		ИсполнимыйФайл 		= Объект.АдресПодключения;
		ПараметрыЗапуска 	= Объект.ПараметрыЗапуска;
		
	КонецЕсли;
	
	// Добавляем двойную кавычку.
	Если Лев(ИсполнимыйФайл, 1) <> """" Тогда
		ИсполнимыйФайл = """" + ИсполнимыйФайл + """";
	КонецЕсли;
	
	Возврат СокрЛП(Префикс + " " + ИсполнимыйФайл + " " + ПараметрыЗапуска);
	
КонецФункции

// Выполняет заполнение подменю с коммандами удаленного управления.
//
// Параметры:
//	
//
// Возвращаемое значение:
//	
//
Процедура ЗаполнитьПодменюУдаленногоУправления(Знач ОбъектУправления, Форма) Экспорт
	
	//УдаленноеУправлениеВызовСервера.ЗаполнитьУдаленноеУправление(ОбъектУправления, Форма.Элементы, Форма.Команды);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ВыполнитьЗапускПриложенияЗавершение(КодВозврата, ДополнительныеПараметры) Экспорт
	
	ВернутьРезультат(ДополнительныеПараметры.ОбработчикРезультата, ДополнительныеПараметры);

КонецПроцедуры

// Возвращает результат прямого вызова, когда не потребовалось открывать диалог.
Процедура ВернутьРезультат(ОбработчикРезультата, Результат)
	
	Если ТипЗнч(ОбработчикРезультата) = Тип("ОписаниеОповещения") Тогда		
		ВыполнитьОбработкуОповещения(ОбработчикРезультата, Результат);
		УправлениеITОтделом8УФ.ЗаписьВЖурналРегистрации("УдаленноеУправление.Подключение",,, Результат.КомманднаяСтрока);
	КонецЕсли;
	
КонецПроцедуры

// Параметры:
//  Элементы               - ВсеЭлементыФормы
//  Команды               - КомандыФормы
//  ИмяКнопки               - Строка
//  Синоним               - Строка
//  ИмяДействия           - Строка
//  ГруппаРодитель           - ГруппаФормы (По умолчанию = Неопределено)
//  ТолькоВоВсехДействиях - Булево (По умолчанию = Ложь)
//  Картинка               - Картинка (По умолчанию = Неопределено)
//  Пометка               - Булево (По умолчанию = Ложь) 
//
Процедура ДобавитьКнопкуНаФорму(Элементы, Команды, ИмяКнопки, Синоним, ИмяДействия
    , ГруппаРодитель = Неопределено, ТолькоВоВсехДействиях = Ложь, Картинка = Неопределено, Пометка = Ложь) Экспорт

	//ИмяКоманды = "Команда" + ИмяКнопки;
	//
	//// Добавляем новую команду обработки выбора вида операции.
	//Команда = Команды.Добавить(ИмяКоманды);
	//Команда.Действие = ИмяДействия;
	//Если Картинка <> Неопределено Тогда
	//    Команда.Картинка = Картинка;
	//КонецЕсли;
    
    Если ГруппаРодитель = Неопределено Тогда
        НоваяКнопка = Элементы.Добавить(ИмяКнопки, Тип("КнопкаФормы"));
    Иначе    
        НоваяКнопка = Элементы.Добавить(ИмяКнопки, Тип("КнопкаФормы"), ГруппаРодитель);
    КонецЕсли;      
    НоваяКнопка.Вид                        = ВидКнопкиФормы.КнопкаКоманднойПанели;
    НоваяКнопка.ИмяКоманды                 = ИмяДействия;
    НоваяКнопка.Заголовок                  = Синоним;
    НоваяКнопка.ТолькоВоВсехДействиях     = ТолькоВоВсехДействиях;
    НоваяКнопка.Пометка                 = Пометка;

КонецПроцедуры // ДобавитьКнопкуНаФорму()

#КонецОбласти