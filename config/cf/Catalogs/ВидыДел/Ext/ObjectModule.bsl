#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если Картинка.Получить() = Неопределено Тогда
		Если ЭтоГруппа Тогда
			Картинка = Новый ХранилищеЗначения(БиблиотекаКартинок.сстВиды);
		Иначе
			Картинка = Новый ХранилищеЗначения(БиблиотекаКартинок.сстВид);
		КонецЕсли;
	КонецЕсли;	
	
	Если Ссылка.Пустая() Тогда
		НовыйУИД = Новый УникальныйИдентификатор;
		НоваяСсылка = Справочники.ВидыДел.ПолучитьСсылку(НовыйУИД);
		УстановитьСсылкуНового(НоваяСсылка);		
		НавСсылка = ПолучитьНавигационнуюСсылку(НоваяСсылка, "Картинка");
		Если ЭтоГруппа = Ложь Тогда
			ПоказыватьСчетчикГруппировки = Истина;
			ПоказыватьНазваниеГруппировки = Истина;
		КонецЕсли;
	Иначе
		НавСсылка = ПолучитьНавигационнуюСсылку(ЭтотОбъект, "Картинка");
	КонецЕсли;
	ДополнительныеСвойства.Вставить("НеВыполнятьПравилаСобытий", Истина);
    
    Если Не ЗначениеЗаполнено(Автор) Тогда
    	Автор = Пользователи.ТекущийПользователь();
	КонецЕсли; 
	
	Если НЕ ЭтоГруппа Тогда
		ИспользуетToDo = СтрНайти(НРег(Отборы), "спрятатьвtodo") > 0;
		ИспользуетГруппировки = Группировки.Количество() > 0;
	КонецЕсли;
    
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Ошибки = Неопределено;
	
	ТЗ = Столбцы.Выгрузить();
	ТЗ.Колонки.Добавить("Колво");
	ТЗ.ЗаполнитьЗначения(1, "Колво");
	
	ТЗ.Свернуть("Имя,Представление", "Колво");
	Для Каждого Стр Из ТЗ Цикл
		Если Стр.Колво > 1 Тогда
			ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Ошибки, 
				"Объект.Столбцы", 
				СтрШаблон(НСтр("ru = 'Столбец %1 повторяющийся'"), Стр.Представление));			
		КонецЕсли;
	КонецЦикла;
				
	ОбщегоНазначенияКлиентСервер.СообщитьОшибкиПользователю(Ошибки, Отказ);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли