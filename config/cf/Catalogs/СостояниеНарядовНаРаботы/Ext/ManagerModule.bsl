#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ПрограммныйИнтерфейс

// Заполнение состояний нарядов по умолчанию.
//
// Параметры:
//	Нет.
//
Процедура ЗаполнитьСостояниеНарядовНаРаботыПриПервоначальномЗаполнении() Экспорт
	
	// Описываем стуктуру
	ТЗ = Новый ТаблицаЗначений;
	ТЗ.Колонки.Добавить("Объект");
	ТЗ.Колонки.Добавить("Наименование");
	ТЗ.Колонки.Добавить("СтатусЗаказа");
	ТЗ.Колонки.Добавить("Цвет");
	
	СЗП = Справочники.СостояниеНарядовНаРаботы;
	
	ДобавитьВТаблицуЗначений(ТЗ, СЗП.ВРаботе,	НСтр("ru = 'В работе'"),	Перечисления.СтатусыЗаказов.ВРаботе, 	WebЦвета.Синий);
	ДобавитьВТаблицуЗначений(ТЗ, СЗП.Выполнен, 	НСтр("ru = 'Выполнен'"), 	Перечисления.СтатусыЗаказов.Выполнен, 	WebЦвета.Зеленый);	
	ДобавитьВТаблицуЗначений(ТЗ, СЗП.Открыт,		НСтр("ru = 'Заявка'"),		Перечисления.СтатусыЗаказов.Открыт, 	WebЦвета.ТемноГрифельноСерый);
	
	// Записываем
	Для Каждого Строки Из ТЗ Цикл
		Объект = Строки.Объект.ПолучитьОбъект();
		ЗаполнитьЗначенияСвойств(Объект, Строки);
		Объект.Записать();
	КонецЦикла;	
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Добавляет в таблицу значений данные по заполняемым объектам.
//
// Параметры:
//   ТЗ 	- ТаблицаЗначений - таблица, куда надо добавить.
//	 Наименование - Строка - наименование элемента.
//	 Объект - Справочники.СостоянияСобытий - предопределенный элемент.
//	 Цвет	- ХранилищеЗначения - цвет сохраненный в хранилище значений.
//
Процедура ДобавитьВТаблицуЗначений(ТЗ, Объект, Наименование, СтатусЗаказа, Цвет)
	
	НоваяСтрока 				= ТЗ.Добавить();
	НоваяСтрока.Объект			= Объект;
	НоваяСтрока.Наименование 	= Наименование;
	НоваяСтрока.СтатусЗаказа 	= СтатусЗаказа;
	НоваяСтрока.Цвет		 	= Новый ХранилищеЗначения(Цвет);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли