#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ПрограммныйИнтерфейс

// Заполняет предопределенные этапы процессов.
//
Процедура ЗаполнитьГруппыКанбанДоскиПриПервоначальномЗаполнении() Экспорт
	
	// Описываем структуру
	ТЗ = Новый ТаблицаЗначений;
	ТЗ.Колонки.Добавить("Объект");
	ТЗ.Колонки.Добавить("Наименование");
	ТЗ.Колонки.Добавить("Отображать");
	ТЗ.Колонки.Добавить("Комментарий");
	
	ТЗ.Колонки.Добавить("ЭтапыПроцессов");
	
	ВД = Справочники.ГруппыКанбанДоски;
	
	// Заполняем реквизитами
	
	// НОВЫЕ
	НоваяСтрока = ДобавитьВТаблицуЗначений(ТЗ,	ВД.Новые,			НСтр("ru = 'Новые'"), 	Истина, 
		НСтр("ru = 'Сюда попадают все новые задачи'"));
	ДобавитьКолонкиВЭтапыПроцесса(НоваяСтрока.ЭтапыПроцессов);
	Массив = Новый Массив;
	Массив.Добавить(Перечисления.СостоянияЭтаповПроцесса.Новый);
	ДобавитьВТаблицуЗначенийЭтапы(НоваяСтрока.ЭтапыПроцессов, Массив);
	
	// Backlog
	НоваяСтрока = ДобавитьВТаблицуЗначений(ТЗ,	ВД.Backlog,			НСтр("ru = 'Backlog'"), Истина, 
		НСтр("ru = 'Все задачи, которые мы запланировали выполнить в текущем спринте'"));
	ДобавитьКолонкиВЭтапыПроцесса(НоваяСтрока.ЭтапыПроцессов);
	Массив = Новый Массив;
	Массив.Добавить(Перечисления.СостоянияЭтаповПроцесса.Бэклог);
	ДобавитьВТаблицуЗначенийЭтапы(НоваяСтрока.ЭтапыПроцессов, Массив);
	
	// В РАБОТЕ
	НоваяСтрока = ДобавитьВТаблицуЗначений(ТЗ,	ВД.ВРаботе,			НСтр("ru = 'В работе'"), Истина,  
		НСтр("ru = 'Все задачи, над которыми работаем в данный момент'"));
	ДобавитьКолонкиВЭтапыПроцесса(НоваяСтрока.ЭтапыПроцессов);
	Массив = Новый Массив;
	Массив.Добавить(Перечисления.СостоянияЭтаповПроцесса.ВРаботе);
	Массив.Добавить(Перечисления.СостоянияЭтаповПроцесса.Подготовка);
	Массив.Добавить(Перечисления.СостоянияЭтаповПроцесса.Подпроцесс);
	ДобавитьВТаблицуЗначенийЭтапы(НоваяСтрока.ЭтапыПроцессов, Массив);
	
	// ВЫПОЛНЕНЫ
	НоваяСтрока = ДобавитьВТаблицуЗначений(ТЗ,	ВД.Выполненные,		НСтр("ru = 'Выполненные'"),	Истина,
		НСтр("ru = 'Задачи, которые были выполнены'"));
	ДобавитьКолонкиВЭтапыПроцесса(НоваяСтрока.ЭтапыПроцессов);
	Массив = Новый Массив;
	Массив.Добавить(Перечисления.СостоянияЭтаповПроцесса.Выполнен);
	ДобавитьВТаблицуЗначенийЭтапы(НоваяСтрока.ЭтапыПроцессов, Массив);
	
	// ЗАВЕРШЕННЫЕ
	НоваяСтрока = ДобавитьВТаблицуЗначений(ТЗ,	ВД.Завершенные,		НСтр("ru = 'Завершенные'"), Ложь, 
		НСтр("ru = 'Задачи, которые были завершены (нет изменений в течении какого-то времени)'"));
	ДобавитьКолонкиВЭтапыПроцесса(НоваяСтрока.ЭтапыПроцессов);
	Массив = Новый Массив;
	Массив.Добавить(Перечисления.СостоянияЭтаповПроцесса.Закрыт);
	ДобавитьВТаблицуЗначенийЭтапы(НоваяСтрока.ЭтапыПроцессов, Массив);
	
	// Записываем
	Для Каждого Строки Из ТЗ Цикл
		
		Объект 			= Строки.Объект.ПолучитьОбъект();
        Объект.ВидГруппы = Перечисления.ВидыГруппКанбанДоски.Этапы;
        
		ЗаполнитьЗначенияСвойств(Объект, Строки);
		
		Объект.ЭтапыПроцессов.Очистить();
		Для Каждого СтрокиЭтапов Из Строки.ЭтапыПроцессов Цикл
			ЗаполнитьЗначенияСвойств(Объект.ЭтапыПроцессов.Добавить(), СтрокиЭтапов);
		КонецЦикла;
		
		Объект.Записать();
		
	КонецЦикла;

	ОбновитьПовторноИспользуемыеЗначения();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Добавляет в таблицу значений данные
//
// Параметры:
//   нет
//
Функция ДобавитьВТаблицуЗначений(ТЗ, Объект, Наименование, Отображать, Комментарий)
	
	НоваяСтрока 				= ТЗ.Добавить();
	НоваяСтрока.Объект			= Объект;
	НоваяСтрока.Наименование	= Наименование;
	НоваяСтрока.Отображать		= Отображать;
	НоваяСтрока.Комментарий		= Комментарий;
	
	Возврат НоваяСтрока;
	
КонецФункции

Процедура ДобавитьКолонкиВЭтапыПроцесса(ЭтапыПроцессов);
	
	ЭтапыПроцессов = Новый ТаблицаЗначений;
	ЭтапыПроцессов.Колонки.Добавить("ЭтапПроцесса");
	
КонецПроцедуры

Процедура ДобавитьВТаблицуЗначенийЭтапы(ТЗ, МассивСостояний)
	
	Запрос = Новый Запрос();
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ЭтапыПроцессов.Ссылка КАК ЭтапПроцесса
		|ИЗ
		|	Справочник.ЭтапыПроцессов КАК ЭтапыПроцессов
		|ГДЕ
		|	ЭтапыПроцессов.СостояниеЭтапа В(&МассивСостояний)";
	Запрос.УстановитьПараметр("МассивСостояний", МассивСостояний);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		ЗаполнитьЗначенияСвойств(ТЗ.Добавить(), Выборка);
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли