
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("Владелец") Тогда
		РаботаСОтборамиКлиентСервер.УстановитьЭлементОтбораСписка(Список, "Владелец", 
			Параметры.Владелец, , ВидСравненияКомпоновкиДанных.Равно);
	КонецЕсли;
	
	Если Параметры.Свойство("ДанныеВыбора") Тогда
		Элементы.Список.ТекущаяСтрока = Параметры.ДанныеВыбора;
	КонецЕсли;
	
	Если Параметры.Свойство("ОтборКонтрагент") Тогда
		РаботаСОтборамиКлиентСервер.УстановитьЭлементОтбораСписка(Список, "Владелец", 
			Параметры.ОтборКонтрагент, , ВидСравненияКомпоновкиДанных.Равно);	
	КонецЕсли;
	
	Если Параметры.Свойство("УчетОстатков") Тогда
		РаботаСОтборамиКлиентСервер.УстановитьЭлементОтбораСписка(Список, "Владелец.УчетОстатков", 
			Параметры.УчетОстатков, , ВидСравненияКомпоновкиДанных.Равно);	
	КонецЕсли;
	
	ПоказыватьАктуальных = Истина;
	ОбновитьВидимостьДоступность();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПоказыватьТолькоАктуальных(Команда)
	
	ПоказыватьАктуальных = НЕ ПоказыватьАктуальных;
	ОбновитьВидимостьДоступность();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОбновитьВидимостьДоступность()
	
	Элементы.ФормаПоказыватьТолькоАктуальных.Пометка = ПоказыватьАктуальных;
	
	Если ПоказыватьАктуальных Тогда
		// Только актуальные.
		РаботаСОтборамиКлиентСервер.УстановитьЭлементОтбораСписка(Список, "Актуальность", Истина);
	Иначе				
		// Всех подряд
		РаботаСОтборамиКлиентСервер.УдалитьЭлементОтбораСписка(Список, "Актуальность");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти