#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОписаниеПеременных
	
Перем ОсвободитьСтатью Экспорт;

#КонецОбласти

#Область ОбработчикиСобытий

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТКА СОБЫТИЙ МОДУЛЯ

Процедура ПередЗаписью(Отказ)
	
	Если ЭтотОбъект.ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	Если ЭтотОбъект.ЭтоНовый() Тогда
		Если НЕ ЗначениеЗаполнено(Автор) Тогда
			Автор = Пользователи.ТекущийПользователь();
		КонецЕсли;
		Если ДатаСоздания = Дата(1, 1, 1) Тогда
			ДатаСоздания = ТекущаяДатаСеанса();
		КонецЕсли;
	Иначе
		АвторКорректировки = Пользователи.ТекущийПользователь();
		ДатаКорректировки  = ТекущаяДатаСеанса();		
	КонецЕсли;
	
	Если ИерархияСодержанияИзменилась() Тогда
		ОбновитьДанныеУровнейСодержания();
	КонецЕсли;
	
	ПолныйТекстИзм	= ПолучитьПолныйТекстСтатьи();
	ТекстСтатьиИзм	= (ПолныйТекстИзм <> ЭтотОбъект.ПолныйТекст);
	
	Если ТекстСтатьиИзм Тогда
		ЭтотОбъект.ПолныйТекст	= ПолныйТекстИзм;
	КонецЕсли;
	
	Если НЕ ЭтотОбъект.ЭтоНовый() Тогда		
		ПроверитьФайлыПоСтатье();
		ПроверитьСноскиПоСтатье();
	КонецЕсли;
	
	ЭтотОбъект.ДополнительныеСвойства.Вставить("ТекстИзменился"		, ТекстСтатьиИзм);
	ЭтотОбъект.ДополнительныеСвойства.Вставить("ЭтоНовый"			, ЭтотОбъект.ЭтоНовый());
	ЭтотОбъект.ДополнительныеСвойства.Вставить("КатегорииОбновления", ПолучитьКатегорииДляОбновленияСтатистики());
	ЭтотОбъект.ДополнительныеСвойства.Вставить("ПометкаУдаленияСсылки", ?(ЭтотОбъект.ЭтоНовый(), Ложь, ЭтотОбъект.Ссылка.ПометкаУдаления));
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ЭтотОбъект.ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	// Освобождаем статью
	Если ОсвободитьСтатью Тогда
		Справочники.СтатьиБазыЗнаний.ЗавершитьРедактированиеСтатьи(ЭтотОбъект.Ссылка);
	КонецЕсли;
	
	ЭтоНовыйЭлемент			= ЗначениеДопСвойства("ЭтоНовый", Ложь);
	ТекстСтатьИзменился		= ЗначениеДопСвойства("ТекстИзменился", Ложь);
	КатегорииОбновления		= ЗначениеДопСвойства("КатегорииОбновления", Неопределено);
	ПометкаУдаленияСсылки   = (ЭтотОбъект.ДополнительныеСвойства.Свойство("ПометкаУдаленияСсылки") И ЭтотОбъект.ДополнительныеСвойства.ПометкаУдаленияСсылки);
	
	ЗаписатьИзменениеСтатьи = ЭтоНовыйЭлемент;
	Если НЕ БазаЗнанийВызовСервера.СтатьяРедактируется(ЭтотОбъект.Ссылка) И ТекстСтатьИзменился Тогда
		ЗаписатьИсториюСтатьиБазыЗнаний();
		ЗаписатьИзменениеСтатьи = Истина;
	КонецЕсли;
	
	Если ЗаписатьИзменениеСтатьи Тогда
		БазаЗнаний.ЗарегистрироватьИзменениеСтатьи(ЭтотОбъект.Ссылка);
	КонецЕсли;
	
	Если ЭтоНовыйЭлемент ИЛИ ЗаписатьИзменениеСтатьи Тогда
		Если НЕ ЭтотОбъект.Черновик Тогда
			ТипНовости = ?(ЭтоНовыйЭлемент, 1, 2);
			ДобавитьНовостьБазыЗнаний(ТипНовости);
		КонецЕсли;	
	КонецЕсли;
	
	Если ПометкаУдаленияСсылки <> ЭтотОбъект.ПометкаУдаления Тогда		
		Если НЕ ЭтотОбъект.Черновик Тогда
			Если ЭтотОбъект.ПометкаУдаления Тогда
				ДобавитьНовостьБазыЗнаний(3);			
			Иначе
				ДобавитьНовостьБазыЗнаний(4);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если ТипЗнч(КатегорииОбновления) = Тип("Массив") И КатегорииОбновления.Количество() > 0 Тогда
		МассивПараметров = Новый Массив;
		МассивПараметров.Добавить(Новый Структура("Категории", КатегорииОбновления));
		ФоновыеЗадания.Выполнить(
			"БазаЗнаний.ОбновитьСтатистикуКатегорий",
			МассивПараметров, ,
			"Обновление статистики по категориям статьи");
	КонецЕсли;	
		
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	ЭтотОбъект.Автор				= Справочники.Пользователи.ПустаяСсылка();
	ЭтотОбъект.АвторКорректировки	= Справочники.Пользователи.ПустаяСсылка();
	ЭтотОбъект.ДатаСоздания 		= Дата(1, 1, 1);
	ЭтотОбъект.Черновик				= Истина;
	ЭтотОбъект.ОписаниеИзменений	= "";
	ЭтотОбъект.КраткоеОписание		= "";
	ЭтотОбъект.ПолныйТекст			= "";
	ЭтотОбъект.Текст				= "";
	ЭтотОбъект.ТекстHTML			= "";
	ЭтотОбъект.Содержание.Очистить();
	ЭтотОбъект.Картинки.Очистить();
	ЭтотОбъект.Таблицы.Очистить();
	ЭтотОбъект.Сноски.Очистить();
	
КонецПроцедуры

Процедура ПередУдалением(Отказ)
		
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

////////////////////////////////////////////////////////////////////////////////
// ПРОЧИЕ МЕТОДЫ

Процедура РекурсивныйОбходСодержанияПоДаннымРодителя(ТаблицаДанных, ЗначениеРодитель, НомерУровня)
	МассивСтрок = ТаблицаДанных.НайтиСтроки(Новый Структура("Родитель", ЗначениеРодитель));
	Для Каждого СтрокаТаблицы Из МассивСтрок Цикл
		СтрокаТаблицы.Уровень = НомерУровня + 1;
		РекурсивныйОбходСодержанияПоДаннымРодителя(ТаблицаДанных, СтрокаТаблицы.Область, СтрокаТаблицы.Уровень);
	КонецЦикла;
КонецПроцедуры

Процедура ОбновитьДанныеУровнейСодержания() Экспорт
	
	ТаблицаСодержание = ЭтотОбъект.Содержание.Выгрузить();
	ТаблицаСодержание.Индексы.Добавить("Родитель");
	
	РекурсивныйОбходСодержанияПоДаннымРодителя(ТаблицаСодержание, "", 0);
	
	ЭтотОбъект.Содержание.Загрузить(ТаблицаСодержание);
	
КонецПроцедуры

Функция ИерархияСодержанияИзменилась()
	
	Если ЭтотОбъект.Ссылка.Пустая() Тогда
		Если ЭтотОбъект.Содержание.Количество() > 0 Тогда
			Возврат Истина;
		КонецЕсли;
	Иначе
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Ссылка", ЭтотОбъект.Ссылка);
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	СтатьиБазыЗнанийСодержание.НомерСтроки,
		|	СтатьиБазыЗнанийСодержание.Область,
		|	СтатьиБазыЗнанийСодержание.Родитель
		|ИЗ
		|	Справочник.СтатьиБазыЗнаний.Содержание КАК СтатьиБазыЗнанийСодержание
		|ГДЕ
		|	СтатьиБазыЗнанийСодержание.Ссылка = &Ссылка";
		Результат = Запрос.Выполнить().Выгрузить();
		
		КоличествоСтрок = Результат.Количество();
		Если КоличествоСтрок <> ЭтотОбъект.Содержание.Количество() Тогда
			Возврат Истина;
		Иначе 
			Для Индекс = 0 По КоличествоСтрок - 1 Цикл
				СтрокаРезультат = Результат[Индекс];
				СтрокаОбъект	= ЭтотОбъект.Содержание[Индекс];
				
				Если СтрокаРезультат.Область <> СтрокаОбъект.Область
					ИЛИ СтрокаРезультат.Родитель <> СтрокаОбъект.Родитель Тогда
					Возврат Истина;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

Функция ПолучитьКатегорииДляОбновленияСтатистики()
	
	МассивКатегорий = ЭтотОбъект.Категории.ВыгрузитьКолонку("Категория");
	
	Если ЭтотОбъект.Ссылка.Пустая() Тогда
		Возврат МассивКатегорий;
	Иначе
		ПометкаУдаленияИзменилась = (ЭтотОбъект.ПометкаУдаления <> ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ЭтотОбъект.Ссылка, "ПометкаУдаления"));
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Ссылка", ЭтотОбъект.Ссылка);
		Запрос.УстановитьПараметр("Категории", МассивКатегорий);
		Запрос.УстановитьПараметр("ПометкаУдаленияИзменилась", ПометкаУдаленияИзменилась);
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	СтатьиБазыЗнанийКатегории.Категория КАК Категория
		|ПОМЕСТИТЬ КатегорииПоСтатье
		|ИЗ
		|	Справочник.СтатьиБазыЗнаний.Категории КАК СтатьиБазыЗнанийКатегории
		|ГДЕ
		|	СтатьиБазыЗнанийКатегории.Ссылка = &Ссылка
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Категория
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	КатегорииБазыЗнаний.Ссылка КАК Категория
		|ИЗ
		|	Справочник.КатегорииБазыЗнаний КАК КатегорииБазыЗнаний
		|		ЛЕВОЕ СОЕДИНЕНИЕ КатегорииПоСтатье КАК КатегорииПоСтатье
		|		ПО КатегорииБазыЗнаний.Ссылка = КатегорииПоСтатье.Категория
		|ГДЕ
		|	КатегорииБазыЗнаний.Ссылка В(&Категории)
		|	И (КатегорииПоСтатье.Категория ЕСТЬ NULL 
		|			ИЛИ &ПометкаУдаленияИзменилась)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	КатегорииПоСтатье.Категория
		|ИЗ
		|	КатегорииПоСтатье КАК КатегорииПоСтатье
		|ГДЕ
		|	НЕ КатегорииПоСтатье.Категория В (&Категории)";
		МассивКатегорий = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Категория");
	КонецЕсли;
	
	Возврат МассивКатегорий;
	
КонецФункции

Функция ПолучитьПолныйТекстСтатьи()
	
	Значение = ЭтотОбъект.Текст;
	
	Для Каждого СтрокаСодержания Из ЭтотОбъект.Содержание Цикл
		Значение = Значение + "
		|
		|Подраздел: " + СтрокаСодержания.Наименование + "
		|" + СтрокаСодержания.Текст;
	КонецЦикла;
	
	Возврат Значение;
	
КонецФункции

Функция ЗначениеДопСвойства(Имя, ПоУмолчанию)
	
	Если ЭтотОбъект.ДополнительныеСвойства.Свойство(Имя) Тогда
		Возврат ЭтотОбъект.ДополнительныеСвойства[Имя];
	Иначе 
		Возврат ПоУмолчанию;
	КонецЕсли;
	
КонецФункции

// ТипНовости: 1 - создание, 2 - изменение, 3 - удаление
Функция ДобавитьНовостьБазыЗнаний(ТипНовости)
	
	Если ЭтотОбъект.Черновик Тогда
		Возврат Ложь;
	КонецЕсли;	
	
	ПараметрыСсылкиОтв	= Новый Структура("id", Строка(ЭтотОбъект.Автор.УникальныйИдентификатор()));
	АдресСсылкиОтв		= БазаЗнанийAPIКлиентСервер.КонструкторСсылки_page("user", ПараметрыСсылкиОтв);
	
	ПараметрыСсылкиСтат	= Новый Структура("id", Строка(ЭтотОбъект.Ссылка.УникальныйИдентификатор()));
	АдресСсылкиСтат		= БазаЗнанийAPIКлиентСервер.КонструкторСсылки_page("article", ПараметрыСсылкиСтат);
	
	Если ТипНовости = 1 Тогда
		ВидНовости	= Перечисления.ВидыНовостейБазыЗнаний.Создание;
		Заголовок	= "Новая статья '%3'";
		ТекстHTML	= "<div>%1 в %2 добавлена новая статья базы знаний.
		|	<br>Заголовок: %3.
		|	<br>Автор: <a href='" + АдресСсылкиОтв + "'>%4</a>
		|	<br>Категории: %5
		|	<br><a href='" + АдресСсылкиСтат + "'>Перейти</a>
		|</div>";
	ИначеЕсли ТипНовости = 2 Тогда
		ВидНовости	= Перечисления.ВидыНовостейБазыЗнаний.Изменение;
		Заголовок	= "Изменена статья '%3'";
		ТекстHTML	= "<div>%1 в %2 пользователем <a href='" + АдресСсылкиОтв + "'>%4</a> изменена статья базы знаний.
		|	<br>Заголовок: %3.
		|	<br>Категории: %5
		|	<br><a href='" + АдресСсылкиСтат + "'>Перейти</a>
		|</div>";
	ИначеЕсли ТипНовости = 3 Тогда
		ВидНовости	= Перечисления.ВидыНовостейБазыЗнаний.Удаление;
		Заголовок	= "Удалена статья '%3'";
		ТекстHTML	= "<div>%1 в %2 пользователем <a href='" + АдресСсылкиОтв + "'>%4</a> удалена статья базы знаний.
		|	<br>Заголовок: %3.
		|	<br>Категории: %5
		|</div>";
	ИначеЕсли ТипНовости = 4 Тогда
		ВидНовости	= Перечисления.ВидыНовостейБазыЗнаний.Изменение;
		Заголовок	= "Восстановлена статья '%3'";
		ТекстHTML	= "<div>%1 в %2 пользователем <a href='" + АдресСсылкиОтв + "'>%4</a> восстановлена статья после удаления.
		|	<br>Заголовок: %3.
		|	<br>Категории: %5
		|</div>";		
	КонецЕсли;
	
	ДатаНовости = ТекущаяДатаСеанса();
	
	ТекстКатегории = "";
	Для Каждого СтрокаТаблицы Из ЭтотОбъект.Категории Цикл
		Если ЗначениеЗаполнено(СтрокаТаблицы.Категория.ТипКатегории) И СтрокаТаблицы.Категория.ТипКатегории = Перечисления.ТипыКатегорийСтатейБазыЗнаний.Публичная Тогда
			
			ПараметрыСсылкиКат	= Новый Структура("id", Строка(СтрокаТаблицы.Категория.УникальныйИдентификатор()));
			АдресСсылкиКат		= БазаЗнанийAPIКлиентСервер.КонструкторСсылки_page("category", ПараметрыСсылкиКат);
			
			ТекстКатегории = ТекстКатегории + ?(ПустаяСтрока(ТекстКатегории), "", "; ") + 
			"<a href='" + АдресСсылкиКат + "'>" + Строка(СтрокаТаблицы.Категория) + "</a>";
		КонецЕсли;
	КонецЦикла;
	
	МассивПараметров = Новый Массив;
	МассивПараметров.Добавить(Формат(ДатаНовости, "ДЛФ=DD"));
	МассивПараметров.Добавить(Формат(ДатаНовости, "ДФ=HH:mm"));
	МассивПараметров.Добавить(СокрЛП(ЭтотОбъект.Наименование));
	МассивПараметров.Добавить(Строка(ЭтотОбъект.Автор));
	МассивПараметров.Добавить(ТекстКатегории);
	
	Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтрокуИзМассива(Заголовок, МассивПараметров);
	ТекстHTML = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтрокуИзМассива(ТекстHTML, МассивПараметров);
	
	ПараметрыНовости = Новый Структура;
	ПараметрыНовости.Вставить("Автор"				, ЭтотОбъект.Автор);
	ПараметрыНовости.Вставить("Заголовок"			, Заголовок);
	ПараметрыНовости.Вставить("ТекстHTML"			, ТекстHTML);
	ПараметрыНовости.Вставить("ОбъектБазыЗнаний"	, ЭтотОбъект.Ссылка);	
	ПараметрыНовости.Вставить("Важность"			, 2);
	ПараметрыНовости.Вставить("ВидНовости"			, ВидНовости);
	
	БазаЗнанийВызовСервера.ДобавитьНовость(ПараметрыНовости);
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// ФОРМИРОВАНИЕ ДВИЖЕНИЙ СПРАВОЧНИКА

Процедура ЗаписатьИсториюСтатьиБазыЗнаний()
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", ЭтотОбъект.Ссылка);
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЕСТЬNULL(МАКСИМУМ(ИсторияСтатейБазыЗнаний.НомерВерсии), 0) КАК НомерВерсии
	|ИЗ
	|	РегистрСведений.ИсторияСтатейБазыЗнаний КАК ИсторияСтатейБазыЗнаний
	|ГДЕ
	|	ИсторияСтатейБазыЗнаний.СтатьяБазыЗнаний = &Ссылка";
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		НомерВерсии = 1;
	Иначе 
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		НомерВерсии = Выборка.НомерВерсии + 1;
	КонецЕсли;
	
	ЗаписьРегистра = РегистрыСведений.ИсторияСтатейБазыЗнаний.СоздатьМенеджерЗаписи();
	ЗаписьРегистра.СтатьяБазыЗнаний = ЭтотОбъект.Ссылка;
	ЗаписьРегистра.НомерВерсии		= НомерВерсии;
	ЗаписьРегистра.Текст			= ЭтотОбъект.ПолныйТекст;
	ЗаписьРегистра.Описание			= ЭтотОбъект.ОписаниеИзменений;
	ЗаписьРегистра.Ответственный	= Пользователи.ТекущийПользователь();
	ЗаписьРегистра.Изменено			= ТекущаяДатаСеанса();
	ЗаписьРегистра.Записать(Истина);
	
КонецПроцедуры

Процедура ПроверитьФайлыПоСтатье()
	
	МассивФайлыУдалить  = Новый Массив;
	МассивСтрокУдалить  = Новый Массив;	
	
	Для Каждого СтрокаТаблицы Из ЭтотОбъект.Картинки Цикл		
		СтрокаПоиска = ?(ЭтотОбъект.РазделИмпортирован, "[pict_" + СтрЗаменить(СтрокаТаблицы.Идентификатор, "-", "") + "]", "[picture='" + СтрокаТаблицы.Имя + "']");		
		Если СтрНайти(ЭтотОбъект.ПолныйТекст, СтрокаПоиска) = 0 Тогда
			МассивСтрокУдалить.Добавить(СтрокаТаблицы);				
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого СтрокаУдаления Из МассивСтрокУдалить Цикл			
		Если МассивФайлыУдалить.Найти(СтрокаУдаления.Файл) = Неопределено Тогда
			МассивФайлыУдалить.Добавить(СтрокаУдаления.Файл);
		КонецЕсли;
		ЭтотОбъект.Картинки.Удалить(СтрокаУдаления);					
	КонецЦикла;
	
	МассивСтрокУдалить.Очистить();	
	Для Каждого СтрокаТаблицы Из ЭтотОбъект.Таблицы Цикл
		СтрокаПоиска = "[table='" + СтрокаТаблицы.Имя + "']";
		Если СтрНайти(ЭтотОбъект.ПолныйТекст, СтрокаПоиска) = 0 Тогда
			МассивСтрокУдалить.Добавить(СтрокаТаблицы);			
		КонецЕсли;
	КонецЦикла;	
	Для Каждого СтрокаУдаления Из МассивСтрокУдалить Цикл			
		Если МассивФайлыУдалить.Найти(СтрокаУдаления.Файл) = Неопределено Тогда
			МассивФайлыУдалить.Добавить(СтрокаУдаления.Файл);
		КонецЕсли;
		ЭтотОбъект.Таблицы.Удалить(СтрокаУдаления);		
	КонецЦикла;

	МассивСтрокУдалить.Очистить();	
	Для Каждого СтрокаФайлы Из ЭтотОбъект.Файлы Цикл
		СтрокаПоиска = "[fileid='" + СтрокаФайлы.Идентификатор + "'";
		Если СтрНайти(ЭтотОбъект.ПолныйТекст, СтрокаПоиска) = 0 Тогда
			МассивСтрокУдалить.Добавить(СтрокаФайлы);			
		КонецЕсли;
	КонецЦикла;	
	Для Каждого СтрокаУдаления Из МассивСтрокУдалить Цикл			
		Если МассивФайлыУдалить.Найти(СтрокаУдаления.Файл) = Неопределено Тогда
			МассивФайлыУдалить.Добавить(СтрокаУдаления.Файл);
		КонецЕсли;
		ЭтотОбъект.Файлы.Удалить(СтрокаУдаления);		
	КонецЦикла;
	
	Если МассивФайлыУдалить.Количество() > 0 Тогда
		Для Каждого СтрокаФайл Из МассивФайлыУдалить Цикл
			
			МожноУдалитьФайл = Истина;
			
			МассивСтрок = ЭтотОбъект.Картинки.НайтиСтроки(Новый Структура("Файл", СтрокаФайл));
			Если МассивСтрок.Количество() > 0 Тогда
				МожноУдалитьФайл = Ложь;
				Продолжить;
			КонецЕсли;
			
			МассивСтрок = ЭтотОбъект.Таблицы.НайтиСтроки(Новый Структура("Файл", СтрокаФайл));
			Если МассивСтрок.Количество() > 0 Тогда
				МожноУдалитьФайл = Ложь;
				Продолжить;
			КонецЕсли;
			
			МассивСтрок = ЭтотОбъект.Файлы.НайтиСтроки(Новый Структура("Файл", СтрокаФайл));
			Если МассивСтрок.Количество() > 0 Тогда
				МожноУдалитьФайл = Ложь;
				Продолжить;
			КонецЕсли;
			
			Если МожноУдалитьФайл Тогда
				Попытка
					ФайлОбъект = СтрокаФайл.ПолучитьОбъект();
					ФайлОбъект.ПометкаУдаления = Истина;
					ФайлОбъект.Записать();
				Исключение
					Продолжить;
				КонецПопытки;	
			КонецЕсли;			
		КонецЦикла;	
	КонецЕсли;
	
КонецПроцедуры	

Процедура ПроверитьСноскиПоСтатье()
	
	МассивУдаления = Новый Массив;
	
	Для Каждого СтрокаТаблицы Из ЭтотОбъект.Сноски Цикл
		СтрокаПоиска = "href='ref:" + СтрокаТаблицы.Идентификатор + "'";
		Если Найти(ЭтотОбъект.ТекстHTML, СтрокаПоиска) = 0 Тогда
			ЕстьВПодразделах = Ложь;
			Для Каждого СтрокаСодержания Из ЭтотОбъект.Содержание Цикл
				Если Найти(СтрокаСодержания.ТекстHTML, СтрокаПоиска) > 0 Тогда
					ЕстьВПодразделах = Истина;
					Прервать;
				КонецЕсли;
			КонецЦикла;
			
			Если НЕ ЕстьВПодразделах Тогда
				МассивУдаления.Добавить(СтрокаТаблицы);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого СтрокаУдаления Из МассивУдаления Цикл
		ЭтотОбъект.Сноски.Удалить(СтрокаУдаления);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

ОсвободитьСтатью = Ложь;

#КонецЕсли

