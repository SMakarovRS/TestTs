
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Статья.Пустая() Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	ЗаполнитьИсториюСтатьиНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ИсторияСравнитьПриИзменении(Элемент)
	ТекущиеДанные = Элементы.История.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;

	МассивСтрок = История.НайтиСтроки(Новый Структура("Сравнить", Истина));
	Если МассивСтрок.Количество() > 2 Тогда
		ПоказатьПредупреждение(, НСтр("ru='Сравнивать можно не более двух версий статьей.'"));
		ТекущиеДанные.Сравнить = Ложь;
	КонецЕсли;	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыполнитьСравнение(Команда)
	ПоказатьПредупреждение(, НСтр("ru = 'Сравнение версий недоступно.'"));
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьИсториюСтатьиНаСервере()
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", Параметры.Статья);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ИсторияСтатейБазыЗнаний.НомерВерсии КАК НомерВерсии,
	|	ИсторияСтатейБазыЗнаний.Текст,
	|	ИсторияСтатейБазыЗнаний.Описание,
	|	ИсторияСтатейБазыЗнаний.Ответственный,
	|	ИсторияСтатейБазыЗнаний.Изменено
	|ИЗ
	|	РегистрСведений.ИсторияСтатейБазыЗнаний КАК ИсторияСтатейБазыЗнаний
	|ГДЕ
	|	ИсторияСтатейБазыЗнаний.СтатьяБазыЗнаний = &Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерВерсии";
	История.Загрузить(Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры

#КонецОбласти

