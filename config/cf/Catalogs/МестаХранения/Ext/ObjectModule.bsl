#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
		
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если Картинка.Получить() = Неопределено Тогда 
		Картинка = Новый ХранилищеЗначения(БиблиотекаКартинок.вмхКартинкаНеВыбранногоВидаСклада);
	КонецЕсли;	
	
	Если ЭтоНовый() Тогда		
		НоваяСсылка = Справочники.МестаХранения.ПолучитьСсылку(Новый УникальныйИдентификатор);
		УстановитьСсылкуНового(НоваяСсылка);
	Иначе
		НоваяСсылка = Ссылка;
	КонецЕсли;
	
	НавСсылка = ПолучитьНавигационнуюСсылку(НоваяСсылка, "Картинка");
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	ПоследнийОтветственный = Справочники.Сотрудники.ПустаяСсылка();
	СетевоеИмя			   = "";
	СетевойПользователь	   = "";
	Картинка			   = Новый ХранилищеЗначения(ОбъектКопирования.Картинка.Получить());	
	
	СетевыеИнтерфейсы.Очистить();
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	Если Не Справочники.ГруппыДоступаМестХранения.ИспользуютсяГруппыДоступа() Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ГруппаДоступа");
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения);
		
	КонецЕсли;
	
	ДозаполнитьПоУмолчанию();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ДозаполнитьПоУмолчанию()		
	
	Если Не ЗначениеЗаполнено(ГруппаДоступа) Тогда
		ГруппаДоступа = Справочники.ГруппыДоступаМестХранения.ГруппаДоступаПоУмолчанию();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
