
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Образец = НСтр("ru='Это пример тега, который будет отображен'", "ru");
	ОбновитьВидимостьДоступность();
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Оповестить("Теги_Запись", Объект.Ссылка, ЭтаФорма);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СтильЦветТекста(Команда)
	
    Структура = Новый Структура("РедактируемыйЦвет", Объект.ЦветТекста);
    Результат = Неопределено;
    ОткрытьФорму("ОбщаяФорма.ФормаВыбораЦвета", Структура, ЭтаФорма,,,,
        Новый ОписаниеОповещения("СтильЦветТекстаЗавершение", ЭтотОбъект),
        РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);        
	
КонецПроцедуры

&НаКлиенте
Процедура СтильЦветТекстаЗавершение(Результат, ДополнительныеПараметры) Экспорт
    
    Если Результат <> Неопределено Тогда
        Объект.ЦветТекста = РаботаСЦветомКлиентСервер.ЦветВHex(Результат);
        ОбновитьВидимостьДоступность();
    КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура СтильЦветФона(Команда)
    
    Структура = Новый Структура("РедактируемыйЦвет", Объект.ЦветФона);
    Результат = Неопределено;
    ОткрытьФорму("ОбщаяФорма.ФормаВыбораЦвета", Структура, ЭтаФорма,,,, 
        Новый ОписаниеОповещения("СтильЦветФонаЗавершение", ЭтотОбъект), 
            РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
    	
КонецПроцедуры

&НаКлиенте
Процедура СтильЦветФонаЗавершение(Результат, ДополнительныеПараметры) Экспорт
    
    Если Результат <> Неопределено Тогда        
        Объект.ЦветФона = РаботаСЦветомКлиентСервер.ЦветВHex(Результат);
        ОбновитьВидимостьДоступность();
    КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура СлучайныйЦвет(Команда)
	
	СлучайныйЦветНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОбновитьВидимостьДоступность()
    
    Элементы.Образец.ЦветФона   = ?(ПустаяСтрока(Объект.ЦветФона), WebЦвета.Белый,
        РаботаСЦветомКлиентСервер.HexВЦвет(Объект.ЦветФона));
    Элементы.Образец.ЦветТекста = ?(ПустаяСтрока(Объект.ЦветТекста), WebЦвета.Черный,
        РаботаСЦветомКлиентСервер.HexВЦвет(Объект.ЦветТекста));   
    
КонецПроцедуры

&НаСервере
Процедура СлучайныйЦветНаСервере()
	
	Объект.ЦветФона 	= РаботаСЦветомКлиентСервер.ЦветВHex(РаботаСЦветомКлиентСервер.СлучайныйЦветRALStandart());
	Объект.ЦветТекста 	= РаботаСЦветомКлиентСервер.ЦветВHex(РаботаСЦветомКлиентСервер.КонтрастныйЦвет(Объект.ЦветФона));
	ОбновитьВидимостьДоступность();
	
КонецПроцедуры

#КонецОбласти