
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьУсловноеОформление();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Теги_Запись" Тогда
		УстановитьУсловноеОформление();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УО = ЭтаФорма.УсловноеОформление.Элементы;	
	УО.Очистить();
	
	Запрос = Новый Запрос();
	Запрос.Текст =
		"ВЫБРАТЬ		
		|	Теги.Ссылка КАК Ссылка,
		|	Теги.ЦветТекста КАК ЦветТекста,
		|	Теги.ЦветФона КАК ЦветФона
		|ИЗ
		|	Справочник.Теги КАК Теги";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
        ЭлементУО 						= УО.Добавить();
        Если НЕ ПустаяСтрока(Выборка.ЦветФона) Тогда
        	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветФона", 
                РаботаСЦветомКлиентСервер.HexВЦвет(Выборка.ЦветФона));
        КонецЕсли; 
        Если НЕ ПустаяСтрока(Выборка.ЦветТекста) Тогда
            ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", 
                РаботаСЦветомКлиентСервер.HexВЦвет(Выборка.ЦветТекста));
		КонецЕсли;
			
        ЭлементУсловия 					= ЭлементУО.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
        ЭлементУсловия.ЛевоеЗначение 	= Новый ПолеКомпоновкиДанных("Список.Ссылка");
        ЭлементУсловия.ВидСравнения 	= ВидСравненияКомпоновкиДанных.Равно;
        ЭлементУсловия.ПравоеЗначение 	= Выборка.Ссылка;
        ПолеОформления 					= ЭлементУО.Поля.Элементы.Добавить();
       	ПолеОформления.Поле 	    	= Новый ПолеКомпоновкиДанных("Образец");
        ПолеОформления.Использование 	= Истина;
        		
	КонецЦикла;	
		
КонецПроцедуры

#КонецОбласти