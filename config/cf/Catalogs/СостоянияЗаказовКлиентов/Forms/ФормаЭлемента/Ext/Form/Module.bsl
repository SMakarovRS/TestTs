
#Область ОбработчикиСобытийФормы

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

// Процедура ПриСозданииНаСервере
//
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Цвет = Объект.Ссылка.Цвет.Получить();
		Если Объект.Ссылка = УправлениеITОтделом8УФПовтИсп.ПолучитьЗначениеНастройки("СостояниеНовогоЗаказаКлиента") Тогда
			Элементы.ФормаКомандаУстановитьОсновнойЭлемент.Заголовок = НСтр("ru = 'Используется при создании новых заказов'");
			Элементы.ФормаКомандаУстановитьОсновнойЭлемент.Доступность = Ложь;
		КонецЕсли; 
	Иначе
		ЗначениеКопирования = Неопределено;
		Параметры.Свойство("ЗначениеКопирования", ЗначениеКопирования);
		Если ЗначениеКопирования <> Неопределено Тогда
			Цвет = ЗначениеКопирования.Цвет.Получить();
		Иначе
			Цвет = Новый Цвет(0, 0, 0);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // ПриСозданииНаСервере()

// Процедура ПередЗаписьюНаСервере
//
&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если Цвет = Новый Цвет(0, 0, 0) Тогда
		ТекущийОбъект.Цвет = Новый ХранилищеЗначения(Неопределено);
	Иначе
		ТекущийОбъект.Цвет = Новый ХранилищеЗначения(Цвет);
	КонецЕсли;
	
КонецПроцедуры // ПередЗаписьюНаСервере()

// Процедура ПослеЗаписи
//
&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Оповестить("Запись_СостоянияЗаказовКлиентов");
	
КонецПроцедуры // ПослеЗаписи()

#КонецОбласти

#Область ОбработчикиКомандФормы

// Процедура - обработчик выполнения команды УстановитьОсновнойЭлемент.
//
&НаКлиенте
Процедура КомандаУстановитьОсновнойЭлемент(Команда)
	
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		УстановитьОсновнойЭлемент();
		Оповестить("ИзмененыНастройкиПользователя");
	Иначе
		ПоказатьПредупреждение(,НСтр("ru='Элемент необходимо вначале записать.'"));
	КонецЕсли;
	
КонецПроцедуры // КомандаУстановитьОсновнойЭлемент()

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ СОХРАНЕНИЯ НАСТРОЕК

// Процедура сохраняет выбранный элемент в настройки.
//
&НаСервере
Процедура УстановитьОсновнойЭлемент()
	
	Если Объект.Ссылка <> УправлениеITОтделом8УФПовтИсп.ПолучитьЗначениеНастройки("СостояниеНовогоЗаказаКлиента") Тогда
		УправлениеITОтделом8УФ.УстановитьНастройкуПользователя(Объект.Ссылка, "СостояниеНовогоЗаказаКлиента");	
		Элементы.ФормаКомандаУстановитьОсновнойЭлемент.Заголовок = НСтр("ru = 'Используется при создании новых заказов'");
		Элементы.ФормаКомандаУстановитьОсновнойЭлемент.Доступность = Ложь;
	КонецЕсли; 
		
КонецПроцедуры

#КонецОбласти