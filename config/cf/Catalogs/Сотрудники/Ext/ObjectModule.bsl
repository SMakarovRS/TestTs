#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

// Обработка события ПередЗаписью.
//
Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	// Нужна ли синхронизация при изменении сотрудника.
	Если НЕ ЭтоГруппа И УправлениеITОтделом8УФПовтИсп.ПолучитьКонстанту("СозданиеИзменениеМестХраненияПриИзмененииСотрудников") = Истина И ПравоДоступа("Изменение", Метаданные.Справочники.МестаХранения) Тогда
		   
		Если Ссылка.Пустая() ИЛИ НЕ ЗначениеЗаполнено(МестоХранения) Тогда
			МестоХраненияОбъект = Справочники.МестаХранения.СоздатьЭлемент();
			МестоХраненияОбъект.ВидМестаХранения = Справочники.ВидыМестХранения.РабочееМесто;
		Иначе
			МестоХраненияОбъект = МестоХранения.ПолучитьОбъект();
		КонецЕсли;
		
		МестоХраненияОбъект.Наименование	= Наименование;
		Если НЕ Актуальность Тогда
			МестоХраненияОбъект.Наименование= МестоХраненияОбъект.Наименование + " " + НСтр("ru = '(увол.)'");
		КонецЕсли;
		
		МестоХраненияОбъект.Организация 	= Организация;
		МестоХраненияОбъект.Подразделение 	= Подразделение;
		МестоХраненияОбъект.Местоположение	= Местоположение;
				
		МестоХраненияОбъект.Записать();
		
		Если Не ЗначениеЗаполнено(МестоХранения) И ПометкаУдаления = Ложь Тогда
			МестоХранения					= МестоХраненияОбъект.Ссылка;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
