
#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьОтборВидОтношений(Форма, ИмяПоля, Использование)
	
	ЧислоИзмененных = ОбщегоНазначенияКлиентСервер.ИзменитьЭлементыОтбора(
		Форма.Список.КомпоновщикНастроек.ФиксированныеНастройки.Отбор,
		ИмяПоля,
		,
		Истина,
		ВидСравненияКомпоновкиДанных.Равно,
		Использование,
		РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный);
		
	Если ЧислоИзмененных = 0 Тогда
		
		ГруппаВидОтношений = ОбщегоНазначенияКлиентСервер.НайтиЭлементОтбораПоПредставлению(
			Форма.Список.КомпоновщикНастроек.ФиксированныеНастройки.Отбор.Элементы, "ВидОтношений");
		
		Если ГруппаВидОтношений = Неопределено Тогда
			ГруппаВидОтношений = ОбщегоНазначенияКлиентСервер.СоздатьГруппуЭлементовОтбора(
				Форма.Список.КомпоновщикНастроек.ФиксированныеНастройки.Отбор.Элементы,
				"ВидОтношений",
				ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИЛИ);
		КонецЕсли;
		
		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
			ГруппаВидОтношений,
			ИмяПоля,
			ВидСравненияКомпоновкиДанных.Равно,
			Истина,
			,
			Использование,
			РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный);
			
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПараметрыРазмещения = ПодключаемыеКоманды.ПараметрыРазмещения();
	ПараметрыРазмещения.КоманднаяПанель = Элементы.КоманднаяПанель;
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект, ПараметрыРазмещения);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды		
		
	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	ПроверкаКонтрагентов.ПриСозданииНаСервереСписокКонтрагентов(Список);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	
	// Команды1СПАРКРиски
	СПАРКРиски.ДобавитьПодключаемыеКомандыКонтрагента(ЭтотОбъект, Список, Элементы.Подменю1СПАРКРиски);
	// Конец Команды1СПАРКРиски
	
	МожноРедактировать 									= ПравоДоступа("Редактирование", Метаданные.Справочники.Контрагенты);
    Элементы.ФормаГрупповоеИзменениеОбъектов.Видимость 	= МожноРедактировать;
	Элементы.ФормаОбъединитьВыделенные.Видимость		= МожноРедактировать;
	Элементы.ФормаПоказатьМестаИспользования.Видимость 	= МожноРедактировать;
	
	Если Параметры.Свойство("ОтборКлиенты") Тогда
		ОтборКлиенты = Параметры.ОтборКлиенты;		
	КонецЕсли;
	
	Если Параметры.Свойство("ОтборПоставщики") Тогда
		ОтборПоставщики = Параметры.ОтборПоставщики;		
	КонецЕсли;
	
	Если Параметры.Свойство("ОтборРемонтСервис") Тогда
		ОтборРемонтСервис = Параметры.ОтборРемонтСервис;		
	КонецЕсли;
		
	УстановитьОтборВидОтношений(ЭтотОбъект, "Клиент",		     ОтборКлиенты);
	УстановитьОтборВидОтношений(ЭтотОбъект, "Поставщик",	  ОтборПоставщики);
	УстановитьОтборВидОтношений(ЭтотОбъект, "РемонтСервис",	ОтборРемонтСервис);	
	
	Если УправлениеITОтделом8УФПовтИсп.ВедетсяСкладскойУчетКлиентов() Тогда
		Элементы.ОтборУчетОстатков.Видимость = Истина;
		Если Параметры.Свойство("ОтборУчетОстатков") Тогда
			ОтборУчетОстатков = Параметры.ОтборУчетОстатков;			
			УстановитьОтборВидОтношений(ЭтотОбъект, "УчетОстатков", ОтборУчетОстатков);
		КонецЕсли;		
	Иначе
		Элементы.ОтборУчетОстатков.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

// СтандартныеПодсистемы.ПоискИУдалениеДублей
&НаКлиенте
Процедура ОбъединитьВыделенные(Команда)
    ПоискИУдалениеДублейКлиент.ОбъединитьВыделенные(Элементы.Список);
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьМестаИспользования(Команда)
	ПоискИУдалениеДублейКлиент.ПоказатьМестаИспользования(Элементы.Список);	
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПоискИУдалениеДублей

// СтандартныеПодсистемы.ГрупповоеИзменениеОбъектов
&НаКлиенте
Процедура ИзменитьВыделенные(Команда)
	ГрупповоеИзменениеОбъектовКлиент.ИзменитьВыделенные(Элементы.Список, Список);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ГрупповоеИзменениеОбъектов

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Элементы.Список);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Элементы.Список, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Элементы.Список);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

// Команды1СПАРКРиски
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду1СПАРКРиски(Команда)
	
	СПАРКРискиКлиент.ВыполнитьПодключаемуюКоманду(Команда, ЭтотОбъект, Элементы.Список);
	
КонецПроцедуры
// Конец Команды1СПАРКРиски

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы	

&НаКлиенте
Процедура ОтборКлиентПриИзменении(Элемент)
	УстановитьОтборВидОтношений(ЭтотОбъект, "Клиент", ОтборКлиенты);
КонецПроцедуры

&НаКлиенте
Процедура ОтборПоставщикПриИзменении(Элемент)
	УстановитьОтборВидОтношений(ЭтотОбъект, "Поставщик", ОтборПоставщики);
КонецПроцедуры

&НаКлиенте
Процедура ОтборРемонтСервисПриИзменении(Элемент)
	УстановитьОтборВидОтношений(ЭтотОбъект, "РемонтСервис", ОтборРемонтСервис);	
КонецПроцедуры

&НаКлиенте
Процедура ОтборУчетОстатковПриИзменении(Элемент)
	УстановитьОтборВидОтношений(ЭтотОбъект, "УчетОстатков", ОтборУчетОстатков);	
КонецПроцедуры

&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	ЗначенияЗаполнения = Новый Структура;
	ЗначенияЗаполнения.Вставить("Клиент",		ОтборКлиенты);
	ЗначенияЗаполнения.Вставить("Поставщик",	ОтборПоставщики);
	ЗначенияЗаполнения.Вставить("РемонтСервис",	ОтборРемонтСервис);
	ЗначенияЗаполнения.Вставить("УчетОстатков",	ОтборУчетОстатков);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
	ОткрытьФорму("Справочник.Контрагенты.ФормаОбъекта", ПараметрыФормы, Элемент);
	
КонецПроцедуры

#КонецОбласти
