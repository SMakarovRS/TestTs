
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Заголовок 					= Параметры.Заголовок;
	Метод 						= Параметры.Метод;
	НазваниеПроцедуры 			= Параметры.НазваниеПроцедуры;
	ЗаполнитьТаблицуПараметров();	
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТаблица

&НаКлиенте
Процедура ТаблицаПередУдалением(Элемент, Отказ)
	
	Отказ = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Вставить(Команда)
	
	Ответ = "Сообщение = Новый Структура;";
	Ответ = Ответ + Символы.ПС;
	
	ПоляПоиска = Новый Структура("Добавить", Истина);
	СтрокиДобавления = Таблица.НайтиСтроки(ПоляПоиска);
	Для Каждого СтрокаПеренести Из СтрокиДобавления Цикл
		
		Если ДобавитьКомментарии Тогда
			Ответ = Ответ + ФорматироватьВКомментарий(СтрокаПеренести.Описание);
		КонецЕсли;
		Ответ = Ответ + СтрШаблон("Сообщение.Вставить(""%1"", <%2>);", СтрокаПеренести.Параметр, 
			СтрокаПеренести.ТипПараметра) + Символы.ПС;
		
	КонецЦикла;
	
	Ответ = Ответ + Символы.ПС + СтрШаблон("%1(Сообщение, НакопленныеДанные);", НазваниеПроцедуры);
		
	Закрыть(Ответ);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьТаблицуПараметров()
	
	Таблица.Очистить();
	Если Метод = "sendMessage" Тогда
		
		ДобавитьИдентификаторЧата(Таблица);
		ДобавитьТекст(Таблица, Истина, Истина);
		ДобавитьВыключитьПредпросмотрВебСтраницы(Таблица);
		ДобавитьВыключитьУведомление(Таблица);
		ДобавитьОтвет(Таблица);
		ДобавитьКлавиатура(Таблица);
		ДобавитьРежимАнализаТекста(Таблица);
		ДобавитьСкрытьКлавиатуру(Таблица);
		
	ИначеЕсли Метод = "sendPhoto" Тогда
		
		ДобавитьИдентификаторЧата(Таблица);
		ДобавитьПодпись(Таблица);
		ДобавитьВыключитьУведомление(Таблица);
		ДобавитьОтвет(Таблица);
		ДобавитьКлавиатура(Таблица);
		ДобавитьФайл(Таблица, Истина, Истина);
		ДобавитьИмяФайла(Таблица, Истина, Истина);
		ДобавитьСкрытьКлавиатуру(Таблица);
		
	ИначеЕсли Метод = "sendVideo" Тогда
		
		ДобавитьИдентификаторЧата(Таблица);
		ДобавитьПодпись(Таблица);
		ДобавитьВыключитьУведомление(Таблица);
		ДобавитьОтвет(Таблица);
		ДобавитьКлавиатура(Таблица);
		ДобавитьФайл(Таблица, Истина, Истина);
		ДобавитьИмяФайла(Таблица, Истина, Истина);
		ДобавитьСкрытьКлавиатуру(Таблица);
		
		// Не хватает (на данный момент игнорируем).
		// duration	Integer	Необязательный	Duration of sent video in seconds
		// width	Integer	Необязательный	Video width
		// height	Integer	Необязательный	Video height
		
	ИначеЕсли Метод = "sendAudio" Тогда
		
		ДобавитьИдентификаторЧата(Таблица);
		ДобавитьПодпись(Таблица);
		ДобавитьВыключитьУведомление(Таблица);
		ДобавитьОтвет(Таблица);
		ДобавитьКлавиатура(Таблица);
		ДобавитьФайл(Таблица, Истина, Истина);
		ДобавитьИмяФайла(Таблица, Истина, Истина);
		ДобавитьСкрытьКлавиатуру(Таблица);
		
		// Не хватает (на данный момент игнорируем).
		// duration	Integer	Необязательный	Duration of the audio in seconds
		// performer	String	Необязательный	Performer
		// title	String	Необязательный	Track name
		
	ИначеЕсли Метод = "sendDocument" Тогда
		
		ДобавитьИдентификаторЧата(Таблица);
		ДобавитьПодпись(Таблица);
		ДобавитьВыключитьУведомление(Таблица);
		ДобавитьОтвет(Таблица);
		ДобавитьКлавиатура(Таблица);
		ДобавитьФайл(Таблица, Истина, Истина);
		ДобавитьИмяФайла(Таблица, Истина, Истина);
		ДобавитьСкрытьКлавиатуру(Таблица);
		
	ИначеЕсли Метод = "sendVoice" Тогда
		
		ДобавитьИдентификаторЧата(Таблица);
		ДобавитьПодпись(Таблица);
		ДобавитьВыключитьУведомление(Таблица);
		ДобавитьОтвет(Таблица);
		ДобавитьКлавиатура(Таблица);
		ДобавитьФайл(Таблица, Истина, Истина);
		ДобавитьИмяФайла(Таблица, Истина, Истина);
		ДобавитьСкрытьКлавиатуру(Таблица);
		
		// Не хватает (на данный момент игнорируем).
		// duration	Integer	Необязательный	Duration of sent audio in seconds
		
	ИначеЕсли Метод = "sendLocation" Тогда
		
		ДобавитьИдентификаторЧата(Таблица);
		ДобавитьВыключитьУведомление(Таблица);
		ДобавитьОтвет(Таблица);
		ДобавитьКлавиатура(Таблица);
		ДобавитьШирота(Таблица, Истина, Истина);
		ДобавитьДолгота(Таблица, Истина, Истина);
		ДобавитьСкрытьКлавиатуру(Таблица);
		
	ИначеЕсли Метод = "sendVenue" Тогда
		
		ДобавитьИдентификаторЧата(Таблица);
		ДобавитьВыключитьУведомление(Таблица);
		ДобавитьОтвет(Таблица);
		ДобавитьКлавиатура(Таблица);
		ДобавитьШирота(Таблица, Истина, Истина);
		ДобавитьДолгота(Таблица, Истина, Истина);
		ДобавитьАдрес(Таблица, Истина, Истина);
		ДобавитьПодпись(Таблица, Истина, Истина);
		ДобавитьСкрытьКлавиатуру(Таблица);
		
	ИначеЕсли Метод = "editMessageText" Тогда
		
		ДобавитьИдентификаторЧата(Таблица);
		ДобавитьИдентификаторИзменяемогоСообщения(Таблица);
		ДобавитьТекст(Таблица, Истина, Истина);
		ДобавитьВыключитьПредпросмотрВебСтраницы(Таблица);
		ДобавитьРежимАнализаТекста(Таблица);
		ДобавитьКлавиатура(Таблица);
		ДобавитьСкрытьКлавиатуру(Таблица);
		
	ИначеЕсли Метод = "deleteMessage" Тогда
		
		ДобавитьИдентификаторЧата(Таблица);
		ДобавитьИдентификаторУдаляемогоСообщения(Таблица);
		
	КонецЕсли;
	
	Таблица.Сортировать("Параметр");
	
КонецПроцедуры

&НаКлиенте
Функция ФорматироватьВКомментарий(Знач Описание)
	
	Если ЗначениеЗаполнено(Описание) Тогда
		Ответ = СтрЗаменить(Описание, Символы.ПС, Символы.ПС + "// ");
		Ответ = "// " + Ответ;
		Ответ = Символы.ПС + Ответ + Символы.ПС;
	Иначе
		Ответ = "";
	КонецЕсли;
	
	Возврат Ответ;
	
КонецФункции

&НаСервере
Процедура ДобавитьИдентификаторЧата(Таб, Знач Добавить = Ложь, Знач Обязательное = Ложь)
	
	НоваяСтрока 				= Таб.Добавить();
	НоваяСтрока.Параметр 		= "ИдентификаторЧата";
	НоваяСтрока.ТипПараметра	= "Число";
	НоваяСтрока.Описание		= НСтр("ru = 'Идентификатор чата текущего собеседника — здесь его можно переопределить.'");
	НоваяСтрока.Добавить		= Добавить;
	НоваяСтрока.Обязательное	= Обязательное;
		
КонецПроцедуры

&НаСервере
Процедура ДобавитьТекст(Таб, Знач Добавить = Ложь, Знач Обязательное = Ложь)
	
	НоваяСтрока 				= Таб.Добавить();
	НоваяСтрока.Параметр 		= "Текст";
	НоваяСтрока.ТипПараметра	= "Строка";
	НоваяСтрока.Описание		= НСтр("ru = 'Текст сообщения, которое будет отправлено.'");
	НоваяСтрока.Добавить		= Добавить;
	НоваяСтрока.Обязательное	= Обязательное;
		
КонецПроцедуры

&НаСервере
Процедура ДобавитьВыключитьПредпросмотрВебСтраницы(Таб, Знач Добавить = Ложь, Знач Обязательное = Ложь)
	
	НоваяСтрока 				= Таб.Добавить();
	НоваяСтрока.Параметр 		= "ВыключитьПредпросмотрВебСтраницы";
	НоваяСтрока.ТипПараметра	= "Булево";
	НоваяСтрока.Описание		= НСтр("ru = 'Отключает превью страницы для ссылки в этом сообщении.'");
	НоваяСтрока.Добавить		= Добавить;
	НоваяСтрока.Обязательное	= Обязательное;
		
КонецПроцедуры

&НаСервере
Процедура ДобавитьВыключитьУведомление(Таб, Знач Добавить = Ложь, Знач Обязательное = Ложь)
	
	НоваяСтрока 				= Таб.Добавить();
	НоваяСтрока.Параметр 		= "ВыключитьУведомление";
	НоваяСтрока.ТипПараметра	= "Булево";
	НоваяСтрока.Описание		= 
		НСтр("ru = 'Посылает сообщение молча.
              |Пользователи IOS не будут получать уведомления, пользователи Android получат уведомление без звука.'");
	НоваяСтрока.Добавить		= Добавить;
	НоваяСтрока.Обязательное	= Обязательное;
		
КонецПроцедуры

&НаСервере
Процедура ДобавитьОтвет(Таб, Знач Добавить = Ложь, Знач Обязательное = Ложь)
	
	НоваяСтрока 				= Таб.Добавить();
	НоваяСтрока.Параметр 		= "Ответ";
	НоваяСтрока.ТипПараметра	= "Число";
	НоваяСтрока.Описание		= НСтр("ru = 'Если сообщение является ответом, идентификатор исходного сообщения.'");
	НоваяСтрока.Добавить		= Добавить;
	НоваяСтрока.Обязательное	= Обязательное;
		
КонецПроцедуры

&НаСервере
Процедура ДобавитьКлавиатура(Таб, Знач Добавить = Ложь, Знач Обязательное = Ложь)
	
	НоваяСтрока 				= Таб.Добавить();
	НоваяСтрока.Параметр 		= "Клавиатура";
	НоваяСтрока.ТипПараметра	= "Строка";
	НоваяСтрока.Описание		= НСтр("ru = 'Точное наименование передаваемой клавиатуры (как оно задано в справочнике).'");
	НоваяСтрока.Добавить		= Добавить;
	НоваяСтрока.Обязательное	= Обязательное;
		
КонецПроцедуры

&НаСервере
Процедура ДобавитьПодпись(Таб, Знач Добавить = Ложь, Знач Обязательное = Ложь)
	
	НоваяСтрока 				= Таб.Добавить();
	НоваяСтрока.Параметр 		= "Подпись";
	НоваяСтрока.ТипПараметра	= "Строка";
	НоваяСтрока.Описание		= НСтр("ru = 'Подпись под фото.'");
	НоваяСтрока.Добавить		= Добавить;
	НоваяСтрока.Обязательное	= Обязательное;
		
КонецПроцедуры

&НаСервере
Процедура ДобавитьФайл(Таб, Знач Добавить = Ложь, Знач Обязательное = Ложь)
	
	НоваяСтрока 				= Таб.Добавить();
	НоваяСтрока.Параметр 		= "Файл";
	НоваяСтрока.ТипПараметра	= "ДвоичныеДанные, Строка";
	НоваяСтрока.Описание		= НСтр("ru = 'Передаваемый файл на сервер Telegram. Может быть нескольких типов:
                                        |- Бинарные данные (ДвоичныеДанные) - данные файла;
                                        |- Путь к файлу (Строка) - полный путь к файлу на диске доступный на сервере 1С (C:\Files.png);
                                        |- Адрес двоичных данных во временном хранилище (Строка) - ДвоичныеДанные помещенные методом ПоместитьВоВременноеХранилище.'");
	НоваяСтрока.Добавить		= Добавить;
	НоваяСтрока.Обязательное	= Обязательное;
		
КонецПроцедуры

&НаСервере
Процедура ДобавитьИмяФайла(Таб, Знач Добавить = Ложь, Знач Обязательное = Ложь)
	
	НоваяСтрока 				= Таб.Добавить();
	НоваяСтрока.Параметр 		= "ИмяФайла";
	НоваяСтрока.ТипПараметра	= "Строка";
	НоваяСтрока.Описание		= НСтр("ru = 'Имя передаваемого файла в виде ""Файл.png"" без кавычек.'");
	НоваяСтрока.Добавить		= Добавить;
	НоваяСтрока.Обязательное	= Обязательное;
		
КонецПроцедуры

&НаСервере
Процедура ДобавитьРежимАнализаТекста(Таб, Знач Добавить = Ложь, Знач Обязательное = Ложь)
	
	НоваяСтрока 				= Таб.Добавить();
	НоваяСтрока.Параметр 		= "РежимАнализаТекста";
	НоваяСтрока.ТипПараметра	= "Строка";
	НоваяСтрока.Описание		= НСтр("ru = 'Может принимать значение ""HTML"" или ""Markdown"" (без кавычек).
                                        |В этом случае, в тексте отправляемого сообщения будет выполнен сервером Telegram анализ и отображен форматированный текст (полужирный, курсивный, подчеркивания  или встроенные URL-адреса в сообщении вашего бота и т.д.).
                                        |Для HTML поддерживаются теги:
                                        |<b>жирный</b>, <strong>жирный</strong>
                                        |<i>курсив</i>, <em>курсив</em>
                                        |<a href=""URL"">Адрес сайта</a>
                                        |<code>Код</code>
                                        |<pre>не форматированный код</pre>
                                        |&lt; - символ ""<""
                                        |&gt; - символ "">""
                                        |&amp; - символ ""&""
                                        |&quot; - символ циататы
                                        |Все остальные теги должны отсутствовать.
                                        |Для Markdown поддерживается:
                                        |*жирный*
                                        |_курсив_
                                        |[Текст сайта](URL)
                                        |`Код`
                                        |```Не форматированный код```'");
	НоваяСтрока.Добавить		= Добавить;
	НоваяСтрока.Обязательное	= Обязательное;
		
КонецПроцедуры

&НаСервере
Процедура ДобавитьШирота(Таб, Знач Добавить = Ложь, Знач Обязательное = Ложь)
	
	НоваяСтрока 				= Таб.Добавить();
	НоваяСтрока.Параметр 		= "Широта";
	НоваяСтрока.ТипПараметра	= НСтр("ru = 'Число с плавающей точкой'");
	НоваяСтрока.Описание		= НСтр("ru = 'Координата широты'");
	НоваяСтрока.Добавить		= Добавить;
	НоваяСтрока.Обязательное	= Обязательное;
		
КонецПроцедуры

&НаСервере
Процедура ДобавитьДолгота(Таб, Знач Добавить = Ложь, Знач Обязательное = Ложь)
	
	НоваяСтрока 				= Таб.Добавить();
	НоваяСтрока.Параметр 		= "Долгота";
	НоваяСтрока.ТипПараметра	= НСтр("ru = 'Число с плавающей точкой'");
	НоваяСтрока.Описание		= НСтр("ru = 'Координата долготы'");
	НоваяСтрока.Добавить		= Добавить;
	НоваяСтрока.Обязательное	= Обязательное;
		
КонецПроцедуры

&НаСервере
Процедура ДобавитьАдрес(Таб, Знач Добавить = Ложь, Знач Обязательное = Ложь)
	
	НоваяСтрока 				= Таб.Добавить();
	НоваяСтрока.Параметр 		= "Адрес";
	НоваяСтрока.ТипПараметра	= "Строка";
	НоваяСтрока.Описание		= НСтр("ru = 'Название места'");
	НоваяСтрока.Добавить		= Добавить;
	НоваяСтрока.Обязательное	= Обязательное;
		
КонецПроцедуры

&НаСервере
Процедура ДобавитьИдентификаторИзменяемогоСообщения(Таб, Знач Добавить = Ложь, Знач Обязательное = Ложь)
	
	НоваяСтрока 				= Таб.Добавить();
	НоваяСтрока.Параметр 		= "ИдентификаторСообщения";
	НоваяСтрока.ТипПараметра	= "Число";
	НоваяСтрока.Описание		= НСтр("ru = 'Идентификатор сообщения, которое будет изменено. Если не заполнен, то автоподставляется из НакопленныеДанные.ИдентификаторСообщения.'");
	НоваяСтрока.Добавить		= Добавить;
	НоваяСтрока.Обязательное	= Обязательное;
		
КонецПроцедуры

&НаСервере
Процедура ДобавитьИдентификаторУдаляемогоСообщения(Таб, Знач Добавить = Ложь, Знач Обязательное = Ложь)
	
	НоваяСтрока 				= Таб.Добавить();
	НоваяСтрока.Параметр 		= "ИдентификаторСообщения";
	НоваяСтрока.ТипПараметра	= "Число";
	НоваяСтрока.Описание		= НСтр("ru = 'Идентификатор удаляемого сообщения, которое будет изменено. Если не заполнен, то автоподставляется из НакопленныеДанные.ИдентификаторСообщения.'");
	НоваяСтрока.Добавить		= Добавить;
	НоваяСтрока.Обязательное	= Обязательное;
		
КонецПроцедуры

&НаСервере
Процедура ДобавитьСкрытьКлавиатуру(Таб, Знач Добавить = Ложь, Знач Обязательное = Ложь)
	
	НоваяСтрока 				= Таб.Добавить();
	НоваяСтрока.Параметр 		= "СкрытьКлавиатуру";
	НоваяСтрока.ТипПараметра	= "Булево";
	НоваяСтрока.Описание		= НСтр("ru = 'Скрывает клавиатуру если она видима. Имеет больший приоритет над определенным свойством ""Клавиатура"".'");
	НоваяСтрока.Добавить		= Добавить;
	НоваяСтрока.Обязательное	= Обязательное;
		
КонецПроцедуры

#КонецОбласти