
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Если Объект.Ссылка.Пустая() Тогда
				
		ТекущаяДата				= ТекущаяДатаСеанса();
		Объект.ВидПериода		= Перечисления.ВидПериодаПервичногоДокумента.Месяц;
		Объект.Год				= Год(ТекущаяДата);		
		Объект.ЗначениеПериода	= Перечисления.ПериодыПервичныхДокументов[Месяц(ТекущаяДата) - 1];
		
		АвтоНаименование();
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ВидПериодаПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Объект.ВидПериода) Тогда
		
		Объект.ЗначениеПериода = 
				ПредопределенноеЗначение("Перечисление.ПериодыПервичныхДокументов.ПустаяСсылка");

		Если Объект.ВидПериода = 
			ПредопределенноеЗначение("Перечисление.ВидПериодаПервичногоДокумента.Год") Тогда
			
			Объект.ЗначениеПериода = 
				ПредопределенноеЗначение("Перечисление.ПериодыПервичныхДокументов.ПустаяСсылка");
				
			Элементы.ЗначениеПериода.Видимость = Ложь;
			
		ИначеЕсли Объект.ВидПериода = 
			ПредопределенноеЗначение("Перечисление.ВидПериодаПервичногоДокумента.Разовый") Тогда
			
			Объект.ЗначениеПериода = 
				ПредопределенноеЗначение("Перечисление.ПериодыПервичныхДокументов.ПустаяСсылка");
				
			Объект.Год = 0;	
			Элементы.ЗначениеПериода.Видимость	= Ложь;	
			Элементы.Год.Видимость 				= Ложь;	
			
		Иначе
			Элементы.ЗначениеПериода.Видимость = Истина;
			
		КонецЕсли;	
	КонецЕсли;	
	
	АвтоНаименование();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗначениеПериодаПриИзменении(Элемент)
	
	АвтоНаименование();
	
КонецПроцедуры

&НаКлиенте
Процедура ГодПриИзменении(Элемент)
	
	АвтоНаименование();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗначениеПериодаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ЗаполнитьЗначенияПериода();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьЗначенияПериода()
	
	Элементы.ЗначениеПериода.СписокВыбора.Очистить();
	
	Если Объект.ВидПериода = Перечисления.ВидПериодаПервичногоДокумента.Год Тогда
		Элементы.ЗначениеПериода.Видимость = Ложь;
		Возврат;
	КонецЕсли;
	
	Элементы.ЗначениеПериода.Видимость = Истина;
	
	мПараметрыВыбора= Новый Структура;	
	мОтбор			= Новый Структура;	
	мОтбор.Вставить("ВидПериода", Объект.ВидПериода); 
	мПараметрыВыбора.Вставить("Отбор", мОтбор);
	мПараметрыВыбора.Вставить("СтрокаПоиска", "");
	
	ЗначенияПериода = Перечисления.ПериодыПервичныхДокументов.ПолучитьДанныеВыбора(мПараметрыВыбора);
	Элементы.ЗначениеПериода.СписокВыбора.ЗагрузитьЗначения(ЗначенияПериода.ВыгрузитьЗначения());
	
КонецПроцедуры	

&НаСервере
Процедура АвтоНаименование()
	
	Объект.Наименование = ПервичныеДокументыСервер.АвтонаименованиеПериода(Объект);	
	Модифицированность = Истина;
	
КонецПроцедуры	

#КонецОбласти