#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ПрограммныйИнтерфейс

Функция Inbox(Знач Пользователь = Неопределено) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Пользователь = Неопределено Тогда
		Пользователь = Пользователи.ТекущийПользователь();
	КонецЕсли;
	
    Запрос = Новый Запрос();
    Запрос.Текст =
        "ВЫБРАТЬ
        |   Дела.Ссылка КАК Ссылка
        |ИЗ
        |   Справочник.Дела КАК Дела
        |ГДЕ
        |   Дела.ПометкаУдаления = ЛОЖЬ
        |   И Дела.Пользователь = &Пользователь
        |   И Дела.Идентификатор = &Идентификатор";
    Запрос.УстановитьПараметр("Пользователь", Пользователь);
    Запрос.УстановитьПараметр("Идентификатор", "Inbox");
    
    Выборка = Запрос.Выполнить().Выбрать();
    Если Выборка.Следующий() Тогда
    	Возврат Выборка.Ссылка;
    КонецЕсли; 
    
	Входящие 			= Справочники.Дела.Inbox.ПолучитьОбъект();
	Входящие.ЭтоПапка 	= Истина;
	Входящие.Тема 		= НСтр("ru = '<Inbox>'");
	Входящие.Важность 	= 100;
	Входящие.Срочность 	= 100;
	Входящие.Усилие 	= 100;
	Входящие.ВидЦели 	= Перечисления.ВидЦелиДела.Нет;
	Входящие.ЭтоПапка	= Истина;
	Входящие.ДополнительныеСвойства.Вставить("ИгнорированиеПроверок", Истина);
    Входящие.Пользователь = Пользователи.ТекущийПользователь();
    Входящие.Идентификатор = "Inbox";
	Входящие.Записать();    
    
    Возврат Входящие.Ссылка;
    
КонецФункции

Процедура ОбработкаПолученияПолейПредставления(Поля, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Поля.Добавить("Тема");
	
КонецПроцедуры

Процедура ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
    Представление = Данные.Тема;
    
КонецПроцедуры

#Область ТекущиеДела

// Заполняет список текущих дел пользователя.
// Описание параметров процедуры см. в ТекущиеДелаСлужебный.НоваяТаблицаТекущихДел().
//
Процедура ПриЗаполненииСпискаТекущихДел(ТекущиеДела) Экспорт

	ГруппаДел 	= НСтр("ru = 'Дела'");
	ИмяФормы			= "Справочник.Дела.Форма.ФормаСписка";
	
	Если Не ПравоДоступа("Редактирование", Метаданные.Справочники.Дела)
		ИЛИ ТекущиеДелаСервер.ДелоОтключено(ГруппаДел)
		ИЛИ УправлениеITОтделом8УФПовтИсп.ПолучитьКонстанту("ИспользоватьДела") = Ложь Тогда
		
		Возврат;
	КонецЕсли;
	
	ОбщиеПараметрыЗапросов = ТекущиеДелаСлужебный.ОбщиеПараметрыЗапросов();
		
	Запрос = Новый Запрос;
	Запрос.Текст = 
	    "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	    |	СУММА(ВЫБОР
	    |			КОГДА Дела.Срок >= &ДатаНач
	    |					И Дела.Срок <= &ДатаКон
	    |				ТОГДА 1
	    |			ИНАЧЕ 0
	    |		КОНЕЦ) КАК КоличествоСегодня,
	    |	СУММА(ВЫБОР
	    |			КОГДА Дела.Срок < &ДатаНач
	    |				ТОГДА 1
	    |			ИНАЧЕ 0
	    |		КОНЕЦ) КАК КоличествоПросрочено
	    |ИЗ
	    |	Справочник.Дела КАК Дела
	    |ГДЕ
	    |	Дела.Пользователь = &Пользователь
	    |	И Дела.Срок <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	    |	И Дела.ПометкаУдаления = ЛОЖЬ
	    |	И Дела.Выполнено = ЛОЖЬ
	    |АВТОУПОРЯДОЧИВАНИЕ";
		
	Запрос.УстановитьПараметр("Пользователь", ОбщиеПараметрыЗапросов.Пользователь);
	Запрос.УстановитьПараметр("ДатаНач", НачалоДня(ТекущаяДатаСеанса()));
	Запрос.УстановитьПараметр("ДатаКон", КонецДня(ТекущаяДатаСеанса()));
		
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда

		Количество = ?(ТипЗнч(Выборка.КоличествоПросрочено) = Тип("Число"), Выборка.КоличествоПросрочено, 0);
		Дело = ТекущиеДела.Добавить();
		Дело.Идентификатор	 = "ДелаПросрочено";
		Дело.ЕстьДела		= Количество > 0;
		Дело.Представление	= НСтр("ru = 'Просрочено'");
		Дело.Количество		= Количество;
		Дело.Форма			= ИмяФормы;
		Дело.ПараметрыФормы	= Новый Структура("Просрочено", Истина);
		Дело.Владелец		= ГруппаДел;
		Дело.Подсказка		= "";
		
		Количество = ?(ТипЗнч(Выборка.КоличествоСегодня) = Тип("Число"), Выборка.КоличествоСегодня, 0);
		Дело = ТекущиеДела.Добавить();
		Дело.Идентификатор	 = "ДелаСегодня";
		Дело.ЕстьДела		= Количество > 0;
		Дело.Представление	= НСтр("ru = 'Сегодня'");
		Дело.Количество		= Количество;
		Дело.Форма			= ИмяФормы;
		Дело.ПараметрыФормы	= Новый Структура("Сегодня", Истина);
		Дело.Владелец		= ГруппаДел;
		Дело.Подсказка		= "";
		
	КонецЕсли;
		
КонецПроцедуры
	
#КонецОбласти

#КонецОбласти

#КонецЕсли