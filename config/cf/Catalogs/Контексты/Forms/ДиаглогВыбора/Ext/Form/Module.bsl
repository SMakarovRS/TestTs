
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	МассивКонтекстов = Новый Массив;
	Если Параметры.Свойство("Контексты", МассивКонтекстов) = Ложь Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Контексты.Очистить();
	Запрос = Новый Запрос();
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВложенныйЗапрос.Контекст КАК Контекст,
		|	СУММА(1) КАК Количество,
		|	ЛОЖЬ КАК Флаг
		|ИЗ
		|	(ВЫБРАТЬ РАЗЛИЧНЫЕ
		|		ВложенныйЗапрос.Контекст КАК Контекст
		|	ИЗ
		|		(ВЫБРАТЬ
		|			Контексты.Ссылка КАК Контекст
		|		ИЗ
		|			Справочник.Контексты КАК Контексты
		|		ГДЕ
		|			Контексты.Пользователь = &Пользователь
		|			И Контексты.ПометкаУдаления = ЛОЖЬ
		|			И Контексты.СпрятатьКонтекстПриВыбореВДеле = ЛОЖЬ
		|		
		|		ОБЪЕДИНИТЬ ВСЕ
		|		
		|		ВЫБРАТЬ
		|			КонтекстыВидимость.Ссылка
		|		ИЗ
		|			Справочник.Контексты.Видимость КАК КонтекстыВидимость
		|		ГДЕ
		|			КонтекстыВидимость.Пользователь = &Пользователь
		|			И КонтекстыВидимость.Ссылка.ПометкаУдаления = ЛОЖЬ
		|			И КонтекстыВидимость.Ссылка.СпрятатьКонтекстПриВыбореВДеле = ЛОЖЬ) КАК ВложенныйЗапрос) КАК ВложенныйЗапрос
		|
		|СГРУППИРОВАТЬ ПО
		|	ВложенныйЗапрос.Контекст
		|
		|УПОРЯДОЧИТЬ ПО
		|	ВложенныйЗапрос.Контекст.Представление";
	Запрос.УстановитьПараметр("Пользователь", Пользователи.ТекущийПользователь());
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		НоваяСтрока = Контексты.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
		Если МассивКонтекстов.Найти(Выборка.Контекст) <> Неопределено Тогда
			НоваяСтрока.Флаг = Истина;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Выбрать(Команда)
	
	Массив = Новый Массив;
	Для Каждого Строки Из Контексты Цикл
		Если Строки.Флаг = Истина Тогда
			Массив.Добавить(Строки.Контекст);
		КонецЕсли;
	КонецЦикла;
	ОповеститьОВыборе(Массив);
	
КонецПроцедуры

#КонецОбласти


