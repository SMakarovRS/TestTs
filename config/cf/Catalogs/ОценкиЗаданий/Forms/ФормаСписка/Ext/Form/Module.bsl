
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Список.Порядок.Элементы.Очистить();
	НовыйПорядок = Список.Порядок.Элементы.Добавить(Тип("ЭлементПорядкаКомпоновкиДанных"));
	    
	НовыйПорядок.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Авто;
	НовыйПорядок.ТипУпорядочивания = НаправлениеСортировкиКомпоновкиДанных.Возр;
	    
	// Реквизит сортировки - Порядок.
	НовыйПорядок.Поле = Новый ПолеКомпоновкиДанных("Порядок");
	НовыйПорядок.Использование = Истина;
	
	ШаблонОформления = Справочники.ОформленияОповещенийEmailПравилСобытий.СовременныйШаблон;
	
	ОбновитьПредпросмотр();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_ОценкиЗаданий" Тогда
		
		ОбновитьПредпросмотр();
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ТестовоеЗаданиеПриИзменении(Элемент)
	
	ОбновитьПредпросмотр();
	
КонецПроцедуры

&НаКлиенте
Процедура ШаблонОформленияПриИзменении(Элемент)
	
	ОбновитьПредпросмотр();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОбновитьПредпросмотр1(Команда)
	
	ОбновитьПредпросмотр();
	
КонецПроцедуры

&НаКлиенте
Процедура ТекстовыеОценкиКнопки(Команда)
	
	ЗаполнитьПредопределенныеНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура КартинкиОценки(Команда)
	
	ЗаполнитьПредопределенныеНаСервере("КартинкиОценки");
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьПредопределенныеНаСервере(Знач ИмяНастроек = "")
	
	Справочники.ОценкиЗаданий.ЗаполнитьОценкиЗаданийПриПервоначальномЗаполнении(ИмяНастроек);
	Элементы.Список.Обновить();
	ОбновитьПредпросмотр();
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьПредпросмотр()
	
	Структура = ПравилаСобытийСервер.ПараметрыШаблонаСообщенияМеханизмОценкиВыполнения(ТестовоеЗадание);
	Если ЗначениеЗаполнено(ШаблонОформления) Тогда
		ТекстHTML = ШаблонОформления.БлокШапка 
			+ "<p>" + НСтр("ru = 'Здесь основной текст письма'") + "</p>"
			+ Структура.ТекстHTML + ШаблонОформления.БлокПодвал;
	Иначе
		ТекстHTML = НСтр("ru = 'Выберите шаблон оформления'");
	КонецЕсли;
	
	Текст = Структура.Текст;
	
КонецПроцедуры

#КонецОбласти