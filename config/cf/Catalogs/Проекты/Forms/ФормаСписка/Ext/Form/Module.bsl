#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды 
	
	УстановитьУсловноеОформление();
	
	ОбновитьВидимостьДоступность();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ОбновитьЦветаПроектов" Тогда
		УстановитьУсловноеОформление();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПоказыватьНеАктуальныеПроекты(Команда)
	
	ПоказыватьНеАктуальные = НЕ ПоказыватьНеАктуальные;
	ОбновитьВидимостьДоступность();
	
КонецПроцедуры

&НаКлиенте
Процедура УправлениеПроектом(Команда)
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура("Проект", ТекущиеДанные.Ссылка);
	ОткрытьФорму("Отчет.УправлениеПроектами.Форма.ФормаУправляемая", ПараметрыФормы, 
		ЭтаФорма, 
		ТекущиеДанные.Ссылка);
	
КонецПроцедуры

#Область ОбработчикиБиблиотек

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Элементы.Список);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Элементы.Список, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Элементы.Список);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УО = Список.УсловноеОформление.Элементы;
	УО.Очистить();
	Запрос = Новый Запрос();
	Запрос.Текст =
		"ВЫБРАТЬ
		|	Проекты.Ссылка КАК Ссылка,
		|	Проекты.ЦветФона КАК ЦветФона,
		|	Проекты.ЦветТекста КАК ЦветТекста
		|ИЗ
		|	Справочник.Проекты КАК Проекты";
	
	мWebЦветаБелый = WebЦвета.Белый;
	мWebЦветаЧерный = WebЦвета.Черный;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Попытка
			ЦветФона 	= РаботаСЦветомКлиентСервер.HexВЦвет(Выборка.ЦветФона);
			ЦветТекста 	= РаботаСЦветомКлиентСервер.HexВЦвет(Выборка.ЦветТекста);
		Исключение
			ЦветФона 	= мWebЦветаБелый;
			ЦветТекста 	= мWebЦветаЧерный;
		КонецПопытки;
		
		Если ЦветФона <> мWebЦветаБелый ИЛИ ЦветТекста <> мWebЦветаЧерный Тогда
			ЭлементУО 						= УО.Добавить();
			Если ЦветФона <> мWebЦветаБелый Тогда
				ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветФона", ЦветФона);
			КонецЕсли;
			Если ЦветТекста <> мWebЦветаЧерный Тогда
				ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветТекста);
			КонецЕсли;
			ЭлементУсловия 					= ЭлементУО.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
			ЭлементУсловия.ЛевоеЗначение 	= Новый ПолеКомпоновкиДанных("Ссылка");
			ЭлементУсловия.ВидСравнения 	= ВидСравненияКомпоновкиДанных.Равно;
			ЭлементУсловия.ПравоеЗначение 	= Выборка.Ссылка;		
		КонецЕсли;
	КонецЦикла;
	
	// Помеченные на удаление.
	ЭлементУО 						= УО.Добавить();
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", Новый Цвет(192, 192, 192));
	ЭлементУсловия 					= ЭлементУО.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементУсловия.ЛевоеЗначение 	= Новый ПолеКомпоновкиДанных("ПометкаУдаления");
	ЭлементУсловия.ВидСравнения 	= ВидСравненияКомпоновкиДанных.Равно;
	ЭлементУсловия.ПравоеЗначение 	= Истина;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьЦветовоеОформление(Знач ИмяТемы = "")
	
	Справочники.Проекты.УстановитьОформлениеЦветом(ИмяТемы);
	УстановитьУсловноеОформление();	
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьВидимостьДоступность()
	
	Элементы.ФормаПоказыватьНеАктуальныеПроекты.Пометка = ПоказыватьНеАктуальные;
	Если ПоказыватьНеАктуальные Тогда
		РаботаСОтборамиКлиентСервер.УдалитьЭлементОтбораСписка(Список, "Статус");
		
	Иначе		
		// Только актуальные.
		СЗ = Новый СписокЗначений;
		СЗ.Добавить(Перечисления.СтатусыПроекта.Планируемый);
		СЗ.Добавить(Перечисления.СтатусыПроекта.ВРаботе);		
		РаботаСОтборамиКлиентСервер.УстановитьЭлементОтбораСписка(Список, "Статус", СЗ);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПоУмолчанию(Команда)
	
	ЗаполнитьЦветовоеОформление();
	Элементы.Список.Обновить();
	Оповестить("ОбновитьЦветаПроектов");
	
КонецПроцедуры

&НаКлиенте
Процедура ПастельныеТона(Команда)
	
	ЗаполнитьЦветовоеОформление("ПастельныеТона");
	Элементы.Список.Обновить();
	Оповестить("ОбновитьЦветаПроектов");
	
КонецПроцедуры

&НаКлиенте
Процедура СовременнаяТема(Команда)
	
	ЗаполнитьЦветовоеОформление("СовременнаяТема");
	Элементы.Список.Обновить();
	Оповестить("ОбновитьЦветаПроектов");
	
КонецПроцедуры

#КонецОбласти