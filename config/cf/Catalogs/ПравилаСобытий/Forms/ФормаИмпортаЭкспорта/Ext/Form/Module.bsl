
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("ПравилоСобытий", ПравилоСобытий);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура РежимЗагрузкиПриИзменении(Элемент)
	
	Элементы.ПравилоСобытий.Видимость = Не РежимЗагрузки;
	
КонецПроцедуры

&НаКлиенте
Процедура ИмяФайлаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Если НЕ ПодключитьРасширениеРаботыСФайлами() Тогда
		Возврат;
	КонецЕсли;
	
	Диалог = Новый ДиалогВыбораФайла(?(РежимЗагрузки, РежимДиалогаВыбораФайла.Открытие,
		РежимДиалогаВыбораФайла.Сохранение));
		
	Диалог.Фильтр			= НСтр("ru = 'Файл ZIP-архива (*.zip)|*.zip|Все файлы (*.*)|*.*'");
	Диалог.ПолноеИмяФайла	= ИмяФайла;
	
	Если НЕ Диалог.Выбрать() Тогда
		Возврат;
	КонецЕсли;
	
	ИмяФайла = Диалог.ПолноеИмяФайла;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыполнитьОперацию(Команда)
	
	Если ПустаяСтрока(ИмяФайла) Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Не указан путь для выгрузки/загрузки.'"));
		Возврат;
		
	ИначеЕсли НЕ ПодключитьРасширениеРаботыСФайлами() Тогда
		
		Возврат;
		
	КонецЕсли;
	
	Если РежимЗагрузки Тогда
		ТекПуть = СокрЛП(ИмяФайла) + ?(Прав(СокрП(ИмяФайла), 1) = "\", "", "\");
		ЗагрузитьФайлы();
		Оповестить("ПравилаСобытий_ОбовитьФормуСписка");
		ПоказатьПредупреждение(, НСтр("ru = 'Загрузка завершена.'"));
		
	Иначе		
		
		Если Не ЗначениеЗаполнено(ПравилоСобытий) Тогда
			ПоказатьПредупреждение(, НСтр("ru = 'Не указано правило/группа правил для выгрузки.'"));
			Возврат;
		КонецЕсли;
		
		ТекПуть = СокрЛП(ИмяФайла) + ?(Прав(СокрП(ИмяФайла), 1) = "\", "", "\");
		ВыгрузитьФайлы();
		ПоказатьПредупреждение(, НСтр("ru = 'Выгрузка завершена.'"));
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ЗагрузитьФайлы()

	 ДД		= Новый ДвоичныеДанные(ИмяФайла);
	 Адрес  = ПоместитьВоВременноеХранилище(ДД);
	 ЗагрузитьНаСервере(Адрес);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьФайлы()
	
	Адрес 	= ВыгрузитьНаСервере();
	ДД 		= ПолучитьИзВременногоХранилища(Адрес);
	ДД.Записать(ИмяФайла);
	УдалитьИзВременногоХранилища(Адрес);
	
КонецПроцедуры

&НаСервере
Функция ВыгрузитьНаСервере()	
	
	Возврат Справочники.ПравилаСобытий.ВыгрузитьГруппуПравилСобытий(ПравилоСобытий);
	
КонецФункции

&НаСервереБезКонтекста
Процедура ЗагрузитьНаСервере(Знач Адрес)
	
	Справочники.ПравилаСобытий.ЗагрузитьПравилоСобытий(Адрес);	
	
КонецПроцедуры

#КонецОбласти