
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПроверитьКонстантуИспользоватьШаблоныСообщений();	
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ПравилаСобытий_ОбовитьФормуСписка" Тогда
		Элементы.Список.Обновить();
	КонецЕсли;	
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаполнитьПредопределенные(Команда)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ЗаполнитьПредопределенныеПослеВопроса", ЭтотОбъект);
	ПоказатьВопрос(ОписаниеОповещения, 
		НСтр("ru = 'Будут восстановлены предопределенные правила событий, действия правил и шаблоны сообщений. Продолжить?'"),
		РежимДиалогаВопрос.ДаНет);	
	
КонецПроцедуры

&НаКлиенте
Процедура ИмпортЭкспорт(Команда)
	
	ПараметрыФормы	= Новый Структура;
	ТекущиеДанные	= Элементы.Список.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		Если ЗначениеЗаполнено(ТекущиеДанные.Ссылка) Тогда
			ПараметрыФормы.Вставить("ПравилоСобытий", ТекущиеДанные.Ссылка);		
		КонецЕсли;	
	КонецЕсли;	
	
	ОткрытьФорму("Справочник.ПравилаСобытий.Форма.ФормаИмпортаЭкспорта", ПараметрыФормы, ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьПредопределенныеНаСервере()
	
	Справочники.ПравилаСобытий.ЗаполнитьПравилаСобытийПриПервоначальномЗаполнении();
	
КонецПроцедуры

&НаСервере
Процедура ПроверитьКонстантуИспользоватьШаблоныСообщений()
	
	УстановитьПривилегированныйРежим(Истина);
	Если Не Константы.ИспользоватьШаблоныСообщений.Получить() Тогда
		Константы.ИспользоватьШаблоныСообщений.Установить(Истина);
	КонецЕсли;
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПредопределенныеПослеВопроса(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьПредопределенныеНаСервере();
	Элементы.Список.Обновить();
	
КонецПроцедуры

#КонецОбласти

