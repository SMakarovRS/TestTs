
// Выполнение действия объекта
// 
// Параметры:
//  ДополнительныеПараметры - Структура - дополнительные параметры метрики, полученные из JSON-а.
//
// Возвращаемое значение
//  РезультатДейстия - Булево/Строка - результат выполнения действия. 
// 	При успешном выполнении - Истина, при неуспешном - Ложь, при возникновении ошибки - текст ошибки.
//
Функция ВыполнитьДействие(ДополнительныеПараметры) Экспорт
	РезультатДейстия = Ложь;
	
	// Подготовка параметров
	СтруктураПараметров = Новый Структура;
	Для Каждого ПараматерЗапроса ИЗ ПараметрыЗапроса Цикл
		ИмяПараметра = ПараматерЗапроса.Имя;
		ЗначениеПараметра = "";
		Если ПараматерЗапроса.ЭтоСписок Тогда
			ЗначениеПараметра = СформироватьСписокЗначений(ИмяПараметра);
		ИначеЕсли НЕ ПустаяСтрока(ПараматерЗапроса.Выражение) Тогда
			Выполнить(ПараматерЗапроса.Выражение);
		Иначе
			ЗначениеПараметра = ПараматерЗапроса.Значение;
		КонецЕсли;
		СтруктураПараметров.Вставить(ИмяПараметра, ЗначениеПараметра);
	КонецЦикла;
	
	// Подготовка дополнительных параметров
	Если ДополнительныеПараметры <> Неопределено Тогда	
		Для Каждого ДопПараметр Из ДополнительныеПараметры Цикл
			СтруктураПараметров.Вставить(ДопПараметр.Ключ, ДопПараметр.Значение);	
		КонецЦикла;	
	КонецЕсли;
	
	// Подстановка параметров в код
	КодДляВыполнения = ИсполняемыйКод;
	Для Каждого ПараматерЗапроса Из ПараметрыЗапроса Цикл
		ПараметрДляЗамены = "[" + ПараматерЗапроса.Имя + "]";
		ПараметрЗначение = "СтруктураПараметров." + ПараматерЗапроса.Имя;
		КодДляВыполнения = СтрЗаменить(КодДляВыполнения, ПараметрДляЗамены, ПараметрЗначение);
	КонецЦикла;
	
	// Выполнение кода
	Попытка
		Выполнить(КодДляВыполнения);
	Исключение
		РезультатДействия = ОписаниеОшибки();	
	КонецПопытки;
	
	Возврат РезультатДействия;
	
КонецФункции

// Формирует список значений переданного параметра
//  
// Параметры:
//  ИмяПараметра - Строка - имя параметра, списочное значение которого необходимо получить.
//
// Возвращаемое значение
//  СписокЗначений - СписокЗначений - список значений параметра.
//
Функция СформироватьСписокЗначений(ИмяПараметра) Экспорт 
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДействияZabbixЗначенияСписочныхПараметров.Значение КАК Значение
		|ИЗ
		|	Справочник.ДействияZabbix.ЗначенияСписочныхПараметров КАК ДействияZabbixЗначенияСписочныхПараметров
		|ГДЕ
		|	ДействияZabbixЗначенияСписочныхПараметров.ИмяПараметра = &ИмяПараметра
		|	И ДействияZabbixЗначенияСписочныхПараметров.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("ИмяПараметра", ИмяПараметра);
	
	РезультатЗапроса = Запрос.Выполнить();
	СписокЗначений = Новый СписокЗначений;
	СписокЗначений.ЗагрузитьЗначения(РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("Значение"));
	Возврат СписокЗначений;	
	
КонецФункции

