
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Объект.Ссылка.Пустая() Тогда
		Объект.Активность 	= Истина;
		Объект.Протокол     = Перечисления.TelegramПроксиПротоколы.HTTPS;
		Объект.Порт 	    = 3128;
	КонецЕсли;
	
	ОбновитьВидимостьДоступность();
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	ОбновитьВидимостьДоступность();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПроверкаСвязи(Команда)
	
	Если Объект.Ссылка.Пустая() ИЛИ Модифицированность = Истина Тогда
		
		ОписаниеОповещения = Новый ОписаниеОповещения("ПослеВопросаЗаписиПроверкиСвязи", ЭтотОбъект);
		ПоказатьВопрос(ОписаниеОповещения, НСтр("ru = 'Для продолжения необходимо записать элемент. Продолжить?'"), 
			РежимДиалогаВопрос.ДаНет);
		Возврат;
		
	КонецЕсли;
	
	ПроверкаСвязиНаКлиенте();
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВопросаЗаписиПроверкиСвязи(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	Записать();
	
	ПроверкаСвязиНаКлиенте();
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверкаСвязиНаКлиенте()
	
	ДлительнаяОперация = ПроверитьПроксиСервераВФоне();
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ВыводитьОкноОжидания 			= Истина;
	ПараметрыОжидания.ВыводитьПрогрессВыполнения 	= Истина;
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ПроверкаПроксиСерверовВФонеЗавершение", ЭтотОбъект);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);
	
КонецПроцедуры

&НаСервере
Функция ПроверитьПроксиСервераВФоне()
	
	Если ДлительнаяОперация <> Неопределено Тогда
		ДлительныеОперации.ОтменитьВыполнениеЗадания(ДлительнаяОперация.ИдентификаторЗадания);
	КонецЕсли;
		
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Проверка прокси-серверов на доступность'");
	
	
	Массив = Новый Массив;
	Массив.Добавить(Объект.Ссылка);
	
	ПараметрыПроверки = Новый Структура("Массив", Массив);
	
	Результат = ДлительныеОперации.ВыполнитьВФоне("TelegramСервер.ПроверитьПроксиСерверы",
		ПараметрыПроверки, ПараметрыВыполнения);
		
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ПроверкаПроксиСерверовВФонеЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	ДлительнаяОперация = Неопределено;
	ОбновитьВидимостьДоступность();
	ОповеститьОбИзменении(Объект.Ссылка);
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьИсториюПрокси(Команда)
	
	Если Объект.Ссылка.Пустая() Тогда
		Возврат;
	КонецЕсли;
	
	ОчиститьИсториюПроксиСервера();
	ОповеститьОбИзменении(Объект.Ссылка);
	ОбновитьВидимостьДоступность();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСтатистику(Команда)
	
	ОбновитьВидимостьДоступность();	
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОчиститьИсториюПроксиСервера()
	
	TelegramСервер.ОчиститьИсториюПрокси(Объект.Ссылка);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьВидимостьДоступность()
	
	Элементы.Статистика.Видимость = НЕ Объект.Ссылка.Пустая();
	Если Объект.Ссылка.Пустая() Тогда		
		Возврат;
    КонецЕсли;
    
    ВремяПоследнегоОтвета   = 0;
    ВсегоОтправлено         = 0;
    Оценка      = 0;
    Процент     = 0;
    Успешно     = 0;
    Ошибка      = 0;
    
	
	УстановитьПривилегированныйРежим(Истина);
	Запрос = Новый Запрос();
	Запрос.Текст =
		"ВЫБРАТЬ
        |   СУММА(TelegramСтатистикаОтправкиСообщений.Отправлено) КАК Отправлено,
        |   СУММА(TelegramСтатистикаОтправкиСообщений.Оценка) КАК Оценка,
        |   СУММА(ВЫБОР
        |           КОГДА TelegramСтатистикаОтправкиСообщений.Оценка = 1
        |               ТОГДА 1
        |           ИНАЧЕ 0
        |       КОНЕЦ) КАК Успешно,
        |   СУММА(ВЫБОР
        |           КОГДА TelegramСтатистикаОтправкиСообщений.Оценка = -1
        |               ТОГДА 1
        |           ИНАЧЕ 0
        |       КОНЕЦ) КАК Ошибка
        |ПОМЕСТИТЬ ВТ_ОценкаВремяОтвета
        |ИЗ
        |   РегистрСведений.TelegramСтатистикаОтправкиСообщений КАК TelegramСтатистикаОтправкиСообщений
        |ГДЕ
        |   TelegramСтатистикаОтправкиСообщений.Прокси = &Прокси
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ
        |   ЕСТЬNULL(TelegramСтатистикаОтправкиСообщенийСрезПоследних.ВремяОтвета, 0) КАК ВремяПоследнегоОтвета,
        |   ЕСТЬNULL(TelegramСтатистикаОтправкиСообщенийСрезПоследних.Оценка, 0) КАК ПоследняяОценка,
        |   ЕСТЬNULL(ВТ_ОценкаВремяОтвета.Отправлено, 0) КАК ВсегоОтправлено,
        |   ЕСТЬNULL(ВТ_ОценкаВремяОтвета.Оценка, 0) КАК ТекущаяОценка,
        |   ЕСТЬNULL(ВТ_ОценкаВремяОтвета.Успешно, 0) КАК Успешно,
        |   ЕСТЬNULL(ВТ_ОценкаВремяОтвета.Ошибка, 0) КАК Ошибка,
        |   ВЫБОР
        |       КОГДА ЕСТЬNULL(ВТ_ОценкаВремяОтвета.Отправлено, 0) > 0
        |           ТОГДА ЕСТЬNULL(ВТ_ОценкаВремяОтвета.Успешно, 0) * 100 / ЕСТЬNULL(ВТ_ОценкаВремяОтвета.Отправлено, 0)
        |       ИНАЧЕ 0
        |   КОНЕЦ КАК Процент
        |ИЗ
        |   РегистрСведений.TelegramСтатистикаОтправкиСообщений.СрезПоследних(, Прокси = &Прокси) КАК TelegramСтатистикаОтправкиСообщенийСрезПоследних,
        |   ВТ_ОценкаВремяОтвета КАК ВТ_ОценкаВремяОтвета";
	
	Запрос.УстановитьПараметр("Прокси", Объект.Ссылка);
	Выборка = Запрос.Выполнить().Выбрать();
	СтрокаДоступности = "";
	Если Выборка.Следующий() Тогда
		Если Выборка.ПоследняяОценка = 1 Тогда
			Элементы.СтрокаДоступности.ЦветТекста = WebЦвета.ТемноЗеленый;
			СтрокаДоступности = НСтр("ru = 'Прокси-сервер на момент последней проверки был доступен'");
		Иначе
			Элементы.СтрокаДоступности.ЦветТекста = WebЦвета.ТемноКрасный;
			СтрокаДоступности = НСтр("ru = 'Прокси-сервер на момент последней проверки не доступен'");
		КонецЕсли;
		ВремяПоследнегоОтвета   = Выборка.ВремяПоследнегоОтвета;
		ВсегоОтправлено         = Выборка.ВсегоОтправлено;
		Оценка      = Выборка.ТекущаяОценка;
        Процент     = Выборка.Процент;
        Успешно     = Выборка.Успешно;
        Ошибка      = Выборка.Ошибка;
    Иначе
        Элементы.СтрокаДоступности.ЦветТекста = WebЦвета.Серый;
        СтрокаДоступности = НСтр("ru = 'Нет информации по отправленным данным прокси-сервером'");        
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти