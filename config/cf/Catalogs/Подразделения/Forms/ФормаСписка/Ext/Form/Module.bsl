
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Если Параметры.Свойство("РежимВыбора") И Параметры.РежимВыбора Тогда
		ЭтаФорма.ЗакрыватьПриВыборе = Истина;
		Элементы.Список.РежимВыбора = Истина;
	КонецЕсли;
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	мОрганизация = Неопределено;
	
	Если Параметры.Свойство("Отбор") Тогда
		Если Параметры.Отбор.Количество() > 0 Тогда
			мОрганизация = Параметры.Отбор.Организация;
		КонецЕсли;
	КонецЕсли;
	
	ПолучитьСписокОрганизаций(мОрганизация);
	
	ВыводСпискаВладельцев = НЕ (Параметры.Отбор.Свойство("Организация"));
	
	ОбновитьВидимость();	
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОбработатьАктивизациюСтрокиПанелиНавигации();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОбработатьАктивизациюСтрокиПанелиНавигации();
	
	ТекущиеДанные = Элементы.ОрганизацияПанельНавигации.ТекущиеДанные;
	
	Если ТекущиеДанные <> Неопределено Тогда
		
		Если ЗначениеЗаполнено(ТекущиеДанные.Ссылка) Тогда			
			РаботаСОтборамиКлиентСервер.УдалитьЭлементОтбораСписка(Список, "Организация");
			РаботаСОтборамиКлиентСервер.УстановитьЭлементОтбораСписка(Список, "Организация", ТекущиеДанные.Ссылка);
				
		Иначе
			РаботаСОтборамиКлиентСервер.УдалитьЭлементОтбораСписка(Список, "Организация");
			РаботаСОтборамиКлиентСервер.УстановитьЭлементОтбораСписка(Список, 
				"Организация", , , ВидСравненияКомпоновкиДанных.НеЗаполнено);
				
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОбновитьВидимость()

	Если ВыводСпискаВладельцев Тогда
		Элементы.СтраницаОрганизации.Видимость = НЕ ПанельНавигацииСкрыта;	
	Иначе
		Элементы.СтраницаОрганизации.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПолучитьСписокОрганизаций(мОрганизация)
	
	Запрос = Новый Запрос;
	
	Если мОрганизация = Неопределено Тогда 
	     		
		Запрос.Текст = 
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
			|	Организации.Ссылка КАК Ссылка,
			|	Организации.Наименование КАК Наименование,
			|	0 КАК Порядок,
			|	Организации.ИНН КАК ИНН,
			|	Организации.КПП КАК КПП
			|ИЗ
			|	Справочник.Организации КАК Организации
			|ГДЕ
			|	НЕ Организации.ПометкаУдаления
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	Контрагенты.Ссылка,
			|	Контрагенты.Наименование,
			|	1,
			|	Контрагенты.ИНН,
			|	Контрагенты.КПП
			|ИЗ
			|	Справочник.Контрагенты КАК Контрагенты
			|ГДЕ
			|	Контрагенты.УчетОстатков
			|	И НЕ Контрагенты.ПометкаУдаления
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	NULL,
			|	""<Не выбрана>"",
			|	2,
			|	"""",
			|	""""
			|
			|УПОРЯДОЧИТЬ ПО
			|	Порядок,
			|	Наименование";
		
	ИначеЕсли ТипЗнч(мОрганизация) = Тип("СправочникСсылка.Организации") Тогда 
		
		Запрос.Текст =		
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
			|	Организации.Ссылка КАК Ссылка,
			|	Организации.Наименование КАК Наименование,
			|	Организации.ИНН КАК ИНН,
			|	Организации.КПП КАК КПП
			|ИЗ
			|	Справочник.Организации КАК Организации
			|ГДЕ
			|	НЕ Организации.ПометкаУдаления
			|	И Организации.Ссылка = &Ссылка";
		
		Запрос.УстановитьПараметр("Ссылка", мОрганизация);
		
	ИначеЕсли ТипЗнч(мОрганизация) = Тип("СправочникСсылка.Контрагенты") Тогда 
		
		Запрос.Текст =
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
			|	Контрагенты.Ссылка,
			|	Контрагенты.Наименование,
			|	Контрагенты.ИНН,
			|	Контрагенты.КПП
			|ИЗ
			|	Справочник.Контрагенты КАК Контрагенты
			|ГДЕ
			|	Контрагенты.УчетОстатков
			|	И НЕ Контрагенты.ПометкаУдаления
			|	И Контрагенты.Ссылка = &Ссылка";
		
		Запрос.УстановитьПараметр("Ссылка", мОрганизация);
		
	КонецЕсли;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		НоваяСтрока = Организации.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПанельНавигацииПриАктивизацииСтроки(Элемент)
	
	Если Элементы.ОрганизацияПанельНавигации.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПодключитьОбработчикОжидания("ОбработатьАктивизациюСтрокиПанелиНавигации", 0.1, Истина);
	
КонецПроцедуры

#Область ОбработчикиБиблиотек

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Элементы.Список);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Элементы.Список, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Элементы.Список);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти

#КонецОбласти





