
#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыбратьПервуюКартинку(Счетчик)
	
	ЭлОбъект = ПолучитьДанныеВыборки(Счетчик - 1);
	
	ПараметрыПоиска = ЭлОбъект.Наименование;
	КоличествоКартинок = 1;
	НачальнаяКартинка = 0;
	
	Форма = ПолучитьФорму("Обработка.ПоискКартинок.Форма.Форма");
	
	Результаты = Форма.НайтиКартинкиКлиентСервер(ПараметрыПоиска, КоличествоКартинок, НачальнаяКартинка, Ложь);	
	Если Результаты.Количество() > 0 Тогда                     
		СсылкаВоВременномХранилище 	= ПоместитьВоВременноеХранилище(Результаты[0].Картинка.ПолучитьДвоичныеДанные());
		ЗаписатьКартинку(ЭлОбъект.Ссылка, СсылкаВоВременномХранилище, Результаты[0].Ссылка);
		УдалитьИзВременногоХранилища(СсылкаВоВременномХранилище);
	Иначе
		ОбщегоНазначенияКлиент.СообщитьПользователю(СтрШаблон(НСтр("ru = 'По запросу ""%1"" ничего не найдено...'"), ЭлОбъект.Наименование));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьИзображенияДляНоменклатурыГруппы(Команда)
	
	Количество      = ИнициализацияОбработкиСервер();
	Пояснение 		= НСтр("ru = 'Для прерывания нажмите Ctrl+Break'");
	
	Для Счетчик = 1 По Количество Цикл
		
		ВыбратьПервуюКартинку(Счетчик);
				
		Текст = СтрШаблон(НСтр("ru = 'Обработано: %1 из %2'"), Формат(Счетчик), Формат(Количество));
		Состояние(Текст, Окр(Счетчик * 100 / Количество, 0), Пояснение);
		ОбработкаПрерыванияПользователя();
		
	КонецЦикла;
		
	ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Обработка завершена'"));	
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ИнициализацияОбработкиСервер()

    Запрос = Новый Запрос;
    Запрос.Текст =
	    "ВЫБРАТЬ
	    |	Номенклатура.Ссылка,
	    |	Номенклатура.Наименование
	    |ИЗ
	    |	Справочник.Номенклатура КАК Номенклатура
	    |ГДЕ
	    |	Номенклатура.Родитель В ИЕРАРХИИ(&Родитель)
	    |	И Номенклатура.ЭтоГруппа = ЛОЖЬ";

    Запрос.УстановитьПараметр("Родитель", Объект.Ссылка);
    Данные      = Запрос.Выполнить().Выгрузить();
    АдресДанных = ПоместитьВоВременноеХранилище(Данные, УникальныйИдентификатор);

    Возврат Данные.Количество();

КонецФункции

&НаСервере
Процедура ЗаписатьКартинку(Знач Ссылка, Знач ТекКартинкаИзВременногоХранилища, Знач СсылкаВИнтернете)
	
	ИмяФайла = Сред(СсылкаВИнтернете, СтрНайти(СсылкаВИнтернете, "/", НаправлениеПоиска.СКонца) + 1);	
	Расширение = ОбщегоНазначенияКлиентСервер.ПолучитьРасширениеИмениФайла(ИмяФайла);
	Если ПустаяСтрока(Расширение) Тогда
		Расширение = "png";
	КонецЕсли;
	ИмяБезРасширения = Лев(ИмяФайла, СтрНайти(ИмяФайла, ".", НаправлениеПоиска.СКонца) - 1);
	
	Запрос = Новый Запрос();
	Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
		|	НоменклатураПрисоединенныеФайлы.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.НоменклатураПрисоединенныеФайлы КАК НоменклатураПрисоединенныеФайлы
		|ГДЕ
		|	НоменклатураПрисоединенныеФайлы.ВладелецФайла = &ВладелецФайла
		|	И НоменклатураПрисоединенныеФайлы.Наименование = &Наименование";
	Запрос.УстановитьПараметр("ВладелецФайла", Ссылка);
	Запрос.УстановитьПараметр("Наименование", ИмяБезРасширения);
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		
		ПараметрыФайла 						= РаботаСФайлами.ПараметрыДобавленияФайла();
		ПараметрыФайла.ВладелецФайлов 		= Ссылка;
		ПараметрыФайла.ИмяБезРасширения 	= ИмяБезРасширения;
		ПараметрыФайла.РасширениеБезТочки 	= Расширение;
		
		НачатьТранзакцию();
		Попытка
			ЭлементСправочника 				= Ссылка.ПолучитьОбъект();
			ЭлементСправочника.ФайлКартинки = РаботаСФайлами.ДобавитьФайл(ПараметрыФайла, ТекКартинкаИзВременногоХранилища);
			ЭлементСправочника.Записать();
			ЗафиксироватьТранзакцию();
		Исключение
		    ОтменитьТранзакцию();
		КонецПопытки;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьДанныеВыборки(Счетчик)
	
	Данные = ПолучитьИзВременногоХранилища(АдресДанных);
	
	Структура = Новый Структура();
	Структура.Вставить("Ссылка", Данные[Счетчик].Ссылка);
	Структура.Вставить("Наименование", Данные[Счетчик].Наименование);
	
	Возврат Структура;
	
КонецФункции

#КонецОбласти

