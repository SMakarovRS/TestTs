
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
    
    Если Объект.Ссылка.Пустая() Тогда
    	Объект.ВидКлавиатуры = Перечисления.TelegramВидКлавиатуры.Стандартная;
	КонецЕсли;
	
	Элементы.Страницы.ОтображениеСтраниц = ОтображениеСтраницФормы.Нет;
	ОбновитьВидимостьДоступность();
    
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	Алгоритм = ТекущийОбъект.Алгоритм.Получить();
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ТекущийОбъект.Алгоритм = Новый ХранилищеЗначения(Алгоритм, Новый СжатиеДанных(9));
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ВидКлавиатурыПриИзменении(Элемент)
	
	ОбновитьВидимостьДоступность();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОбновитьВидимостьДоступность()
	
	Если Объект.ВидКлавиатуры = Перечисления.TelegramВидКлавиатуры.СкрытьКлавиатуру Тогда
	    Элементы.Страницы.Видимость = Ложь;
	ИначеЕсли Объект.ВидКлавиатуры = Перечисления.TelegramВидКлавиатуры.Программная Тогда
		Элементы.Страницы.Видимость = Истина;
		Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаКод;
	ИначеЕсли Объект.ВидКлавиатуры = Перечисления.TelegramВидКлавиатуры.Стандартная Тогда
		Элементы.Страницы.Видимость = Истина;
		Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаКнопки;
		Элементы.КнопкиURL.Видимость = Ложь;
		Элементы.Кнопкиcallback_data.Видимость = Ложь;
	ИначеЕсли Объект.ВидКлавиатуры = Перечисления.TelegramВидКлавиатуры.Встроенная Тогда
		Элементы.Страницы.Видимость = Истина;
		Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаКнопки;
		Элементы.КнопкиURL.Видимость = Истина;
		Элементы.Кнопкиcallback_data.Видимость = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИнициализацияКлавиатуры(Команда)
	
	Модифицированность = Истина;
	Алгоритм = Алгоритм + Символы.ПС 
		+ 	"Клавиатура = Новый Структура();
			|МассивСтрок = Новый Массив();";
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьКнопку(Команда)
	
	Модифицированность = Истина;
	Алгоритм = Алгоритм + Символы.ПС
		+ "МассивКнопокСтроки = Новый Массив();
		|// Добавляем кнопку.
		|Кнопка = Новый Структура;
		|Кнопка.Вставить(""text"", ""Текст на кнопке"");
		|Кнопка.Вставить(""url"", ""URL, который откроется при нажатии на кнопку"");
		|Кнопка.Вставить(""callback_data"", ""Данные, которые будут отправлены в callback_query при нажатии на кнопку"");
		|МассивКнопокСтроки.Добавить(Кнопка);
		|// ... 			
		|МассивСтрок.Добавить(МассивКнопокСтроки);";
	
КонецПроцедуры

&НаКлиенте
Процедура АвторазмерКлавиатуры(Команда)
	
	Модифицированность = Истина;
	Алгоритм = Алгоритм + Символы.ПС + "Клавиатура.Вставить(""resize_keyboard"", Истина);";
	
КонецПроцедуры

&НаКлиенте
Процедура СкрытьПослеИспользования(Команда)
	
	Модифицированность = Истина;
	Алгоритм = Алгоритм + Символы.ПС + "Клавиатура.Вставить(""one_time_keyboard"", Истина);";
	
КонецПроцедуры

&НаКлиенте
Процедура КлавиатураГотова(Команда)
	
	Модифицированность = Истина;
	Алгоритм = Алгоритм + Символы.ПС 
		+ "Клавиатура.Вставить(""keyboard"", МассивСтрок); // вместо keyboard можно использовать inline_keyboard";
	
КонецПроцедуры

#КонецОбласти