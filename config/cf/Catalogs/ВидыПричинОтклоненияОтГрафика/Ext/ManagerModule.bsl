#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ПрограммныйИнтерфейс

Процедура ЗаполнитьВидыПричинОтклоненияОтГрафикаПриПервоначальномЗаполнении() Экспорт
	
	Спр = Справочники.ВидыПричинОтклоненияОтГрафика;
	
	Пользователь 	= Перечисления.ТипыОбъектовПричинОтклоненияОтГрафиков.Пользователь;
	Сервис 			= Перечисления.ТипыОбъектовПричинОтклоненияОтГрафиков.Сервис;
	
	ЗаписатьЭлемент(Спр.Заболевание, 				"Б", НСтр("ru = 'Заболевание'"), 		Пользователь, "#708090", "#FFFFFF");
	ЗаписатьЭлемент(Спр.Прогул, 					"ПРГ", НСтр("ru = 'Прогул'"), 			Пользователь, "#FF5754", "#FFFFFF");
	ЗаписатьЭлемент(Спр.Коммандировка, 				"К", НСтр("ru = 'Коммандировка'"), 		Пользователь, "#CC81A2", "#FFFFFF");
	ЗаписатьЭлемент(Спр.Отпуск, 					"ОТ", НСтр("ru = 'Отпуск'"), 			Пользователь, "#328999", "#FFFFFF");
	ЗаписатьЭлемент(Спр.ОтсутствуетПоНевыясненнойПричине, "НЕВ", НСтр("ru = 'Отсутствует по невыясненной причине'"), Пользователь, "#FF5754", "#FFFFFF");
	ЗаписатьЭлемент(Спр.Отгул,						"ОТ", НСтр("ru = 'Отгул'"), 			Пользователь, "#AAAAAA", "#FFFFFF");
	ЗаписатьЭлемент(Спр.ОтпускБезСохраненияЗаработнойПлаты,	"ОТ", НСтр("ru = 'Отпуск без сохранения заработной платы'"), Пользователь, "#AAAAAA", "#FFFFFF");
	
	ЗаписатьЭлемент(Спр.Авария, 					"АВ", НСтр("ru = 'Авария'"), 				Сервис, "#FF3609", "#FFFFFF");
	ЗаписатьЭлемент(Спр.РегламентныеРаботы, 		"РЕГ", НСтр("ru = 'Регламентные работы'"), 	Сервис, "#5670B3", "#FFFFFF");
	
КонецПроцедуры

Процедура УстановитьУсловноеОформление(Список) Экспорт
	
	УО = Список.УсловноеОформление.Элементы;
	УО.Очистить();
	Запрос = Новый Запрос();
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВидыПричинОтклоненияОтГрафика.Ссылка КАК Ссылка,
		|	ВидыПричинОтклоненияОтГрафика.ЦветТекста КАК ЦветТекста,
		|	ВидыПричинОтклоненияОтГрафика.ЦветФона КАК ЦветФона
		|ИЗ
		|	Справочник.ВидыПричинОтклоненияОтГрафика КАК ВидыПричинОтклоненияОтГрафика";
	
	мWebЦветаЧерный = WebЦвета.Черный;
	мWebЦветаБелый = WebЦвета.Белый;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Попытка
			ЦветТекста 	= РаботаСЦветомКлиентСервер.HexВЦвет(Выборка.ЦветТекста);
		Исключение
			ЦветТекста 	= мWebЦветаЧерный;
		КонецПопытки;
		Попытка
			ЦветФона 	= РаботаСЦветомКлиентСервер.HexВЦвет(Выборка.ЦветФона);
		Исключение
			ЦветФона 	= мWebЦветаБелый;
		КонецПопытки;
		
		ЭлементУО 						= УО.Добавить();
		ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", 	ЦветТекста);
		ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветФона", 	ЦветФона);
		ЭлементУсловия 					= ЭлементУО.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ЭлементУсловия.ЛевоеЗначение 	= Новый ПолеКомпоновкиДанных("Ссылка");
		ЭлементУсловия.ВидСравнения 	= ВидСравненияКомпоновкиДанных.Равно;
		ЭлементУсловия.ПравоеЗначение 	= Выборка.Ссылка;		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаписатьЭлемент(ЭлементСсылка, БуквенноеОбозначение, Наименование, ТипОбъекта, ЦветТекста, ЦветФона)
	
	Элемент 						= ЭлементСсылка.ПолучитьОбъект();
	Элемент.БуквенноеОбозначение	= БуквенноеОбозначение;
	Элемент.Наименование 			= Наименование;
	Элемент.ЦветТекста 				= ЦветТекста;
	Элемент.ЦветФона				= ЦветФона;
	Элемент.ТипОбъекта				= ТипОбъекта;
	Элемент.Записать();
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли