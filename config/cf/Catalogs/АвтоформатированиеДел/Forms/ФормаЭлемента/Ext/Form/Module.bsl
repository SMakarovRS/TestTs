
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
    Попытка
        ШрифтСтиля 	                    	= ЗначениеИзСтрокиВнутр(Объект.Стиль);
        Элементы.СтильЖирный.Пометка    	= ШрифтСтиля.Полужирный;
        Элементы.СтильКурсив.Пометка    	= ШрифтСтиля.Наклонный;
        Элементы.СтильПодчеркнутый.Пометка 	= ШрифтСтиля.Подчеркивание;
    Исключение
        Элементы.СтильЖирный.Пометка    	= Ложь;
        Элементы.СтильКурсив.Пометка    	= Ложь;
        Элементы.СтильПодчеркнутый.Пометка 	= Ложь;
    КонецПопытки;
    
    Образец = НСтр("ru='Это пример дела, которое будет отображено в списке'", "ru");
    ОбновитьВидимостьДоступность();
    
	СписокСтолбцов = УправлениеДелами.СписокСтолбцов();
	Для Каждого Элемент Из СписокСтолбцов Цикл		
		Элементы.Столбец.СписокВыбора.Добавить(Элемент.Значение, Элемент.Представление);
	КонецЦикла;	
    
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
    
    Оповестить("ИзменениеАвтоформатированиеДел",,Объект.Ссылка);
    
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СтильЖирный(Команда)
	
	Элементы.СтильЖирный.Пометка = НЕ Элементы.СтильЖирный.Пометка;
    ОбработатьСтиль();
    ОбновитьВидимостьДоступность();    
	
КонецПроцедуры

&НаКлиенте
Процедура СтильКурсив(Команда)

    Элементы.СтильКурсив.Пометка = НЕ Элементы.СтильКурсив.Пометка;
    ОбработатьСтиль();
    ОбновитьВидимостьДоступность();	
    
КонецПроцедуры

&НаКлиенте
Процедура СтильПодчеркнутый(Команда)
	
	Элементы.СтильПодчеркнутый.Пометка = НЕ Элементы.СтильПодчеркнутый.Пометка;
    ОбработатьСтиль();
    ОбновитьВидимостьДоступность();
    
КонецПроцедуры

&НаКлиенте
Процедура СтильЦветТекста(Команда)
	
    Структура = Новый Структура("РедактируемыйЦвет", Объект.ЦветТекста);
    Результат = Неопределено;
    ОткрытьФорму("ОбщаяФорма.ФормаВыбораЦвета", Структура, ЭтаФорма,,,,
        Новый ОписаниеОповещения("СтильЦветТекстаЗавершение", ЭтотОбъект),
        РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);        
	
КонецПроцедуры

&НаКлиенте
Процедура СтильЦветТекстаЗавершение(Результат, ДополнительныеПараметры) Экспорт
    
    Если Результат <> Неопределено Тогда
        Объект.ЦветТекста = РаботаСЦветомКлиентСервер.ЦветВHex(Результат);
        ОбновитьВидимостьДоступность();
    КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура СтильЦветФона(Команда)
    
    Структура = Новый Структура("РедактируемыйЦвет", Объект.ЦветФона);
    Результат = Неопределено;
    ОткрытьФорму("ОбщаяФорма.ФормаВыбораЦвета", Структура, ЭтаФорма,,,, Новый ОписаниеОповещения("СтильЦветФонаЗавершение", ЭтотОбъект), РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
    	
КонецПроцедуры

&НаКлиенте
Процедура СтильЦветФонаЗавершение(Результат, ДополнительныеПараметры) Экспорт
    
    Если Результат <> Неопределено Тогда        
        Объект.ЦветФона = РаботаСЦветомКлиентСервер.ЦветВHex(Результат);
        ОбновитьВидимостьДоступность();
    КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ВыбратьКартинку(Команда)
	
    Результат = Неопределено;	
    ОткрытьФорму("Справочник.Дела.Форма.ВыборКартинки", 
        Новый Структура("Картинка", Объект.Картинка),,,,, 
        Новый ОписаниеОповещения("ВыбратьИзображениеИзНабораЗавершение", ЭтотОбъект), 
        РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);	
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьИзображениеИзНабораЗавершение(Результат, ДополнительныеПараметры) Экспорт
    
	Если Результат <> Неопределено Тогда
		Объект.Картинка = Результат;
    КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОчиститьКартинку(Команда)
	
	Объект.Картинка = -1;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОбработатьСтиль()
	
	Если Элементы.СтильЖирный.Пометка 
		ИЛИ Элементы.СтильКурсив.Пометка
		ИЛИ Элементы.СтильПодчеркнутый.Пометка Тогда
		
		ШрифтСтиля 		= Новый Шрифт(,,Элементы.СтильЖирный.Пометка,Элементы.СтильКурсив.Пометка,
            Элементы.СтильПодчеркнутый.Пометка,,);
		Объект.Стиль 	= ЗначениеВСтрокуВнутр(ШрифтСтиля);
		
	Иначе
		
		Объект.Стиль = "";
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьВидимостьДоступность()
    
    Элементы.Образец.ЦветФона   = ?(ПустаяСтрока(Объект.ЦветФона), WebЦвета.Белый,
        РаботаСЦветомКлиентСервер.HexВЦвет(Объект.ЦветФона));
    Элементы.Образец.ЦветТекста = ?(ПустаяСтрока(Объект.ЦветТекста), WebЦвета.Черный,
        РаботаСЦветомКлиентСервер.HexВЦвет(Объект.ЦветТекста));
    
    Попытка
    	Если НЕ ПустаяСтрока(Объект.Стиль) Тогда
        	ШрифтСтиля = ЗначениеИзСтрокиВнутр(Объект.Стиль); 
        Иначе
            ШрифтСтиля 		= Новый Шрифт();
        КонецЕсли; 
    Исключение
        ШрифтСтиля 		= Новый Шрифт();
    КонецПопытки;
    Элементы.Образец.Шрифт = ШрифтСтиля;    
    
КонецПроцедуры

#КонецОбласти
