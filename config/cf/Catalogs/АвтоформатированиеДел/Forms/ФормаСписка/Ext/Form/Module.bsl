
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
    
    РаботаСОтборамиКлиентСервер.УстановитьЭлементОтбораСписка(Список, "Пользователь", 
        Пользователи.ТекущийПользователь());

	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	УстановитьУсловноеОформление();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Элементы.Список);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Элементы.Список, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Элементы.Список);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

&НаКлиенте
Процедура ЗаполнитьПредопределенные(Команда)
	
	ЗаполнитьПредопределенныеНаСервере();
	Оповестить("ИзменениеВидаДел");
	Элементы.Список.Обновить();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьПредопределенныеНаСервере()
	
	Справочники.АвтоформатированиеДел.ЗаполнитьАвтоформатированиеДелУмолчанию(Пользователи.ТекущийПользователь());
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПредопределенные1(Команда)
    
    ЗаполнитьПредопределенныеНаСервере();
	Оповестить("ИзменениеАвтоформатированиеДел");
	Элементы.Список.Обновить();
    
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УО = ЭтаФорма.УсловноеОформление.Элементы;	
	УО.Очистить();
	
	Запрос = Новый Запрос();
	Запрос.Текст =
		"ВЫБРАТЬ
        |   АвтоформатированиеДел.Ссылка КАК Ссылка,
        |   АвтоформатированиеДел.ЦветТекста КАК ЦветТекста,
        |   АвтоформатированиеДел.ЦветФона КАК ЦветФона,
        |   АвтоформатированиеДел.Стиль КАК Стиль,
        |   АвтоформатированиеДел.Идентификатор КАК Идентификатор,
        |   АвтоформатированиеДел.ИспользоватьСтиль КАК ИспользоватьСтиль,
        |   АвтоформатированиеДел.Картинка КАК Картинка,
        |   АвтоформатированиеДел.Условие КАК Условие,
        |   АвтоформатированиеДел.Столбец КАК Столбец
        |ИЗ
        |   Справочник.АвтоформатированиеДел КАК АвтоформатированиеДел
        |ГДЕ
        |   АвтоформатированиеДел.Пользователь = &Пользователь
        |   И АвтоформатированиеДел.Использование = ИСТИНА
        |
        |УПОРЯДОЧИТЬ ПО
        |   АвтоформатированиеДел.РеквизитДопУпорядочивания";

    Запрос.УстановитьПараметр("Пользователь", Пользователи.ТекущийПользователь());
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
        Если НЕ ПустаяСтрока(Выборка.ЦветФона) ИЛИ НЕ ПустаяСтрока(Выборка.ЦветТекста) 
            ИЛИ Выборка.ИспользоватьСтиль = Истина Тогда
            ЭлементУО 						= УО.Добавить();
            Если НЕ ПустаяСтрока(Выборка.ЦветФона) Тогда
            	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветФона", 
                    РаботаСЦветомКлиентСервер.HexВЦвет(Выборка.ЦветФона));
            КонецЕсли; 
            Если НЕ ПустаяСтрока(Выборка.ЦветТекста) Тогда
                ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", 
                    РаботаСЦветомКлиентСервер.HexВЦвет(Выборка.ЦветТекста));
            КонецЕсли;
            Если Выборка.ИспользоватьСтиль = Истина Тогда        
        		Попытка
        			Шрифт = ЗначениеИзСтрокиВнутр(Выборка.Стиль);
                    ЭлементУО.Оформление.УстановитьЗначениеПараметра("Шрифт", Шрифт);
        		Исключение
        		КонецПопытки;
            КонецЕсли;
            ЭлементУсловия 					= ЭлементУО.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
            ЭлементУсловия.ЛевоеЗначение 	= Новый ПолеКомпоновкиДанных("Список.Ссылка");
            ЭлементУсловия.ВидСравнения 	= ВидСравненияКомпоновкиДанных.Равно;
            ЭлементУсловия.ПравоеЗначение 	= Выборка.Ссылка;
            ПолеОформления 					= ЭлементУО.Поля.Элементы.Добавить();
           	ПолеОформления.Поле 	    	= Новый ПолеКомпоновкиДанных("Список");
            ПолеОформления.Использование 	= Истина;
        КонецЕсли;
        		
	КонецЦикла;	
		
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ИзменениеАвтоформатированиеДел" Тогда
		Элементы.Список.Обновить();
		УстановитьУсловноеОформление();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
