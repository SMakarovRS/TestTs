#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
		
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ВидНоменклатуры) Тогда
		ВидНоменклатуры = Владелец.ВидНоменклатуры;
	КонецЕсли;
		
	// Изменяем представление объекта.
	Если ВидНоменклатуры.ВестиУчетПоКарточкамНоменклатуры Тогда
		
		// Гарантия
		ЕстьГарантия = ГарантияМесяцев <> 0;
		
		Если ЕстьГарантия И ДатаВводаВЭксплуатацию <> Дата(1, 1, 1) И ДатаОкончанияГарантии = Дата(1, 1, 1) Тогда
			ДатаОкончанияГарантии = ДобавитьМесяц(ДатаВводаВЭксплуатацию, ГарантияМесяцев);
		КонецЕсли;
		
		УправлениеITОтделом8УФ.ЗаполнитьНаименованиеКарточкиПоВыражению(ЭтотОбъект);
		
		// Проверка уникальности серийного номера.
		Если УправлениеITОтделом8УФПовтИсп.ПроверятьУникальностьСерийногоНомера() Тогда
			
			ТЗУникально = Справочники.КарточкиНоменклатуры.РеквизитУникален(ЭтотОбъект, "СерийныйНомер");
			Если ТЗУникально.Количество() > 0 Тогда		
				ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Значение поля ""Серийный номер"" не уникально. Контроль уникальности можно отключить в настройках параметров учета.'"));
				Для Каждого Строки Из ТЗУникально Цикл
					ОбщегоНазначения.СообщитьПользователю("  " + СтрШаблон(НСтр("ru = 'дубль - %1 [код %2]'"), Строка(Строки.КарточкаНоменклатуры), Строки.КарточкаНоменклатуры.Код));
				КонецЦикла;
				Отказ = Истина;		
			КонецЕсли;
			
		КонецЕсли;
		
		// Проверка уникальности инвентарного номера.
		Если УправлениеITОтделом8УФПовтИсп.ПроверятьУникальностьИнвентарногоНомера() Тогда
			
			ТЗУникально = Справочники.КарточкиНоменклатуры.РеквизитУникален(ЭтотОбъект, "ИнвентарныйНомер");	
			Если ТЗУникально.Количество() > 0 Тогда		
				ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Значение поля ""Инвентарный номер"" не уникально. Контроль уникальности можно отключить в настройках параметров учета.'"));
				Для Каждого Строки Из ТЗУникально Цикл
					ОбщегоНазначения.СообщитьПользователю("  " + СтрШаблон(НСтр("ru = 'дубль - %1 [код %2]'"), Строка(Строки.КарточкаНоменклатуры), Строки.КарточкаНоменклатуры.Код));
				КонецЦикла;
				Отказ = Истина;		
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;	
		
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	ИнвентарныйНомер				= "";
	СерийныйНомер					= "";
	БухНомер						= "";
	ДатаНачалаЛицензии				= Дата(1, 1, 1);
	ДатаОкончанияЛицензии			= Дата(1, 1, 1);
	ДатаНачалаПодпискиОбновлений	= Дата(1, 1, 1);
	ДатаОкончанияПодпискиОбновлений	= Дата(1, 1, 1);
	КоличествоПользователейЛицензии	= 0;
	ДатаУстановкиПО					= Дата(1, 1, 1);
	КлючАктивации					= "";
	ДокументПоступления				= Документы.Поступление.ПустаяСсылка();	
	ПервоначальнаяСтоимость			= 0;
	ДатаВводаВЭксплуатацию			= Дата(1, 1, 1);
	ПорядковыйНомер					= 0;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли