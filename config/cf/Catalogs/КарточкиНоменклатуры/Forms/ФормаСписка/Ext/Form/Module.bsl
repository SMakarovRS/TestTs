
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Отбор.Свойство("Владелец") Тогда
		ВидНоменклатуры = Параметры.Отбор.Владелец.ВидНоменклатуры;
		Если НЕ ЗначениеЗаполнено(ВидНоменклатуры) ИЛИ НЕ ВидНоменклатуры.ВестиУчетПоКарточкамНоменклатуры Тогда
			АвтоЗаголовок = Ложь;
			Заголовок = СтрШаблон(НСтр("ru = 'Для элемента: ""%1"" учет по карточкам не ведется.'"), Строка(Параметры.Отбор.Владелец));
			Элементы.Список.ТолькоПросмотр = Истина;
		КонецЕсли;
	КонецЕсли;
	
	// Убираем карточки по которым не ведется учет по карточкам.
	РаботаСОтборамиКлиентСервер.УстановитьЭлементОтбораСписка(Список, "ВидНоменклатуры.ВестиУчетПоКарточкамНоменклатуры", 
		Истина, , ВидСравненияКомпоновкиДанных.Равно); 
	
	#Область БСП_ПриСозданииНаСервере
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды	
	
	#КонецОбласти
	
	УстановитьВидимостьИДоступность();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВидимостьОтбора(Команда)
	
	Элементы.ФормаВидимостьОтбора.Пометка = НЕ Элементы.ФормаВидимостьОтбора.Пометка;
	УстановитьВидимостьИДоступность();
	
КонецПроцедуры

&НаКлиенте
Процедура УдалитьНеИспользованные(Команда)
	
	ПометитьНеИспользованныеНаУдаление();
	ОбновитьИнтерфейс();
	
КонецПроцедуры

&НаКлиенте
Процедура СнятьПометкуНаУдалениеСПомеченных(Команда)
	
	СнятьПометкиНаУдалениеНаСервере();
	ОбновитьИнтерфейс();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область БСП

// СтандартныеПодсистемы.ПоискИУдалениеДублей
&НаКлиенте
Процедура ОбъединитьВыделенные(Команда)
    ПоискИУдалениеДублейКлиент.ОбъединитьВыделенные(Элементы.Список);
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьМестаИспользования(Команда)
	ПоискИУдалениеДублейКлиент.ПоказатьМестаИспользования(Элементы.Список);	
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПоискИУдалениеДублей

// СтандартныеПодсистемы.ГрупповоеИзменениеОбъектов
&НаКлиенте
Процедура ИзменитьВыделенные(Команда)
	ГрупповоеИзменениеОбъектовКлиент.ИзменитьВыделенные(Элементы.Список, Список);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ГрупповоеИзменениеОбъектов

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Элементы.Список);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Элементы.Список, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Элементы.Список);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти

&НаСервере
Процедура УстановитьВидимостьИДоступность()
	
	Элементы.ГруппаСписокОтбор.Видимость = Элементы.ФормаВидимостьОтбора.Пометка;
	
	МожноРедактировать 									= ПравоДоступа("Редактирование", Метаданные.Справочники.КарточкиНоменклатуры);
    Элементы.ФормаИзменитьВыделенные.Видимость 			= МожноРедактировать;
	Элементы.ФормаОбъединитьВыделенные.Видимость		= МожноРедактировать;
	Элементы.ФормаПоказатьМестаИспользования.Видимость 	= МожноРедактировать;

КонецПроцедуры

&НаСервере
Процедура ПометитьНеИспользованныеНаУдаление()
	
	Запрос = Новый Запрос();
	Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Остатки.Номенклатура,
		|	СУММА(1) КАК Количество
		|ПОМЕСТИТЬ ВТ_Карточки
		|ИЗ
		|	РегистрНакопления.Остатки КАК Остатки
		|
		|СГРУППИРОВАТЬ ПО
		|	Остатки.Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	КарточкиНоменклатуры.Ссылка КАК КарточкаНоменклатуры,
		|	ЕСТЬNULL(ВТ_Карточки.Количество, 0) КАК Количество
		|ИЗ
		|	Справочник.КарточкиНоменклатуры КАК КарточкиНоменклатуры
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Карточки КАК ВТ_Карточки
		|		ПО КарточкиНоменклатуры.Ссылка = ВТ_Карточки.Номенклатура
		|ГДЕ
		|	ЕСТЬNULL(ВТ_Карточки.Количество, 0) = 0
		|	И КарточкиНоменклатуры.ВидНоменклатуры.ВестиУчетПоКарточкамНоменклатуры = ИСТИНА";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ОбъектСпр = Выборка.КарточкаНоменклатуры.ПолучитьОбъект();
		ОбъектСпр.УстановитьПометкуУдаления(Истина, Истина);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура СнятьПометкиНаУдалениеНаСервере()
	
	Запрос = Новый Запрос();
	Запрос.Текст =
		"ВЫБРАТЬ
		|	КарточкиНоменклатуры.Ссылка КАК КарточкаНоменклатуры
		|ИЗ
		|	Справочник.КарточкиНоменклатуры КАК КарточкиНоменклатуры
		|ГДЕ
		|	КарточкиНоменклатуры.ПометкаУдаления = ИСТИНА";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ОбъектСпр = Выборка.КарточкаНоменклатуры.ПолучитьОбъект();
		ОбъектСпр.УстановитьПометкуУдаления(Ложь, Ложь);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти