
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
		
	// Устанавливаем отборы при открытии.
	Если Параметры.Свойство("ОтборНоменклатура") Тогда
		ОтборНоменклатура = Параметры.ОтборНоменклатура;
	КонецЕсли;
	Если Параметры.Свойство("ОтборПоступление") Тогда
		ОтборПоступление = Параметры.ОтборПоступление;
	КонецЕсли;	
	Если Параметры.Свойство("ОтборТолькоНеИспользованные") Тогда		
		ОтборТолькоНеИспользованные = Параметры.ОтборТолькоНеИспользованные;		
	КонецЕсли;	
	Если Параметры.Свойство("ОтборТолькоКомплекты") Тогда
		ОтборТолькоКомплекты = Параметры.ОтборТолькоКомплекты;
	Иначе
		ОтборТолькоКомплекты = Ложь;
	КонецЕсли;
	Если Параметры.Свойство("ДатаАктуальности") Тогда
		ДатаАктуальности = Параметры.ДатаАктуальности;
	Иначе
		ДатаАктуальности = КонецДня(ТекущаяДатаСеанса());
	КонецЕсли;
	Если Параметры.Свойство("Организация") Тогда
		ОтборОрганизация = Параметры.Организация;
	Иначе
		ОтборОрганизация = Справочники.Организации.ПустаяСсылка();
	КонецЕсли;
	Если Параметры.Свойство("МестоХранения") Тогда
		ОтборМестоХранения = Параметры.МестоХранения;
	Иначе
		ОтборМестоХранения = Справочники.МестаХранения.ПустаяСсылка();
	КонецЕсли;
	Если Параметры.Свойство("ОтборПоОрганизацииМестуХранения") Тогда
		ОтборПоОрганизацииМестуХранения = Параметры.ОтборПоОрганизацииМестуХранения;
	Иначе
		ОтборПоОрганизацииМестуХранения = Ложь;
	КонецЕсли;
		
	Список.Параметры.УстановитьЗначениеПараметра("ДатаАктуальности", 				ДатаАктуальности);
	Список.Параметры.УстановитьЗначениеПараметра("ТолькоКомплекты", 				ОтборТолькоКомплекты);
	Список.Параметры.УстановитьЗначениеПараметра("Организация", 					ОтборОрганизация);
	Список.Параметры.УстановитьЗначениеПараметра("МестоХранения", 					ОтборМестоХранения);
	Список.Параметры.УстановитьЗначениеПараметра("ОтборПоОрганизацииМестуХранения", ОтборПоОрганизацииМестуХранения);
	
	// Обновление отборов на форме.
	УстановитьОтборы();	
		
	// Выбранное значение.
	Если Параметры.Свойство("ВыбранноеЗначение") И ЗначениеЗаполнено(Параметры.ВыбранноеЗначение) Тогда
		Элементы.Список.ТекущаяСтрока = Параметры.ВыбранноеЗначение;
	КонецЕсли;
	
	УстановитьВидимостьИДоступность();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОтборНоменклатураПриИзменении(Элемент)
	
	УстановитьОтборы();
	
КонецПроцедуры

&НаКлиенте
Процедура ТолькоНеИспользованныеПриИзменении(Элемент)
	
	УстановитьОтборы();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВидимостьОтбора(Команда)
	
	Элементы.ФормаВидимостьОтбора.Пометка = НЕ Элементы.ФормаВидимостьОтбора.Пометка;
	УстановитьВидимостьИДоступность();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьВидимостьИДоступность()
	
	Элементы.ГруппаСписокОтбор.Видимость = Элементы.ФормаВидимостьОтбора.Пометка;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОтборы()
	
	Список.Отбор.Элементы.Очистить();	
	РаботаСОтборамиКлиентСервер.УстановитьЭлементОтбораСписка(Список, "Владелец", ОтборНоменклатура, 
		ЗначениеЗаполнено(ОтборНоменклатура));
		
	РаботаСОтборамиКлиентСервер.УстановитьЭлементОтбораСписка(Список, "ДокументПоступления", Новый СписокЗначений, 
		ОтборТолькоНеИспользованные, ВидСравненияКомпоновкиДанных.НеЗаполнено);
		
	РаботаСОтборамиКлиентСервер.УстановитьЭлементОтбораСписка(Список, "ВидНоменклатуры.ВестиУчетПоКарточкамНоменклатуры", 
		Истина, Истина); 
	
	Если ЗначениеЗаполнено(ОтборПоступление) Тогда		
		ГруппаЭлементовОтбора 			= Список.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
 		ГруппаЭлементовОтбора.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли;
		ГруппаЭлементовОтбора.Использование = Истина;
		
		ЭлементОтбора 					= ГруппаЭлементовОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
 		ЭлементОтбора.ЛевоеЗначение 	= Новый ПолеКомпоновкиДанных("ДокументПоступления");
 		ЭлементОтбора.ВидСравнения  	= ВидСравненияКомпоновкиДанных.Равно;
 		ЭлементОтбора.ПравоеЗначение 	= ОтборПоступление;
		ЭлементОтбора.Использование		= Истина;
		
		ЭлементОтбора 					= ГруппаЭлементовОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
 		ЭлементОтбора.ЛевоеЗначение 	= Новый ПолеКомпоновкиДанных("ДокументПоступления");
 		ЭлементОтбора.ВидСравнения  	= ВидСравненияКомпоновкиДанных.НеЗаполнено; 		
		ЭлементОтбора.Использование		= Истина;
	КонецЕсли;
	
	Если ОтборПоОрганизацииМестуХранения Тогда
		
		Запрос = Новый Запрос();
		Запрос.Текст =
			"ВЫБРАТЬ
			|	ОстаткиОстатки.Номенклатура КАК Номенклатура
			|ИЗ
			|	РегистрНакопления.Остатки.Остатки(
			|			&ДатаАктуальности,
			|			Организация = &Организация
			|				И МестоХранения = &МестоХранения) КАК ОстаткиОстатки
			|ГДЕ
			|	ОстаткиОстатки.КоличествоОстаток > 0";
		Запрос.УстановитьПараметр("ДатаАктуальности", ДатаАктуальности);
		Запрос.УстановитьПараметр("Организация", ОтборОрганизация);
		Запрос.УстановитьПараметр("МестоХранения", ОтборМестоХранения);
		
		Массив = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Номенклатура");
		РаботаСОтборамиКлиентСервер.УстановитьЭлементОтбораСписка(Список, "Ссылка", Массив, Массив.Количество() > 0);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти