#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПриКопировании(ОбъектКопирования)
	
	Идентификатор  = "";
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Ошибки = Неопределено;
	
	Если ТипДействия = Перечисления.ТипДействийПравилСобытий.АвтоназначениеИсполнителя Тогда
		
		мПравила = Перечисления.ПравилаАвтоназначенияИсполнителей;
		Если НЕ ЗначениеЗаполнено(ПравилоАвтоназначенияИсполнителей) Тогда
			ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Ошибки,	"Объект.ПравилоАвтоназначенияИсполнителей",
				НСтр("ru = 'Не заполнено правило автоназначения исполнителей.'"), "");
		Иначе
			Если НЕ ЗначениеЗаполнено(АвтоназначениеЗначение)
				И (ПравилоАвтоназначенияИсполнителей = мПравила.ФиксированнаяГруппаИсполнителей
				ИЛИ ПравилоАвтоназначенияИсполнителей = мПравила.ФиксированныйИсполнитель
				ИЛИ ПравилоАвтоназначенияИсполнителей = мПравила.ИсполнительСНаименьшейНагрузкойИзФиксированнойЛинииПоддержки
				ИЛИ ПравилоАвтоназначенияИсполнителей = мПравила.ИсполнительСНаименьшейНагрузкойИзФиксированнойГруппы) Тогда
				ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Ошибки, "Объект.АвтоназначениеЗначение",
					НСтр("ru = 'Не заполнено значение реквизита автоназначения.'"), "");
			КонецЕсли;
		КонецЕсли;
		
	ИначеЕсли ТипДействия = Перечисления.ТипДействийПравилСобытий.ВыполнениеОбработки Тогда
		
		Если НЕ ЗначениеЗаполнено(ВнешняяОбработка) Тогда
			ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Ошибки,	"Объект.ВнешняяОбработка",
				НСтр("ru = 'Не заполнена внешняя обработка.'"), "");
		КонецЕсли;
		
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.СообщитьОшибкиПользователю(Ошибки, Отказ);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли