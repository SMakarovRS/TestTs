
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Параметры.ТипУсловия) И ЗначениеЗаполнено(Параметры.ТипПравила) Тогда
		
		Если Параметры.ТипУсловия = Перечисления.ТипУсловийПравилСобытий.ПроверкаРеквизитовОбъекта Тогда
			Если Параметры.Свойство("ПолноеИмяОснования") Тогда
				Параметры.Отбор.Вставить("ПолноеИмяОснования", Параметры.ПолноеИмяОснования);
				
				РаботаСОтборамиКлиентСервер.УстановитьЭлементОтбораСписка(Список.Отбор, 
					"ПолноеИмяОснования", Параметры.ПолноеИмяОснования);
					
			КонецЕсли;			
			
			СписокНеДоступных = Новый СписокЗначений;
			СписокНеДоступных.Добавить(Перечисления.ТипДействийПравилСобытий.РасчетМетрик);				
			
			РаботаСОтборамиКлиентСервер.УстановитьЭлементОтбораСписка(Список.Отбор, 
				"ТипДействия", СписокНеДоступных, , ВидСравненияКомпоновкиДанных.НеВСписке);
			
		ИначеЕсли Параметры.ТипУсловия = Перечисления.ТипУсловийПравилСобытий.ПроизвольныйАлгоритм Тогда
			Параметры.Отбор.Вставить("ТипДействия", Перечисления.ТипДействийПравилСобытий.ПроизвольныйАлгоритм);
			Параметры.Отбор.Вставить("ПолноеИмяОснования", "");
			
			РаботаСОтборамиКлиентСервер.УстановитьЭлементОтбораСписка(Список.Отбор, 
				"ТипДействия", Перечисления.ТипДействийПравилСобытий.ПроизвольныйАлгоритм);
			
		ИначеЕсли Параметры.ТипУсловия = Перечисления.ТипУсловийПравилСобытий.РасчетМетрик Тогда
			Параметры.Отбор.Вставить("ТипДействия", Перечисления.ТипДействийПравилСобытий.РасчетМетрик);
			Параметры.Отбор.Вставить("ПолноеИмяОснования", Параметры.ПолноеИмяОснования);
			
			РаботаСОтборамиКлиентСервер.УстановитьЭлементОтбораСписка(Список.Отбор, 
				"ТипДействия", Перечисления.ТипДействийПравилСобытий.РасчетМетрик);
				
			РаботаСОтборамиКлиентСервер.УстановитьЭлементОтбораСписка(Список.Отбор, 
				"ПолноеИмяОснования", Параметры.ПолноеИмяОснования);
			
		КонецЕсли;
					
	КонецЕсли;
	
	Если Параметры.Отбор.Свойство("ПолноеИмяОснования") И ЗначениеЗаполнено(Параметры.Отбор.ПолноеИмяОснования) Тогда
		
		МетаданныеОснования = Метаданные.НайтиПоПолномуИмени(Параметры.Отбор.ПолноеИмяОснования);
		Если МетаданныеОснования <> Неопределено Тогда
						
			Заголовок = СтрШаблон(НСтр("ru = 'Действия правил событий: %1'"), МетаданныеОснования.Представление());
			АвтоЗаголовок = Ложь;
			
		КонецЕсли;
		
	КонецЕсли;
	
	// Установим настройки формы для случая открытия в режиме выбора.
	Элементы.Список.РежимВыбора = Параметры.РежимВыбора;
	Элементы.Список.МножественныйВыбор = ?(Параметры.ЗакрыватьПриВыборе = Неопределено, Ложь, Не Параметры.ЗакрыватьПриВыборе);
	
	Если Параметры.РежимВыбора Тогда
		КлючНазначенияИспользования = "ВыборПодбор";
		РежимОткрытияОкна = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
	Иначе
		КлючНазначенияИспользования = "Список";
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
