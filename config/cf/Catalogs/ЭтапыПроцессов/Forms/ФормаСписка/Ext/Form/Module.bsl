
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	УстановитьУсловноеОформление();
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если ИмяСобытия = "ОбновитьЦветаЭтаповПроцесса" Тогда
		УстановитьУсловноеОформление();
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УО = Список.УсловноеОформление.Элементы;
	УО.Очистить();
	Запрос = Новый Запрос();
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ЭтапыПроцессов.Ссылка,
		|	ЭтапыПроцессов.ЦветФона,
		|	ЭтапыПроцессов.ЦветТекста
		|ИЗ
		|	Справочник.ЭтапыПроцессов КАК ЭтапыПроцессов
		|ГДЕ
		|	ЭтапыПроцессов.ЭтоГруппа = ЛОЖЬ";
	
	мWebЦветаБелый = WebЦвета.Белый;
	мWebЦветаЧерный = WebЦвета.Черный;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Попытка
			ЦветФона 	= РаботаСЦветомКлиентСервер.HexВЦвет(Выборка.ЦветФона);
			ЦветТекста 	= РаботаСЦветомКлиентСервер.HexВЦвет(Выборка.ЦветТекста);
		Исключение
			ЦветФона 	= мWebЦветаБелый;
			ЦветТекста 	= мWebЦветаЧерный;
		КонецПопытки;
		
		Если ЦветФона <> мWebЦветаБелый ИЛИ ЦветТекста <> мWebЦветаЧерный Тогда
			ЭлементУО 						= УО.Добавить();
			Если ЦветФона <> мWebЦветаБелый Тогда
				ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветФона", ЦветФона);
			КонецЕсли;
			Если ЦветТекста <> мWebЦветаЧерный Тогда
				ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветТекста);
			КонецЕсли;
			ЭлементУсловия 					= ЭлементУО.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
			ЭлементУсловия.ЛевоеЗначение 	= Новый ПолеКомпоновкиДанных("Ссылка");
			ЭлементУсловия.ВидСравнения 	= ВидСравненияКомпоновкиДанных.Равно;
			ЭлементУсловия.ПравоеЗначение 	= Выборка.Ссылка;		
		КонецЕсли;
	КонецЦикла;
	
	// Помеченные на удаление.
	ЭлементУО 						= УО.Добавить();
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", Новый Цвет(192, 192, 192));
	ЭлементУсловия 					= ЭлементУО.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементУсловия.ЛевоеЗначение 	= Новый ПолеКомпоновкиДанных("ПометкаУдаления");
	ЭлементУсловия.ВидСравнения 	= ВидСравненияКомпоновкиДанных.Равно;
	ЭлементУсловия.ПравоеЗначение 	= Истина;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПредопределенныеНаСервере(Знач ИмяТемы = "")
	
	Справочники.ЭтапыПроцессов.ЗаполнитьЭтапыПроцессовПриПервоначальномЗаполнении(ИмяТемы);
	УстановитьУсловноеОформление();	
	
КонецПроцедуры

&НаКлиенте
Процедура ПоУмолчанию(Команда)
	
	ЗаполнитьПредопределенныеНаСервере();
	Элементы.Список.Обновить();
	Оповестить("ОбновитьЦветаЭтаповПроцесса");
	
КонецПроцедуры

&НаКлиенте
Процедура ПастельныеТона(Команда)
	
	ЗаполнитьПредопределенныеНаСервере("ПастельныеТона");
	Элементы.Список.Обновить();
	Оповестить("ОбновитьЦветаЭтаповПроцесса");
	
КонецПроцедуры

&НаКлиенте
Процедура СовременнаяТема(Команда)

	ЗаполнитьПредопределенныеНаСервере("СовременнаяТема");
	Элементы.Список.Обновить();
	Оповестить("ОбновитьЦветаЭтаповПроцесса");
	
КонецПроцедуры

#КонецОбласти