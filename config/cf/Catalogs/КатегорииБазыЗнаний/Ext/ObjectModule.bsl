#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОбработчикиСобытий

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТКА СОБЫТИЙ МОДУЛЯ

Процедура ПередЗаписью(Отказ)
	
	Если ЭтотОбъект.ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	КатегорииОбновления = Новый Массив;
	Если ЗначениеЗаполнено(ЭтотОбъект.Родитель) Тогда
		КатегорииОбновления.Добавить(ЭтотОбъект.Родитель);
	КонецЕсли;
	Если НЕ ЭтотОбъект.ЭтоНовый() Тогда
		РодительСсылка = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ЭтотОбъект.Ссылка, "Родитель");
		Если ЗначениеЗаполнено(РодительСсылка) Тогда
			КатегорииОбновления.Добавить(РодительСсылка);
		КонецЕсли;
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(Автор) Тогда
		Автор = Пользователи.ТекущийПользователь();
	КонецЕсли;
	
	ЭтотОбъект.ДополнительныеСвойства.Вставить("ЭтоНовый", ЭтотОбъект.ЭтоНовый());
	ЭтотОбъект.ДополнительныеСвойства.Вставить("КатегорииОбновления", КатегорииОбновления);
	ЭтотОбъект.ДополнительныеСвойства.Вставить("ПометкаУдаленияСсылки", ?(ЭтотОбъект.ЭтоНовый(), Ложь, ЭтотОбъект.Ссылка.ПометкаУдаления));
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ЭтотОбъект.ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ЭтотОбъект.ДополнительныеСвойства.Свойство("КатегорииОбновления")
		И ТипЗнч(ЭтотОбъект.ДополнительныеСвойства.КатегорииОбновления) = Тип("Массив")
		И ЭтотОбъект.ДополнительныеСвойства.КатегорииОбновления.Количество() > 0 Тогда
		
		МассивПараметров = Новый Массив;
		МассивПараметров.Добавить(Новый Структура("Категории", ЭтотОбъект.ДополнительныеСвойства.КатегорииОбновления));
		
		ФоновыеЗадания.Выполнить(
			"БазаЗнаний.ОбновитьСтатистикуКатегорий",
			МассивПараметров, ,
			"Обновление статистики по категориям");
	КонецЕсли;

	Если ЭтотОбъект.ДополнительныеСвойства.Свойство("ЭтоНовый") И ЭтотОбъект.ДополнительныеСвойства.ЭтоНовый Тогда
		ДобавитьКатегориюВИзбранное();		
		ДобавитьНовостьБазыЗнаний(1);		
	КонецЕсли;
	
	ПометкаУдаленияСсылки = (ЭтотОбъект.ДополнительныеСвойства.Свойство("ПометкаУдаленияСсылки") И ЭтотОбъект.ДополнительныеСвойства.ПометкаУдаленияСсылки);
	Если ПометкаУдаленияСсылки <> ЭтотОбъект.ПометкаУдаления Тогда
		
		Если ЭтотОбъект.ПометкаУдаления Тогда
			ДобавитьНовостьБазыЗнаний(3);			
		Иначе
			ДобавитьНовостьБазыЗнаний(4);
		КонецЕсли;			
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередУдалением(Отказ)	
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

////////////////////////////////////////////////////////////////////////////////
// ПРОЧИЕ МЕТОДЫ

// ТипНовости: 1 - создание, 2 - изменение, 3 - удаление
Функция ДобавитьНовостьБазыЗнаний(ТипНовости)
	
	Если ЭтотОбъект.ТипКатегории = Перечисления.ТипыКатегорийСтатейБазыЗнаний.Приватная Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ТекПользователь		= Пользователи.ТекущийПользователь();
	
	ПараметрыСсылкиОтв	= Новый Структура("id", Строка(ТекПользователь.УникальныйИдентификатор()));
	АдресСсылкиОтв		= БазаЗнанийAPIКлиентСервер.КонструкторСсылки_page("user", ПараметрыСсылкиОтв);
	
	ПараметрыСсылкиКат	= Новый Структура("id", Строка(ЭтотОбъект.Ссылка.УникальныйИдентификатор()));
	АдресСсылкиКат		= БазаЗнанийAPIКлиентСервер.КонструкторСсылки_page("category", ПараметрыСсылкиКат);
	
	Если ТипНовости = 1 Тогда
		ВидНовости	= Перечисления.ВидыНовостейБазыЗнаний.Создание;
		Заголовок	= "Новая категория '%3'";
		ТекстHTML	= "<div>%1 в %2 добавлена новая категория базы знаний.
		|	<br>Заголовок: %3.
		|	<br>Автор: <a href='" + АдресСсылкиОтв + "'>%4</a>
		|	<br>Описание: " + СокрЛП(ЭтотОбъект.Описание) + "
		|	<br><a href='" + АдресСсылкиКат + "'>Перейти</a>
		|</div>";
	ИначеЕсли ТипНовости = 3 Тогда
		ВидНовости	= Перечисления.ВидыНовостейБазыЗнаний.Удаление;
		Заголовок	= "Удалена категория '%3'";
		ТекстHTML	= "<div>%1 в %2 удалена категория базы знаний.
		|	<br>Заголовок: %3.
		|	<br>Автор: <a href='" + АдресСсылкиОтв + "'>%4</a>
		|	<br>Описание: " + СокрЛП(ЭтотОбъект.Описание) + "
		|</div>";
	ИначеЕсли ТипНовости = 4 Тогда
		ВидНовости	= Перечисления.ВидыНовостейБазыЗнаний.Изменение;
		Заголовок	= "Восстановлена категория '%3'";
		ТекстHTML	= "<div>%1 в %2 восстановлена категория базы знаний.
		|	<br>Заголовок: %3.
		|	<br>Автор: <a href='" + АдресСсылкиОтв + "'>%4</a>
		|	<br>Описание: " + СокрЛП(ЭтотОбъект.Описание) + "
		|</div>";		
	КонецЕсли;
	
	ДатаНовости = ТекущаяДатаСеанса();
	
	МассивПараметров = Новый Массив;
	МассивПараметров.Добавить(Формат(ДатаНовости, "ДЛФ=DD"));
	МассивПараметров.Добавить(Формат(ДатаНовости, "ДФ=HH:mm"));
	МассивПараметров.Добавить(СокрЛП(ЭтотОбъект.Наименование));
	МассивПараметров.Добавить(Строка(ТекПользователь));
	
	Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтрокуИзМассива(Заголовок, МассивПараметров);
	ТекстHTML = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтрокуИзМассива(ТекстHTML, МассивПараметров);
	
	ПараметрыНовости = Новый Структура;
	ПараметрыНовости.Вставить("Автор"				, ТекПользователь);
	ПараметрыНовости.Вставить("Заголовок"			, Заголовок);
	ПараметрыНовости.Вставить("ТекстHTML"			, ТекстHTML);
	ПараметрыНовости.Вставить("ОбъектБазыЗнаний"	, ЭтотОбъект.Ссылка);	
	ПараметрыНовости.Вставить("Важность"			, 2);
	ПараметрыНовости.Вставить("ВидНовости"			, ВидНовости);
	
	БазаЗнанийВызовСервера.ДобавитьНовость(ПараметрыНовости);
	
КонецФункции

Процедура ДобавитьКатегориюВИзбранное()
	
	МенеджерЗаписи = РегистрыСведений.ИзбранноеБазыЗнаний.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Пользователь			= Пользователи.ТекущийПользователь();
	МенеджерЗаписи.КатегорияБазыЗнаний	= ЭтотОбъект.Ссылка;
	МенеджерЗаписи.Прочитать();
	Если МенеджерЗаписи.Выбран() Тогда
		Возврат;
	КонецЕсли;
	
	МенеджерЗаписи.Пользователь			= Пользователи.ТекущийПользователь();
	МенеджерЗаписи.КатегорияБазыЗнаний	= ЭтотОбъект.Ссылка;
	МенеджерЗаписи.Создание				= Истина;
	МенеджерЗаписи.Изменение			= Истина;
	МенеджерЗаписи.Удаление				= Истина;
	МенеджерЗаписи.Оповещение			= Истина;
	МенеджерЗаписи.Прочее				= Истина;
	МенеджерЗаписи.Записать();
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли