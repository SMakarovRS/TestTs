#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;		
	КонецЕсли;
	
	Если Организации.Количество() = 0 И ЭтоГруппа = ЛОЖЬ Тогда
		ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'В бюджете должна быть выбрана хотя бы одна организация'"));
		Отказ = Истина;
	КонецЕсли;
	
	Если НЕ ЭтоГруппа Тогда
		// Проверка, даты начала бюджета.
		Запрос = Новый Запрос();
		Запрос.Текст =
			"ВЫБРАТЬ ПЕРВЫЕ 1
			|	ПериодыБюджетов.ДатаНачала,
			|	ПериодыБюджетов.ДатаОкончания
			|ИЗ
			|	Справочник.ПериодыБюджетов КАК ПериодыБюджетов
			|ГДЕ
			|	ПериодыБюджетов.Владелец = &Владелец
			|
			|УПОРЯДОЧИТЬ ПО
			|	ПериодыБюджетов.ДатаНачала";
		Запрос.УстановитьПараметр("Владелец", Ссылка);
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			Если НачалоДня(Выборка.ДатаНачала) <> НачалоДня(ДатаНачала) Тогда
				ОбщегоНазначения.СообщитьПользователю(
					СтрШаблон(НСтр("ru = 'В бюджете дата начала должна быть равна минимальной дате периода бюджета %1'"),
						Формат(Выборка.ДатаНачала, НСтр("ru = 'ДФ=dd.MM.yyyy'"))));
				Отказ = Истина;			
			КонецЕсли;
		КонецЕсли;
		
		// Проверка, даты начала бюджета.
		Запрос = Новый Запрос();
		Запрос.Текст =
			"ВЫБРАТЬ ПЕРВЫЕ 1
			|	ПериодыБюджетов.ДатаНачала,
			|	ПериодыБюджетов.ДатаОкончания
			|ИЗ
			|	Справочник.ПериодыБюджетов КАК ПериодыБюджетов
			|ГДЕ
			|	ПериодыБюджетов.Владелец = &Владелец
			|
			|УПОРЯДОЧИТЬ ПО
			|	ПериодыБюджетов.ДатаНачала УБЫВ";
		Запрос.УстановитьПараметр("Владелец", Ссылка);
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			Если КонецДня(Выборка.ДатаОкончания) <> КонецДня(ДатаОкончания) Тогда
				ОбщегоНазначения.СообщитьПользователю(
					СтрШаблон(НСтр("ru = 'В бюджете дата окончания должна быть равна максимальной дате периода бюджета %1'"),
						Формат(Выборка.ДатаОкончания, НСтр("ru = 'ДФ=dd.MM.yyyy'"))));
				Отказ = Истина;			
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#КонецЕсли