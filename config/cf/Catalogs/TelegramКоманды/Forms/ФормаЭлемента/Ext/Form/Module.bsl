
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Объект.Ссылка.Пустая() Тогда
		Объект.Активность = Истина;
		Объект.ПрименитьОстальныеКоманды = Истина;
	КонецЕсли;
	
	ОбновитьВидимостьДоступность();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыВходящийТекстИ

&НаКлиенте
Процедура ВходящийТекстИПриАктивизацииСтроки(Элемент)
	
	ОбновитьДоступностьСтолбцаЗначениеТаблицыВходящегоТекста("ВходящийТекстИ");
	
КонецПроцедуры

&НаКлиенте
Процедура ВходящийТекстИВидСравненияПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.ВходящийТекстИ.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ТекущиеДанные.ВидСравнения = ПредопределенноеЗначение("Перечисление.TelegramВидыСравнения.Заполнено")
		ИЛИ ТекущиеДанные.ВидСравнения = ПредопределенноеЗначение("Перечисление.TelegramВидыСравнения.НеЗаполнено") Тогда
		
		ТекущиеДанные.Значение = "";
		
	КонецЕсли;
	
	ОбновитьДоступностьСтолбцаЗначениеТаблицыВходящегоТекста("ВходящийТекстИ");
	
КонецПроцедуры

&НаКлиенте
Процедура ВходящийТекстИПослеУдаления(Элемент)
	
	ОбновитьВидимостьДоступность();
	
КонецПроцедуры

&НаКлиенте
Процедура ВходящийТекстИПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	ОбновитьВидимостьДоступность();
	
КонецПроцедуры

&НаКлиенте
Процедура УсловияИПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	ОбновитьВидимостьДоступность();
	
КонецПроцедуры

&НаКлиенте
Процедура УсловияИПослеУдаления(Элемент)
	
	ОбновитьВидимостьДоступность();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыВходящийТекстИЛИ

&НаКлиенте
Процедура ВходящийТекстИЛИПриАктивизацииСтроки(Элемент)
	
	ОбновитьДоступностьСтолбцаЗначениеТаблицыВходящегоТекста("ВходящийТекстИЛИ");
	
КонецПроцедуры

&НаКлиенте
Процедура ВходящийТекстИЛИВидСравненияПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.ВходящийТекстИЛИ.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ТекущиеДанные.ВидСравнения = ПредопределенноеЗначение("Перечисление.TelegramВидыСравнения.Заполнено")
		ИЛИ ТекущиеДанные.ВидСравнения = ПредопределенноеЗначение("Перечисление.TelegramВидыСравнения.НеЗаполнено") Тогда
		
		ТекущиеДанные.Значение = "";
		
	КонецЕсли;
	
	ОбновитьДоступностьСтолбцаЗначениеТаблицыВходящегоТекста("ВходящийТекстИЛИ");	
	
КонецПроцедуры

&НаКлиенте
Процедура ВходящийТекстИЛИПослеУдаления(Элемент)
	
	ОбновитьВидимостьДоступность();
	
КонецПроцедуры

&НаКлиенте
Процедура ВходящийТекстИЛИПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	ОбновитьВидимостьДоступность();
	
КонецПроцедуры

&НаКлиенте
Процедура УсловияИЛИПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
		
	ОбновитьВидимостьДоступность();
	
КонецПроцедуры

&НаКлиенте
Процедура УсловияИЛИПослеУдаления(Элемент)
	
	ОбновитьВидимостьДоступность();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОбновитьДоступностьСтолбцаЗначениеТаблицыВходящегоТекста(Знач ИмяТаблицы)
	
	ТекущиеДанные = Элементы[ИмяТаблицы].ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Элементы[ИмяТаблицы + "Значение"].Доступность = ЗначениеЗаполнено(ТекущиеДанные.ВидСравнения)
		И ТекущиеДанные.ВидСравнения <> ПредопределенноеЗначение("Перечисление.TelegramВидыСравнения.Заполнено")
		И ТекущиеДанные.ВидСравнения <> ПредопределенноеЗначение("Перечисление.TelegramВидыСравнения.НеЗаполнено");
		
КонецПроцедуры

&НаСервере
Процедура ОбновитьВидимостьДоступность()
	
	Элементы.СтраницаВходящийТекстИ.Заголовок = НСтр("ru = 'Условия И'");
	Если Объект.ВходящийТекстИ.Количество() + Объект.УсловияИ.Количество() > 0 Тогда
		Элементы.СтраницаВходящийТекстИ.Заголовок = Элементы.СтраницаВходящийТекстИ.Заголовок
			+ " (" + Формат(Объект.ВходящийТекстИ.Количество(), "ЧРД=; ЧРГ=; ЧН=0; ЧГ=") + "+"
			+ Формат(Объект.УсловияИ.Количество(), "ЧРД=; ЧРГ=; ЧН=0; ЧГ=") + ")"
	КонецЕсли;
	
	Элементы.СтраницаВходящийТекстИЛИ.Заголовок = НСтр("ru = 'Условия ИЛИ'");
	Если Объект.ВходящийТекстИЛИ.Количество() + Объект.УсловияИЛИ.Количество() > 0 Тогда
		Элементы.СтраницаВходящийТекстИЛИ.Заголовок = Элементы.СтраницаВходящийТекстИЛИ.Заголовок
			+ " (" + Формат(Объект.ВходящийТекстИЛИ.Количество(), "ЧРД=; ЧРГ=; ЧН=0; ЧГ=") + "+"
			+ Формат(Объект.УсловияИЛИ.Количество(), "ЧРД=; ЧРГ=; ЧН=0; ЧГ=") + ")"
	КонецЕсли;
		
КонецПроцедуры

#КонецОбласти