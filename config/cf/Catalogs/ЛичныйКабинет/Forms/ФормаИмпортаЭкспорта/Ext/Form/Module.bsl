
#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура РежимЗагрузкиПриИзменении(Элемент)
	Элементы.ВыполнитьОперацию.Заголовок = ?(РежимЗагрузки, "Выполнить загрузку", НСтр("ru = 'Выполнить выгрузку'"));
КонецПроцедуры

&НаКлиенте
Процедура ПутьНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Если НЕ ПодключитьРасширениеРаботыСФайлами() Тогда
		Возврат;
	КонецЕсли;
	
	Диалог = Новый ДиалогВыбораФайла(?(РежимЗагрузки, РежимДиалогаВыбораФайла.Открытие, 
		РежимДиалогаВыбораФайла.Сохранение));
	Диалог.Фильтр = НСтр("ru = 'Файл ZIP-архива (*.zip)|*.zip|Все файлы (*.*)|*.*'");
	Диалог.ПолноеИмяФайла = ИмяФайла;
	Если НЕ Диалог.Выбрать() Тогда
		Возврат;
	КонецЕсли;
	ИмяФайла = Диалог.ПолноеИмяФайла;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыполнитьОперацию(Команда)
	
	Если ПустаяСтрока(ИмяФайла) Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Не указан путь для выгрузки/загрузки'"));
		Возврат;
	ИначеЕсли НЕ ПодключитьРасширениеРаботыСФайлами() Тогда
		Возврат;
	КонецЕсли;
	
	Если РежимЗагрузки Тогда
		ЗагрузитьФайлы();
		Оповестить("ОбновитьСтраницыЛичногоКабинета");
		ПоказатьПредупреждение(, НСтр("ru = 'Загрузка завершена'"));
	Иначе
		ВыгрузитьФайлы();
		ПоказатьПредупреждение(, НСтр("ru = 'Выгрузка завершена'"));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Очистить(Команда)

	ПоказатьВопрос(Новый ОписаниеОповещения("ОчиститьЗавершение", ЭтотОбъект), 
		НСтр("ru = 'Вы действительно хотите удалить ВСЕ страницы?'"), РежимДиалогаВопрос.ДаНет);
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Ответ = РезультатВопроса; 
	Если Ответ = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
	
	ОчиститьНаСервере();
	Оповестить("ОбновитьСтраницыЛичногоКабинета");
	ПоказатьПредупреждение(, НСтр("ru = 'Очистка завершена'"));

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция ВыгрузитьНаСервере()
	
	Возврат Справочники.ЛичныйКабинет.ВыгрузитьСтраницы();
	
КонецФункции

&НаСервереБезКонтекста
Процедура ЗагрузитьНаСервере(Знач Адрес)
	
	Справочники.ЛичныйКабинет.ЗагрузитьСтраницы(Адрес);	
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьФайлы()

	 ДД		= Новый ДвоичныеДанные(ИмяФайла);
	 Адрес  = ПоместитьВоВременноеХранилище(ДД);
	 ЗагрузитьНаСервере(Адрес);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьФайлы()
	
	УдалитьФайлы(ИмяФайла);
	Адрес 	= ВыгрузитьНаСервере();
	ДД 		= ПолучитьИзВременногоХранилища(Адрес);	
	ДД.Записать(ИмяФайла);
	УдалитьИзВременногоХранилища(Адрес);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ОчиститьНаСервере()
	
	Справочники.ЛичныйКабинет.ОчиститьСтраницыИНастройки();
   
КонецПроцедуры

#КонецОбласти

