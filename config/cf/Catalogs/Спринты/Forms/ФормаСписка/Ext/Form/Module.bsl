
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
			
	Если Параметры.Свойство("РежимВыбора") И Параметры.РежимВыбора Тогда
		ЗакрыватьПриВыборе = Истина;
		Элементы.Список.РежимВыбора = Истина;
		Элементы.Список.ТекущаяСтрока = Параметры.ТекущаяСтрока;
	КонецЕсли;
	
	// Только актуальные.
	РаботаСОтборамиКлиентСервер.УстановитьЭлементОтбораСписка(Список, "Завершен", Ложь); 
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ОбновитьФормуСпискаЗаданий" Тогда
		
		Элементы.Список.Обновить();
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПоказыватьТолькоВключаяЗавершенныеСпринты(Команда)
	
	Элементы.ФормаПоказыватьТолькоОткрытыеСпринты.Пометка = НЕ Элементы.ФормаПоказыватьТолькоОткрытыеСпринты.Пометка;
	
	Если Элементы.ФормаПоказыватьТолькоОткрытыеСпринты.Пометка Тогда
		РаботаСОтборамиКлиентСервер.УдалитьЭлементОтбораСписка(Список, "Завершен");
		
	Иначе		
		
		// Только актуальные.
		РаботаСОтборамиКлиентСервер.УстановитьЭлементОтбораСписка(Список, "Завершен", Ложь);
			
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьСпринт(Команда)
	
	МассивВыделенныхСтрок = Новый Массив;
	
	Если Команда <> Неопределено Тогда
		Если Элементы.Список.ТекущиеДанные = Неопределено Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Выделите спринты для продолжения'"));
			Возврат;	
		КонецЕсли;	
		
		МассивВыделенныхСтрок = Элементы.Список.ВыделенныеСтроки;
		Если МассивВыделенныхСтрок.Количество() = 0 Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Выделите спринты для их открытия'"));
			Возврат;
		КонецЕсли;	
	Иначе
		МассивВыделенныхСтрок.Добавить(Элементы.Список.ТекущиеДанные.Ссылка);
	КонецЕсли;
		
	ЗаданияСервер.ОткрытьИлиЗавершитьСпринты(МассивВыделенныхСтрок,  Команда.Имя <> "ОткрытьСпринт");	
	Элементы.Список.Обновить();	
	
КонецПроцедуры

&НаКлиенте
Процедура ПеренестиНевыполненныеЗаданияДругогоСпринтаВТекущийСпринт(Команда)
	
	Если Элементы.Список.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Результат = Неопределено;
	ОткрытьФорму("Справочник.Спринты.ФормаВыбора", Новый Структура("РежимВыбора", Истина),ЭтаФорма,,,, 
		Новый ОписаниеОповещения("ПеренестиВсеЗаданияДругогоСпринтаВТекущийСпринтЗавершение", ЭтотОбъект),
		РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
	
КонецПроцедуры

&НаКлиенте
Процедура ПеренестиВсеЗаданияДругогоСпринтаВТекущийСпринтЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если ЗначениеЗаполнено(Результат) Тогда
		ЗаданияСервер.ВыполнитьПереносВНовыйСпринт(Результат, Элементы.Список.ТекущиеДанные.Ссылка);
		ОповеститьОбИзменении(Элементы.Список.ТекущиеДанные.Ссылка);	
		ОповеститьОбИзменении(Результат);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПеренестиНевыполненныеЗаданияТекущегоСпринтаВДругойСпринт(Команда)
	
	Если Элементы.Список.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Результат = Неопределено;
	ОткрытьФорму("Справочник.Спринты.ФормаВыбора", Новый Структура("РежимВыбора", Истина),ЭтаФорма,,,,
		Новый ОписаниеОповещения("ПеренестиВсеЗаданияТекущегоСпринтаВДругойСпринтЗавершение", ЭтотОбъект),
		РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
	
КонецПроцедуры

&НаКлиенте
Процедура ПеренестиВсеЗаданияТекущегоСпринтаВДругойСпринтЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если ЗначениеЗаполнено(Результат) Тогда
		ЗаданияСервер.ВыполнитьПереносВНовыйСпринт(Элементы.Список.ТекущиеДанные.Ссылка, Результат);
		ОповеститьОбИзменении(Элементы.Список.ТекущиеДанные.Ссылка);
		ОповеститьОбИзменении(Результат);
	КонецЕсли;

КонецПроцедуры

#КонецОбласти