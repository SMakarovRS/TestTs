
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Объект.Ссылка.Пустая() Тогда
		
		Объект.ДатаНачала = ЗаданияСервер.ПоследняяДатаВСпринтах();
		Длительность = УправлениеITОтделом8УФПовтИсп.ПолучитьКонстанту("ДлительностьСпринтаПоУмолчанию");
		Если Длительность = 0 Тогда
			Длительность = 14;
		КонецЕсли;
		Объект.ДатаОкончания = КонецДня(Объект.ДатаНачала + 86400 * Длительность);
		Автонаименование();
		
	КонецЕсли;
	
	ОбновитьВидимостьДоступность();
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если ТекущийОбъект.Завершен Тогда
		
		МассивЗаданий = ЗаданияСервер.АктивныеЗаданияВСпринте(ТекущийОбъект.Ссылка);
		Если МассивЗаданий.Количество() > 0 Тогда
			
			ОбщегоНазначения.СообщитьПользователю(
				НСтр("ru = 'Нельзя завершить спринт, в котором есть не выполненные задания'"));
			Отказ = Истина;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	ОбновитьВидимостьДоступность();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ДатаНачалаПриИзменении(Элемент)
	
	Автонаименование();
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаОкончанияПриИзменении(Элемент)
	
	Автонаименование();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершенПриИзменении(Элемент)
	
	Автонаименование();
		
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
// Процедура - обработчик команды "УстановитьИнтервал".
//
Процедура ВыборПериода(Команда)
	
	Диалог = Новый ДиалогРедактированияСтандартногоПериода();
	Диалог.Период.Вариант = ВариантСтандартногоПериода.Месяц;
	Диалог.Период.ДатаНачала = Объект.ДатаНачала;
	Диалог.Период.ДатаОкончания = Объект.ДатаОкончания;	
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПослеВыбораПериода", ЭтотОбъект);
	Диалог.Показать(ОписаниеОповещения);
	
КонецПроцедуры // УстановитьИнтервал()

&НаКлиенте
// Процедура обрабатывает результат выбора периода заполнения текущего документа
//
Процедура ПослеВыбораПериода(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	Если РезультатЗакрытия <> Неопределено Тогда
		
		Объект.ДатаНачала = РезультатЗакрытия.ДатаНачала;
		Объект.ДатаОкончания = РезультатЗакрытия.ДатаОкончания;
		Автонаименование();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПеренестиВсеЗаданияДругогоСпринтаВТекущийСпринт(Команда)
	
	Если Объект.Ссылка.Пустая() Тогда		
		ПоказатьПредупреждение(, НСтр("ru = 'Спринт не записан. Для продолжения необходима запись спринта.'"));
		Возврат;
	КонецЕсли;
	
	Результат = Неопределено;
	ОткрытьФорму("Справочник.Спринты.ФормаВыбора", Новый Структура("РежимВыбора", Истина),ЭтаФорма,,,, 
		Новый ОписаниеОповещения("ПеренестиВсеЗаданияДругогоСпринтаВТекущийСпринтЗавершение", ЭтотОбъект),
		РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
	
КонецПроцедуры

&НаКлиенте
Процедура ПеренестиВсеЗаданияДругогоСпринтаВТекущийСпринтЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если ЗначениеЗаполнено(Результат) Тогда
		ЗаданияСервер.ВыполнитьПереносВНовыйСпринт(Результат, Объект.Ссылка);
		ОповеститьОбИзменении(Объект.Ссылка);
		ОповеститьОбИзменении(Результат);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПеренестиВсеЗаданияТекущегоСпринтаВДругойСпринт(Команда)
	
	Если Объект.Ссылка.Пустая() Тогда		
		ПоказатьПредупреждение(, НСтр("ru = 'Спринт не записан. Для продолжения необходима запись спринта.'"));
		Возврат;
	КонецЕсли;
	
	Результат = Неопределено;
	ОткрытьФорму("Справочник.Спринты.ФормаВыбора", Новый Структура("РежимВыбора", Истина),ЭтаФорма,,,, 
		Новый ОписаниеОповещения("ПеренестиВсеЗаданияТекущегоСпринтаВДругойСпринтЗавершение", ЭтотОбъект),
		РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
	
КонецПроцедуры

&НаКлиенте
Процедура ПеренестиВсеЗаданияТекущегоСпринтаВДругойСпринтЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если ЗначениеЗаполнено(Результат) Тогда
		ЗаданияСервер.ВыполнитьПереносВНовыйСпринт(Объект.Ссылка, Результат);
		ОповеститьОбИзменении(Объект.Ссылка);
		ОповеститьОбИзменении(Результат);
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОбновитьВидимостьДоступность()
	
	Элементы.ФормаПеренестиВсеЗаданияТекущегоСпринтаВДругойСпринт.Видимость = НЕ Объект.Ссылка.Пустая();
	
КонецПроцедуры

&НаСервере
Процедура Автонаименование()
	
	Запрос = Новый Запрос();
	Запрос.Текст =
		"ВЫБРАТЬ
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Спринты.Ссылка) КАК Сумма
		|ИЗ
		|	Справочник.Спринты КАК Спринты
		|ГДЕ
		|	Спринты.ДатаНачала < &ДатаНачала";
	Запрос.УстановитьПараметр("ДатаНачала", ?(Объект.ДатаНачала = Дата(1, 1, 1), ТекущаяДатаСеанса(),
		Объект.ДатаНачала));
		
	Выборка = Запрос.Выполнить().Выбрать();
	Количество = "";
	Если Выборка.Следующий() Тогда
		Количество = Формат(Выборка.Сумма + 1, "ЧРД=; ЧРГ=; ЧН=0; ЧГ=");
	КонецЕсли;
	
	Если Объект.Завершен Тогда
		
		Объект.Наименование = СтрШаблон(НСтр("ru = 'Спринт %1 (%2) завершен'"), Количество,
			ПредставлениеПериода(НачалоДня(Объект.ДатаНачала), КонецДня(Объект.ДатаОкончания)));
		
	Иначе
		
		Объект.Наименование = СтрШаблон(НСтр("ru = 'Спринт %1 (%2)'"), Количество, 
			ПредставлениеПериода(НачалоДня(Объект.ДатаНачала), КонецДня(Объект.ДатаОкончания)));
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция АктивныеЗаданияВСпринте(Знач Спринт)
	
	Возврат ЗаданияСервер.АктивныеЗаданияВСпринте(Спринт);
	
КонецФункции

#КонецОбласти