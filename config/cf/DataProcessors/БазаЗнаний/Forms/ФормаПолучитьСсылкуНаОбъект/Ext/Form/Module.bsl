#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("СсылкаНаОбъект") Тогда
		СсылкаНаОбъект = Параметры.СсылкаНаОбъект;
	Иначе
		Отказ = Истина;
	КонецЕсли;	
	
	Элементы.ДекорацияЗаголовок.Заголовок = "Ссылка для """ + Строка(СсылкаНаОбъект) + """:";
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	#Если ВебКлиент Тогда
		Элементы.КопироватьВБуфер.Видимость = Ложь;
	#Иначе
		Элементы.КопироватьВБуфер.Видимость = Истина;
	#КонецЕсли

	ВнешняяСсылкаНаОбъект = ПолучитьНавигационнуюСсылку(СсылкаНаОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Внешняя(Команда)
	
	Элементы.ФормаВнешняя.Пометка = НЕ Элементы.ФормаВнешняя.Пометка;
	ПолучитьВнешнююСсылкуДляОбъекта();
	
КонецПроцедуры

&НаКлиенте
Процедура КопироватьВБуфер(Команда)
	
	#Если Не ВебКлиент Тогда
	Если Не ПустаяСтрока(ВнешняяСсылкаНаОбъект) Тогда
		Попытка
			КомОбъект = Новый COMОбъект("htmlfile");
			КомОбъект.ParentWindow.ClipboardData.Setdata("Text", СокрЛП(ВнешняяСсылкаНаОбъект));
			ЭтотОбъект.Закрыть();
		Исключение
			СообщениеОбОшибке = ОписаниеОшибки();
			ПоказатьПредупреждение(, СообщениеОбОшибке);
		КонецПопытки;		
	КонецЕсли;		
	#КонецЕсли

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции
&НаКлиенте
Процедура ПолучитьВнешнююСсылкуДляОбъекта()
	
	Если Элементы.ФормаВнешняя.Пометка Тогда
		ВнешняяСсылкаНаОбъект = АдресПубликацииИБ() + "#" + ПолучитьНавигационнуюСсылку(СсылкаНаОбъект);
	Иначе
		ВнешняяСсылкаНаОбъект = ПолучитьНавигационнуюСсылку(СсылкаНаОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция АдресПубликацииИБ()
	
	Возврат Константы.АдресПубликацииИнформационнойБазыВИнтернете.Получить();
	
КонецФункции	

#КонецОбласти
