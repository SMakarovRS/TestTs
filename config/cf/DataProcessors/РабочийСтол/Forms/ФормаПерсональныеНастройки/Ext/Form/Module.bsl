
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("ВидСтандартногоПериода",	 ВидСтандартногоПериода);
	Параметры.Свойство("ВариантСтандартногоПериода", ВариантСтандартногоПериода);
	Параметры.Свойство("ЗаВсеВремя", 				 ЗаВсеВремя);
	
	Если ЗначениеЗаполнено(ВидСтандартногоПериода) Тогда
		Элементы.ВариантСтандартногоПериода.СписокВыбора.Очистить();
		Элементы.ВариантСтандартногоПериода.СписокВыбора.ЗагрузитьЗначения(
			МетрикиКлиентСервер.СписокВычисляемыхПериодов(ВидСтандартногоПериода).ВыгрузитьЗначения());
	КонецЕсли;
		
	Элементы.ГруппаПериод.Доступность = Не ЗаВсеВремя;	
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ВидСтандартногоПериодаПриИзменении(Элемент)
	
	Элементы.ВариантСтандартногоПериода.СписокВыбора.Очистить();
	
	Элементы.ВариантСтандартногоПериода.СписокВыбора.ЗагрузитьЗначения(
		МетрикиКлиентСервер.СписокВычисляемыхПериодов(ВидСтандартногоПериода).ВыгрузитьЗначения());
		
	Если ЗначениеЗаполнено(ВидСтандартногоПериода) Тогда
		ВариантСтандартногоПериода = 
			МетрикиКлиентСервер.ПредопределенноеЗначениеВариантаПериодаПоВидуПериода(ВидСтандартногоПериода);
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаВсёВремяПриИзменении(Элемент)
	
	Элементы.ГруппаПериод.Доступность = Не ЗаВсеВремя;	
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыбратьПериод(Команда)
	
	ОповещениеВыбораПериода = Новый ОписаниеОповещения("ПослеВыбораПериода", ЭтотОбъект);    
	ФормаПараметры 			= Новый Структура;
	ФормаПараметры.Вставить("ВариантСтандартногоПериода", ВариантСтандартногоПериода);
	ФормаПараметры.Вставить("ВидСтандартногоПериода", 		ВидСтандартногоПериода);
	
	ОткрытьФорму("ОбщаяФорма.ФормаВыбраВариантаСтандартногоПериода", ФормаПараметры, ЭтотОбъект,,,, 
		ОповещениеВыбораПериода, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);

КонецПроцедуры

&НаКлиенте
Процедура СохранитьИЗакрыть(Команда)
	
	СохранитьНастройкиНаСервере();
	Закрыть(Модифицированность);
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	ЭтаФорма.Закрыть(Неопределено);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура СохранитьНастройкиНаСервере()
	
	СтруктураНастроек = Новый Структура;
	СтруктураНастроек.Вставить("ВидСтандартногоПериода", 	ВидСтандартногоПериода);
	СтруктураНастроек.Вставить("ВариантСтандартногоПериода",ВариантСтандартногоПериода);
	СтруктураНастроек.Вставить("ЗаВсеВремя",				ЗаВсеВремя);
	
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить("НастройкиДляРабочегоСтола", "НастройкиДляРабочегоСтола", 
		СтруктураНастроек, НСтр("ru = 'Настройки рабочего стола'"));
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыбораПериода(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ВидСтандартногоПериода	= Результат.ВидСтандартногоПериода;
	
	Если ЗначениеЗаполнено(ВидСтандартногоПериода) Тогда
		Элементы.ВариантСтандартногоПериода.СписокВыбора.Очистить();
		Элементы.ВариантСтандартногоПериода.СписокВыбора.ЗагрузитьЗначения(
			МетрикиКлиентСервер.СписокВычисляемыхПериодов(ВидСтандартногоПериода).ВыгрузитьЗначения());
	КонецЕсли;
		
	ВариантСтандартногоПериода	= Результат.ВариантСтандартногоПериода;
	
КонецПроцедуры

#КонецОбласти