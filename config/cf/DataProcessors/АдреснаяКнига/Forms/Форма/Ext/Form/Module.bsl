
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ВариантСписка = 0;
	СтрокаПоиска = "";
	ОбновитьСписокНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ВыводитьКонтрагентов = Ложь И ВыводитьСотрудников = Ложь Тогда
		ВыводитьСотрудников = Истина;
	КонецЕсли;
	ОбновитьСписокНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ОбновитьСписокТелефонов" Тогда
		
		Структура = Новый Структура("Ссылка", Неопределено);
		Если Элементы.ТаблицаТелефонов.ТекущиеДанные <> Неопределено Тогда
			Структура.Ссылка = Элементы.ТаблицаТелефонов.ТекущиеДанные.Ссылка;
		КонецЕсли;
		
        ОбновитьСписокНаСервере();
		
		НайденныеСтроки = ТаблицаТелефонов.НайтиСтроки(Структура);
		
		Если НайденныеСтроки.Количество() > 0 Тогда
			Элементы.ТаблицаТелефонов.ТекущаяСтрока = НайденныеСтроки[0].ПолучитьИдентификатор();
		КонецЕсли;
	КонецЕсли; 
		
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СтрокаПоискаПриИзменении(Элемент)
	
	ОбновитьСписокНаСервере();
		
КонецПроцедуры

&НаКлиенте
Процедура ВариантСпискаПриИзменении(Элемент)
	
	Если ВариантСписка = 1 Тогда		
		ОбновитьСписокНаСервере(Избранное)
	Иначе
		ОбновитьСписокНаСервере()
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыводитьСотрудниковПриИзменении(Элемент)
	
	ОбновитьСписокНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыводитьКонтрагентовПриИзменении(Элемент)
	
	ОбновитьСписокНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТаблица

&НаКлиенте
Процедура ТаблицаТелефоновВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТС = Элементы.ТаблицаТелефонов.ТекущиеДанные;
	Если ТС <> Неопределено Тогда
		ПоказатьЗначение(, ТС.Ссылка); 
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОбновитьСписок(Команда)
		
	Если ВариантСписка = 1 Тогда 
		ОбновитьСписокНаСервере(Избранное)
	Иначе
		ОбновитьСписокНаСервере();	
	КонецЕсли;
	                              	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьВИзбранное(Команда)
	
	ТС = Элементы.ТаблицаТелефонов.ТекущиеДанные;
	Если ТС = Неопределено Тогда
		Возврат;
	КонецЕсли;
	ТС = ТС.Ссылка;
	Если Избранное.НайтиПоЗначению(ТС) = Неопределено Тогда
		Избранное.Добавить(ТС);
		ОбновитьСписокНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УдалитьИзИзбранного(Команда)
	
	ТС = Элементы.ТаблицаТелефонов.ТекущиеДанные;
	Если ТС = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого Элемент Из Избранное Цикл
		Если ТС.Ссылка = Элемент.Значение Тогда
			Избранное.Удалить(Элемент);
		КонецЕсли;
	КонецЦикла;
	ОбновитьСписок(Команда);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьОтчёт(Команда)
	
	ОткрытьФорму("Отчет.АдреснаяКнига.Форма", Новый Структура);	
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Функция ПолучитьИмяЭтойОбработки()
	
	Возврат "Обработка.АдреснаяКнига";
	
КонецФункции

&НаСервере
Процедура ОбновитьСписокНаСервере(Знач МассивСсылок = Неопределено)
	
	Если ТипЗнч(МассивСсылок) = Тип("СписокЗначений") Тогда
		МассивСсылок = МассивСсылок.ВыгрузитьЗначения();
	КонецЕсли;
	
	Таблица = УправлениеITОтделом8УФ.ИнициалироватьТаблицуВыводаАдреснойКниги();
	
	УправлениеITОтделом8УФ.ПолучитьТаблицуВыводаАдреснойКниги(Таблица, МассивСсылок, ВыводитьСотрудников, 
		ВыводитьКонтрагентов, СтрокаПоиска); 
		
	ТабТелефонов = РеквизитФормыВЗначение("ТаблицаТелефонов");
	ТабТелефонов.Очистить();
	
	Для Каждого Строки Из Таблица Цикл
		НоваяСтрока = ТабТелефонов.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Строки);
		
		мТелефоны = "";	
		Для Каждого СтрокаТелефона Из Строки.Телефоны Цикл	
			Если НЕ ПустаяСтрока(мТелефоны) Тогда
				мТелефоны = мТелефоны + ", ";
			КонецЕсли;
			мТелефоны = мТелефоны + УправлениеКонтактнойИнформацией.ПредставлениеКонтактнойИнформации(СтрокаТелефона);
			Комментарий = УправлениеКонтактнойИнформацией.КомментарийКонтактнойИнформации(СтрокаТелефона);
			Если НЕ ПустаяСтрока(Комментарий) Тогда
				мТелефоны = мТелефоны + " (" + Комментарий + ")";
			КонецЕсли;
			//Местоположение = Местоположение + ОбработатьТеги(СтрокаТелефона);
		КонецЦикла;		
		
		Местоположение = Строка(Строки.Местоположение);
		Если ПустаяСтрока(Местоположение) Тогда
			Местоположение = Строки.АдресФактический;
		КонецЕсли;
			
		мПочта = "";
		Для Каждого Ящик Из Строки.Почта Цикл
			Если НЕ ПустаяСтрока(мПочта) Тогда
				мПочта = мПочта + ", ";
			КонецЕсли;
			мПочта = мПочта + Ящик;
		КонецЦикла;
		
		НоваяСтрока.Телефоны = мТелефоны;
		НоваяСтрока.Почта = мПочта;
		НоваяСтрока.Местоположение = Местоположение;
		
		Если ТипЗнч(Строки.Ссылка) = Тип("СправочникСсылка.Пользователи") Тогда
			НоваяСтрока.ИндексКартинки = 2;
		ИначеЕсли ТипЗнч(Строки.Ссылка) = Тип("СправочникСсылка.Сотрудники") Тогда
			НоваяСтрока.ИндексКартинки = 1;
		ИначеЕсли ТипЗнч(Строки.Ссылка) = Тип("СправочникСсылка.ФизическиеЛица") Тогда
			НоваяСтрока.ИндексКартинки = 1;
		ИначеЕсли ТипЗнч(Строки.Ссылка) = Тип("СправочникСсылка.Контрагенты") Тогда
			НоваяСтрока.ИндексКартинки = 0;
		ИначеЕсли ТипЗнч(Строки.Ссылка) = Тип("СправочникСсылка.КонтактныеЛица") Тогда
			НоваяСтрока.ИндексКартинки = 3;
		КонецЕсли;
		
	КонецЦикла;
	ЗначениеВРеквизитФормы(ТабТелефонов, "ТаблицаТелефонов");
	
	ОбновитьНадписиНаКнопках();
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьНадписиНаКнопках()
	
	Таблица = УправлениеITОтделом8УФ.ИнициалироватьТаблицуВыводаАдреснойКниги();
	УправлениеITОтделом8УФ.ПолучитьТаблицуВыводаАдреснойКниги(Таблица, , Истина, Истина, СтрокаПоиска);
	КолВоВсех = Таблица.Количество();
	Элементы.ВариантСписка.СписокВыбора.НайтиПоЗначению(1).Представление = "Избранное ("+Избранное.Количество()+")";
	Элементы.ВариантСписка.СписокВыбора.НайтиПоЗначению(0).Представление = "Все ("+КолВоВсех+")";
	 
КонецПроцедуры

#КонецОбласти
