
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.Свойство("Задание") Тогда
		Задание = Параметры.Задание;
		ОбновитьДиаграммуГанта();
	КонецЕсли;	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ЗаданиеПриИзменении(Элемент)
	ОбновитьДиаграммуГанта();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОбновитьДиаграммуГантаСобытие(Команда)
	ОбновитьДиаграммуГанта();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОбновитьДиаграммуГанта()
	
	Если НЕ ЗначениеЗаполнено(Задание) Тогда
		Возврат;
	КонецЕсли;
	
	Диаграмма.Очистить();
	Диаграмма.АвтоОпределениеПолногоИнтервала 	= Ложь;
	Диаграмма.УстановитьПолныйИнтервал(Задание.Дата, ?(ЗначениеЗаполнено(Задание.ДатаВыполнения), Задание.ДатаВыполнения,
		ТекущаяДатаСеанса())); 
	Диаграмма.ПоддержкаМасштаба 				= ПоддержкаМасштабаДиаграммыГанта.ВсеДанные;
	Диаграмма.ОтображениеИнтервала 				= ОтображениеИнтервалаДиаграммыГанта.Градиент;
	Диаграмма.ОтображатьЛегенду 				= Ложь;
	Диаграмма.РастягиваниеПоВертикали 			= РастягиваниеПоВертикалиДиаграммыГанта.РастягиватьСтроки;
	
	ШкалаВремени = Диаграмма.ОбластьПостроения.ШкалаВремени.Элементы.Получить(0);
	Время = (?(Задание.ДатаВыполнения = Дата(1, 1, 1), ТекущаяДата(), Задание.ДатаВыполнения) - Задание.Дата) / 3600;
	Если Время < 2 Тогда
		ШкалаВремени.Единица = ТипЕдиницыШкалыВремени.Минута;
	ИначеЕсли Время < 24 Тогда 
		ШкалаВремени.Единица = ТипЕдиницыШкалыВремени.Час;
	Иначе
		ШкалаВремени.Единица = ТипЕдиницыШкалыВремени.День;
		ШкалаВремени.ФорматДня = ФорматДняШкалыВремени.ДеньМесяца;
	КонецЕсли;	
	
	// Добавляем крайнюю дату
	Серия = Диаграмма.УстановитьСерию("Исполнитель"); 
	Точка = Диаграмма.УстановитьТочку("Крайняя дата завершения");
	
	Значение 		= Диаграмма.ПолучитьЗначение(Точка, Серия);
	Интервал 		= Значение.Добавить(); 
	Интервал.Начало = Задание.Дата;
	Если Задание.Выполнено Тогда
		Интервал.Конец 	= ?(ЗначениеЗаполнено(Задание.КрайняяДатаВыполнения), Задание.КрайняяДатаВыполнения,
			Задание.ДатаВыполнения); 
	Иначе
		Интервал.Конец 	= ?(ЗначениеЗаполнено(Задание.КрайняяДатаВыполнения), Задание.КрайняяДатаВыполнения,
			ТекущаяДата()); 
	КонецЕсли;	
	Интервал.Цвет 	= WebЦвета.ТемноКрасный;
	Интервал.Текст 	= Строка(Интервал.Начало) + " - " + Строка(Интервал.Конец);
	
	Запрос = Новый Запрос();
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ВложенныйЗапрос.Дата КАК Дата
		|ИЗ
		|	(ВЫБРАТЬ
		|		ЗаданиеИсполнители.ДатаНачала КАК Дата
		|	ИЗ
		|		Документ.Задание.Исполнители КАК ЗаданиеИсполнители
		|	ГДЕ
		|		ЗаданиеИсполнители.Ссылка = &Ссылка
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ЗаданиеИсполнители.ДатаОкончания
		|	ИЗ
		|		Документ.Задание.Исполнители КАК ЗаданиеИсполнители
		|	ГДЕ
		|		ЗаданиеИсполнители.Ссылка = &Ссылка
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ЗаданиеЭтапы.ДатаНачала
		|	ИЗ
		|		Документ.Задание.Этапы КАК ЗаданиеЭтапы
		|	ГДЕ
		|		ЗаданиеЭтапы.Ссылка = &Ссылка
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ЗаданиеЭтапы.ДатаОкончания
		|	ИЗ
		|		Документ.Задание.Этапы КАК ЗаданиеЭтапы
		|	ГДЕ
		|		ЗаданиеЭтапы.Ссылка = &Ссылка) КАК ВложенныйЗапрос
		|ГДЕ
		|	ВложенныйЗапрос.Дата <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
		|
		|УПОРЯДОЧИТЬ ПО
		|	Дата
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЗаданиеИсполнители.Исполнитель КАК Исполнитель,
		|	ЗаданиеИсполнители.ДатаНачала КАК ДатаНачала,
		|	ЗаданиеИсполнители.ДатаОкончания КАК ДатаОкончания
		|ИЗ
		|	Документ.Задание.Исполнители КАК ЗаданиеИсполнители
		|ГДЕ
		|	ЗаданиеИсполнители.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЗаданиеЭтапы.Этап КАК Этап,
		|	ЗаданиеЭтапы.ДатаНачала КАК ДатаНачала,
		|	ЗаданиеЭтапы.ДатаОкончания КАК ДатаОкончания
		|ИЗ
		|	Документ.Задание.Этапы КАК ЗаданиеЭтапы
		|ГДЕ
		|	ЗаданиеЭтапы.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", Задание);
	МассивРезультатов	= Запрос.ВыполнитьПакет();
	Выборка				= МассивРезультатов[0].Выбрать();
	ТЗИсполнители		= МассивРезультатов[1].Выгрузить();
	ТЗЭтапы				= МассивРезультатов[2].Выгрузить();
	ТЗДанные			= Новый ТаблицаЗначений;
	ТЗДанные.Колонки.Добавить("ДатаНачала");
	ТЗДанные.Колонки.Добавить("ДатаОкончания");
	ТЗДанные.Колонки.Добавить("Этап");
	ТЗДанные.Колонки.Добавить("Исполнитель");
	ТЗДанные.Колонки.Добавить("НомерСтроки");
	
	НоваяСтрока = Истина;
	ПервыйОбход = Истина;
	НомерСтроки = 1;
	
	Пока Выборка.Следующий() Цикл
		Если НоваяСтрока = Истина Тогда
			Если ПервыйОбход = истина Тогда 
				Стр = ТЗДанные.Добавить();
				Стр.ДатаНачала = Выборка.Дата;
				НоваяСтрока = Ложь;
			Иначе
				Стр.ДатаОкончания = Выборка.Дата;
				Стр = ТЗДанные.Добавить();
				Стр.ДатаНачала = Выборка.Дата;
				НоваяСтрока = Истина;
			КонецЕсли;
		Иначе
			Стр.ДатаОкончания = Выборка.Дата;
			Стр = ТЗДанные.Добавить();
			Стр.ДатаНачала = Выборка.Дата;
			НоваяСтрока = Истина;
		КонецЕсли;
		Стр.НомерСтроки = НомерСтроки;
		НомерСтроки = НомерСтроки + 1;
		ПервыйОбход = Ложь;	
	КонецЦикла;
	
	Серия = Диаграмма.УстановитьСерию("Исполнитель");
	
	Для Каждого Строка Из ТЗДанные Цикл
		
		Если Строка.ДатаОкончания = Неопределено  Тогда 
			Строка.ДатаОкончания = Строка.ДатаНачала;
		КонецЕсли;
				
		ОтборЭтап = Новый Структура;
		ОтборЭтап.Вставить("ДатаНачала", Строка.ДатаНачала);
		СтрокаЭтап = ТЗЭтапы.НайтиСтроки(ОтборЭтап); 
		Если СтрокаЭтап.Количество() > 0 Тогда
			Строка.Этап = СтрокаЭтап[0].Этап;
		КонецЕсли;
		
		ОтборИсполнитель = Новый Структура;
		ОтборИсполнитель.Вставить("ДатаНачала", Строка.ДатаНачала);
		СтрокаИсполнитель = ТЗИсполнители.НайтиСтроки(ОтборИсполнитель); 
		Если СтрокаИсполнитель.Количество() > 0 Тогда
			Строка.Исполнитель = СтрокаИсполнитель[0].Исполнитель;
		КонецЕсли;		
		
		Если Строка.Этап <> Неопределено Тогда 
			Точка = Диаграмма.УстановитьТочку(Строка.Этап.Наименование + Строка(Строка.НомерСтроки));
			
			Если ЗначениеЗаполнено(Строка.Исполнитель) Тогда
				Точка.Текст = Строка.Этап.Наименование + " (" + Строка(Строка.Исполнитель) + ")";
			Иначе
				Точка.Текст = Строка.Этап.Наименование + " (Не выбран)";
			КонецЕсли;		
			
			Значение = Диаграмма.ПолучитьЗначение(Точка, Серия);
			Интервал = Значение.Добавить(); 
			Интервал.Начало = Строка.ДатаНачала; 
			Интервал.Конец = ?(Год(Строка.ДатаОкончания) > 1, Строка.ДатаОкончания, ТекущаяДата()); 
			Интервал.Текст = Строка(Интервал.Начало) + " - " + Строка(Интервал.Конец);		
			Интервал.Цвет = WebЦвета.Зеленый;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

