
#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаполнитьВыполнить(Команда)

	ЗаполнитьБазуТоваров();

КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьВыполнитьЗавершение(Результат, Параметры) Экспорт
	
	Если Результат = Истина Тогда
		ТекстСообщения = НСтр("ru = 'Данные успешно выгружены.'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьВыполнить()

	ОчиститьСообщения();
	
	ТаблицаЗагрузкиТоваров = Новый Массив;
	ОповещенияПриЗавершение = Новый ОписаниеОповещения("ВыгрузитьВыполнитьЗавершение", ЭтотОбъект);
	
	Для Каждого Товар ИЗ ТаблицаВыгрузки Цикл
		ТаблицаЗагрузкиТоваров.Добавить(
			Новый Структура("Код, Штрихкод, Наименование, ЕдиницаИзмерения, Количество",
				Товар.SKU, Товар.Штрихкод, Товар.Наименование, Товар.ЕдиницаИзмерения, 0));
	КонецЦикла;
	МенеджерОборудованияКлиент.НачатьВыгрузкуДанныеВТСД(ОповещенияПриЗавершение, УникальныйИдентификатор, ТаблицаЗагрузкиТоваров);
	                         
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьСписокЗавершение(Результат, Параметры) Экспорт
	
	Если Результат = Истина Тогда
		ТекстСообщения = НСтр("ru = 'Операция завершена.'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьСписокТоваров(Команда)
	
	ОчиститьСообщения();
		
	ОповещенияПриЗавершение = Новый ОписаниеОповещения("ОчиститьСписокЗавершение", ЭтотОбъект);
	
	МенеджерОборудованияКлиент.НачатьОчисткуДанныеВТСД(ОповещенияПриЗавершение, УникальныйИдентификатор);
	           
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьБазуТоваров()

	Запрос = Новый Запрос();
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВЫБОР
		|		КОГДА ТИПЗНАЧЕНИЯ(ШтрихкодыНоменклатуры.Номенклатура) = ТИП(Справочник.Номенклатура)
		|			ТОГДА ШтрихкодыНоменклатуры.Номенклатура.Код
		|		ИНАЧЕ ШтрихкодыНоменклатуры.Номенклатура.Код
		|	КОНЕЦ КАК SKU,
		|	ВЫБОР
		|		КОГДА ТИПЗНАЧЕНИЯ(ШтрихкодыНоменклатуры.Номенклатура) = ТИП(Справочник.Номенклатура)
		|			ТОГДА ШтрихкодыНоменклатуры.Номенклатура.Артикул
		|		ИНАЧЕ ШтрихкодыНоменклатуры.Номенклатура.Владелец.Артикул
		|	КОНЕЦ КАК Артикул,
		|	ШтрихкодыНоменклатуры.Номенклатура,
		|	ВЫБОР
		|		КОГДА ТИПЗНАЧЕНИЯ(ШтрихкодыНоменклатуры.Номенклатура) = ТИП(Справочник.Номенклатура)
		|			ТОГДА ШтрихкодыНоменклатуры.Номенклатура.ВидНоменклатуры
		|		ИНАЧЕ ШтрихкодыНоменклатуры.Номенклатура.Владелец.ВидНоменклатуры
		|	КОНЕЦ КАК ВидНоменклатуры,
		|	ШтрихкодыНоменклатуры.ЕдиницаИзмерения,
		|	ШтрихкодыНоменклатуры.Штрихкод,
		|	ШтрихкодыНоменклатуры.Номенклатура КАК Наименование
		|ИЗ
		|	РегистрСведений.ШтрихкодыНоменклатуры КАК ШтрихкодыНоменклатуры
		|ГДЕ
		|	ШтрихкодыНоменклатуры.Штрихкод <> """"";

	ТекТаблица = Запрос.Выполнить().Выгрузить();

	ЗначениеВРеквизитФормы(ТекТаблица, "ТаблицаВыгрузки");

КонецПроцедуры

#КонецОбласти
