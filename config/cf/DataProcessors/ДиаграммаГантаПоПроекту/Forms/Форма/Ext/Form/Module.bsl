
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("Проект") Тогда
		Проект = Параметры.Проект;
		ОбновитьДиаграммуГанта();
	КонецЕсли;	
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПроектПриИзменении(Элемент)
	ОбновитьДиаграммуГанта();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОбновитьДиаграммуГантаСобытие(Команда)
	
	ОбновитьДиаграммуГанта();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОбновитьДиаграммуГанта()
	
	Если НЕ ЗначениеЗаполнено(Проект) Тогда
		Диаграмма.Очистить();
		Возврат;
	КонецЕсли;
	
	Диаграмма.Очистить();
	Диаграмма.АвтоОпределениеПолногоИнтервала 	= Ложь;
	Диаграмма.ПоддержкаМасштаба 				= ПоддержкаМасштабаДиаграммыГанта.ВсеДанные;
	Диаграмма.ОтображениеИнтервала 				= ОтображениеИнтервалаДиаграммыГанта.Градиент;
	Диаграмма.ОтображатьЛегенду 				= Ложь;
	Диаграмма.РастягиваниеПоВертикали 			= РастягиваниеПоВертикалиДиаграммыГанта.РастягиватьСтроки;  
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	Задание.Ссылка КАК Ссылка,
		|	Задание.ДатаВыполнения КАК ДатаВыполнения,
		|	Задание.ДатаСоздания КАК Дата,
		|	Задание.Выполнено КАК Выполнено,
		|	Задание.КрайняяДатаВыполнения КАК КрайняяДатаВыполнения
		|ИЗ
		|	Документ.Задание КАК Задание
		|ГДЕ
		|	Задание.Проект = &Проект
		|
		|УПОРЯДОЧИТЬ ПО
		|	Задание.Дата";
	
	Запрос.УстановитьПараметр("Проект", Проект);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда 
		
		ТЗ = РезультатЗапроса.Выгрузить();
		ДатаВыполненияИнтервал = Дата(1, 1, 1);
		
		Если ТЗ.Количество() > 0 Тогда 
			ДатаНачала = ТЗ[0].Дата;
		КонецЕсли;
		
		Для Каждого Строка Из ТЗ Цикл
			Если Строка.ДатаВыполнения > ДатаВыполненияИнтервал Тогда
				ДатаВыполненияИнтервал = Строка.ДатаВыполнения;
			КонецЕсли;
			Если Строка.Дата < ДатаНачала Тогда
				ДатаНачала = Строка.Дата;
			КонецЕсли;
		КонецЦикла;
		
		Диаграмма.УстановитьПолныйИнтервал(ДатаНачала, 
			?(ДатаВыполненияИнтервал <> Дата(1, 1, 1), ДатаВыполненияИнтервал, ТекущаяДатаСеанса()));
		Серия = Диаграмма.УстановитьСерию(Проект);
		
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			Если Выборка.Выполнено Тогда
				ДатаВыполнения = 
					?(ЗначениеЗаполнено(Выборка.КрайняяДатаВыполнения), Выборка.КрайняяДатаВыполнения, Выборка.ДатаВыполнения);
			Иначе
				ДатаВыполнения =
					?(ЗначениеЗаполнено(Выборка.КрайняяДатаВыполнения), Выборка.КрайняяДатаВыполнения, ТекущаяДата());
			КонецЕсли;
			Точка 			= Диаграмма.УстановитьТочку(Выборка.Ссылка);		
			Значение 		= Диаграмма.ПолучитьЗначение(Точка, Серия);             		
			Интервал 		= Значение.Добавить();                           		
			Интервал.Начало = Выборка.Дата;
			Интервал.Конец 	= ?(Год(Выборка.ДатаВыполнения) > 1, Выборка.ДатаВыполнения, ТекущаяДата()); 
			Интервал.Текст 	= Строка(Интервал.Начало) + " - " + Строка(Интервал.Конец);
			Интервал.Цвет 	= WebЦвета.СинийСоСтальнымОттенком;
		КонецЦикла;
		
		Диаграмма.Обновление = Истина;	
		
	Иначе 
		
		Диаграмма.Очистить();
		
	КонецЕсли;

КонецПроцедуры

#КонецОбласти