//++ 07.01.2021, Тарасов Михаил: подключение телефонии Дом.РУ к DevBase
Функция ПолучитьСообщение(ИдентификаторПользователяАТС, ТелефонКлиента, Тип, ВнутреннийНомер, IDЗвонка)
	
	Если Тип = "OUTGOING" Тогда
		МенеджерЗаписи = РегистрыСведений.РС_ОповещениеОНовомЗвонке.СоздатьМенеджерЗаписи();
		МенеджерЗаписи.UUID = IDЗвонка; 
		МенеджерЗаписи.Записать(Истина);
		Возврат "";
	КонецЕсли;
	
	ТаблицаСообщений = Новый ТаблицаЗначений;
	ТаблицаСообщений.Колонки.Добавить("ИдентификаторПользователяАТС", новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(100)));
	ТаблицаСообщений.Колонки.Добавить("ВнутреннийНомер", новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(15)));
	ТаблицаСообщений.Колонки.Добавить("IDЗвонка", новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(50)));
	ТаблицаСообщений.Колонки.Добавить("КонтактныйТелефон", новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(СтрДлина(СокрЛП(ТелефонКлиента)))));
	СтрокаТаблицыСообщений = ТаблицаСообщений.Добавить();
	СтрокаТаблицыСообщений.ИдентификаторПользователяАТС = ИдентификаторПользователяАТС;
	СтрокаТаблицыСообщений.КонтактныйТелефон = СокрЛП(ТелефонКлиента); 
	СтрокаТаблицыСообщений.ВнутреннийНомер = ВнутреннийНомер;
	СтрокаТаблицыСообщений.IDЗвонка = IDЗвонка;
	
	РС_ТелефонныйЗвонокЗавершен = ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоРеквизиту("Имя", "РС_ТелефонныйЗвонокЗавершен");
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ТаблицаСообщений.ИдентификаторПользователяАТС КАК ИдентификаторПользователяАТС,
	               |	ТаблицаСообщений.ВнутреннийНомер КАК ВнутреннийНомер,
	               |	ТаблицаСообщений.IDЗвонка КАК IDЗвонка,
	               |	ТаблицаСообщений.КонтактныйТелефон КАК КонтактныйТелефон
	               |ПОМЕСТИТЬ ВТ
	               |ИЗ
	               |	&ТаблицаСообщений КАК ТаблицаСообщений
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВТ.ИдентификаторПользователяАТС КАК ИдентификаторПользователяАТС,
	               |	ВТ.ВнутреннийНомер КАК ВнутреннийНомер,
	               |	ВТ.IDЗвонка КАК IDЗвонка,
	               |	ВТ.КонтактныйТелефон КАК КонтактныйТелефон,
	               |	МАКСИМУМ(ЕСТЬNULL(КонтактныеЛицаКонтактнаяИнформация.Ссылка.Владелец, НЕОПРЕДЕЛЕНО)) КАК Контрагент,
	               |	МАКСИМУМ(ЕСТЬNULL(КонтактныеЛицаКонтактнаяИнформация.Ссылка, НЕОПРЕДЕЛЕНО)) КАК КонтактноеЛицо,
	               |	КОЛИЧЕСТВО(ЕСТЬNULL(КонтактныеЛицаКонтактнаяИнформация.Ссылка, НЕОПРЕДЕЛЕНО)) КАК КоличествоКонтактныхЛиц
	               |ПОМЕСТИТЬ ВТ2
	               |ИЗ
	               |	ВТ КАК ВТ
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КонтактныеЛица.КонтактнаяИнформация КАК КонтактныеЛицаКонтактнаяИнформация
	               |		ПО (КонтактныеЛицаКонтактнаяИнформация.НомерТелефона = ВТ.КонтактныйТелефон)
	               |			И (КонтактныеЛицаКонтактнаяИнформация.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.Телефон))
	               |			И (НЕ КонтактныеЛицаКонтактнаяИнформация.Ссылка.ПометкаУдаления)
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ВТ.КонтактныйТелефон,
	               |	ВТ.ИдентификаторПользователяАТС,
	               |	ВТ.ВнутреннийНомер,
	               |	ВТ.IDЗвонка
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВТ2.ИдентификаторПользователяАТС КАК ИдентификаторПользователяАТС,
	               |	ВТ2.ВнутреннийНомер КАК ВнутреннийНомер,
	               |	ВТ2.IDЗвонка КАК IDЗвонка,
	               |	ВТ2.КонтактныйТелефон КАК КонтактныйТелефон,
	               |	ВТ2.Контрагент КАК Контрагент,
	               |	ВТ2.КонтактноеЛицо КАК КонтактноеЛицо,
	               |	ВТ2.КоличествоКонтактныхЛиц КАК КоличествоКонтактныхЛиц,
	               |	МАКСИМУМ(ТелефонныйЗвонок.Ссылка) КАК ДокументЗвонка,
	               |	РС_ОповещениеОНовомЗвонке.UUID ЕСТЬ NULL КАК Входящий
	               |ИЗ
	               |	ВТ2 КАК ВТ2
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ТелефонныйЗвонок КАК ТелефонныйЗвонок
	               |		ПО ВТ2.IDЗвонка = ТелефонныйЗвонок.РС_UUID
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РС_ОповещениеОНовомЗвонке КАК РС_ОповещениеОНовомЗвонке
	               |		ПО ВТ2.IDЗвонка = РС_ОповещениеОНовомЗвонке.UUID
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ВТ2.ИдентификаторПользователяАТС,
	               |	ВТ2.КонтактныйТелефон,
	               |	ВТ2.Контрагент,
	               |	ВТ2.КонтактноеЛицо,
	               |	ВТ2.КоличествоКонтактныхЛиц,
	               |	ВТ2.IDЗвонка,
	               |	ВТ2.ВнутреннийНомер,
	               |	РС_ОповещениеОНовомЗвонке.UUID ЕСТЬ NULL";
		
	Запрос.УстановитьПараметр("ТаблицаСообщений", ТаблицаСообщений);
	ВДЗ = Запрос.Выполнить().Выбрать();
	ВДЗ.Следующий();
	
	Если Тип = "ACCEPTED" Тогда
		
		ТекДок = Документы.ТелефонныйЗвонок.СоздатьДокумент();
		ТекДок.Дата = ТекущаяДата();
		ТекДок.АбонентКакСвязаться = ВДЗ.КонтактныйТелефон;
		Если ВДЗ.КоличествоКонтактныхЛиц <= 1 И ЗначениеЗаполнено(ВДЗ.КонтактноеЛицо) Тогда
			ТекДок.АбонентКонтакт = ВДЗ.КонтактноеЛицо;
			ТекДок.АбонентПредставление = "" + ВДЗ.КонтактноеЛицо + " (" + ВДЗ.КонтактноеЛицо.Владелец + ")";
		КонецЕсли;
		Если ВДЗ.Входящий Тогда
			ТекДок.Тема = "[Приём звонка]";
		Иначе
			ТекДок.Тема = "[Инициация звонка]";
		КонецЕсли;
		ТекДок.Входящий = ВДЗ.Входящий;
		ТекДок.Описание = "[Создано автоматически]";
		ТекДок.Ответственный = ПараметрыСеанса.ТекущийПользователь;
		ТекДок.РС_UUID = ВДЗ.IDЗвонка;
		ТекДок.РС_ВнутреннийНомер = ВДЗ.ВнутреннийНомер;
		ТекДок.РС_ИдентификаторПользователяАТС = ВДЗ.ИдентификаторПользователяАТС;
		ТекДок.Записать();
		
		МенеджерЗаписи = РегистрыСведений.ДополнительныеСведения.СоздатьМенеджерЗаписи();
		МенеджерЗаписи.Объект = ТекДок.Ссылка;
		МенеджерЗаписи.Свойство = РС_ТелефонныйЗвонокЗавершен;
		МенеджерЗаписи.Значение = Ложь;
		МенеджерЗаписи.Записать(Истина);
		
	ИначеЕсли Тип = "COMPLETED" Тогда
		
		Если ЗначениеЗаполнено(ВДЗ.ДокументЗвонка) Тогда
			МенеджерЗаписи = РегистрыСведений.ДополнительныеСведения.СоздатьМенеджерЗаписи();
			МенеджерЗаписи.Объект = ВДЗ.ДокументЗвонка;
			МенеджерЗаписи.Свойство = РС_ТелефонныйЗвонокЗавершен;
			МенеджерЗаписи.Значение = Истина;
			МенеджерЗаписи.Записать(Истина);
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат "";
КонецФункции
//-- 07.01.2021, Тарасов Михаил: подключение телефонии Дом.РУ к DevBase
