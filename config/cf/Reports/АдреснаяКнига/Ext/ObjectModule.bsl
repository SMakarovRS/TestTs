#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОбработчикиСобытий

// Процедура - обработчик события "ПриКомпоновкеРезультата".
//
Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	УстановитьПривилегированныйРежим(Истина);
	
	ВыводитьСотрудников = Ложь;
	ВыводитьКонтрагентов = Ложь;
	ПодстрокаПоиска = "";          
	Для Каждого Элемент Из КомпоновщикНастроек.ПользовательскиеНастройки.Элементы Цикл
		
		Если Элемент.Использование Тогда
			Если Элемент.Параметр = Новый ПараметрКомпоновкиДанных("ВыводитьСотрудников") Тогда
				ВыводитьСотрудников = Элемент.Значение;
			ИначеЕсли Элемент.Параметр = Новый ПараметрКомпоновкиДанных("ВыводитьКонтрагентов") Тогда
				ВыводитьКонтрагентов = Элемент.Значение;
			ИначеЕсли Элемент.Параметр = Новый ПараметрКомпоновкиДанных("ПодстрокаПоиска") Тогда
				ПодстрокаПоиска = Элемент.Значение;				
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
	Таблица = УправлениеITОтделом8УФ.ИнициалироватьТаблицуВыводаАдреснойКниги();
	УправлениеITОтделом8УФ.ПолучитьТаблицуВыводаАдреснойКниги(Таблица, , ВыводитьСотрудников, ВыводитьКонтрагентов, 
		ПодстрокаПоиска);
	
	Для Каждого Строки Из Таблица Цикл
		
		мТелефоны = "";	
		Для Каждого СтрокаТелефона Из Строки.Телефоны Цикл	
			Если НЕ ПустаяСтрока(мТелефоны) Тогда
				мТелефоны = мТелефоны + ", ";
			КонецЕсли;
			мТелефоны = мТелефоны + УправлениеКонтактнойИнформацией.ПредставлениеКонтактнойИнформации(СтрокаТелефона);
			Комментарий = УправлениеКонтактнойИнформацией.КомментарийКонтактнойИнформации(СтрокаТелефона);
			Если НЕ ПустаяСтрока(Комментарий) Тогда
				мТелефоны = мТелефоны + " (" + Комментарий + ")";
			КонецЕсли;
			// Местоположение = Местоположение + ОбработатьТеги(СтрокаТелефона);
		КонецЦикла;		
		
		Местоположение = Строка(Строки.Местоположение);
		Если ПустаяСтрока(Местоположение) Тогда
			Местоположение = Строки.АдресФактический;
		КонецЕсли;
			
		мПочта = "";
		Для Каждого Ящик Из Строки.Почта Цикл
			Если НЕ ПустаяСтрока(мПочта) Тогда
				мПочта = мПочта + ", ";
			КонецЕсли;
			мПочта = мПочта + Ящик;
		КонецЦикла;
		
		Строки.Телефоны = мТелефоны;
		Строки.Почта = мПочта;
		Строки.Местоположение = Местоположение;

	КонецЦикла;	
	
	// Связь между таблицей значений и именами в СКД
	ВнешниеНаборыДанных = Новый Структура;
	ВнешниеНаборыДанных.Вставить("Таблица", Таблица);
	
	// Макет компоновки
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, 
		КомпоновщикНастроек.ПолучитьНастройки(), ДанныеРасшифровки);
	
	// Компоновка данных
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки, ВнешниеНаборыДанных, ДанныеРасшифровки);
	
	// Вывод результата
	ДокументРезультат.Очистить();
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(ДокументРезультат);
	ПроцессорВывода.Вывести(ПроцессорКомпоновки);
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры // ПриКомпоновкеРезультата()

#КонецОбласти

#КонецЕсли