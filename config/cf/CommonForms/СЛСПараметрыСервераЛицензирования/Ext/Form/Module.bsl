
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЭтоКлиентСервернаяБаза = Не СтрНайти(ВРег(СтрокаСоединенияИнформационнойБазы()), "FILE=");
	Стр = СтрШаблон(НСтр("ru = 'Локально, на комьютере %1%2'"), ИмяКомпьютера(), 
		?(ЭтоКлиентСервернаяБаза, ", где работает сервер приложений", ", где запущено клиентское приложение"));
	
	Элементы.РасположениеСервера.СписокВыбора.Добавить(0, Стр);
	Элементы.РасположениеСервера.СписокВыбора.Добавить(1, НСтр("ru = 'На другом компьютере сети'"));
	
	ПараметрыСвязи = СЛС.ПолучитьПараметрыСвязи();
	АдресСервера = ПараметрыСвязи.АдресСервера;
	ПортСервера = ПараметрыСвязи.ПортСервера;
	РасположениеСервера = ?(ПараметрыСвязи.АдресСервера = "localhost", 0, 1);

	Элементы.АдресСервера.Доступность = РасположениеСервера > 0;
	Элементы.ПортСервера.Доступность = РасположениеСервера > 0;	
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте 
Процедура РасположениеСервераПриИзменении(Элемент)
	
	АдресСервера = ?(РасположениеСервера = 0, "localhost", ?(АдресСервера = "localhost", "", АдресСервера));
	ПортСервера = ?(РасположениеСервера = 0, 9555, ?(ПортСервера = "", 9555, ПортСервера));
	
	Элементы.АдресСервера.Доступность = РасположениеСервера > 0;
	Элементы.ПортСервера.Доступность = РасположениеСервера > 0;
	
	Если Элемент <> Неопределено Тогда
		ТекущийЭлемент = Элементы.АдресСервера;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Декорация3Нажатие(Элемент)
	
	ФайловаяСистемаКлиент.ОткрытьНавигационнуюСсылку("https://softonit.ru/sls/");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Сохранить(Команда)
	
	СЛС.СохранитьПараметрыСвязи(АдресСервера, ПортСервера);
	СЛС.ОбновитьПараметрыДоступа();
	СЛСКлиент.Старт();
	СтандартныеПодсистемыКлиент.УстановитьРасширенныйЗаголовокПриложения();
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьСкоростьДоступаКСерверуЛицензирования(Команда)
	
	ПроверитьСкоростьДоступаКСерверуЛицензированияНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПроверитьСкоростьДоступаКСерверуЛицензированияНаСервере()
	
	Время = ТекущаяУниверсальнаяДатаВМиллисекундах();
	Результат = СЛС.СкоростьДоступаКСерверу(АдресСервера, ПортСервера);
	Если Результат <> Неопределено Тогда
		ВремяДоступа = ТекущаяУниверсальнаяДатаВМиллисекундах() - Время;
		ОбщегоНазначения.СообщитьПользователю(СтрШаблон(НСтр("ru = 'Сервер доступен. Время доступа %1 мс.'"), ВремяДоступа));
	Иначе
		ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Сервер НЕ доступен.'"));
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти