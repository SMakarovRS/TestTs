
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ТекущаяВерсия = СокрЛП(Метаданные.Версия);
	
	Если Параметры.Свойство("ТекстПриОбновлении") Тогда
		ПараметрыОбновлений = Параметры;
	Иначе
		ПараметрыОбновлений = УправлениеITОтделом8УФ.ПолучитьСтруктуруОписанияОбновлений(Истина);
	КонецЕсли;
	
	Элементы.ГруппаЕстьВерсии.Видимость = Ложь;
	Элементы.ГруппаПоследняяВерсия.Видимость = Ложь;
	Элементы.ГруппаСтраницы.ОтображениеСтраниц = ОтображениеСтраницФормы.Нет;
	
	Если ПараметрыОбновлений.Свойство("КоличествоОбновлений") И ПараметрыОбновлений.КоличествоОбновлений > 0 Тогда
		// Есть другие версии.
		Элементы.ГруппаЕстьВерсии.Видимость = Истина;
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаЕстьВерсии;
		ХТМЛ = ПараметрыОбновлений.ТекстПриОбновлении;                        
		ДоступностьОбновлений = Новый ФорматированнаяСтрока(?(ПараметрыОбновлений.КоличествоОбновлений = 1, 
			НСтр("ru = 'Доступно новое обновление'"), НСтр("ru = 'Доступны новые обновления'")), Новый Шрифт(,,Истина), 
			Новый Цвет(0, 150, 70));
	Иначе
		// Текущая версия последняя.
		Элементы.ГруппаПоследняяВерсия.Видимость = Истина;
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаПоследняяВерсия;		
	КонецЕсли;
	ВладелецЛицензии 			= СЛС.ВладелецЛицензии();
	РедакцияКонфигурации 		= СЛС.РедакцияКонфигурации();
	ДанныеТехподдержки 			= СЛС.ДанныеТехподдержки();	
	СрокДействияТехподдержки 	= ДанныеТехподдержки["ДатаОкончанияТехподдержки"];
	Если СрокДействияТехподдержки >= ДобавитьМесяц(ТекущаяДатаСеанса(), 1)  Тогда
		Элементы.СрокДействияТехподдержки.ЦветТекста = Новый Цвет(0, 150, 70);
		Элементы.Комментарий.Заголовок = НСтр("ru = 'Ваша версия обновлена до последней'");
		Элементы.Комментарий.ЦветТекста = Новый Цвет(0, 150, 70);
	ИначеЕсли СрокДействияТехподдержки <= ТекущаяДатаСеанса() Тогда
		Элементы.СрокДействияТехподдержки.ЦветТекста = WebЦвета.Красный;
		Элементы.Комментарий.Заголовок = 
			НСтр("ru = 'У Вас последняя версия, но Вы больше не можете использовать новые версии ПО и задавать вопросы техподдержке.
                  |Рекомендуем Вам продлить техподдержку.'");
		Элементы.Комментарий.ЦветТекста = WebЦвета.Красный;
		Элементы.Комментарий.Гиперссылка = Истина;
	Иначе
		Элементы.СрокДействияТехподдержки.ЦветТекста = WebЦвета.Киноварь;
		Элементы.Комментарий.Заголовок = 
			НСтр("ru = 'У Вас последняя версия, но техподдержка и получение новых версий скоро закончится.
                  |При непрерывном продлении цена на техподдержку ниже.'");
		Элементы.Комментарий.Гиперссылка = Истина;
		Элементы.Комментарий.ЦветТекста = WebЦвета.Киноварь;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура НаписатьПисьмоАвтору(Команда)
	
	ОткрытьФорму("ОбщаяФорма.ОтправкаСообщенияВТехподдержку",,,,,,, РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
	
КонецПроцедуры

&НаКлиенте
Процедура ПерейтиНаСайт(Команда)
	
	ФайловаяСистемаКлиент.ОткрытьНавигационнуюСсылку("https://softonit.ru?referrer=utm_source&utm_campaign=from1c_itnews&utm_content=monitor");
	
КонецПроцедуры

&НаКлиенте
Процедура ПродлитьТехподдержкуИПолучениеОбновлений(Команда)
	
	ФайловаяСистемаКлиент.ОткрытьНавигационнуюСсылку("https://softonit.ru/catalog/updateit/?referrer=utm_source&utm_campaign=from1c_itnews&utm_content=monitor");
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗагрузитьХТМЛ(Адрес)
	
	ХТМЛ = Адрес;	
	
КонецПроцедуры

&НаКлиенте
Процедура КомментарийНажатие(Элемент)
	
	ФайловаяСистемаКлиент.ОткрытьНавигационнуюСсылку("https://softonit.ru/catalog/updateit/?view=table&id=it3&referrer=utm_source&utm_campaign=from1c_itnews&utm_content=monitor");
	
КонецПроцедуры

#КонецОбласти