
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Если Параметры.Свойство("ОтКого") Тогда 
		ОтКого = Параметры.ОтКого;
	Иначе
		ОтКого = УправлениеITОтделом8УФПовтИсп.УчетнаяЗаписьЭлектроннойПочтыПоУмолчанию();
	КонецЕсли;
	
	Если Параметры.Свойство("Текст") Тогда 
		Текст = Параметры.Текст;
		СтрокаПозицияКурсора = НСтр("ru = 'ПозицияКурсора'");
		СтрокаКурсора = ОпределитьНомерПозицииДляКурсора(Текст, СтрокаПозицияКурсора) - 10;
		Текст = СтрЗаменить(Текст, СтрокаПозицияКурсора, "");
		Содержание.УстановитьHTML(Текст, Новый Структура);
	КонецЕсли;
	
	Если Параметры.Свойство("Вложения") Тогда 
		Если ТипЗнч(Параметры.Вложения) = Тип("СписокЗначений") Тогда 
			Для Каждого СтрокаСписка Из Параметры.Вложения Цикл 
				Вложения.Добавить(СтрокаСписка.Значение, СтрокаСписка.Представление);
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
	Если Параметры.Свойство("Тема") Тогда 
		Тема = Параметры.Тема;
	Иначе
		Тема = СтрШаблон(НСтр("ru = 'Пожелания от %1'"), УправлениеITОтделом8УФ.ВладелецЛицензии());
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	МаксимальныйРазмерФайлов = 10;
	
	УстановитьКурсорВШаблонеТекста();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура ПрикрепитьФайл(Элемент)
	
#Если ВебКлиент Тогда
	РасширениеПодключено = ПодключитьРасширениеРаботыСФайлами();
#Иначе
	РасширениеПодключено = Истина;
#КонецЕсли
	
	ДобавитьВнешниеФайлы(РасширениеПодключено);
	
КонецПроцедуры

&НаКлиенте
Процедура УдалитьФайл(Элемент)
	
	Для Итерация = 0 По ВыбираемыеФайлы.Количество() - 1 Цикл 
		
		Если ВыбираемыеФайлы.Получить(Итерация).ИмяКнопкиУдаления = Элемент.Имя Тогда 
			ИндексИмени = ПолучитьИндексЭлементаФормы(Элемент.Имя);
			УдалитьВсеПодчиненныеЭлементы(ИндексИмени);
			УдалитьИзВременногоХранилища(ВыбираемыеФайлы.Получить(Итерация).АдресХранилища);
			ЭлементСписка = Вложения.НайтиПоИдентификатору(ВыбираемыеФайлы.Получить(Итерация).ИдентификаторВСпискеЗначений);
			Если ЭлементСписка <> Неопределено Тогда 
				Вложения.Удалить(ЭлементСписка);
			КонецЕсли;
			ВыбираемыеФайлы.Получить(Итерация).Размер = 0;
			Возврат;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры


#КонецОбласти

#Область ОбработчикиКомандФормы

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура Отправить(Команда)
	
	ОчиститьСообщения();
	
	Если Не ПроверитьЗаполнениеРеквизитов() Тогда 
		Возврат;
	КонецЕсли;
	
	РезультатОтправки = ОтправитьСообщениеСервер();
	Если РезультатОтправки Тогда 
		ПоказатьОповещениеПользователя(НСтр("ru = 'Сообщение отправлено.'"));
		Закрыть();
	Иначе		
		ПоказатьСообщениеПользователю(НСтр("ru = 'К сожалению сообщение не было отправлено.
			|Повторите попытку позже.'"));
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаСервере
Процедура УдалитьВсеПодчиненныеЭлементы(ИндексЭлемента)
	
	НайденнаяЭлемент = Элементы.Найти("ГруппаФайла" + Строка(ИндексЭлемента));
	Если НайденнаяЭлемент <> Неопределено Тогда 
		Элементы.Удалить(НайденнаяЭлемент);
	КонецЕсли;
	
	НайденнаяЭлемент = Элементы.Найти("ТекстИмениФайла" + Строка(ИндексЭлемента));
	Если НайденнаяЭлемент <> Неопределено Тогда 
		Элементы.Удалить(НайденнаяЭлемент);
	КонецЕсли;
	
	НайденнаяЭлемент = Элементы.Найти("КнопкаУдаленияФайла" + Строка(ИндексЭлемента));
	Если НайденнаяЭлемент <> Неопределено Тогда 
		Элементы.Удалить(НайденнаяЭлемент);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ПолучитьИндексЭлементаФормы(ИмяЭлемента)
	
	НачалоПозиции = СтрДлина("КнопкаУдаленияФайла") + 1;
	Возврат Число(Сред(ИмяЭлемента, НачалоПозиции));
	
КонецФункции

&НаСервере
Функция ПроверитьЗаполнениеРеквизитов()
	
	Если Не ЗначениеЗаполнено(ОтКого) Тогда 		
		ПоказатьСообщениеПользователю(НСтр("ru = 'Адрес ответа не заполнен.'"));
		Возврат Ложь;
	КонецЕсли;
	
	Если Найти(ОтКого.АдресЭлектроннойПочты, "@") = 0 Тогда 		
		ПоказатьСообщениеПользователю(НСтр("ru = 'Адрес ответа заполнен некорректно.'"));
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

&НаКлиенте
Процедура ДобавитьВнешниеФайлы(РасширениеПодключено)
	
	Если РасширениеПодключено Тогда 
		ПоместитьФайлыСРасширением();
	Иначе
		ПоместитьФайлыБезРасширения();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПоместитьФайлыСРасширением()
	
	// Вызов диалога выбора файлов.
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	Диалог.Заголовок = НСтр("ru = 'Выберите файл'");
	Диалог.МножественныйВыбор = Истина;
	Если Не Диалог.Выбрать() Тогда
		Возврат;
	КонецЕсли;
	ВыбранныеФайлы = Диалог.ВыбранныеФайлы;
	
	// Проверка на корректность общего размера файлов.
	Если Не ОбщийРазмерФайловОптималенКлиент(МаксимальныйРазмерФайлов, ВыбранныеФайлы) Тогда 
		ТекстПредупреждения = НСтр("ru = 'Размер выбранных файлов превышает предел в %1 Мб'");
		ТекстПредупреждения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстПредупреждения, МаксимальныйРазмерФайлов);
		ОчиститьСообщения();
		ПоказатьСообщениеПользователю(ТекстПредупреждения);
		Возврат;
	КонецЕсли;
	
	Состояние(НСтр("ru = 'Подождите. Выбранные файлы добавляются к сообщению.'"));
	
	// Добавить файлы в таблицу.
	ДобавитьФайлыВТаблицуВыбираемыхФайлов(ВыбранныеФайлы);
	
	ДобавитьФайлыНаФорму();
	
	Состояние();
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьФайлыНаФорму()
	
	// Создаются элементы для вложенных файлов.
	СоздатьЭлементыФормыДляВложенногоФайла();
	
	// Добавляются файлы в список значений.
	ДобавитьФайлыВСписокОтправляемых();
	
КонецПроцедуры

&НаКлиенте
Процедура ПоместитьФайлыБезРасширения()
	
	АдресХранилища		= "";
	ВыбранноеИмяФайла	= "";
	
	НачатьПомещениеФайла(Новый ОписаниеОповещения("ПоместитьФайлыБезРасширенияЗавершение", ЭтотОбъект, Новый Структура("АдресХранилища, ВыбранноеИмяФайла", АдресХранилища, ВыбранноеИмяФайла)), АдресХранилища, ,Истина, УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоместитьФайлыБезРасширенияЗавершение(Результат, Адрес, ВыбранноеИмяФайла, ДополнительныеПараметры) Экспорт
    
    Если Не Результат Тогда
        Возврат;
    КонецЕсли;
    
    ИмяФайла = ПолучитьИмяФайла(ВыбранноеИмяФайла);
    ПоместитьФайлыБезРасширенияНаСервере(Адрес, ИмяФайла);

КонецПроцедуры

&НаКлиенте
Функция ПолучитьИмяФайла(ВыбранноеИмяФайла)
	
	ИмяФайла = ВыбранноеИмяФайла;
	
	Пока СтрНайти(ИмяФайла, "/") <> 0 ИЛИ СтрНайти(ИмяФайла, "\") <> 0 Цикл
		
		ПозицияЭлемента = СтрНайти(ВыбранноеИмяФайла, "/");
		ИмяФайла = Сред(ИмяФайла, ПозицияЭлемента + 1);
		
		ПозицияЭлемента = СтрНайти(ВыбранноеИмяФайла, "\");
		ИмяФайла = Сред(ИмяФайла, ПозицияЭлемента + 1);
		
	КонецЦикла;
	
	Возврат ИмяФайла;
	
КонецФункции

&НаСервере
Процедура ПоместитьФайлыБезРасширенияНаСервере(АдресХранилища, ИмяФайла)
	
	НовыйФайл = ПолучитьИзВременногоХранилища(АдресХранилища);
	
	// Проверка на корректность общего размера файлов.
	Если Не ОбщийРазмерФайловОптималенСервер(МаксимальныйРазмерФайлов, НовыйФайл) Тогда 
		ТекстПредупреждения = НСтр("ru = 'Размер выбранных файлов превышает предел в %1 Мб'");
		ТекстПредупреждения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстПредупреждения, МаксимальныйРазмерФайлов);
		ПоказатьСообщениеПользователю(ТекстПредупреждения);
		УдалитьИзВременногоХранилища(АдресХранилища);
		Возврат;
	КонецЕсли;
	
	// Удаление файла "TechnicalParameters.xml", если он имеется
	Если ВРег(ИмяФайла) = ВРег(ИмяФайлаТехническихПараметров) Тогда 
		ТекстПредупреждения = НСтр("ru = 'Файл с именем ''%1'' нельзя прикладывать к сообщению.
									|Переименуйте добавляемый файл.'");
		ТекстПредупреждения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстПредупреждения, ИмяФайла);
		ПоказатьСообщениеПользователю(ТекстПредупреждения);
		УдалитьИзВременногоХранилища(АдресХранилища);
		Возврат;
	КонецЕсли;
	
	СтрокаТаблицы				 = ВыбираемыеФайлы.Добавить();
	СтрокаТаблицы.ИмяФайла		 = ИмяФайла;
	СтрокаТаблицы.Размер		 = НовыйФайл.Размер() / 1024;
	СтрокаТаблицы.АдресХранилища = АдресХранилища;
	
	СоздатьЭлементыФормыДляВложенногоФайла();
	
	ДобавитьФайлыВСписокОтправляемых();
	
КонецПроцедуры

&НаКлиенте
Функция ОбщийРазмерФайловОптималенКлиент(МаксимальныйРазмер, ВыбранныеФайлы)
	
	Если ПолучитьОбщийРазмерВыбранногоФайла(ВыбранныеФайлы) > МаксимальныйРазмер Тогда 
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

&НаСервере
Функция ОбщийРазмерФайловОптималенСервер(МаксимальныйРазмерФайлов, НовыйФайл)
	
	Размер = НовыйФайл.Размер() / 1024 / 1024 ;
	
	// Подсчет общего размера приложенных к письму файлов (с установленной пометкой).
	Для Итерация = 0 По ВыбираемыеФайлы.Количество() - 1 Цикл
		Размер	= Размер + (ВыбираемыеФайлы.Получить(Итерация).Размер / 1024);
	КонецЦикла;
	
	Если Размер > МаксимальныйРазмерФайлов Тогда 
		Возврат Ложь;
	Иначе
		Возврат Истина;
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Функция ПолучитьОбщийРазмерВыбранногоФайла(ВыбранныеФайлы)
	
	Размер = 0;
	
	Для Итерация = 0 По ВыбранныеФайлы.Количество() -1 Цикл 
		ТекущийФайл		= Новый Файл(ВыбранныеФайлы.Получить(Итерация));
		Размер			= Размер + ТекущийФайл.Размер();
	КонецЦикла;
	
	// Подсчет общего размера приложенных к письму файлов (с установленной пометкой).
	Для Итерация = 0 По ВыбираемыеФайлы.Количество() - 1 Цикл
		Размер	= Размер + (ВыбираемыеФайлы.Получить(Итерация).Размер / 1024);
	КонецЦикла;
	
	РазмерВМегабайтах = Размер / 1024 / 1024;
	
	Возврат РазмерВМегабайтах;
	
КонецФункции

&НаКлиенте
Процедура ДобавитьФайлыВТаблицуВыбираемыхФайлов(ВыбранныеФайлы)
	
#Если ВебКлиент Тогда
	МассивОпераций = Новый Массив;
	Для Итерация = 0 По ВыбранныеФайлы.Количество() - 1 Цикл
		ОписаниеВызова = Новый Массив;
		ОписаниеВызова.Добавить("ПоместитьФайлы");
		
		ПомещаемыеФайлы = Новый Массив;
		Описание = Новый ОписаниеПередаваемогоФайла(ВыбранныеФайлы.Получить(Итерация), "");
		ПомещаемыеФайлы.Добавить(Описание);
		ОписаниеВызова.Добавить(ПомещаемыеФайлы);
		
		ОписаниеВызова.Добавить(Неопределено);
		ОписаниеВызова.Добавить(Неопределено);
		ОписаниеВызова.Добавить(Ложь);
		
		МассивОпераций.Добавить(ОписаниеВызова);
	КонецЦикла;
	
	Если Не ЗапроситьРазрешениеПользователя(МассивОпераций) Тогда 
		Возврат;
	КонецЕсли;
#КонецЕсли
	
	Для Итерация = 0 По ВыбранныеФайлы.Количество() - 1 Цикл 
		
		ВыбранныйФайл = Новый Файл(ВыбранныеФайлы.Получить(Итерация));
		// Удаление файла "TechnicalParameters.xml", если он имеется
		Если ВРег(ВыбранныйФайл.Имя) = ВРег(ИмяФайлаТехническихПараметров) Тогда 
			ТекстПредупреждения = НСтр("ru = 'Файл с именем ''%1'' нельзя прикладывать к сообщению.
										|Переименуйте добавляемый файл.'");
			ТекстПредупреждения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстПредупреждения, ВыбранныйФайл.Имя);
			ОчиститьСообщения();
			ПоказатьСообщениеПользователю(ТекстПредупреждения);
			Продолжить;
		КонецЕсли;
		
		// Заполнение таблицы значений.
		СтрокаТаблицы				= ВыбираемыеФайлы.Добавить();
		СтрокаТаблицы.ИмяФайла		= ВыбранныйФайл.Имя;
		СтрокаТаблицы.Размер		= ВыбранныйФайл.Размер() / 1024;
		
		ПомещаемыеФайлы = Новый Массив;
		Описание = Новый ОписаниеПередаваемогоФайла(ВыбранныйФайл.ПолноеИмя, "");
		ПомещаемыеФайлы.Добавить(Описание);
		ПомещенныеФайлы = Новый Массив;
		
		Если Не ПоместитьФайлы(ПомещаемыеФайлы, ПомещенныеФайлы, , Ложь, УникальныйИдентификатор) Тогда
			
			СообщениеОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Ошибка при загрузке файла %1'"), ВыбранныйФайл.Имя);
			ОчиститьСообщения();
			ПоказатьСообщениеПользователю(СообщениеОбОшибке);
			ВыбираемыеФайлы.Удалить(ВыбираемыеФайлы.Индекс(СтрокаТаблицы));
			Продолжить;
			
		КонецЕсли;
		
		СтрокаТаблицы.АдресХранилища = ПомещенныеФайлы.Получить(0).Хранение;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьФайлыВСписокОтправляемых()
	
	Для каждого ТекущийФайл Из ВыбираемыеФайлы Цикл 
		
		Если ТекущийФайл.ИдентификаторВСпискеЗначений <> 0 Тогда 
			Продолжить;
		КонецЕсли;
		
		ИмяФайла = ТекущийФайл.ИмяФайла;
		АдресХранилища = ТекущийФайл.АдресХранилища;
		НовыйФайлВложения = Вложения.Добавить(ПолучитьИзВременногоХранилища(АдресХранилища), ИмяФайла);
		ТекущийФайл.ИдентификаторВСпискеЗначений = НовыйФайлВложения.ПолучитьИдентификатор();
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
// Записывает параметры в XML.
//
// Параметры:
//	ТекстXML - ЗаписьXML - запись XML.
//	МассивПараметров - массив параметров.
//
Процедура ЗаписатьПараметрыВXML(ТекстXML, МассивПараметров)
	
	ТекстXML.ЗаписатьНачалоЭлемента("parameters");
	Для Итерация = 0 По МассивПараметров.Количество() - 1 Цикл 
		ТекстXML.ЗаписатьНачалоЭлемента("parameter");
		Элемент = МассивПараметров.Получить(Итерация);
		ТекстXML.ЗаписатьАтрибут(Элемент.Имя, Элемент.Значение);
		ТекстXML.ЗаписатьКонецЭлемента();
	КонецЦикла;
	ТекстXML.ЗаписатьКонецЭлемента();
	
КонецПроцедуры

&НаСервере
// Возвращает текст технических параметров.
//
// Возвращаемое значение:
//	Соответствие:
//		Ключ - строка - наименование вложения.
//		Значение - ДвоичныеДанные - файл вложения.
//
Функция СформироватьXMLСТехническимиПараметрами() Экспорт
	
	МассивПараметров = ОпределитьМассивТехническихПараметров();
	
	ФайлXML = ПолучитьИмяВременногоФайла("xml");
	
	ТекстXML = Новый ЗаписьXML;
	ТекстXML.ОткрытьФайл(ФайлXML);
	ТекстXML.ЗаписатьОбъявлениеXML();
	ЗаписатьПараметрыВXML(ТекстXML, МассивПараметров);
	ТекстXML.Закрыть();
	
	ДвоичныеДанныеФайла = Новый ДвоичныеДанные(ФайлXML);
	
	Попытка
		УдалитьФайлы(ФайлXML);
	Исключение
		ЗаписьЖурналаРегистрации(НСтр("ru = 'Информационный центр. Отправка сообщения в техподдержку. Не удалось удалить временный файл технических параметров.'"), 
			УровеньЖурналаРегистрации.Ошибка, , , ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
	КонецПопытки;
	
	Вложение = Новый Соответствие;
	Вложение.Вставить(ПолучитьИмяФайлаТехническихПараметровДляСообщенияВТехПоддержку(), ДвоичныеДанныеФайла);
	Возврат Вложение;
	
КонецФункции

&НаСервере
// Возвращает имя файла, в котором находятся технические параметры
// для службы техподдержки.
//
// Возвращаемое значение:
//	Строка - имя файла.
//
Функция ПолучитьИмяФайлаТехническихПараметровДляСообщенияВТехПоддержку() Экспорт
	
	Возврат "TechnicalParameters.xml";
	
КонецФункции

&НаСервере
// Возвращает массив технических параметров.
//
// Возвращаемое значение:
//	Массив  - массив структуры технических параметров с полями:
//		Имя - Строка - имя параметра.
//		Значение - Строка - значение параметра.
//
Функция ОпределитьМассивТехническихПараметров()
	
	ИнформацияСистемная = Новый СистемнаяИнформация;
	
	МассивПараметров = Новый Массив;
	МассивПараметров.Добавить(Новый Структура("Имя, Значение", "ИмяКонфигурации",		Метаданные.Имя));
	МассивПараметров.Добавить(Новый Структура("Имя, Значение", "ВерсияКонфигурации",	Метаданные.Версия));
	МассивПараметров.Добавить(Новый Структура("Имя, Значение", "ВерсияПлатформы",		ИнформацияСистемная.ВерсияПриложения));
	МассивПараметров.Добавить(Новый Структура("Имя, Значение", "Логин",					ИмяПользователя()));
	МассивПараметров.Добавить(Новый Структура("Имя, Значение", "РегистрационныеДанные",	СЛС.ВладелецЛицензии()));
	
	Возврат МассивПараметров;
	
КонецФункции

&НаСервере
Функция ОтправитьСообщениеСервер()
	
	Текст = "";
	ВложенияHTML = Неопределено;
	Содержание.ПолучитьHTML(Текст, ВложенияHTML);
	Текст = Текст 		
		+ "<p>" + СтрШаблон(НСтр("ru = 'Владелец лицензии: %1'"), УправлениеITОтделом8УФ.ВладелецЛицензии()) + "</p>"
		+ "<p>" + СтрШаблон(НСтр("ru = 'ПО: %1'"), НСтр("ru = 'Управление IT-отделом 8'")) + "</p>"
		+ "<p>" + СтрШаблон(НСтр("ru = 'Редакция: %1'"), УправлениеITОтделом8УФ.РедакцияКонфигурации()) + "</p>"
		+ "<p>" + СтрШаблон(НСтр("ru = 'Автор: %1'"), Строка(Пользователи.ТекущийПользователь())) + "</p>";
	
	ВложенияСтруктура = СформироватьXMLСТехническимиПараметрами();
	Для Каждого Элемент Из Вложения Цикл
		ВложенияСтруктура.Вставить(Элемент.Представление, Элемент.Значение);
	КонецЦикла;
	Для Каждого Элемент Из ВложенияHTML Цикл
		ВложенияСтруктура.Вставить(Элемент.Ключ, Элемент.Значение);
	КонецЦикла;
	
	ПараметрыСообщения = Новый Структура();
	ПараметрыСообщения.Вставить("Тема",			Тема);
	ПараметрыСообщения.Вставить("Тело",     	Текст);
	ПараметрыСообщения.Вставить("Кому",     	"tickets@softonit.ru");
	ПараметрыСообщения.Вставить("Вложения", 	ВложенияСтруктура);
	ПараметрыСообщения.Вставить("ТипТекста",	"HTML");
	
	Результат = Истина;
	
	Попытка		
		Письмо = РаботаСПочтовымиСообщениями.ПодготовитьПисьмо(ОтКого, ПараметрыСообщения);
		РаботаСПочтовымиСообщениями.ОтправитьПисьмо(ОтКого, Письмо);
	Исключение
		Результат = Ложь;
		ОбщегоНазначения.СообщитьПользователю(ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
	КонецПопытки;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура УстановитьКурсорВШаблонеТекста()
	
	ПодключитьОбработчикОжидания("ОбработчикУстановитьКурсорВШаблонеТекста", 0.5, Истина);
	
КонецПроцедуры

&НаСервере
Функция ОпределитьНомерПозицииДляКурсора(ТекстПараметр, СтрокаПозицияКурсора)
	
	Возврат Найти(ТекстПараметр, СтрокаПозицияКурсора);
	
КонецФункции

&НаКлиенте
Процедура ОбработчикУстановитьКурсорВШаблонеТекста()
	
	ТекущийЭлемент = Элементы.Содержание;
	Закладка = Содержание.ПолучитьЗакладкуПоПозиции(СтрокаКурсора);
	Элементы.Содержание.УстановитьГраницыВыделения(Закладка, Закладка);
	
КонецПроцедуры

&НаСервере
Функция СоздатьЭлементыФормыДляВложенногоФайла()
	
	Для Итерация = 0 По ВыбираемыеФайлы.Количество() - 1 Цикл
		
		Если Не ПустаяСтрока(ВыбираемыеФайлы.Получить(Итерация).ИмяКнопкиУдаления) Тогда 
			Продолжить;
		КонецЕсли;
		
		ГруппаФайла							= Элементы.Добавить("ГруппаФайла" + Строка(Итерация), Тип("ГруппаФормы"), Элементы.ГруппаПрикрепленныхФайлов);
		ГруппаФайла.Вид						= ВидГруппыФормы.ОбычнаяГруппа;
		ГруппаФайла.ОтображатьЗаголовок		= Ложь;
		ГруппаФайла.Группировка				= ГруппировкаПодчиненныхЭлементовФормы.Горизонтальная;
		ГруппаФайла.Отображение				= ОтображениеОбычнойГруппы.Нет;
		
		ТекстИмениФайла						= Элементы.Добавить("ТекстИмениФайла" + Строка(Итерация), Тип("ДекорацияФормы"), ГруппаФайла);
		ТекстИмениФайла.Вид					= ВидДекорацииФормы.Надпись;
		ТекстИмениФайла.Заголовок			= ВыбираемыеФайлы.Получить(Итерация).ИмяФайла + " (" + ВыбираемыеФайлы.Получить(Итерация).Размер + " Кб)";
		
		КнопкаУдаленияФайла					= Элементы.Добавить("КнопкаУдаленияФайла" + Строка(Итерация), Тип("ДекорацияФормы"), ГруппаФайла);
		КнопкаУдаленияФайла.Вид				= ВидДекорацииФормы.Картинка;
		КнопкаУдаленияФайла.Картинка		= БиблиотекаКартинок.УдалитьНепосредственно;
		КнопкаУдаленияФайла.Подсказка		= НСтр("ru = 'Удалить файл'");
		КнопкаУдаленияФайла.Ширина			= 2;
		КнопкаУдаленияФайла.Высота			= 1;
		КнопкаУдаленияФайла.РазмерКартинки	= РазмерКартинки.Растянуть;
		КнопкаУдаленияФайла.Гиперссылка		= Истина;
		КнопкаУдаленияФайла.УстановитьДействие("Нажатие", "УдалитьФайл");
		
		ВыбираемыеФайлы.Получить(Итерация).ИмяКнопкиУдаления = КнопкаУдаленияФайла.Имя;
		
	КонецЦикла;
	
	// Подключается обработчик ожидания добавления файла.
	ДобавитьФайлыВСписокОтправляемых();
	
КонецФункции

&НаСервере
Функция ПоказатьСообщениеПользователю(Текст)
	
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = Текст;
	Сообщение.Сообщить();
	
КонецФункции

#КонецОбласти