
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Параметры.Свойство("ПользовательМП") Тогда
		ПользовательИБ = Параметры.ПользовательМП;
	Иначе	
		ПользовательИБ = Пользователи.ТекущийПользователь();
	КонецЕсли;
	
	ПараметрыСинхронизации = 
		ОбменМобильноеПриложениеПовтИсп.ПолучитьПараметрыСинхронизацииПоПользователю(ПользовательИБ);
	
	Если ПараметрыСинхронизации.Свойство("ПользовательЦБ") Тогда
		
		ПользовательИБ = ПараметрыСинхронизации.ПользовательЦБ;
		
		Если ПользовательИБ = Неопределено Тогда
			ОбщегоНазначения.СообщитьПользователю(
				НСтр("ru = 'Не найден пользователь мобильного приложения. Обратитесь к администратору.'"));
			Возврат;
		Иначе			
			СвойстваПользователя  = Пользователи.СвойстваПользователяИБ(ПользовательИБ.ИдентификаторПользователяИБ);
			ИмяПользователя       = СвойстваПользователя.Имя;
			ПарольУстановлен      = ?(СвойстваПользователя.ПарольУстановлен, 1, 0);
		КонецЕсли;	
		
		АдресПодключения       = СокрЛП(Константы.АдресПубликацииИнформационнойБазыВИнтернете.Получить());
		УстановитьПривилегированныйРежим(Ложь);
		
		ПодробныйЖурналРаботы   = ?(ПараметрыСинхронизации.ПодробныйЖурналРаботы,1,0);
		СрокУстареванияДанных   = ПараметрыСинхронизации.СрокУстареванияДанных;
		МаксимальныйРазмерФайла = ПараметрыСинхронизации.МаксимальныйРазмерФайлов;
		
		СсылкаПараметры = СтрШаблон("%1;%2;%3;%4;%5;%6", АдресПодключения, ИмяПользователя, ПарольУстановлен, 
									ПодробныйЖурналРаботы, СрокУстареванияДанных,  МаксимальныйРазмерФайла);	
		
		КодНастроек = СформироватьQRКод(СсылкаПараметры);
		
	Иначе
		
		ОбщегоНазначения.СообщитьПользователю(
			НСтр("ru = 'Не найден пользователь мобильного приложения. Обратитесь к администратору.'"));
		
	КонецЕсли;	
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция СформироватьQRКод(ДанныеКода)

	ДанныеQRКода = УправлениеПечатью.ДанныеQRКода(ДанныеКода, 0, 250);

	Возврат ПоместитьВоВременноеХранилище(ДанныеQRКода, УникальныйИдентификатор);

КонецФункции

#КонецОбласти