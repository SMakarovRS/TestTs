
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьВидимостьИДоступность();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура НаборКонстантИспользоватьМобильноеПриложениеПриИзменении(Элемент)
		
	ИспользоватьМобильныхКлиентов = НаборКонстант.ИспользоватьМобильноеПриложение;
	Если ИспользоватьМобильныхКлиентов Тогда
		ОписаниеОповещения = Новый ОписаниеОповещения("ПослеВопросаИспользоватьМобильныхКлиентов", ЭтотОбъект);
		ПоказатьВопрос(ОписаниеОповещения, 
			НСтр("ru = 'Для использования мобильных клиентов необходима предварительная подготовка данных.
                  |Это может занять некоторое время. Продолжить?'"), РежимДиалогаВопрос.ДаНет);
	КонецЕсли;		
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВопросаИспользоватьМобильныхКлиентов(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> КодВозвратаДиалога.Да Тогда
		НаборКонстант.ИспользоватьМобильноеПриложение = Ложь;
		Возврат;
	КонецЕсли;
	
	Записать();
	
	ДлительнаяОперация = ВыполнитьЗаполнениеДоступностиНаСервере();
	Если ДлительнаяОперация <> Неопределено Тогда
		
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
		ПараметрыОжидания.ВыводитьОкноОжидания = Истина;
		ПараметрыОжидания.ТекстСообщения       = НСтр("ru = 'Идет подготовка данных. Это может занять некоторое время'");
		ПараметрыОжидания.Интервал             = 2;
		
		ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, , ПараметрыОжидания);
		
	КонецЕсли;
	
	УстановитьВидимостьИДоступность();
	
КонецПроцедуры

&НаСервере
Функция ВыполнитьЗаполнениеДоступностиНаСервере()
	
	Если ДлительнаяОперация <> Неопределено Тогда
		ДлительныеОперации.ОтменитьВыполнениеЗадания(ДлительнаяОперация.ИдентификаторЗадания);
	КонецЕсли;
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.ОжидатьЗавершение           = 0;
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = "ЗаполнитьСведенияОДоступностиЗаданийПользователямФоновымЗаданием";
	ПараметрыВыполнения.ЗапуститьВФоне              = Истина;		
	ПараметрыЗадания 								= Новый Структура;	
	Результат = ДлительныеОперации.ВыполнитьВФоне(
		"ОбменМобильноеПриложениеВызовСервера.ЗаполнитьСведенияОДоступностиЗаданийПользователямФоновымЗаданием",
		ПараметрыЗадания, ПараметрыВыполнения);
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СписокМобильныхКлиентов(Команда)
	
	ОткрытьФорму("ПланОбмена.МобильноеПриложение.Форма.ФормаСписка");
	
КонецПроцедуры

&НаКлиенте
Процедура ЖурналРаботыСМобильнымиКлиентами(Команда)
	
	ОткрытьФорму("РегистрСведений.ЖурналРаботыСМобильнымиКлиентами.Форма.ФормаСписка");
	
КонецПроцедуры

&НаКлиенте
Процедура ПользователиМобильныхКлиентов(Команда)
	
	ОткрытьФорму("Справочник.ПользователиМобильныхПриложений.ФормаСписка");
	
КонецПроцедуры

&НаКлиенте
Процедура СообщенияОбменаСМобильнымиКлиентами(Команда)
	
	ОткрытьФорму("Справочник.СообщенияОбменаМобильноеПриложение.Форма.ФормаСписка");
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьВидимостьИДоступность()
	
	ИспользоватьМобильныхКлиентов = НаборКонстант.ИспользоватьМобильноеПриложение;
	Элементы.ЖурналРаботыСМобильнымиКлиентами.Доступность 	 = ИспользоватьМобильныхКлиентов;
	Элементы.СписокМобильныхКлиентов.Доступность 		  	 = ИспользоватьМобильныхКлиентов;
	Элементы.ПользователиМобильныхКлиентов.Доступность	  	 = ИспользоватьМобильныхКлиентов;
	Элементы.СообщенияОбменаСМобильнымиКлиентами.Доступность = ИспользоватьМобильныхКлиентов;
	
КонецПроцедуры	

#КонецОбласти