#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура УстановкаПараметровСеанса(ИменаПараметровСеанса)	
	
	// Установка стиля конфигурации.
	Если ИменаПараметровСеанса = Неопределено Тогда
		
		ТекущийПользователь	= Пользователи.ТекущийПользователь();
		СтильОформления		= УправлениеITОтделом8УФПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(
			ТекущийПользователь, "ОсновнойСтиль");

		ИмяСтиля = "";
		Если СтильОформления = Перечисления.СтилиОформления.Стандартный Тогда
			ИмяСтиля = "Стандартный";		
		ИначеЕсли СтильОформления = Перечисления.СтилиОформления.Темный Тогда 
			ИмяСтиля = "Темный";
		ИначеЕсли СтильОформления = Перечисления.СтилиОформления.Светлый Тогда 
			ИмяСтиля = "Светлый";
		ИначеЕсли СтильОформления = Перечисления.СтилиОформления.Серый Тогда 
			ИмяСтиля = "Серый";
		КонецЕсли;
		
		Если НЕ ПустаяСтрока(ИмяСтиля) Тогда
			ГлавныйСтиль = БиблиотекаСтилей[ИмяСтиля];
		КонецЕсли;
		
	КонецЕсли;
	
	// СтандартныеПодсистемы
	СтандартныеПодсистемыСервер.УстановкаПараметровСеанса(ИменаПараметровСеанса);
	// Конец СтандартныеПодсистемы
	
	//<< УИТ
	Если ИменаПараметровСеанса <> Неопределено Тогда		
		
		// параметры сеанса, инициализация которых требует обращения к одним и тем же данным
		// следует инициализировать сразу группой. для того, чтобы избежать их повторной инициализации, 
		// имена уже установленных параметров сеанса сохраняются в структуре УстановленныеПараметры
		УстановленныеПараметры = Новый Массив();
		Для Каждого ИмяПараметра Из ИменаПараметровСеанса Цикл
			УстановитьЗначениеПараметраСеанса(ИмяПараметра, УстановленныеПараметры);
		КонецЦикла;
				
	КонецЕсли;	
	//>>
	
КонецПроцедуры

Процедура УстановитьЗначениеПараметраСеанса(Знач ИмяПараметра, УстановленныеПараметры)
	
	// УниверсальныеМеханизмы
	Если УстановленныеПараметры.Найти(ИмяПараметра) <> Неопределено Тогда
		Возврат;
	КонецЕсли;
	// Конец УниверсальныеМеханизмы

	// Переопределяемый блок

	// ПодключаемоеОборудование
	МенеджерОборудованияВызовСервера.УстановитьПараметрыСеансаПодключаемогоОборудования(ИмяПараметра, УстановленныеПараметры);
	// Конец ПодключаемоеОборудование

	СЛС.УстановитьЗначениеПараметраСеанса(ИмяПараметра, УстановленныеПараметры);
	
	// Конец переопределяемого блока	
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли