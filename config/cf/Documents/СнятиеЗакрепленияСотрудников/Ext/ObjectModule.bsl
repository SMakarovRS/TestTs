#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ПрограммныйИнтерфейс

// Проверка объекта на обязательные реквизиты перед проведением.
Процедура ПроверитьДокументПередПроведением(ДополнительныеСвойства, Отказ) Экспорт
	
	// Проверка, что нет дублей в ТЧ по сотрудникам
	ТЗ = Сотрудники.Выгрузить();
	ТЗ.Колонки.Добавить("КоличествоМестХранения");	
	ТЗ.ЗаполнитьЗначения(1, "КоличествоМестХранения");	
	ТЗ.Свернуть("Сотрудник,МестоХранения", "КоличествоМестХранения");
	
	Для Каждого Стр Из ТЗ Цикл 
		Если Стр.КоличествоМестХранения > 1 Тогда   
			УправлениеITОтделом8УФ.СообщитьОбОшибке(ЭтотОбъект, 
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'В таблице найден повторяющийся сотрудник и место хранения %1 (%2)'"), "'" + Строка(Стр.Сотрудник) + "'", Строка(Стр.МестоХранения))
			, , , "Сотрудники", Отказ);
		КонецЕсли;
	КонецЦикла;
	
	// Проверка, что сотрудник не закреплен за местом хранения
	ЗапросОтветственных = Новый Запрос();
	ЗапросОтветственных.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ОтветственныеСотрудникиСрезПоследних.МестоХранения,
		|	ОтветственныеСотрудникиСрезПоследних.Сотрудник,
		|	ОтветственныеСотрудникиСрезПоследних.Регистратор
		|ИЗ
		|	РегистрСведений.ОтветственныеСотрудники.СрезПоследних(&Дата, ) КАК ОтветственныеСотрудникиСрезПоследних
		|ГДЕ
		|	ОтветственныеСотрудникиСрезПоследних.МестоХранения = &МестоХранения";
	ЗапросОтветственных.УстановитьПараметр("МестоХранения", ЭтотОбъект.Сотрудники[0].МестоХранения);
	ЗапросОтветственных.УстановитьПараметр("Дата", ЭтотОбъект.Дата);
	НоваяТЗ = ЗапросОтветственных.Выполнить().Выгрузить();
	
	Если НоваяТЗ.Количество() = 0 Тогда
			УправлениеITОтделом8УФ.СообщитьОбОшибке(ЭтотОбъект, 
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'За сотрудником %1 место хранения %2 не закреплено'"), "'" + Строка(ЭтотОбъект.Сотрудники[0].Сотрудник) + "'", Строка(ЭтотОбъект.Сотрудники[0].МестоХранения))
			, , , "Сотрудники", Отказ);
	КонецЕсли;
			
	ПараметрыОтбора = Новый Структура;
	Для Каждого Строки Из НоваяТЗ Цикл
		ПараметрыОтбора.Вставить("Сотрудник", Строки.Сотрудник);
		ПараметрыОтбора.Вставить("МестоХранения", Строки.МестоХранения);
		ПараметрыОтбора.Вставить("Регистратор", Строки.Регистратор);
		НайденныеСтроки = НоваяТЗ.НайтиСтроки(ПараметрыОтбора);
		Если Не ЗначениеЗаполнено(НайденныеСтроки[0].Сотрудник) Или ЭтотОбъект.Сотрудники[0].Сотрудник <> Строки.Сотрудник Тогда
			УправлениеITОтделом8УФ.СообщитьОбОшибке(ЭтотОбъект, 
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'За сотрудником %1 место хранения %2 не закреплено'"), "'" + Строка(ЭтотОбъект.Сотрудники[0].Сотрудник) + "'", Строка(ЭтотОбъект.Сотрудники[0].МестоХранения))
			, , , "Сотрудники", Отказ);
		КонецЕсли;	
	 КонецЦикла;

КонецПроцедуры

Функция ОбновитьОписаниеНаСервере(Сотрудник, ДатаАктуальности) Экспорт
	
	Если Сотрудник = Неопределено Тогда
		Возврат "";
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(СОтрудник) Тогда		
		Возврат "";
	КонецЕсли;
	
	Запрос = Новый Запрос();
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ОтветственныеСотрудникиСрезПоследних.МестоХранения КАК МестоХранения
		|ИЗ
		|	РегистрСведений.ОтветственныеСотрудники.СрезПоследних(&ДатаАктуальности, Сотрудник = &Сотрудник) КАК ОтветственныеСотрудникиСрезПоследних
		|
		|УПОРЯДОЧИТЬ ПО
		|	МестоХранения
		|АВТОУПОРЯДОЧИВАНИЕ";
	
	Запрос.УстановитьПараметр("ДатаАктуальности", ДатаАктуальности);
	Запрос.УстановитьПараметр("Сотрудник", СОтрудник);
	Описание = "";
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Если ПустаяСтрока(Описание) Тогда
			Описание = НСтр("ru = 'Выбытие'") + ": ";
		КонецЕсли;
		Описание = Описание + Строка(Выборка.МестоХранения) + "; ";
	КонецЦикла;	
	
	Возврат Описание;
	
КонецФункции

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
		
	// Инициализация дополнительных свойств для проведения документа.
	УправлениеITОтделом8УФ.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства);
	
	// Инициализация данных документа.
	СЛС.ИнициализироватьДанныеДокумента(Ссылка, ДополнительныеСвойства);	
	
	// Проверка объекта
	ПроверитьДокументПередПроведением(ДополнительныеСвойства, Отказ);
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	// Подготовка наборов записей.
	УправлениеITОтделом8УФ.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	// Отражение в разделах учета.
	СЛС.ОтразитьДвиженияВРазделахУчета(Ссылка, ДополнительныеСвойства, Движения, Отказ);	
	
	// Запись наборов записей.
	УправлениеITОтделом8УФ.ЗаписатьНаборыЗаписей(ЭтотОбъект);
		
	ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц.Закрыть();
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("СправочникСсылка.Сотрудники") Тогда
		Дата = ТекущаяДатаСеанса();
		Организация = ДанныеЗаполнения.Организация;
		Комментарий = Строка(ДанныеЗаполнения);
		Основание = ДанныеЗаполнения;
		
		НоваяСтрока = Сотрудники.Добавить();
		НоваяСтрока.Сотрудник = ДанныеЗаполнения;
		НоваяСтрока.Описание = ОбновитьОписаниеНаСервере(ДанныеЗаполнения, Дата);
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.Задание") Тогда
		Организация					= ДанныеЗаполнения.Организация;
		Комментарий					= ДанныеЗаполнения.Тема;
		Основание					= ДанныеЗаполнения;
		НоваяСтрока					= Сотрудники.Добавить();
		НоваяСтрока.МестоХранения	= ДанныеЗаполнения.МестоХранения;
		Если ТипЗнч(ДанныеЗаполнения.Инициатор)	= Тип("СправочникСсылка.ФизическиеЛица") Тогда
			НоваяСтрока.Сотрудник	= УправлениеITОтделом8УФ.СотрудникПоОрганизацииИФизЛицу(Организация, ДанныеЗаполнения.Инициатор);
		КонецЕсли;
	КонецЕсли
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли