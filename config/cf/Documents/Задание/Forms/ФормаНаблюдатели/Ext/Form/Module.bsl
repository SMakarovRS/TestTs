
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("МассивНаблюдателей") Тогда
		Для Каждого СтрокаМассива Из Параметры.МассивНаблюдателей Цикл
			СтрокаНаблюдатель = Наблюдатели.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаНаблюдатель, СтрокаМассива);
		КонецЦикла;	
	КонецЕсли;	
	
	мПравоИзмененияИсполнителя 			= ЗаданияСервер.ПравоРедактированияТекущийПользователь(Параметры.Объект);
	Если мПравоИзмененияИсполнителя = Ложь Тогда
		Элементы.Наблюдатели.ТолькоПросмотр = Истина;
	Иначе
		Элементы.Наблюдатели.ТолькоПросмотр = Ложь;
	КонецЕсли;
	
	Элементы.Наблюдатели.ТолькоПросмотр = Параметры.ТолькоПросмотрФормы;
	
	ОбновитьКнопкуДобавитьИсключитьСебя();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыНаблюдатели

&НаКлиенте
Процедура НаблюдателиПриИзменении(Элемент)
	
	ВнесеныИзменения = Истина;
	ОбновитьНумерациюСтрок();
	
КонецПроцедуры

&НаКлиенте
Процедура НаблюдателиНаблюдательАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, 
	СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если Ожидание <> 0 И НЕ ПустаяСтрока(Текст) Тогда        
	    СтандартнаяОбработка = Ложь;
		Элемент.СписокВыбора.Очистить();
	    ДанныеВыбора = ЗаданияСервер.ПолучитьСписокВыбораНаблюдателя(Текст);
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура НаблюдателиНаблюдательОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(ВыбранноеЗначение) Тогда
		Если НЕ ТипЗНЧ(ВыбранноеЗначение) = Тип("Тип") Тогда		
			СтандартнаяОбработка = Ложь;
			Модифицированность   = Истина;
			МассивТипов 		 = Новый Массив;
			МассивТипов.Добавить(ТипЗнч(ВыбранноеЗначение));
			Элемент.ОграничениеТипа = Новый ОписаниеТипов(МассивТипов, Новый КвалификаторыСтроки(200));
			Элементы.Наблюдатели.ТекущиеДанные.Адресат = ВыбранноеЗначение;
		КонецЕсли;
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура НаблюдателиНаблюдательОчистка(Элемент, СтандартнаяОбработка)
	
	МассивТипов = Новый Массив;
	МассивТипов.Добавить(Тип("СправочникСсылка.Пользователи"));
	МассивТипов.Добавить(Тип("СправочникСсылка.ГруппыПользователей"));
	МассивТипов.Добавить(Тип("СправочникСсылка.ФизическиеЛица"));
	МассивТипов.Добавить(Тип("СправочникСсылка.Контрагенты"));
	МассивТипов.Добавить(Тип("СправочникСсылка.КонтактныеЛица"));
	МассивТипов.Добавить(Тип("СправочникСсылка.Подразделения"));
	МассивТипов.Добавить(Тип("СправочникСсылка.ПотенциальныеКлиенты"));
	МассивТипов.Добавить(Тип("СправочникСсылка.Сотрудники"));
	Элемент.ОграничениеТипа = Новый ОписаниеТипов(МассивТипов, Новый КвалификаторыСтроки(200));
	Элемент.КнопкаВыбора = Истина;	
	
КонецПроцедуры

&НаКлиенте
Процедура НаблюдателиПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	МассивТипов = Новый Массив;
	МассивТипов.Добавить(Тип("СправочникСсылка.Пользователи"));
	Элементы.НаблюдателиНаблюдатель.ОграничениеТипа = Новый ОписаниеТипов(МассивТипов, Новый КвалификаторыСтроки(200));
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПринятьИЗакрыть(Команда)
				
	Если ВнесеныИзменения Тогда
		МассивНаблюдателей = Новый Массив;
		ВыгрузитьНаблюдателей(МассивНаблюдателей);
		Закрыть(МассивНаблюдателей);
	Иначе
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьФорму(Команда)
	
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьИсключитьСебяВНаблюдатели(Команда)
	
	ДобавитьИсключитьСебяВНаблюдателиНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Функция ВыгрузитьНаблюдателей(МассивНаблюдателей)
	
	Если Наблюдатели.Количество() > 0 Тогда
		Для Каждого СтрокаНаблюдатель Из Наблюдатели Цикл
			СтруктураСтроки = Новый Структура("НомерСтроки, Адресат");
			ЗаполнитьЗначенияСвойств(СтруктураСтроки, СтрокаНаблюдатель); 
			МассивНаблюдателей.Добавить(СтруктураСтроки);
		КонецЦикла;	
	КонецЕсли;
	
	Возврат МассивНаблюдателей;
	
КонецФункции

&НаКлиенте
Процедура ОбновитьНумерациюСтрок()
	
	Если Наблюдатели.Количество() > 0 Тогда
		НомерСтроки = 1;
		Для Каждого СтрокаНаблюдатели Из Наблюдатели Цикл
			СтрокаНаблюдатели.НомерСтроки = НомерСтроки;
			НомерСтроки 				  = НомерСтроки + 1; 
		КонецЦикла;	
	КонецЕсли;
	
КонецПроцедуры	

&НаСервере
Процедура ДобавитьИсключитьСебяВНаблюдателиНаСервере()
	
	Массив = Наблюдатели.НайтиСтроки(Новый Структура("Адресат", Пользователи.ТекущийПользователь()));
	Если Массив.Количество() = 0 Тогда
		НоваяСтрока = Наблюдатели.Добавить();
		НоваяСтрока.Адресат = Пользователи.ТекущийПользователь();
	Иначе
		Наблюдатели.Удалить(Массив[0]);
	КонецЕсли;
	ВнесеныИзменения = Истина;
	ОбновитьКнопкуДобавитьИсключитьСебя();
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьКнопкуДобавитьИсключитьСебя()
	
	Массив = Наблюдатели.НайтиСтроки(Новый Структура("Адресат", Пользователи.ТекущийПользователь()));
	Элементы.ФормаДобавитьИсключитьСебяВНаблюдатели.Картинка = ?(Массив.Количество() > 0,
		БиблиотекаКартинок.СнятьСИсполнения, 
		БиблиотекаКартинок.ВзятьНаИсполнение);
	
КонецПроцедуры


#КонецОбласти