
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("Задание") Тогда
		Результат = УправлениеSLA.РассчитатьSLA(Параметры.Задание);
	Иначе
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(Параметры.Задание) = Тип("ДанныеФормыСтруктура") Тогда
		Если Параметры.Задание.Ссылка.Пустая() Тогда
			Элементы.Задание.Видимость = Ложь;
		Иначе
			Задание = Параметры.Задание.Ссылка;
		КонецЕсли;
	Иначе
		Задание = Параметры.Задание;
	КонецЕсли;
	
	Если Результат.Свойство("ДатаНачала") Тогда
		ДатаНачала = Результат.ДатаНачала;
	Иначе
		Элементы.ДатаНачала.Видимость = Ложь;
	КонецЕсли;
	Если Результат.Свойство("SLA") Тогда
		SLA = Результат.SLA;
	Иначе
		Элементы.SLA.Видимость = Ложь;
	КонецЕсли;
	Если Результат.Свойство("Клиент") Тогда
		Клиент = Результат.Клиент;
	Иначе
		Элементы.Клиент.Видимость = Ложь;
	КонецЕсли;
	Если Результат.Свойство("Сервис") Тогда
		Сервис = Результат.Сервис;
	Иначе
		Элементы.Сервис.Видимость = Ложь;
	КонецЕсли;
	Если Результат.Свойство("Услуга") Тогда
		Услуга = Результат.Услуга;
	Иначе
		Элементы.Услуга.Видимость = Ложь;
	КонецЕсли;
	Если Результат.Свойство("УровеньРеакцииИВыполнения") Тогда
		УровеньРеакцииИВыполнения = Результат.УровеньРеакцииИВыполнения;
	Иначе
		Элементы.УровеньРеакцииИВыполнения.Видимость = Ложь;
	КонецЕсли;
	Если Результат.Свойство("КрайняяДатаРеакции") Тогда
		КрайняяДатаРеакции = Результат.КрайняяДатаРеакции;
		ОбновитьЦветность(КрайняяДатаРеакции, Элементы.КрайняяДатаРеакции);
	Иначе
		Элементы.КрайняяДатаРеакции.Видимость = Ложь;
	КонецЕсли;
	Если Результат.Свойство("КрайняяДатаВыполнения") Тогда
		КрайняяДатаВыполнения = Результат.КрайняяДатаВыполнения;
		ОбновитьЦветность(КрайняяДатаВыполнения, Элементы.КрайняяДатаВыполнения);
	Иначе
		Элементы.КрайняяДатаВыполнения.Видимость = Ложь;
	КонецЕсли;
	Если Результат.Свойство("График") Тогда
		График = Результат.График;
	Иначе
		Элементы.График.Видимость = Ложь;
	КонецЕсли;
	
	Если Результат.Свойство("РучнаяУстановкаДатВSLA") И Результат.РучнаяУстановкаДатВSLA = Ложь Тогда
		
		Если Результат.Свойство("НеВсеДанные") Тогда
			Стр = "";
			Если Результат.Свойство("НеВсеДанныеКлиентИнициатор") Тогда
				Стр = Стр + Символы.ПС + НСтр("ru = 'Не заполнен инициатор и/или клиент.'")
			КонецЕсли;
			Если Результат.Свойство("НеВсеДанныеУслуга") Тогда
				Стр = Стр + Символы.ПС + НСтр("ru = 'Не заполнен сервис/услуга.'")
			КонецЕсли;
			Информация = СтрШаблон(НСтр("ru = 'Содержатся не все данные для рассчетов дат. %1'"), Стр);
		ИначеЕсли Результат.Свойство("Ошибки") Тогда
			Информация = "";
			Для Каждого Ош Из Результат.Ошибки Цикл 
				Информация = Информация + СтрШаблон(НСтр("ru = '[%1] %2'"), Ош.Раздел, Ош.Текст) + Символы.ПС;
			КонецЦикла;
		Иначе
			Элементы.Информация.Видимость = Ложь;
		КонецЕсли;
	Иначе
		Информация = НСтр("ru = 'В документе включен флаг ""Ручная установка дат в SLA"". Даты SLA устанавливаются вручную.'");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедуры

&НаСервере
Процедура ОбновитьЦветность(Дата, Элемент)
	
	Если Дата <> Дата(1, 1, 1) И Дата < ТекущаяДатаСеанса() Тогда
		Попытка
			РедактируемыйЦветФонаПросроченногоЗадания 	= 
				РаботаСЦветомКлиентСервер.HexВЦвет(УправлениеITОтделом8УФПовтИсп.ПолучитьКонстанту("ЦветФонаПросроченнойЗадачи"));
			РедактируемыйЦветТекстаПросроченногоЗадания = 
				РаботаСЦветомКлиентСервер.HexВЦвет(УправлениеITОтделом8УФПовтИсп.ПолучитьКонстанту("ЦветТекстаПросроченнойЗадачи"));			
		Исключение
			РедактируемыйЦветФонаПросроченногоЗадания 	= Новый Цвет(255, 153, 153);
			РедактируемыйЦветТекстаПросроченногоЗадания = WebЦвета.Черный;
		КонецПопытки;
		
		Элемент.ЦветФона = РедактируемыйЦветФонаПросроченногоЗадания;
		Элемент.ЦветТекста = РедактируемыйЦветТекстаПросроченногоЗадания;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти