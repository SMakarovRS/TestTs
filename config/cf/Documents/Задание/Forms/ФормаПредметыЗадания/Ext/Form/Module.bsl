
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("МассивОснований") Тогда		
		Для Каждого Предмет Из Параметры.МассивОснований Цикл
			НоваяСтрока			= ПредметыСписок.Добавить();
			НоваяСтрока.Предмет = Предмет;
			ВерсияДанных = "";		
			Попытка
				ВерсияДанных = Предмет.ВерсияДанных;						
			Исключение
			    ЗаписьЖурналаРегистрации(НСтр("ru = 'Форма предметы задания'"),
			       УровеньЖурналаРегистрации.Ошибка,,,
			       ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));				
			КонецПопытки;
			
			УстановитьПривилегированныйРежим(Истина);			
			Если Не ЗначениеЗаполнено(ВерсияДанных) Тогда
				НоваяСтрока.Предмет = Строка(Предмет) + " " + НСтр("ru = '< доступ закрыт >'");
			КонецЕсли;
			НоваяСтрока.ПредметСсылка = Предмет;			
			
			Если Документы.ТипВсеСсылки().СодержитТип(ТипЗнч(Предмет)) Тогда	
				
				Если Предмет.Проведен Тогда
					НоваяСтрока.ИндексКартинки = 14;
				ИначеЕсли Предмет.ПометкаУдаления Тогда			
					НоваяСтрока.ИндексКартинки = 13;
				Иначе НоваяСтрока.ИндексКартинки = 12;			
				КонецЕсли;
				
			ИначеЕсли Справочники.ТипВсеСсылки().СодержитТип(ТипЗнч(Предмет)) Тогда
				
				Если Предмет.ПометкаУдаления Тогда
					НоваяСтрока.ИндексКартинки = 4;
				Иначе
					НоваяСтрока.ИндексКартинки = 3;
				КонецЕсли;
				
			КонецЕсли;
			
			УстановитьПривилегированныйРежим(Ложь);
			
		КонецЦикла;	
	КонецЕсли;
	
	мПравоИзмененияИсполнителя 			= ЗаданияСервер.ПравоРедактированияТекущийПользователь(Параметры.Объект);
	Если мПравоИзмененияИсполнителя = Ложь Тогда
		Элементы.ПредметыСписок.ТолькоПросмотр = Истина;
	Иначе
		Элементы.ПредметыСписок.ТолькоПросмотр = Ложь;
	КонецЕсли;
	
	Элементы.ПредметыСписок.ТолькоПросмотр = Параметры.ТолькоПросмотрФормы;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыПредметы

&НаКлиенте
Процедура ПредметыСписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.ПредметыСписок.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(ТекущиеДанные.Предмет) = Тип("Строка") Тогда
		СтандартнаяОбработка = Ложь;
		Возврат;
	Иначе ПоказатьЗначение(, ТекущиеДанные.Предмет); 	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПредметыСписокПредметНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ТекущиеДанные 				 = Элементы.ПредметыСписок.ТекущиеДанные;
	ТекущиеДанные.ИндексКартинки = 12;
	
КонецПроцедуры

&НаКлиенте
Процедура ПредметыСписокПредметОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ВыбранноеЗначение <> Неопределено Тогда		
		Если ВыбранноеЗначение = Тип("СправочникСсылка.ДоговорыКонтрагентов") Тогда
			СтандартнаяОбработка = Ложь;
			ОписаниеОповещения	 = Новый ОписаниеОповещения("ПослеВыбораДоговора", ЭтотОбъект);
			ОткрытьФорму("Справочник.ДоговорыКонтрагентов.Форма.ФормаВыбораСКонтрагентом", , ЭтотОбъект, , , ,
				ОписаниеОповещения, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
			Возврат;
		КонецЕсли;	
		ИндексКартинки = ОбработатьВыборПредметаНаСервере(ВыбранноеЗначение);
		Если ИндексКартинки > 0 Тогда
			ТекущиеДанные 				 = Элементы.ПредметыСписок.ТекущиеДанные;
			ТекущиеДанные.ИндексКартинки = ИндексКартинки;
			ТекущиеДанные.ПредметСсылка  = ВыбранноеЗначение;
		КонецЕсли;	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПредметыСписокПриИзменении(Элемент)
	
	ВнесеныИзменения = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыбораДоговора(Результат, ДополнительныеПараметры) Экспорт 
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ИндексКартинки = ОбработатьВыборПредметаНаСервере(Результат);
	Если ИндексКартинки > 0 Тогда
		ТекущаяСтрока = Элементы.ПредметыСписок.ТекущаяСтрока;
		Если ТекущаяСтрока <> Неопределено Тогда
			ПредметыСписок.Удалить(ТекущаяСтрока);
		КонецЕсли;	
		НоваяСтрока = ПредметыСписок.Добавить();		
		НоваяСтрока.ИндексКартинки = ИндексКартинки;
		НоваяСтрока.Предмет        = Результат;
		НоваяСтрока.ПредметСсылка  = Результат;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаОтмена(Команда)
	
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаОК(Команда)
	
	Если ВнесеныИзменения Тогда		
		МассивПредметов = ВыгрузитьПредметыНаСервере();
		Закрыть(МассивПредметов);
	Иначе
		Закрыть();
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ОбработатьВыборПредметаНаСервере(ВыбранноеЗначение)
	
	ИндексКартинки = 0;
	Если Документы.ТипВсеСсылки().СодержитТип(ТипЗнч(ВыбранноеЗначение)) Тогда	
		Если ВыбранноеЗначение.Проведен Тогда
			ИндексКартинки = 14;
		ИначеЕсли ВыбранноеЗначение.ПометкаУдаления Тогда			
			ИндексКартинки = 13;
		Иначе ИндексКартинки = 12;	
		КонецЕсли;
	ИначеЕсли Справочники.ТипВсеСсылки().СодержитТип(ТипЗнч(ВыбранноеЗначение)) Тогда
		Если ВыбранноеЗначение.ПометкаУдаления Тогда
			ИндексКартинки = 4;
		Иначе
			ИндексКартинки = 3;
		КонецЕсли;	
	КонецЕсли;	
	
	Возврат ИндексКартинки;
	
КонецФункции	

&НаСервере
Функция ВыгрузитьПредметыНаСервере()
	
	УстановитьПривилегированныйРежим(Истина);
	МассивПредметов = Новый Массив;
	Для Каждого СтрокаПредмет Из ПредметыСписок Цикл
		МассивПредметов.Добавить(СтрокаПредмет.ПредметСсылка);
	КонецЦикла;		
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат МассивПредметов; 
	
КонецФункции

#КонецОбласти

