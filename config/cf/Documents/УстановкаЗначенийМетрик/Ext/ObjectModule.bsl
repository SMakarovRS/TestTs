#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ИтогоПлан = Значения.Итог("ЗначениеПлан");
	ИтогоФакт = Значения.Итог("ЗначениеФакт");
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	// Инициализация дополнительных свойств для проведения документа.
	УправлениеITОтделом8УФ.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства);
	
	// Инициализация данных документа.
	СЛС.ИнициализироватьДанныеДокумента(Ссылка, ДополнительныеСвойства);
		
	// Подготовка наборов записей.
	УправлениеITОтделом8УФ.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	// Отражение в разделах учета.
	СЛС.ОтразитьДвиженияВРазделахУчета(Ссылка, ДополнительныеСвойства, Движения, Отказ);		
	
	// Запись наборов записей.
	УправлениеITОтделом8УФ.ЗаписатьНаборыЗаписей(ЭтотОбъект);

	ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц.Закрыть();
		
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	
	// Инициализация дополнительных свойств для удаления проведения документа
	УправлениеITОтделом8УФ.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства);
	
	// Подготовка наборов записей
	УправлениеITОтделом8УФ.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	// Запись наборов записей
	УправлениеITОтделом8УФ.ЗаписатьНаборыЗаписей(ЭтотОбъект);
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Ошибки = Неопределено;
	
	МассивОбязательных = Новый Массив;
	Массив = МетрикиВызовСервера.ЗначенияИзмеренийПоУмолчаниюДляМетрики(Метрика);
	Для Каждого Структура Из Массив Цикл
		Если Структура.Обязательное = Истина Тогда
			МассивОбязательных.Добавить(Структура);
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого Структура Из МассивОбязательных Цикл
		
		Имя = "Измерение" + Формат(Структура.НомерСтроки, "ЧРД=; ЧРГ=; ЧН=0; ЧГ=");
		Для Каждого СтрокаТЧ Из Значения Цикл
			
			Если НЕ ЗначениеЗаполнено(СтрокаТЧ[Имя]) Тогда
				
					ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Ошибки, 
						"Объект.УстановкаЗначенийМетрик["
						+ Формат(СтрокаТЧ.НомерСтроки - 1, "ЧДЦ=; ЧС=; ЧРД=; ЧРГ=; ЧН=0; ЧГ=") + "]." + Имя, 
						СтрШаблон(НСтр("ru = 'Измерение %1 метрики обязательно для заполнения.'"), Структура.Представление), 
						"УстановкаЗначенийМетрик", 
						СтрокаТЧ.НомерСтроки, 
						НСтр("ru = ''Измерение метрики обязательно для заполнения.'"), 
						СтрокаТЧ.НомерСтроки);					
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;	
	
	ОбщегоНазначенияКлиентСервер.СообщитьОшибкиПользователю(Ошибки, Отказ);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
