
#Область ОбработчикиСобытийФормы

// Процедура - обработчик события формы "ПриСозданииНаСервере".
//
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Список.Параметры.УстановитьЗначениеПараметра("Закрыт", 			НСтр("ru = 'Закрыт'"));
	Список.Параметры.УстановитьЗначениеПараметра("КромеЗакрытых", 	НСтр("ru = 'Кроме закрытых'"));
	
	// Если переданы отборы в параметрах - контекстное открытие формы.
	Если Параметры.Свойство("Контрагент")
		ИЛИ Параметры.Свойство("Инициатор")
		ИЛИ Параметры.Свойство("ТекущиеДела")
		ИЛИ Параметры.Свойство("СостояниеЗаказа") Тогда
		
		КонтекстноеОткрытие = Истина;
		
		Если Параметры.Свойство("Контрагент") Тогда			
			УстановитьОтборСписка("Контрагент", "ГруппаОтборКонтрагент", Параметры.Контрагент);
			
		ИначеЕсли Параметры.Свойство("Инициатор") Тогда			
			УстановитьОтборСписка("Инициатор", "ГруппаОтборИнициатор", Параметры.Инициатор);
			
		ИначеЕсли Параметры.Свойство("ТекущиеДела") Тогда
						
			Если Параметры.Свойство("СостояниеЗаказа") Тогда
				УстановитьОтборСписка("СостояниеЗаказа", "ГруппаОтборСостояние", Параметры.СостояниеЗаказа);
			КонецЕсли;	
						
			УстановитьОтборСписка("ОтборЗакрыт", "ГруппаОтборАктуальность", НСтр("ru = 'Кроме закрытых'"));
			
			Если Параметры.Свойство("ТекущийПользователь") Тогда			
				УстановитьОтборСписка("Автор", "ГруппаОтборАвтор", Параметры.ТекущийПользователь);
			КонецЕсли;	
			
		ИначеЕсли Параметры.Свойство("СостояниеЗаказа") Тогда			
			УстановитьОтборСписка("СостояниеЗаказа", "ГруппаОтборСостояние", Параметры.СостояниеЗаказа);						
			УстановитьОтборСписка("ОтборЗакрыт", "ГруппаОтборАктуальность", НСтр("ru = 'Кроме закрытых'"));
			
		КонецЕсли;		
		
	КонецЕсли;	
		
	Если Не КонтекстноеОткрытие Тогда		
		
		РаботаСОтборамиВызовСервера.ВосстановитьНастройкиОтборов(ЭтотОбъект, Список);
		
	КонецЕсли;	
	
	РаскраситьСписок();	
	СоздатьКнопкиРаботыССостояниями();
	
	#Область БСП_ПриСозданииНаСервере
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПараметрыРазмещения = ПодключаемыеКоманды.ПараметрыРазмещения();
	ПараметрыРазмещения.КоманднаяПанель = Элементы.Список.КоманднаяПанель;
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект, ПараметрыРазмещения);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	#КонецОбласти
	
	// Учет остатков контрагентов.
	МассивЭлементов = Новый Массив;
	МассивЭлементов.Добавить("ОтборОрганизация");
	УправлениеITОтделом8УФ.УстановитьОграничениеТипаДляЭлементовФормы(ЭтаФорма, МассивЭлементов);
	
КонецПроцедуры // ПриСозданииНаСервере()

// Процедура - обработчик события ОбработкаОповещения формы.
//
&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_ЗаказПоставщику"
	 ИЛИ ИмяСобытия = "Запись_УчетДенежныхСредств"
	 ИЛИ ИмяСобытия = "Запись_Поступление" Тогда
		Элементы.Список.Обновить();
	КонецЕсли;
	
	Если ИмяСобытия = "Запись_СостоянияЗаказовПоставщикам" Тогда
		РаскраситьСписок();
	КонецЕсли;
		
	Если ИмяСобытия = "ОткрытьСПомощьюСофтфона" Тогда
		
		Если Параметр.Свойство("Инициатор") Тогда			
			
			КонтекстноеОткрытие = Истина;
			УстановитьОтборСписка("Инициатор", "ГруппаОтборИнициатор", Параметры.Инициатор);
			
		КонецЕсли;			
		Активизировать();
		
	КонецЕсли;
	
	Если ИмяСобытия = "ЗаказПоставщику_ФормаСписка_УстановленОтборПериод" Тогда	
		
		Если НЕ КонтекстноеОткрытие Тогда			
			СохранитьНастройкиОтборов();	
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры // ОбработкаОповещения()

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	Если ТипЗнч(ИсточникВыбора) = Тип("УправляемаяФорма") 
		И ИсточникВыбора.ИмяФормы = "ОбщаяФорма.ФормаВыбораОрганизацииКонтрагента"
		И ИсточникВыбора.ВладелецФормы = ЭтаФорма Тогда		
		УправлениеITОтделом8УФКлиент.ВыполнитьОбработчикОбработкаВыбораФормы(ЭтаФорма, 
		 				"ОтборОрганизация",
						ОтборОрганизация,
						ВыбранноеЗначение,
						Новый ОписаниеОповещения("ПослеОбработкиВыбора", ЭтотОбъект));
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если Не КонтекстноеОткрытие И НЕ ЗавершениеРаботы Тогда
		
		СохранитьНастройкиОтборов();
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОтборКонтрагентНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ПараметрыФормы 		= Новый Структура;
	ПараметрыФормы.Вставить("ОтборПоставщики",Истина);
	СтандартнаяОбработка= Ложь;
	ОткрытьФорму("Справочник.Контрагенты.Форма.ФормаВыбора", ПараметрыФормы, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеПериодаНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка= Ложь;
	ДопПараметры 		= Новый Структура;	
	ДопПараметры.Вставить("ОписаниеОповещенияОВыбореПериода", 
		"ЗаказПоставщику_ФормаСписка_УстановленОтборПериод"); 
	
	РаботаСОтборамиКлиент.ПредставлениеПериодаВыбратьПериод(ЭтотОбъект, ДопПараметры);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборОрганизацияОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если Не ЗначениеЗаполнено(ВыбранноеЗначение) Тогда
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка	= Ложь;
	УстановитьОтборСписка("Организация", Элемент.Родитель.Имя, ВыбранноеЗначение);
	ВыбранноеЗначение		= Неопределено;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборКонтрагентОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если Не ЗначениеЗаполнено(ВыбранноеЗначение) Тогда
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка	= Ложь;
	УстановитьОтборСписка("Контрагент", Элемент.Родитель.Имя, ВыбранноеЗначение);
	ВыбранноеЗначение		= Неопределено;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборАвторОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если Не ЗначениеЗаполнено(ВыбранноеЗначение) Тогда
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка	= Ложь;
	УстановитьОтборСписка("Автор", Элемент.Родитель.Имя, ВыбранноеЗначение);
	ВыбранноеЗначение		= Неопределено;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборСостояниеОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если Не ЗначениеЗаполнено(ВыбранноеЗначение) Тогда
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка	= Ложь;
	УстановитьОтборСписка("СостояниеЗаказа", Элемент.Родитель.Имя, ВыбранноеЗначение);
	ВыбранноеЗначение		= Неопределено;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборСтатьяДоходовРасходовОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если Не ЗначениеЗаполнено(ВыбранноеЗначение) Тогда
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка	= Ложь;
	УстановитьОтборСписка("СтатьяДоходовРасходов", Элемент.Родитель.Имя, ВыбранноеЗначение);
	ВыбранноеЗначение		= Неопределено;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборИнициаторОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если Не ЗначениеЗаполнено(ВыбранноеЗначение) Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ТипЗнч(ВыбранноеЗначение) = Тип("Тип") Тогда
		
		СтандартнаяОбработка	= Ложь;
		УстановитьОтборСписка("Инициатор", Элемент.Родитель.Имя, ВыбранноеЗначение);
		ВыбранноеЗначение		= Неопределено;
		
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборАктуальностьОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если Не ЗначениеЗаполнено(ВыбранноеЗначение) Тогда
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка	= Ложь;	
	УстановитьОтборСписка("ОтборЗакрыт", Элемент.Родитель.Имя, ВыбранноеЗначение);
	ВыбранноеЗначение		= Неопределено;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормы

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

КонецПроцедуры

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ОценкаПроизводительности
	КлючеваяОперация = "ДокументЗаказПоставщику (открытие)";
	ОценкаПроизводительностиКлиент.ЗамерВремени(КлючеваяОперация);
	// Конец СтандартныеПодсистемы.ОценкаПроизводительности
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

// Процедура - обработчик нажатия на кнопку ОтборДень.
//
&НаКлиенте
Процедура ОтборДень(Команда)
	
	ОтборПериод.Вариант		= ВариантСтандартногоПериода.Сегодня;
	ПредставлениеПериода	= РаботаСОтборамиКлиентСервер.ОбновитьПредставлениеПериода(ОтборПериод);
	РаботаСОтборамиКлиентСервер.УстановитьОтборПоПериоду(Список.КомпоновщикНастроек.Настройки.Отбор,
				ОтборПериод.ДатаНачала, ОтборПериод.ДатаОкончания, "Дата");
				
	Если Элементы.ПанельОтборов.Видимость = Ложь Тогда
		РаботаСОтборамиКлиент.ПоказатьСкрытьПанельОтборов(ЭтотОбъект, Истина);			
	КонецЕсли;				
				
КонецПроцедуры // ОтборДень()

// Процедура - обработчик нажатия на кнопку ОтборНеделя.
//
&НаКлиенте
Процедура ОтборНеделя(Команда)
	
	ОтборПериод.Вариант		= ВариантСтандартногоПериода.ЭтаНеделя;
	ПредставлениеПериода	= РаботаСОтборамиКлиентСервер.ОбновитьПредставлениеПериода(ОтборПериод);
	РаботаСОтборамиКлиентСервер.УстановитьОтборПоПериоду(Список.КомпоновщикНастроек.Настройки.Отбор,
				ОтборПериод.ДатаНачала, ОтборПериод.ДатаОкончания, "Дата");
				
	Если Элементы.ПанельОтборов.Видимость = Ложь Тогда
		РаботаСОтборамиКлиент.ПоказатьСкрытьПанельОтборов(ЭтотОбъект, Истина);			
	КонецЕсли;
	
КонецПроцедуры // ОтборНеделя()

// Процедура - обработчик нажатия на кнопку ОтборМесяц.
//
&НаКлиенте
Процедура ОтборМесяц(Команда)
	
	ОтборПериод.Вариант		= ВариантСтандартногоПериода.ЭтотМесяц;
	ПредставлениеПериода	= РаботаСОтборамиКлиентСервер.ОбновитьПредставлениеПериода(ОтборПериод);
	РаботаСОтборамиКлиентСервер.УстановитьОтборПоПериоду(Список.КомпоновщикНастроек.Настройки.Отбор,
				ОтборПериод.ДатаНачала, ОтборПериод.ДатаОкончания, "Дата");
				
	Если Элементы.ПанельОтборов.Видимость = Ложь Тогда
		РаботаСОтборамиКлиент.ПоказатьСкрытьПанельОтборов(ЭтотОбъект, Истина);			
	КонецЕсли;
	
КонецПроцедуры // ОтборМесяц()

// Процедура - обработчик нажатия на кнопку ОтборОчистить.
//
&НаКлиенте
Процедура ОтборОчистить(Команда)
	
	ОтборПериод				= Новый СтандартныйПериод;
	ПредставлениеПериода	= РаботаСОтборамиКлиентСервер.ОбновитьПредставлениеПериода(ОтборПериод);
	РаботаСОтборамиКлиентСервер.УстановитьОтборПоПериоду(Список.КомпоновщикНастроек.Настройки.Отбор,
				ОтборПериод.ДатаНачала, ОтборПериод.ДатаОкончания, "Дата");
				
	Если Элементы.ПанельОтборов.Видимость = Ложь Тогда
		РаботаСОтборамиКлиент.ПоказатьСкрытьПанельОтборов(ЭтотОбъект, Истина);			
	КонецЕсли;
	
КонецПроцедуры // ОтборОчистить()

&НаКлиенте
// Проверяет наличие выделенных в списке ссылок и возвращает массив.
//
// Параметры:
// 		Список - ДинамическийСписок.
//
// Возвращаемое значение:
// 		Массив - Массив выделенных с списке ссылок.
//
Функция ПроверитьПолучитьВыделенныеВСпискеСсылки(Список)
	
	МассивСсылок = Новый Массив;
	
	Для нНомерСтроки = 0 По Список.ВыделенныеСтроки.Количество()-1 Цикл
		
		Если ТипЗнч(Список.ВыделенныеСтроки[нНомерСтроки]) <> Тип("СтрокаГруппировкиДинамическогоСписка") Тогда
			МассивСсылок.Добавить(Список.ВыделенныеСтроки[нНомерСтроки]);
		КонецЕсли;
		
	КонецЦикла;
	
	Если МассивСсылок.Количество() = 0 Тогда
		ПоказатьПредупреждение( , НСтр("ru='Команда не может быть выполнена для указанного объекта'"));
	КонецЕсли;
	
	Возврат МассивСсылок;
	
КонецФункции // ПроверитьПолучитьВыделенныеВСпискеСсылки()

&НаКлиенте
Процедура УстановитьСтатус(Команда)
	
	ВыделенныеСтроки = ПроверитьПолучитьВыделенныеВСпискеСсылки(Элементы.Список);
	Если ВыделенныеСтроки.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Попытка
		Номер = Число(Прав(Команда.Имя, СтрДлина(Команда.Имя) -  СтрДлина("УстановитьСтатус")));
	Исключение		
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru='Ошибка установки статуса'"));		
		Возврат;		
	КонецПопытки;
	
	Состояние 	= ПолучитьСостояниеПоНомеру(Номер);			
	ТекстВопроса= СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'У выделенных в списке заказов будет установлен статус %1. Продолжить?'"), Строка(Состояние));
		
	Ответ 		= Неопределено;
	ПоказатьВопрос(Новый ОписаниеОповещения("УстановитьСтатусЗавершение", ЭтотОбъект, 
		Новый Структура("ВыделенныеСтроки, Состояние", ВыделенныеСтроки, Состояние)), 
		ТекстВопроса,РежимДиалогаВопрос.ДаНет);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСтатусЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
    
    ВыделенныеСтроки = ДополнительныеПараметры.ВыделенныеСтроки;
    Состояние = ДополнительныеПараметры.Состояние;
    
    
    Ответ = РезультатВопроса;
    
    Если Ответ = КодВозвратаДиалога.Нет Тогда
        Возврат;
    КонецЕсли;
    
    УстановитьСтатусыУДокументов(ВыделенныеСтроки, Состояние);
    
    Элементы.Список.Обновить();

КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьЗаказы(Команда)
	ВыделенныеСтроки = ПроверитьПолучитьВыделенныеВСпискеСсылки(Элементы.Список);
	Если ВыделенныеСтроки.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;	
		
	ТекстВопроса = НСтр("ru='У выделенных в списке заказов будет установлен флаг ""Закрыт"". Продолжить?'");
	Ответ = Неопределено;

	ПоказатьВопрос(Новый ОписаниеОповещения("ЗакрытьЗаказыЗавершение", ЭтотОбъект, 
		Новый Структура("ВыделенныеСтроки", ВыделенныеСтроки)), 
		ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьЗаказыЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
    
    ВыделенныеСтроки = ДополнительныеПараметры.ВыделенныеСтроки;
    
    Ответ = РезультатВопроса;
    
    Если Ответ = КодВозвратаДиалога.Нет Тогда
        Возврат;
    КонецЕсли;
    
    УстановитьСтатусыУДокументов(ВыделенныеСтроки, Истина);
    
    Элементы.Список.Обновить();

КонецПроцедуры

&НаКлиенте
Процедура СнятьСтатусЗакрыт(Команда)
	
	ВыделенныеСтроки = ПроверитьПолучитьВыделенныеВСпискеСсылки(Элементы.Список);
	Если ВыделенныеСтроки.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;	
		
	ТекстВопроса = НСтр("ru='У выделенных в списке заказов будет снят флаг ""Закрыт"". Продолжить?'");
	Ответ = Неопределено;

	ПоказатьВопрос(Новый ОписаниеОповещения("СнятьСтатусЗакрытЗавершение", ЭтотОбъект, 
		Новый Структура("ВыделенныеСтроки", ВыделенныеСтроки)), 
		ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		
КонецПроцедуры

&НаКлиенте
Процедура СнятьСтатусЗакрытЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
    
    ВыделенныеСтроки = ДополнительныеПараметры.ВыделенныеСтроки;
    
    Ответ = РезультатВопроса;
    
    Если Ответ = КодВозвратаДиалога.Нет Тогда
        Возврат;
    КонецЕсли;
    
    УстановитьСтатусыУДокументов(ВыделенныеСтроки, Ложь);
    
    Элементы.Список.Обновить();
    
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Процедура раскрашивает список.
//
&НаСервере
Процедура РаскраситьСписок()
	
	// Раскраска списка
	СписокУдаляемыхЭлементов = Новый СписокЗначений;
	
	Для каждого ЭлементУсловногоОформления Из Список.УсловноеОформление.Элементы Цикл
		
		Если ЭлементУсловногоОформления.ИдентификаторПользовательскойНастройки = "Предустановленный"
			ИЛИ ЭлементУсловногоОформления.Представление = НСтр("ru = 'Заказ закрыт'") Тогда
			СписокУдаляемыхЭлементов.Добавить(ЭлементУсловногоОформления);
		КонецЕсли;
		
	КонецЦикла;
	
	Для каждого Элемент Из СписокУдаляемыхЭлементов Цикл
		Список.УсловноеОформление.Элементы.Удалить(Элемент.Значение);
	КонецЦикла;
		
	ВыборкаСостоянияЗаказов = Справочники.СостоянияЗаказовПоставщикам.Выбрать();
	
	Пока ВыборкаСостоянияЗаказов.Следующий() Цикл
		
		ЦветФона = ВыборкаСостоянияЗаказов.Цвет.Получить();
		Если ТипЗнч(ЦветФона) <> Тип("Цвет") Тогда
			Продолжить;
		КонецЕсли;
		
		ЭлементУсловногоОформления		= Список.УсловноеОформление.Элементы.Добавить();		
		ЭлементОтбора = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));		
		ЭлементОтбора.ЛевоеЗначение		= Новый ПолеКомпоновкиДанных("СостояниеЗаказа");
		ЭлементОтбора.ВидСравнения		= ВидСравненияКомпоновкиДанных.Равно;
		ЭлементОтбора.ПравоеЗначение	= ВыборкаСостоянияЗаказов.Ссылка;
		
		ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветФона);
		ЭлементУсловногоОформления.РежимОтображения	= РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
		ЭлементУсловногоОформления.ИдентификаторПользовательскойНастройки = "Предустановленный";
		ЭлементУсловногоОформления.Представление = НСтр("ru = 'По состоянию заказа'") 
			+ " " + ВыборкаСостоянияЗаказов.Наименование;
		
	КонецЦикла;
	
	
	ЭлементУсловногоОформления		= Список.УсловноеОформление.Элементы.Добавить();	
	ЭлементОтбора					= ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	
	ЭлементОтбора.ЛевоеЗначение		= Новый ПолеКомпоновкиДанных("Закрыт");
	ЭлементОтбора.ВидСравнения		= ВидСравненияКомпоновкиДанных.Равно;
	ЭлементОтбора.ПравоеЗначение	= Истина;
	
	ШрифтТекстаСтроки = Новый Шрифт(,,,,,Истина);
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("Шрифт", ШрифтТекстаСтроки);
	ЭлементУсловногоОформления.РежимОтображения							= РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
	ЭлементУсловногоОформления.ИдентификаторПользовательскойНастройки	= "Предустановленный";
	ЭлементУсловногоОформления.Представление							= НСтр("ru = 'Заказ закрыт'");
		
КонецПроцедуры // РаскраситьСписок()

&НаСервере
Процедура СоздатьКнопкиРаботыССостояниями()
	
	Запрос 		 = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
		|	СостоянияЗаказовПоставщикам.Ссылка,
		|	СостоянияЗаказовПоставщикам.Наименование
		|ИЗ
		|	Справочник.СостоянияЗаказовПоставщикам КАК СостоянияЗаказовПоставщикам
		|ГДЕ
		|	СостоянияЗаказовПоставщикам.ПометкаУдаления = ЛОЖЬ
		|АВТОУПОРЯДОЧИВАНИЕ";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Номер = 1;
	Пока Выборка.Следующий() Цикл
	
        НоваяКоманда			= Команды.Добавить("УстановитьСтатус" + Формат(Номер, "ЧГ="));
        НоваяКоманда.Действие	= "УстановитьСтатус";
        НоваяКнопка				= Элементы.Добавить("УстановитьСтатус" + Формат(Номер, "ЧГ="),
			Тип("КнопкаФормы"), Элементы.ГруппаСтатусов);
			
        НоваяКнопка.Заголовок	= Выборка.Наименование;
        НоваяКнопка.ИмяКоманды	= "УстановитьСтатус" + Формат(Номер, "ЧГ=");		
		
		Номер					= Номер + 1;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьСостояниеПоНомеру(Номер)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
		|	СостоянияЗаказовПоставщикам.Ссылка
		|ИЗ
		|	Справочник.СостоянияЗаказовПоставщикам КАК СостоянияЗаказовПоставщикам
		|ГДЕ
		|	СостоянияЗаказовПоставщикам.ПометкаУдаления = ЛОЖЬ
		|АВТОУПОРЯДОЧИВАНИЕ";
	
	ТЗ = Запрос.Выполнить().Выгрузить();
	
	Попытка	
		Если ТЗ.Количество() >= Номер Тогда
			Возврат ТЗ[Номер - 1].Ссылка;
		КонецЕсли;
	Исключение
		ОбщегоНазначения.СообщитьПользователю(КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
		ВызватьИсключение;
	КонецПопытки;
	
	Возврат Справочники.СостоянияЗаказовПоставщикам.ПустаяСсылка();
	
КонецФункции

&НаСервереБезКонтекста
Процедура УстановитьСтатусыУДокументов(Массив, Статус)
	
	Для каждого Док Из Массив Цикл
		
		Об = Док.ПолучитьОбъект();
		Если ТипЗнч(Статус) = Тип("СправочникСсылка.СостоянияЗаказовПоставщикам") Тогда
			Об.СостояниеЗаказа = Статус;
		Иначе
			Об.Закрыт = Статус;
		КонецЕсли;		
		Об.Записать(?(Об.Проведен, РежимЗаписиДокумента.Проведение, РежимЗаписиДокумента.Запись));
		
	КонецЦикла;
	
КонецПроцедуры

#Область ПанельОтборов

&НаКлиенте
Процедура ПоказатьСкрытьПанельОтборов(Команда)	
	
	НовоеЗначениеВидимости = НЕ Элементы.ПанельОтборов.Видимость;
	РаботаСОтборамиКлиент.ПоказатьСкрытьПанельОтборов(ЭтотОбъект, НовоеЗначениеВидимости);
	
КонецПроцедуры

&НаСервере
Процедура СохранитьНастройкиОтборов()
	
	РаботаСОтборамиВызовСервера.СохранитьНастройкиОтборов(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОтборСписка(ИмяПоляОтбора, ИмяГруппыРодителя, ЗначениеОтбора)
		
	ДопПараметры = Новый Структура;
	ДопПараметры.Вставить("ИмяПоляОтбора",				ИмяПоляОтбора);
	ДопПараметры.Вставить("ИмяГруппыРодителя",			ИмяГруппыРодителя);
	ДопПараметры.Вставить("ЗначениеОтбора",				ЗначениеОтбора);
	ДопПараметры.Вставить("ПредставлениеЗначенияОтбора",Строка(ЗначениеОтбора));
	
	РаботаСОтборамиВызовСервера.СоздатьЭлементФормыПоЗначениюОтбора(ЭтотОбъект, ДопПараметры);		
	РаботаСОтборамиВызовСервера.УстановитьОтборСписка(ЭтотОбъект, Список, 
		Новый Структура("ИмяПоляОтбора", ИмяПоляОтбора));
		
	Если НЕ КонтекстноеОткрытие Тогда			
		СохранитьНастройкиОтборов();	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОтборОбработкаНавигационнойСсылки(Элемент, 
		НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ОтборИД = Сред(Элемент.Имя, СтрДлина("Отбор_")+1);
	УдалитьЭлементОтбор(ОтборИД);
	
КонецПроцедуры

&НаСервере
Процедура УдалитьЭлементОтбор(ОтборИД)
	
	РаботаСОтборамиВызовСервера.УдалитьОтборСписка(ЭтотОбъект, Список, Новый Структура("ОтборИД", ОтборИД));
	
	Если НЕ КонтекстноеОткрытие Тогда			
		СохранитьНастройкиОтборов();	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СброситьВсеОтборы(Команда)
			
	УдалитьОтборы();
	
КонецПроцедуры

&НаСервере
Процедура УдалитьОтборы()
	
	РаботаСОтборамиВызовСервера.УдалитьВсеОтборыСписка(ЭтотОбъект, Список);	
	ПредставлениеПериода = РаботаСОтборамиКлиентСервер.ОбновитьПредставлениеПериода(ОтборПериод);
	
	Если НЕ КонтекстноеОткрытие Тогда			
		СохранитьНастройкиОтборов();	
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область БСП

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Элементы.Список);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Элементы.Список, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Элементы.Список);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти

#Область УчетОстатковКонтрагентов

&НаКлиенте
Процедура Подключаемый_НачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
			
	УправлениеITОтделом8УФКлиент.ВыполнитьОбработчикНачалоВыбора(ЭтаФорма, ОтборОрганизация, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_Очистка(Элемент, СтандартнаяОбработка)
	
	УправлениеITОтделом8УФКлиент.ВыполнитьОбработчикОчистка(ЭтаФорма, "ОтборОрганизация");
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_АвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, 
	Ожидание, СтандартнаяОбработка)
	
	УправлениеITОтделом8УФКлиент.ВыполнитьОбработчикАвтоПодбор(ЭтаФорма, 
				"ОтборОрганизация",
				Текст, 
				ДанныеВыбора,
				Ожидание,
				СтандартнаяОбработка);	
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)	
		
	УправлениеITОтделом8УФКлиент.ВыполнитьОбработчикОбработкаВыбора(ЭтаФорма, 
				"ОтборОрганизация", 
				ОтборОрганизация,
				Новый ОписаниеОповещения("ПослеОбработкиВыбора", ЭтотОбъект),
				ВыбранноеЗначение,
				СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеОбработкиВыбора(Результат, ДополнительныеПараметры) Экспорт
		
	Если ЗначениеЗаполнено(ОтборОрганизация) Тогда
		УстановитьОтборСписка("Организация", "ГруппаОтборОрганизация", ОтборОрганизация);
		ОтборОрганизация = ПредопределенноеЗначение("Справочник.Организации.ПустаяСсылка"); 
	КонецЕсли;	
	
КонецПроцедуры	

#КонецОбласти

#КонецОбласти

