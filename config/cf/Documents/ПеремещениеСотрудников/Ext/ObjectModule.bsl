#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область СлужебныеПроцедурыИФункции

// Проверка объекта на обязательные реквизиты перед проведением
Процедура ПроверитьДокументПередПроведением(ДополнительныеСвойства, Отказ) Экспорт
	
	// Проверка, что нет дублей в ТЧ по сотрудникам.
	ТЗ = Сотрудники.Выгрузить();
	ТЗ.Колонки.Добавить("КоличествоМестХранения");	
	ТЗ.ЗаполнитьЗначения(1, "КоличествоМестХранения");	
	ТЗ.Свернуть("Сотрудник,СтароеМестоХранения", "КоличествоМестХранения");
	
	Для Каждого Стр Из ТЗ Цикл 
		Если Стр.КоличествоМестХранения > 1 Тогда   
			УправлениеITОтделом8УФ.СообщитьОбОшибке(ЭтотОбъект, 
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'В таблице найден повторяющийся сотрудник и старое место хранения %1 (%2)'"), "'" + Строка(Стр.Сотрудник) + "'", Строка(Стр.СтароеМестоХранения))
			, , , "Сотрудники", Отказ);
		КонецЕсли;
	КонецЦикла;
	
	// Проверка, что СтароеМестоХранения соответствует сотруднику Сотрудник.
	ПараметрыОтбора = Новый Структура;
	НоваяТЗ = УправлениеITОтделом8УФ.ПолучитьТаблицуМестХраненияСотрудников(Дата, ТЗ.ВыгрузитьКолонку("Сотрудник"));
	Для Каждого Строки Из ТЗ Цикл
		ПараметрыОтбора.Вставить("Сотрудник", Строки.Сотрудник);
		ПараметрыОтбора.Вставить("МестоХранения", Строки.СтароеМестоХранения);
		НайденныеСтроки = НоваяТЗ.НайтиСтроки(ПараметрыОтбора);
		Если НайденныеСтроки.Количество() = 0 Тогда
			УправлениеITОтделом8УФ.СообщитьОбОшибке(ЭтотОбъект, 
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'В таблице за сотрудником %1 место хранения %2 не закреплено'"), "'" + Строка(Стр.Сотрудник) + "'", Строка(Стр.СтароеМестоХранения))
			, , , "Сотрудники", Отказ);
		КонецЕсли;
	КонецЦикла;
		
КонецПроцедуры

// СтандартныеПодсистемы.УправлениеДоступом
// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	
	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";
	
КонецПроцедуры
// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
		
	// Инициализация дополнительных свойств для проведения документа.
	УправлениеITОтделом8УФ.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства);
	
	// Инициализация данных документа.
	СЛС.ИнициализироватьДанныеДокумента(Ссылка, ДополнительныеСвойства);	
	
	// Проверка объекта.
	ПроверитьДокументПередПроведением(ДополнительныеСвойства, Отказ);
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	// Подготовка наборов записей.
	УправлениеITОтделом8УФ.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	// Отражение в разделах учета.
	СЛС.ОтразитьДвиженияВРазделахУчета(Ссылка, ДополнительныеСвойства, Движения, Отказ);
	
	// Запись наборов записей.
	УправлениеITОтделом8УФ.ЗаписатьНаборыЗаписей(ЭтотОбъект);
		
	ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц.Закрыть();
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("СправочникСсылка.Сотрудники") Тогда
		Дата = ТекущаяДатаСеанса();
		Организация = ДанныеЗаполнения.Организация;
		Комментарий = Строка(ДанныеЗаполнения);
		Основание = ДанныеЗаполнения;
		
		НоваяСтрока = Сотрудники.Добавить();
		НоваяСтрока.Сотрудник = ДанныеЗаполнения;
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.Задание") Тогда
		Основание						= ДанныеЗаполнения;
		Организация						= ДанныеЗаполнения.Организация;
		Комментарий						= ДанныеЗаполнения.Тема;
		НоваяСтрока						= Сотрудники.Добавить();
		НоваяСтрока.СтароеМестоХранения	= ДанныеЗаполнения.МестоХранения;
		Если ТипЗнч(ДанныеЗаполнения.Инициатор) = Тип("СправочникСсылка.ФизическиеЛица") Тогда
			НоваяСтрока.Сотрудник		= УправлениеITОтделом8УФ.СотрудникПоОрганизацииИФизЛицу(Организация, ДанныеЗаполнения.Инициатор);
		КонецЕсли;
	КонецЕсли
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли