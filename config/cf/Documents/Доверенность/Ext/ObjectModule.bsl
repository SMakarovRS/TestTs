#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОбработчикиСобытий

// Процедура - обработчик события ОбработкаЗаполнения.
//
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ЗаказПоставщику") Тогда
	
		// Заполнение шапки документа.
		ДокументОснование	= ДанныеЗаполнения.Ссылка;
		Организация			= ДанныеЗаполнения.Организация;
		Контрагент			= ДанныеЗаполнения.Контрагент;
		НаПолучениеОт		= ?(ЗначениеЗаполнено(Контрагент.НаименованиеПолное), Контрагент.НаименованиеПолное, Контрагент.Наименование);
		Договор				= ДанныеЗаполнения.Договор;
		Если ПустаяСтрока(ДанныеЗаполнения.НомерВходящегоДокумента) Тогда
			ПоДокументу		= ДанныеЗаполнения.Ссылка;
		Иначе
			ПоДокументу		= СтрШаблон(НСтр("ru = 'Счет №%1 от %2'"), ДанныеЗаполнения.НомерВходящегоДокумента, Формат(ДанныеЗаполнения.ДатаВходящегоДокумента, "ДФ=dd.MM.yy"));
		КонецЕсли;
		НаПолучениеОт		= ДанныеЗаполнения.Контрагент.НаименованиеПолное;
		БанковскийСчет		= ДанныеЗаполнения.Организация.БанковскийСчетПоУмолчанию;
		ДатаДействия		= ТекущаяДатаСеанса() + 5 * 24 * 60 * 60;
		
		// Заполнение табличной части документа.
		Запасы.Очистить();
		
		Для каждого СтрокаТабличнойЧасти Из ДанныеЗаполнения.Номенклатура Цикл
			
			НоваяСтрока 					= Запасы.Добавить();
			НоваяСтрока.НаименованиеТовара 	= СтрокаТабличнойЧасти.Номенклатура.Наименование;
			НоваяСтрока.ЕдиницаИзмерения 	= СтрокаТабличнойЧасти.ЕдиницаИзмерения;
			НоваяСтрока.Количество 			= СтрокаТабличнойЧасти.Количество;
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры // ОбработкаЗаполнения()

// Процедура - обработчик события ПередЗаписью объекта.
//
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Контрагент) И НЕ ЗначениеЗаполнено(Договор) Тогда
		
		Договор = Контрагент.ДоговорПоУмолчанию;
		
	КонецЕсли;
	
КонецПроцедуры // ПередЗаписью()

#КонецОбласти

#КонецЕсли