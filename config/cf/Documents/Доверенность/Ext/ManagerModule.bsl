#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
		
#Область ИнтерфейсПечати

// Процедура формирует печатную форму документа по указанному макету.
//
Функция ПечатнаяФорма(МассивОбъектов, ОбъектыПечати, ТипДокумента)
	
	СтруктураЗаполнения = Новый Структура;
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.КлючПараметровПечати = "ПараметрыПечати_Доверенность";
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Доверенность.Ссылка КАК Ссылка,
		|	Доверенность.Номер КАК Номер,
		|	Доверенность.Дата КАК ДатаДокумента,
		|	Доверенность.Организация КАК Руководители,
		|	Доверенность.Организация,
		|	Доверенность.ФизЛицо,
		|	Доверенность.ФизЛицо.Наименование КАК ФамилияИмяОтчествоДоверенного,
		|	Доверенность.БанковскийСчет КАК БанковскийСчет,
		|	Доверенность.Контрагент КАК Поставщик,
		|	Доверенность.НаПолучениеОт КАК ПоставщикПредставление,
		|	Доверенность.ДатаДействия КАК СрокДействия,
		|	Доверенность.ПоДокументу КАК РеквизитыДокументаНаПолучение,
		|	Доверенность.Организация.Префикс КАК Префикс,
		|	Доверенность.Запасы.(
		|		НомерСтроки КАК Номер,
		|		НаименованиеТовара КАК Ценности,
		|		НаименованиеТовара КАК ЦенностиПредставление,
		|		ЕдиницаИзмерения КАК ЕдиницаИзмеренияПредставление,
		|		Количество
		|	)
		|ИЗ
		|	Документ.Доверенность КАК Доверенность
		|ГДЕ
		|	Доверенность.Ссылка В(&МассивОбъектов)
		|
		|УПОРЯДОЧИТЬ ПО
		|	Ссылка,
		|	Номер";
	
	Шапка = Запрос.Выполнить().Выбрать();
	
	ПервыйДокумент = Истина;
	
	Пока Шапка.Следующий() Цикл
		
		Если Не ПервыйДокумент Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		ПервыйДокумент 		= Ложь;		
		НомерСтрокиНачало 	= ТабличныйДокумент.ВысотаТаблицы + 1;		
		СтруктураЗаполнения = УправлениеITОтделом8УФ.СтруктураЗаполненияПечатнойФормы(Шапка.Ссылка);		
		ВыборкаСтрокТовары  = Шапка.Запасы.Выбрать();		
		
		ТабличныйДокумент.КлючПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_Доверенность_ПФ_MXL_М2";		
		Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.Доверенность.ПФ_MXL_М2");
		
		ФамилияИмяОтчествоДоверенного = СокрЛП(СтруктураЗаполнения.ФизЛицоФамилия) + " " + СокрЛП(СтруктураЗаполнения.ФизЛицоИмя) + " " + СокрЛП(СтруктураЗаполнения.ФизЛицоОтчество);
		Должность                     = СокрЛП(СтруктураЗаполнения.ФизЛицоДолжность);
		СтруктураЗаполнения.Вставить("Поставщик", 			   Шапка.Поставщик);
		СтруктураЗаполнения.Вставить("ПоставщикПредставление", Шапка.ПоставщикПредставление);
		СтруктураЗаполнения.Вставить("СрокДействия",           Шапка.СрокДействия);			
		СтруктураЗаполнения.Вставить("ФИОДоверенного", "" + ?(ПустаяСтрока(Должность), "", Должность + ", " + Символы.ПС) + (ФамилияИмяОтчествоДоверенного));
		Если ТипДокумента= "М2" Тогда
			ОбластьМакета= Макет.ПолучитьОбласть("Отрез");			
			ЗаполнитьЗначенияСвойств(ОбластьМакета.Параметры, СтруктураЗаполнения);
			ТабличныйДокумент.Вывести(ОбластьМакета);
			НазваниеФормы= НСтр("ru = 'Типовая межотраслевая форма № М-2'");
			КодПоОКУД 	 = "0315001";
		Иначе
			НазваниеФормы= НСтр("ru = 'Типовая межотраслевая форма № М-2а'");
			КодПоОКУД 	 = "0315002";
		КонецЕсли;
		
		СведенияОбОрганизации = УправлениеITОтделом8УФ.СведенияОЮрФизЛице(Шапка.Организация, Шапка.ДатаДокумента,, Шапка.БанковскийСчет);
		
		ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
		СтруктураЗаполнения.Вставить("РеквизитыДокументаНаПолучение", Шапка.РеквизитыДокументаНаПолучение);
		СтруктураЗаполнения.Вставить("НазваниеФормы", 				НазваниеФормы);
		СтруктураЗаполнения.Вставить("КодПоОКУД", 					КодПоОКУД);
		СтруктураЗаполнения.Вставить("ОрганизацияПредставление", 	УправлениеITОтделом8УФ.ОписаниеОрганизации(СведенияОбОрганизации, "ПолноеНаименование,ИНН,КПП,ЮридическийАдрес,Телефоны,"));
		СтруктураЗаполнения.Вставить("РеквизитыСчета", 				УправлениеITОтделом8УФ.ОписаниеОрганизации(СведенияОбОрганизации, "НомерСчета,Банк,БИК,КоррСчет,"));
		СтруктураЗаполнения.Вставить("РеквизитыПотребителя", 		СтруктураЗаполнения.ОрганизацияПредставление);
		СтруктураЗаполнения.Вставить("РеквизитыПлательщика", 		СтруктураЗаполнения.ОрганизацияПредставление);
		СтруктураЗаполнения.Вставить("ОрганизацияКодПоОКПО", 		СтруктураЗаполнения.ОрганизацияКодПоОКПО);
		СтруктураЗаполнения.Вставить("ПаспортСерия", 				СтруктураЗаполнения.ФизЛицоДокументСерия);
		СтруктураЗаполнения.Вставить("ПаспортНомер", 				СтруктураЗаполнения.ФизЛицоДокументНомер);
		СтруктураЗаполнения.Вставить("ПаспортВыдан", 				СтруктураЗаполнения.ФизЛицоДокументКемВыдан);
		СтруктураЗаполнения.Вставить("ПаспортДатаВыдачи", 			СтруктураЗаполнения.ФизЛицоДокументДатаВыдачи);
		СтруктураЗаполнения.Вставить("ФамилияИмяОтчествоДоверенного",ФамилияИмяОтчествоДоверенного);
		СтруктураЗаполнения.Вставить("ДолжностьДоверенного", 		Должность);
		ЗаполнитьЗначенияСвойств(ОбластьМакета.Параметры, СтруктураЗаполнения);
		ТабличныйДокумент.Вывести(ОбластьМакета);
		
		ОбластьМакета = Макет.ПолучитьОбласть("ЗаголовокТаблицы");
		ТабличныйДокумент.Вывести(ОбластьМакета);
		
		ОбластьМакета = Макет.ПолучитьОбласть("Строка");
		
		Пока ВыборкаСтрокТовары.Следующий() Цикл
			ЗаполнитьЗначенияСвойств(ОбластьМакета.Параметры, ВыборкаСтрокТовары);
			СтруктураЗаполненияСтроки = Новый Структура;
			СтруктураЗаполненияСтроки.Вставить("КоличествоПрописью",?(ВыборкаСтрокТовары.Количество = 0,
														   "",
														   Строка(ВыборкаСтрокТовары.Количество)
														   + " (" 
														   + УправлениеITОтделом8УФ.КоличествоПрописью(ВыборкаСтрокТовары.Количество) + ")"));
			ЗаполнитьЗначенияСвойств(ОбластьМакета.Параметры, СтруктураЗаполненияСтроки);
			ТабличныйДокумент.Вывести(ОбластьМакета);
		КонецЦикла;
		
		ОбластьМакета = Макет.ПолучитьОбласть("Подвал");		
		ЗаполнитьЗначенияСвойств(ОбластьМакета.Параметры, СтруктураЗаполнения);
		ТабличныйДокумент.Вывести(ОбластьМакета);
 		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, Шапка.Ссылка);
		
	КонецЦикла;
	
	ТабличныйДокумент.АвтоМасштаб = Истина;
	
	Возврат ТабличныйДокумент;
	
КонецФункции // ПечатнаяФорма()

// Сформировать печатные формы объектов
//
// ВХОДЯЩИЕ:
//   ИменаМакетов    - Строка    - Имена макетов, перечисленные через запятую.
//   МассивОбъектов  - Массив    - Массив ссылок на объекты которые нужно распечатать.
//   ПараметрыПечати - Структура - Структура дополнительных параметров печати.
//
// ИСХОДЯЩИЕ:
//   КоллекцияПечатныхФорм - Таблица значений - Сформированные табличные документы.
//   ПараметрыВывода       - Структура        - Параметры сформированных табличных документов.
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	ПараметрыВывода.ДоступнаПечатьПоКомплектно = Истина;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "М2") Тогда
		                                                                                  
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "М2", НСтр("ru = 'Типовая межотраслевая форма № М-2'"), ПечатнаяФорма(МассивОбъектов, ОбъектыПечати, "М2"));
		
	КонецЕсли;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "М2а") Тогда
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "М2а", НСтр("ru = 'Типовая межотраслевая форма № М-2а'"), ПечатнаяФорма(МассивОбъектов, ОбъектыПечати, "М2-а"));
		
	КонецЕсли;
		
КонецПроцедуры

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	// М2
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "М2";
	КомандаПечати.Представление = НСтр("ru = 'М-2'");
	КомандаПечати.СписокФорм = "ФормаДокумента,ФормаСписка";
	КомандаПечати.ПроверкаПроведенияПередПечатью = Ложь;
	КомандаПечати.Порядок = 1;
	
	// М2а
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "М2а";
	КомандаПечати.Представление = НСтр("ru = 'М-2а'");
	КомандаПечати.СписокФорм = "ФормаДокумента,ФормаСписка";
	КомандаПечати.ПроверкаПроведенияПередПечатью = Ложь;
	КомандаПечати.Порядок = 4;
	
КонецПроцедуры

#КонецОбласти

#Область ПодсистемыБСП

// СтандартныеПодсистемы.ЗаполнениеОбъектов
// Определяет список команд заполнения.
//
// Параметры:
//   КомандыЗаполнения - ТаблицаЗначений - Таблица с командами заполнения. Для изменения.
//       См. описание 1 параметра процедуры ЗаполнениеОбъектовПереопределяемый.ПередДобавлениемКомандЗаполнения().
//   Параметры - Структура - Вспомогательные параметры. Для чтения.
//       См. описание 2 параметра процедуры ЗаполнениеОбъектовПереопределяемый.ПередДобавлениемКомандЗаполнения().
//
//@skip-warning
Процедура ДобавитьКомандыЗаполнения(КомандыЗаполнения, Параметры) Экспорт
	
КонецПроцедуры
// Конец СтандартныеПодсистемы.ЗаполнениеОбъектов

// СтандартныеПодсистемы.УправлениеДоступом
// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
//@skip-warning
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	
	
КонецПроцедуры
// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецЕсли